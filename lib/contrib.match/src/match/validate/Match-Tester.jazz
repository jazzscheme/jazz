
(module protected match.validate.Match-Tester jazz


(import (match)
        (jazz.validation))


(class Match-Tester extends Tester
  
  
  ;; (form
  ;;   (<install> title: "Match-Tester"))


  (method override (test)
    (invoke #f test-match))
  
  
  ;;;
  ;;;; Match
  ;;;


  (method (test-match)
    (validate (equal? '(3 2 1) (match '(1 2 3) ((a b c) (list c b a)))))

    (validate (equal? (match 'any (_ 'ok)) 'ok))
    (validate (equal? (match 'ok (x x)) 'ok))
    (validate (equal? (match 28 (28 'ok)) 'ok))
    (validate (equal? (match "good" ("bad" 'fail) ("good" 'ok)) 'ok))
    (validate (equal? (match 'good ('bad 'fail) ('good 'ok)) 'ok))
    (validate (equal? (match '() (() 'ok)) 'ok))
    (validate (equal? (match '(ok) ((x) x)) 'ok))
    (validate (equal? (match '#(ok) (#(x) x)) 'ok))
    (validate (equal? (match '(1 2) ((_ _) 'ok)) 'ok))
    (validate (equal? (match '(o k) ((and) 'ok)) 'ok))
    (validate (equal? (match 'ok ((and x) x)) 'ok))
    (validate (equal? (match 'ok ((and (? symbol?) y) 'ok)) 'ok))
    (validate (equal? (match '(o k) ((or) 'fail) (else 'ok)) 'ok))
    (validate (equal? (match 'ok ((or x) 'ok)) 'ok))
    (validate (equal? (match 'ok ((or (? symbol? y) y) y)) 'ok))
    (validate (equal? (match 28 ((not (a . b)) 'ok)) 'ok))
    (validate (equal? (match 28 ((? number?) 'ok)) 'ok))
    (validate (equal? (match 28 ((? number? x) (+ x 1))) 29))

    (validate (equal? (match '(ok . ok) ((x . x) x)) 'ok))
    (validate (equal? (match '(ok . bad) ((x . x) 'bad) (else 'ok)) 'ok))
    (validate (equal? (match '(ok . ok) ((x . 'bad) x) (('ok . x) x)) 'ok))
    
    (validate (equal? (match '((a . 1) (b . 2) (c . 3))
                        (((x . y) ___) (list x y)))
                      '((a b c) (1 2 3))))
    
    (validate (equal? (match '((a . 1) (b . 2) (c . 3))
                        (((x . y) ...) (list x y)))
                      '((a b c) (1 2 3))))
    
    (validate (equal? (match '#(1 2 3 (a . 1) (b . 2) (c . 3))
                             (#(a b c (hd . tl) ...) (list a b c hd tl)))
                      '(1 2 3 (a b c) (1 2 3))))
    
    (validate (equal? (match '(1 2 3)
                        (((? odd? n) ___) n)
                        (((? number? n) ___) n))
                      '(1 2 3)))
    
    (validate (equal? (match '(1 2)
                        ((a . b) (=> next) (if (even? a) 'fail (next)))
                        ((a . b) 'ok))
                      'ok))
    
    (validate (equal? (match-let ((x 'ok) (y '(o k)))
                                 y)
                      '(o k)))
    
    (validate (equal? (match-let* ((x 'f) (y 'o) ((z w) (list y x)))
                                  (list x y z w))
                      '(f o o f)))
    
    (validate (equal? (match '(1 . 2) (((get! a) . b) (list (a) b)))
                      '(1 2)))
    
    (validate (equal? (match '(1 . 2) ((a . (get! b)) (list a (b))))
                      '(1 2)))
    
    (validate (equal? (match '#(1 2 3) (#((get! a) b c) (list (a) b c)))
                      '(1 2 3)))
    
    (validate (equal? (let ((x '(1 . 2)))
                        (match x (((set! a) . b) (a 3)))
                        x)
                      '(3 . 2)))
    
    (validate (equal? (let ((x '(1 . 2)))
                        (match x ((a . (set! b)) (b 3)))
                        x)
                      '(1 . 3)))
    
    (validate (equal? (let ((x '#(1 2 3)))
                        (match x (#(a (set! b) c) (b 0)))
                        x)
                      '#(1 0 3)))
    
    (validate (equal? (match {Point 10 20} (($ Point h v) (list h v)))
                      '(10 20)))
    
    (validate (equal? (match {Point 10 20} (($$ Point (v: v) (h: h)) (list h v)))
                      '(10 20)))
    
    (validate (equal? (match '(1 2 3) ((_ *** (a b c)) (list a b c)))
                      '(1 2 3)))
    
    (validate (equal? (match '(x (1 2 3)) ((_ *** (a b c)) (list a b c)))
                      '(1 2 3)))
    
    (validate (equal? (match '(x (x (x (1 2 3)))) ((_ *** (a b c)) (list a b c)))
                      '(1 2 3)))
    
    (validate (equal? (match '(x (x (x a b c (1 2 3) d e f))) ((_ *** (a b c)) (list a b c)))
                      '(1 2 3)))
    
    (validate (equal? (match '(x (x (x a b c (1 2 3) d e f))) (('x *** (a b c)) (list a b c)))
                      '(1 2 3)))
    
    (validate (not (match '(x (y (x a b c (1 2 3) d e f)))
                     (('x *** (a b c)) (list a b c))
                     (else #f))))

    ;; (validate (equal? (match '(p (ul (li a (b c) (a (^ (href . "http://synthcode.com/")) "synthcode") d e f)))
    ;;                     (((or 'p 'ul 'li 'b) *** ('a ('^ attrs ...) text ...))
    ;;                      (list attrs text))
    ;;                     (else #f))
    ;;                   '(((href . "http://synthcode.com/")) ("synthcode"))))

    ;; (validate (not (match '(p (ol (li a (b c) (a (^ (href . "http://synthcode.com/")) "synthcode") d e f)))
    ;;                  (((or 'p 'ul 'li 'b) *** ('a ('^ attrs ...) text ...))
    ;;                   (list attrs text))
    ;;                  (else #f))))

    ;; (validate (equal? (match '(p (ul (li a (b c) (a (^ (href . "http://synthcode.com/")) "synthcode") d e f)))
    ;;                     (((and tag (or 'p 'ul 'li 'b)) *** ('a ('^ attrs ...) text ...))
    ;;                      (list tag attrs text))
    ;;                     (else #f))
    ;;                   '((p ul li) ((href . "http://synthcode.com/")) ("synthcode"))))

    )))
