;;; .obj file parser
;;;
;;; Parses a .obj file and produces a list of triangles
;;; representing the mesh.  Right now this assumes that
;;; there's also a material file, which is of the same
;;; name but with the .mtl extension.
;;;
;;; Example usage:
;;; (load-obj "mesh")


(library raytrace.parser jazz


(import (raytrace.log)
        (raytrace.vec)
        (raytrace.obj))


;;; Helpful functions
(definition (while thunk nil? cont)
  (let ((v (thunk)))
    (if (not (nil? v))
          (begin
            (cont v)
            (while thunk nil? cont)))))

(definition (file-foreach f cont)
  (call-with-input-file f
    (lambda (p)
      (while (lambda () (read-line p))
             (lambda (c) (eof-object? c))
             (lambda (line)
               (if (not (string=? line ""))
                   (with-input-from-string line
                     (lambda () (cont)))))))))

(definition (read-token)
  (read-line (current-input-port) #\space))

;;; State based
(definition verts '())
(definition prims '())
(definition materials '())
(definition cur-material #f)

(definition (make-vertex n)
  (apply make-vec3d (vector-ref verts (- n 1))))

(definition (token-map map)
  (let* ((c (read-token))
         (v (assoc c map)))
    (if v
        (cdr v)
        c)))

(definition (get-current-material)
  (if cur-material
      (cdr (assq cur-material materials))))

(definition (material-prop mat name)
  (let ((v (assoc name mat)))
    (if v (cdr v) #f)))

(definition (obj-declaration)
  (token-map '(("v" . vertex) ("f" . face) ("usemtl" . mtlswitch))))
  
(definition (obj-vertex)
  (if (vector? verts)
      (error "Invalid obj file format, vertex came after face")
      (let ((x (* (exact->inexact (read)) 15.0))
            (y (* (exact->inexact (read)) 15.0))
            (z (* (exact->inexact (read)) 15.0)))
        (set! verts (cons `(,x ,z ,y) verts)))))

(definition (obj-face)
  (if (pair? verts)
      (set! verts (list->vector (reverse verts))))
  (let* ((verts (list->vector (read-all)))
         (count (vector-length verts))
         (mat (get-current-material)))
    (if (>= count 3) (set! prims (cons (make-triangle
                                         (apply make-vec3d (material-prop mat "Kd"))
                                         (make-vertex (vector-ref verts 0))
                                         (make-vertex (vector-ref verts 1))
                                         (make-vertex (vector-ref verts 2)))
                                       prims)))
    (if (>= count 4) (set! prims (cons (make-triangle
                                         (apply make-vec3d (material-prop mat "Kd"))
                                         (make-vertex (vector-ref verts 0))
                                         (make-vertex (vector-ref verts 2))
                                         (make-vertex (vector-ref verts 3)))
                                       prims)))))

(definition (obj-material-switch)
  (set! cur-material (string->symbol (read-token))))

(definition (obj-attr)
  (let ((type (obj-declaration)))
    (case type
      ((vertex) (obj-vertex))
      ((face) (obj-face))
      ((mtlswitch) (obj-material-switch)))))

(definition (mat-create)
  (let ((name (read)))
    (set! materials (cons (cons name '())
                          materials))
    (set! cur-material name)))

(definition (mat-declaration)
  (token-map '(("newmtl" . new))))

(definition (mat-property name)
  (let ((prop (read-all))
        (mat (assq cur-material materials)))
    (set-cdr! mat (cons (cons name prop)
                        (cdr mat)))))

(definition (mat-attr)
  (let ((type (mat-declaration)))
    (case type
      ((new) (mat-create))
      (else (if (not (string=? type "#"))
                (mat-property type))))))

(definition (load-obj f)
  (set! verts '())
  (set! prims '())
  (log-args "Loading mesh " f "...")
  (file-foreach (string-append "../../lib/contrib/raytrace/" f ".mtl") mat-attr)
  (file-foreach (string-append "../../lib/contrib/raytrace/" f ".obj") obj-attr)
  (log-args "Primitive count: " (length prims))
  (reverse prims)))
