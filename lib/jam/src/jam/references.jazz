(module jam.references jazz


(import (jazz.runtime))


(definition (package-references)
  (define (package-product package)
    (assq (package-name package) (package-products package)))
  
  (let ((target (string->symbol (last (command-arguments))))
        (repositories (get-repositories))
        (packages '()))
    (for-each (lambda (repository)
                (unless (repository-binary? repository)
                  (iterate-packages repository
                    (lambda (package)
                      (when (or (eq? (package-name package) target)
                                (member? target (package-products package) key: car test: eq?))
                        (set! packages (cons package packages)))))))
              repositories)
    (cond ((null? packages)
           (format :console "Unable to find package {s}" target))
          ((> (length packages) 1)
           (format :console "Ambiguous name: {s}" target))
          (else
           (let ((package (car packages)))
             (let ((name (package-name package))
                   (description (package-description package))
                   (authors (package-authors package))
                   (stage (package-stage package))
                   (products (package-products package)))
               (if description
                   (format :console "{a}{? ({a})~}{?, {a}~}{?, {l detail: :human separator: \", \"}~}{%}" name stage description authors)
                 (format :console "{a}{%}" name))
               (let ((units (package-units package))
                     (explicit (cond-expanded-product-descriptor-dependencies name (package-product package)))
                     (implicit (make-table test: eq?))
                     (references (make-table test: eq?)))
                 (define (add toplevel package)
                   (let ((name (get-name~ toplevel)))
                     (unless (table-ref references name #f)
                       (table-set! implicit (package-name package) package)
                       (table-set! references name toplevel)
                       #t)))
                 
                 (define (follow unit)
                   (follow-declaration (walk-unit unit)))
                 
                 (define (follow-declaration declaration)
                   (typecase declaration
                     ((Unit-Declaration)
                      (when (add declaration (get-container~ declaration))
                        (for-each follow (get-requires~ declaration))))
                     ((Module-Declaration)
                      (when (add declaration (get-container~ declaration))
                        (iterate-table (get-references~ (get-walker~ declaration))
                          (lambda (locator declaration)
                            (let ((resolved-declaration (resolve-binding declaration)))
                              (let ((module (get-toplevel~ resolved-declaration)))
                                (follow-declaration module)))))))))
                 
                 (for-each follow units)
                 (format :console "  Found {a} references in{%}" (table-length references))
                 (for-each (lambda (name)
                             (format :console "    {a}{%}" name))
                           (sort ci<? (table-keys implicit))))))))))


(register-product-run 'jam-references
  (lambda (descriptor)
    (package-references))))
