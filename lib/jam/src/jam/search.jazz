;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jam Search
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jam.search jazz


(import (jazz.runtime))


(definition package (jam-search arguments)
  (let ((output (current-output-port))
        (target (last arguments))
        (repositories (repositories-get))
        (packages '()))
    (define (search-field field)
      (and field (search field target ignore-case?: #t ignore-diacritic?: #t)))
    
    (for-each (lambda (repository)
                (unless (repository-binary? repository)
                  (iterate-packages repository
                    (lambda (package)
                      (when (or (search-field (symbol->string (package-name package)))
                                (search-field (package-title package))
                                (search-field (package-description package)))
                        (set! packages (cons package packages)))))))
              repositories)
    (let ((sorted (sort ci<? packages key: (lambda (package) (symbol->string (package-name package))))))
      (for-each (lambda (package)
                  (unless (eq? package (car sorted))
                    (format output "{%}"))
                  (let ((name (package-name package))
                        (description (package-description package))
                        (authors (package-authors package))
                        (stage (package-stage package)))
                    (if description
                        (format output "{a}{? ({a})~}{?, {a}~}{?, {l detail: :human separator: \", \"}~}" name stage description authors)
                      (format output "{a}" name))))
                sorted))
    (force-output output))))
