;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Finite State Automata
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


;;
;; A search automata is made up of:
;;
;;   start state:   the automata is looking for a word beginning
;;   skip state:    the automata is skipping to the end of the current word
;;   letter states: these states make up the decomposed tree of searched words
;;   found states:  these states collect found strings without advancing the tape
;;
;; Note: No effort was made to generalize this code into a reusable automata class
;;       as this clearly requires usage of templates. I will rather wait for the fully
;;       optimized and typed Jazz compiler and make this automata class in Jazz!
;;
;; Todo: Implemented Listed and Lettered transitions.
;;       Find a clean solution to a whole word that ends the file (i.e. the non-constituent
;;       transition to the found state doesn't get called!).
;;


(module jazz.automata jazz


(hub action)
(hub add-found)
(hub add-start)
(hub add-transition)
(hub follow)
(hub get-col)
(hub get-end)
(hub get-found)
(hub get-index)
(hub get-line)
(hub get-name)
(hub get-nb-vectored)
(hub get-next)
(hub get-paragraphs)
(hub get-start)
(hub get-string)
(hub get-vector)
(hub run)
(hub save-start)
(hub set-nb-vectored)
(hub transition)


;;;
;;;; Automata
;;;


(class Automata extends Object
  
  
  (slot nb-vectored <fx> accessors generate)
  (slot nb-listed   <fx> accessors generate)
  (slot nb-lettered <fx> accessors generate)
  (slot start       getter generate)
  
  
  (method override (initialize self s)
    (set! self.nb-vectored 0)
    (set! self.nb-listed 0)
    (set! self.nb-lettered 0)
    (set! self.start s))
  
  
  (method public (run self tape)
    (declare (proper-tail-calls))
    (let (iter (state start))
      (when state
        (iter (transition state tape))))))


;;;
;;;; Tape
;;;


(class Tape extends Object
  
  
  (slot found)
  
  
  (method override (initialize self)
    (set! self.found (new-queue)))
  
  
  (method package virtual (get-next self) <fx+>
    )
  
  
  (method package virtual (save-start self)
    )
  
  
  (method package virtual (add-start self found)
    )
  
  
  (method package (add-found self info)
    (enqueue found info))
  
  
  (method package (get-found self)
    (queue-list found)))


;;;
;;;; StringTape
;;;


(class StringTape extends Tape
  
  
  (slot string            getter generate)
  (slot index        <fx> getter generate)
  (slot start-index  <fx> accessors generate)
  (slot end          <fx> getter generate)
  
  
  (method override (initialize self s)
    (nextmethod self)
    (set! self.string s)
    (set! self.index 0)
    (set! self.start-index 0)
    (set! self.end (string-length s)))
  
  
  (method override (get-next self) <fx+>
    (if (< index end)
        (prog1 (char->integer (element string index))
          (set! index (+ index 1)))
      #f))
  
  
  (method override (save-start self)
    (set! start-index index))
  
  
  (method override (add-start self found)
    (list found start-index)))


;;;
;;;; TextTape
;;;


(class TextTape extends Tape
  
  
  (slot paragraphs          getter generate)
  (slot line       <fx>     getter generate)
  (slot col        <fx>     getter generate)
  (slot scan       <string> getter generate)
  (slot start-line <fx>     accessors generate)
  (slot start-col  <fx>     accessors generate)
  (slot end-line   <fx>     getter generate)
  (slot end-col    <fx>     getter generate)
  
  
  (method override (initialize self p s e)
    (nextmethod self)
    (set! self.paragraphs p)
    (set! self.line (get-line s))
    (set! self.col (get-col s))
    (set! self.scan (get-string (element paragraphs line)))
    (set! self.start-line 0)
    (set! self.start-col 0)
    (set! self.end-line (get-line e))
    (set! self.end-col (get-col e)))
  
  
  (method override (get-next self) <fx+>
    (cond ((or (> line end-line)
               (and (= line end-line)
                    (>= col end-col)))
           #f)
          ((= col (cardinality scan))
           (set! line (+ line 1))
           (set! col 0)
           (set! scan (get-string (element paragraphs line)))
           (char->integer #\newline))
          (else
           (prog1 (char->integer (element scan col))
             (set! col (+ col 1))))))
  
  
  (method override (save-start self)
    (set! start-line line)
    (set! start-col col))
  
  
  (method override (add-start self found)
    (list found start-line start-col)))


;;;
;;;; Transitions
;;;


(class Transitions extends Object
  
  
  (method public virtual (add-transition self i state))
  (method public virtual (follow self i)))


;;;
;;;; VectoredTransitions
;;;


(definition NbAscii 256)
(definition EOFTransition NbAscii)
(definition NotAsciiTransition (+ NbAscii 1))
(definition NbTransitions (+ NbAscii 2))


(class VectoredTransitions extends Transitions
  
  
  (slot vector getter generate)
  
  
  (method override (initialize self . rest)
    (set! vector (make-vector NbTransitions #f)))
  
  
  (method override (add-transition self i <fx> state)
    (set-element! vector (if (> i NbAscii) NotAsciiTransition i) state))
  
  
  (method override (follow self i <fx>)
    (element vector (if (> i NbAscii) NotAsciiTransition i))))


;;;
;;;; State
;;;
  

(class State extends Object
  
  
  (slot name        getter generate)
  (slot transitions getter generate)
  
  
  (method override (initialize self n t)
    (set! name n)
    (set! transitions t))


  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name))))
  
  
  (method public (add-transition self i <fx> state)
    (add-transition transitions i state))
  
  
  (method public virtual (transition self tape)
    (let ((i (get-next tape)))
      (if i
          (follow self i)
        (action (follow self EOFTransition) tape)
        #f)))
  
  
  (method public (follow self i <fx>)
    (follow transitions i))
  
  
  (method public virtual (action self tape)))


;;;
;;;; StartState
;;;


(class StartState extends State
  
  
  (method override (initialize self t)
    (nextmethod self "start" t))
  
  
  (method override (print self output detail)
    (print-unreadable self output
      (lambda (output)
        )))
  
  
  (method override (transition self tape)
    (save-start tape)
    (nextmethod self tape)))


;;;
;;;; FoundState
;;;


(class FoundState extends State
  
  
  (slot found      getter generate)
  (slot next-state getter generate)
  
  
  (method override (initialize self f ns)
    (set! found f)
    (set! next-state ns)
    (nextmethod self "found" #f))
  
  
  (method override (print self output detail)
    (print-unreadable self output
      (lambda (output)
        )))
  
  
  (method override (transition self tape)
    (action self tape)
    next-state)
  
  
  (method override (action self tape)
    (add-found tape (add-start tape found))))


;;;
;;;; Build
;;;


(definition public (build-search-automata targets whole-words ignore-case constituent-test)
  (let* ((start (new StartState (new VectoredTransitions)))
         (skip (new State "skip" (new VectoredTransitions)))
         (automata (new Automata start)))
    (set-nb-vectored automata (+ (get-nb-vectored automata) 2))
    (add-constituency-transitions start skip start start constituent-test)
    (add-constituency-transitions skip skip start start constituent-test)
    (for-each (lambda (target)
                (process-target target automata start skip whole-words ignore-case constituent-test))
              targets)
    automata))


(definition (add-constituency-transitions state c not-c eof constituent-test)
  (loop (for i from 0 below NbAscii)
        (add-transition state i (if (is-constituent constituent-test i)
                                     c
                                   not-c)))
  (add-transition state EOFTransition eof))


(definition (process-target target automata start skip whole-words ignore-case constituent-test)
  (let ((no-transition #f))
    (when (char? (car target))
      (set! no-transition (char->integer (car target)))
      (set! target (cdr target)))
    (let ((state start)
          (string (car target)))
      (iterate string
        (lambda (c)
          (let ((i (char->integer c))
                (iinv (char->integer (if (lower-case? c) (upcase c) (downcase c)))))
            (let ((new-state (follow state i)))
              (cond ((and new-state (neq? new-state start) (neq? new-state skip))
                     (set! state new-state)
                     (when (and ignore-case (is-constituent constituent-test i))
                       (set! new-state (follow state iinv))
                       (when (not new-state)
                         (set! new-state (new State string (new VectoredTransitions)))
                         (set-nb-vectored automata (+ (get-nb-vectored automata) 1))
                         (add-constituency-transitions new-state skip start start constituent-test)
                         (add-transition state iinv new-state))))
                    (else
                     (set! new-state (new State string (new VectoredTransitions)))
                     (set-nb-vectored automata (+ (get-nb-vectored automata) 1))
                     (add-constituency-transitions new-state skip start start constituent-test)
                     (add-transition state i new-state)
                     (when (and ignore-case (is-constituent constituent-test i))
                       (add-transition state iinv new-state))
                     (set! state new-state)))))))
      (let ((new-state (new FoundState target start)))
        (set-nb-vectored automata (+ (get-nb-vectored automata) 1))
        (loop (for i from 0 below NbAscii)
              (when (and (not (is-constituent constituent-test i))
                         (not (eqv? i no-transition)))
                (add-transition state i new-state)))
        (add-transition state EOFTransition new-state)))))


(definition (is-constituent constituent-test <procedure> i <fx>) <bool>
  (constituent-test (integer->char i)))


;;;
;;;; Search
;;;


(definition public (automata-search automata <Automata> tape <Tape>)
  (run automata tape)
  (get-found tape)))
