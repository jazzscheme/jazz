;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Calendar
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.calendar.Calendar-View jazz


(import (jazz.action)
        (jazz.calendar)
        (jazz.time)
        (jazz.view))


(class Calendar-View extends View


  (property date                initialize #f accessors explicit)
  (property spacing             initialize 1  accessors generate)
  (property multiple-selection? initialize #f accessors generate)
  
  
  (slot selection initialize #f)
  
  
  (method override (finish self rest)
    (nextmethod self rest)
    (set-date self date)
    (install-days self)
    (set-selection self (find-calendar-day self date)))
  
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-date self)
    date)


  (method public (set-date self value)
    (set! date (or value (current-date)))
    (when (get-finished? self)
      (date-update self)))
  
  
  (method protected virtual (date-update self)
    (update-days self)
    (invalidate-view self))


  (method public virtual (get-manager self)
    (find-ascendant self Calendar-Dialog))


  ;;;
  ;;;; Actions
  ;;;
  
  
  (method override (guest-actions self)
    (cons (find-actions 'calendar)
          (nextmethod self)))


  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-prior self evt)
    (prior-press self))
  
  
  (method (on-next self evt)
    (next-press self))
  
  
  ;;;
  ;;;; Focus
  ;;;
  
  
  (method override (focus-gained self)
    (when selection
      (invalidate-view selection)))
  
  
  (method override (focus-lost self)
    (when selection
      (invalidate-view selection)))


  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (left-arrow-press self modifiers)
    (move-selection self -1 0))
  
  
  (method override (up-arrow-press self modifiers)
    (move-selection self 0 -1))
  
  
  (method override (right-arrow-press self modifiers)
    (move-selection self 1 0))
  
  
  (method override (down-arrow-press self modifiers)
    (move-selection self 0 1))
  
  
  (method public virtual (prior-press self)
    (goto-previous-month self))
  
  
  (method public virtual (next-press self)
    (goto-next-month self))
  
  
  (method package (move-selection self dx dy)
    (if (null? selection)
        (bell)
      (let ((nx (max 0 (min 6 (+ (get-day selection) dx))))
            (ny (max 0 (min 4 (+ (get-week selection) dy)))))
        (set-selection self (find-calendar-cell self nx ny)))))
  
  
  ;;;
  ;;;; Install
  ;;;
  
  
  (method (install-days self)
    (define (install-day week day date)
      (new (calendar-day-class self) parent: self week: week day: day date: date))
    
    (let ((date (calendar-base self)))
      (for-each (lambda (week)
                  (for-each (lambda (day)
                              (install-day week day date)
                              (set! date (add-one-day self date)))
                            (naturals 0 7)))
                (naturals 0 6))))
  
  
  (method protected virtual (calendar-day-class self)
    Calendar-Day)
  
  
  (method (calendar-base self)
    (let (subtract (date (date-add (make-date 0 0 0 0 1 (date-month date) (date-year date) (date-zone-offset (current-date))) -1 'day)))
      (if (zero? (date-week-day date))
          date
        (subtract (subtract-one-day self date)))))
  
  
  (method (add-one-day self date)
    (date-add date 1 'day))
  
  
  ;; This method fix a problem with date-add when original date is 31 and the Next or Previous Month has 30 or less days.
  (method (select-correct-month self date month)
    (let ((new-month (if (< month 1) 12 (if (> month 12) 1 month)))
          (correct-date date))
      (while (> (date-month correct-date) new-month)
        (set! correct-date (date-add correct-date -1 'day)))
      correct-date))
  
  
  (method (add-one-month self date)
    (let ((next-month-date (date-add date 1 'month)))
      (select-correct-month self next-month-date (+ (date-month date) 1))))
  
  
  (method (subtract-one-day self date)
    (date-add date -1 'day))
  
  
  (method (subtract-one-month self date)
    (let ((last-month-date (date-add date -1 'month)))
      (select-correct-month self last-month-date (- (date-month date) 1))))
  
  
  (method (normalize-date self date)
    (make-date 0 0 0 0 (date-day date) (date-month date) (date-year date) (date-zone-offset date)))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update-days self)
    (let ((date (calendar-base self))
          (scan children))
      (for-each (lambda (week)
                  (for-each (lambda (day)
                              (let ((calendar-day (car scan)))
                                (set-date calendar-day date)
                                (set! date (add-one-day self date))
                                (set! scan (cdr scan))))
                            (naturals 0 7)))
                (naturals 0 6))))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout self)
    (let ((width (quotient (get-width self) 7))
          (height (quotient (get-height self) 6)))
      (for-each (lambda (view)
                  (when (is? view Calendar-Day)
                    (let ((row (get-week view))
                          (col (get-day view)))
                      (set-position view (new Point (+ 1 (* col (+ width spacing))) (* row (+ height spacing))))
                      (set-size view (new Dimension width height)))))
                children)))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method public virtual (day-color self day)
    #f)
  
  
  ;;;
  ;;;; Days
  ;;;
  
  
  (method public (find-calendar-day self date)
    (find-if (lambda (view)
               (and (is? view Calendar-Day)
                    (nu=? date (get-date view))))
             children))
  
  
  (method public (find-calendar-cell self x y)
    (find-if (lambda (view)
               (and (is? view Calendar-Day)
                    (= x (get-day view))
                    (= y (get-week view))))
             children))
  
  
  ;;;
  ;;;; Selection
  ;;;


  (method public (get-selection self)
    selection)


  (method public (set-selection self day)
    (when (neq? selection day)
      (when selection
        (set-selected? selection #f))
      (set! selection day)
      (when selection
        (set-selected? selection #t))
      (selection-update self)))
  
  
  (method package virtual (selection-update self)
    )
  
  
  ;;;
  ;;;; Navigation
  ;;;
  
  (method public (goto-previous-month self)
    (goto-month self date (lambda (date)
                            (subtract-one-month self date))))
  
  
  (method public (goto-next-month self)
    (goto-month self date (lambda (date)
                            (add-one-month self date))))
  
  
  (method (goto-month self new-date proc)
    (let ((new-date (proc new-date)))
      (set-date self new-date)
      (set-selection self (find-calendar-day self new-date))))))
