;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Catalog
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.catalog jazz


(export (jazz.catalog.autoload))

(import (jazz.catalog.autoload)
        (jazz.io)
        (jazz.version))


(definition public catalog-version
  {Version 2 7 5})


;;;
;;;; Cataloguer
;;;


(definition *cataloguer*
  #f)


(definition public (get-cataloguer)
  (when (not *cataloguer*)
    (call-setup-catalogs))
  *cataloguer*)

(definition public (find-cataloguer)
  *cataloguer*)

(definition public (require-cataloguer)
  (get-cataloguer))

(definition public (set-cataloguer cataloguer)
  (set! *cataloguer* cataloguer))


(definition protected setup-catalogs-hook
  #f)

(definition protected (set-setup-catalogs-hook proc)
  (set! setup-catalogs-hook proc))


(definition protected (call-setup-catalogs)
  (when setup-catalogs-hook
    (setup-catalogs-hook)))


;;;
;;;; Parse
;;;


(definition Parser-Extensions
  '())

(definition Parser-Classes
  '())


(definition public (register-parser name extensions class)
  (set! Parser-Extensions (cons (cons name extensions) Parser-Extensions))
  (set! Parser-Classes (cons (cons name class) Parser-Classes)))


(definition public (get-parser-dialect extension)
  (activate-couplers 'catalog)
  (let ((info
          (rassociate extension Parser-Extensions
            test: (lambda (ext extensions)
                    (member? ext extensions test: extension=?)))))
    (and info
         (car info))))


(definition public (get-parser-class dialect)
  (activate-couplers 'catalog)
  (let ((pair (assq dialect Parser-Classes)))
    (and pair
         (cdr pair))))


(definition public (extension-match-test ext)
  (let ((parser-class (get-parser-class (get-parser-dialect ext))))
    (and parser-class
         (get-match-test parser-class))))


;;;
;;;; Thread
;;;


(definition catalog-thread
  #f)


(definition (get-catalog-thread)
  (or catalog-thread (let ((thread (start-loop-thread 'catalog)))
                       (set! catalog-thread thread)
                       thread)))


(definition public (post-catalog thunk)
  (thread-post (get-catalog-thread) 'post-catalog
    thunk))


;;;
;;;; Container
;;;


;; containers are either
;; - a module name: a.b.c
;; - a file name: foo.scm


(definition container-entries
  (make-table test: equal?))


(definition protected (clear-container-entry container)
  (table-clear container-entries container))


(definition public (cache-container-entry container)
  (define (find-entry)
    (let ((cataloguer (get-cataloguer)))
      (let ((toplevel (get-toplevel-definition cataloguer container)))
        (if toplevel
            (get-parent toplevel)
          (let ((base (filename-base container)))
            (let ((entries (collect-if (lambda (file-entry)
                                         (filename=? (get-name (get-file file-entry)) container))
                                       (get-filename-entries cataloguer base))))
              (and (= (length entries) 1)
                   (car entries))))))))
  
  (or (table-ref container-entries container #f)
      (let ((entry (find-entry)))
        (table-set! container-entries container entry)
        entry)))


;;;
;;;; File
;;;


(definition file-entries
  (make-table test: pathname=? hash: pathname-hash))


(definition protected (clear-file-entry file)
  (table-clear file-entries file))


(definition public (cache-file-entry file)
  (or (table-ref file-entries file #f)
      (let ((entry (get-file-entry (get-cataloguer) file)))
        (table-set! file-entries file entry)
        entry))))
