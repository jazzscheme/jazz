;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Filing Catalogs
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.catalog.catalog.Filing-Catalog jazz


(import (jazz.catalog)
        (jazz.io)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (time))


(class undocumented Filing-Catalog extends Catalog
  

  (slot file              getter generate)
  (slot active?)
  (slot loaded?)
  (slot modified?)
  (slot directories       getter generate)
  (slot extensions)
  (slot char-encoding)
  (slot heuristics)
  (slot references?       getter generate)
  (slot content-index)
  (slot definitions-index)
  (slot references-index)
  (slot mutex)


  (method override (initialize name file directories extensions char-encoding heuristics references?)
    (nextmethod name)
    (set! file~self file)
    (set! active?~self #f)
    (set! loaded?~self #f)
    (set! modified?~self #f)
    (set! directories~self directories)
    (set! extensions~self extensions)
    (set! char-encoding~self char-encoding)
    (set! heuristics~self heuristics)
    (set! references?~self references?)
    (set! content-index~self (make-table test: equal?))
    (set! definitions-index~self (make-table test: equal?))
    (set! references-index~self (make-table test: equal?))
    (set! mutex~self (make-mutex 'catalog)))


  ;;;
  ;;;; Access
  ;;;
    
  
  (method override (get-active?)
    active?)
  
  
  (method override (set-active? flag)
    (set! active? flag))
  
  
  (method (set-modified? flag)
    (set! modified? flag))
  
  
  (method public virtual (get-extensions)
    extensions)
  
  
  (method public virtual (get-char-encoding)
    char-encoding)
  
  
  (method public virtual (get-heuristics)
    heuristics)
  
  
  (method public (iterate-content proc)
    (iterate-table content-index proc))
  
  
  (method public (iterate-definitions proc)
    (iterate-table definitions-index proc))
  
  
  (method public (iterate-references proc)
    (iterate-table references-index proc))
  
  
  (method public (get-file-definitions name)
    (with-mutex
      (lambda ()
        (%%get-file-definitions name))))
  
  
  (method (%%get-file-definitions name)
    (table-ref content-index name '()))
  
  
  (method override (get-file-entry file)
    (with-mutex
      (lambda ()
        (%%get-file-entry file))))
  
  
  (method (%%get-file-entry file)
    (find-if (lambda (definition)
               (pathname=? (get-file~ definition) file))
             (%%get-file-definitions (get-base~ file))))
  
  
  (method protected virtual (setup-contexts)
    )
  
  
  (method public (get-statistics)
    (define (count-file-entries content-index)
      (let ((count 0))
        (iterate-table content-index
          (lambda (name file-entries)
            (increase! count (length file-entries))))
        count))
    
    (with-mutex
      (lambda ()
        (let ((f (count-file-entries content-index))
              (d (table-length definitions-index))
              (r (table-length references-index)))
          (format "{a} file{a}, {a} definition{a}, {a} reference{a}"
                  f (format-plural f)
                  d (format-plural d)
                  r (format-plural r))))))


  ;;;
  ;;;; Thread
  ;;;
  
  
  (method (with-mutex thunk)
    (mutex-lock! mutex)
    (prog1 (thunk)
      (mutex-unlock! mutex)))


  ;;;
  ;;;; Activation
  ;;;
      
  
  (method override (activate (report?: report? #t))
    (define (load-content report?)
      (when (and (not loaded?) (exists?~ file))
        (when report?
          (user-feedback "Loading Catalog..."))
        (with-jazz-readtable
          (lambda ()
            (call-with-input-file (path-settings file eol-encoding: 'cr-lf)
              (lambda (port)
                (table-load content-index port #t)))))
        (update-catalog-indexes report?)
        (set! loaded? #t)
        (when report?
          (user-feedback "Done"))))
    
    (define (update-catalog-indexes report?)
      (when report?
        (user-feedback "Updating Indexes..."))
      (iterate-table content-index
        (lambda (name definitions)
          (for-each (lambda (file-definition)
                      (set-catalog~ file-definition self)
                      (update-indexes~ file-definition definitions-index references-index))
                    definitions))))
    
    (unless (get-active?)
      (with-mutex
        (lambda ()
          (load-content report?)
          (set-active? #t)))))
      
  
  (method override (deactivate)
    (set-active? #f))


  ;;;
  ;;;; Save
  ;;;
  
  
  (method (save)
    (define (save-content)
      (let* ((all (table-keys/values content-index))
             (sorted (sort string<? all key: car)))
        (create-directories~ file)
        (call-with-output-file (path-settings file)
          (lambda (output)
            (for-each (lambda (info)
                        (bind (name . definitions) info
                          (when (not-null? definitions)
                            (format output "{s}{%}" name)
                            (format output " (")
                            (let ((first? #t))
                              (for-each (lambda (definition)
                                          (print-catalog~ definition first? output)
                                          (set! first? #f))
                                        definitions))
                            (format output "){%}"))))
                      sorted)))))
    
    (with-mutex
      (lambda ()
        (when modified?
          (user-feedback "Saving {a} Catalog..." name)
          (save-content)
          (set-modified? #f)
          (user-feedback "Done")))))


  ;;;
  ;;;; Update
  ;;;
  
  
  (method public (update (report?: report? #t))
    (activate report?: report?)
    (when report?
      (user-feedback "Updating {a} Catalog..." name))
    
    (let ((exceptions/entries (scan)))
      (receive (exceptions entries) (partition-binary exceptions/entries exception?)
        (when (not-null? entries)
          (with-mutex
            (lambda ()
              (set-modified? #t)
              (for-each update-entry entries)
              (sweep))))
        (when report?
          (let ((len (length exceptions/entries)))
            (user-feedback "{a} Catalog Updated ({s} file{a} processed)"
                           name
                           len
                           (format-plural len))))
        (when (not-null? exceptions)
          (post-event
            (lambda ()
              @wait
              (for-each debug (map exception-reason exceptions))
              @wait
              (let ((exc (car exceptions)))
                (message-box (exception-reason exc))
                (edit-exception exc))))))))
  
  
  (method public (update-file file)
    (activate report?: #t)
    (let ((exception/entry (scan-file file #f)))
      (when exception/entry
        (if (exception? exception/entry)
            (let ((line (if (is? exception/entry Explore-Error) (format " at line {a}" (+ (get-line~ (get-start~ exception/entry)) 1)) "")))
              (user-message/action-handler (format "Error{a}: {a}" line (exception-reason exception/entry))
                                           (new Event-Handler execute: (lambda (evt) (edit-exception exception/entry))))
              (bell))
          (with-mutex
            (lambda ()
              (set-modified? #t)
              (update-entry exception/entry)
              (sweep)))))))
  
  
  (method (update-entry entry)
    (let ((file (get-file~ entry)))
      (let ((actual (%%get-file-entry file)))
        (when actual
          (set-marked?~ actual #f)))
      (let ((name (get-base~ file)))
        (set-marked?~ entry #t)
        (update-indexes~ entry definitions-index references-index)
        (table-add content-index name entry))))
  
  
  (definition (edit-exception exc)
    (if (is? exc Explore-Error)
        (let ((appl (get-application))
              (source (get-source~ exc))
              (range (get-range~ exc)))
          (edit-document~ appl source selection: range))
      (bell)))


  ;;;
  ;;;; Scan
  ;;;
  
  
  ;; This section is entirely functional and so should not lock the catalog mutex
  
  
  (method protected (scan)
    (reset)
    (let ((queue (new-queue)))
      (scan-directories queue)
      (queue-list queue)))
  
  
  (method protected virtual (scan-directories queue)
    (define (scandir dir)
      (scan-directory dir queue))
    
    (if (is? directories Directory-Group)
        (for-each scandir (get-list~ directories))
      (scandir directories)))
  
  
  (method protected (scan-directory directory queue)
    (iterate-directory~ directory
      (lambda (file)
        (let ((entry (scan-file file #t)))
          (when entry
            (enqueue queue entry))))
      files?: #t
      directories?: #f
      recursive?: #t))

  
  (method protected (scan-file file report?)
    (define (scan? file)
      (let ((extension (get-extension~ file))
            (extensions (get-extensions)))
        (and (or (not extensions) (member? extension extensions test: ci=?))
             (get-parser-class extension))))
    
    (define (scan file time report?)
      (when report?
        (user-feedback "Parsing {a}" file))
      (let ((extension (get-extension~ file)))
        (parse~ (get-parser extension) file time)))
    
    (define (unmodified-file-entry file time)
      (let ((actual (get-file-entry file)))
        (when (and actual (= (time->seconds (get-time~ actual)) (time->seconds time)))
          actual)))
    
    (when (scan? file)
      (if (exists?~ file)
          (let ((time (get-modification-time~ file)))
            (let ((file-entry (unmodified-file-entry file time)))
              (if file-entry
                  (begin
                    (set-marked?~ file-entry #t)
                    #f)
                (with-exception-filter
                  (lambda (exc)
                    #t)
                  (lambda (exc)
                    (wrap-exception exc))
                  (lambda ()
                    (scan file time report?))))))
        (format :console "{%}Unable to catalog inexistant file: {a}" (parse~ file))
        #f)))
  

  ;;;
  ;;;; Parse
  ;;;
  
  
  (definition Parser-Associations
    (list
      (list (lambda () jazz.editor.commonlisp.CommonLisp-Parser) "lisp" "el") ;; quick approximation until a dedicated emacs lisp parser
      (list (lambda () jazz.editor.c.C-Parser) "c" "cpp" "h")
@wait (list (lambda () jazz.editor.css.CSS-Parser) "css")
      (list (lambda () jazz.editor.jazz.Jazz-Parser) "jazz")
@wait (list (lambda () jazz.editor.java.Java-Parser) "java")
@wait (list (lambda () jazz.editor.javascript.JavaScript-Parser) "js")
@wait (list (lambda () jazz.editor.lua.Lua-Parser) "lua")
@wait (list (lambda () jazz.editor.properties.Properties-Parser) "properties")
@wait (list (lambda () jazz.editor.python.Python-Parser) "py")
      (list (lambda () jazz.editor.scheme.Scheme-Parser) "scm" "sch" "ss" "gambcini" "jazzini")))
  
  
  (definition (get-parser-class extension)
    (let ((info
           (rassociate extension Parser-Associations
             test: (lambda (ext extensions)
                     (member? ext extensions test: ci=?)))))
      (when info
        (let ((proc (car info)))
          (proc)))))
  
  
  (method (get-parser extension)
    (let ((class (get-parser-class extension)))
      (when class
        (new class self char-encoding: char-encoding heuristics: (get-heuristics) references?: references?))))


  ;;;
  ;;;; Sweep
  ;;;
  
  
  (method (reset)
    (iterate-table content-index
      (lambda (name definitions)
        (for-each (lambda (file-definition)
                    (set-marked?~ file-definition #f))
                  definitions))))
  
  
  (method (sweep)
    (define (sweep-table table marked? reset?)
      (iterate-table table
        (lambda (name definitions)
          (when (not-null? definitions)
            (let ((previous definitions)
                  (list (cdr definitions)))
              (while (not-null? list)
                (let ((definition (car list)))
                  (if (marked? definition)
                      (begin
                        (set! previous list)
                        (set! list (cdr list)))
                    (when reset?
                      (set-marked?~ definition #f))
                    (set-cdr! previous (cddr previous))
                    (set! list (cdr previous))))))
            (when (not (marked? (car definitions)))
              (set-modified? #t)
              (table-set! table name (cdr definitions)))))))
    
    (define (marked? definition)
      (get-marked?~ (get-file-definition~ definition)))
    
    (sweep-table content-index marked? #t)
    (sweep-table definitions-index marked? #f)
    (sweep-table references-index marked? #f))


  ;;;
  ;;;; Definitions
  ;;;
  
  
  (method override (get-definitions name)
    (with-mutex
      (lambda ()
        (%%get-definitions name))))
  
  
  (method (%%get-definitions name)
    (table-ref definitions-index (canonicalize-name name) '()))
  
  
  (method (search-definitions search-set)
    (with-mutex
      (lambda ()
        (let ((fact (new List-Factory)))
          (iterate-table definitions-index
            (lambda (name definitions)
              (when (contextual-search (->string name) search-set)
                (put-sequence~ fact definitions))))
          (get-output~ fact)))))
  
  
  (method (search-references search-set)
    (with-mutex
      (lambda ()
        (let ((fact (new List-Factory)))
          (iterate-table references-index
            (lambda (name references)
              (when (contextual-search (->string name) search-set)
                (put-sequence~ fact references))))
          (remove-duplicates (get-output~ fact))))))
  
  
  (method override (get-references name)
    (with-mutex
      (lambda ()
        (%%get-references name))))
  
  
  (method (%%get-references name)
    (table-ref references-index (canonicalize-name name) '()))
  
  
  (definition (canonicalize-name name)
    (->string name))


  ;;;
  ;;;; Completions
  ;;;


  (method override (add-completions dest target)
    (let ((len (cardinality target)))
      (define (add-table table)
        (iterate-table table
          (lambda (name value)
            (when (compare-n name target len)
              (table-set! dest name #t)))))
      
      (with-mutex
        (lambda ()
          (add-table definitions-index)
          (add-table references-index)))))))
