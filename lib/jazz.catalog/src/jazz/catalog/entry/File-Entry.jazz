;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; File Entry
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.catalog.entry.File-Entry jazz


(import (jazz.catalog)
        (jazz.io)
        (jazz.resource))


(class undocumented File-Entry extends Catalog-Entry
  
  
  (slot catalog       accessors generate)
  (slot marked?       accessors generate)
  (slot file)
  (slot digest        accessors generate)
  (slot dialect       getter generate)
  (slot definitions)
  (slot location-name)
  (slot lines)
  
  
  (method override (initialize self catalog file digest dialect definitions)
    (nextmethod self)
    (set! self.catalog catalog)
    (set! self.file file)
    (set! self.digest digest)
    (set! self.dialect dialect)
    (set! self.definitions definitions)
    (set! self.location-name (get-base file))
    (set! self.lines #f)
    (set! self.marked? #t)
    (for-each (lambda (definition)
                (set-parent definition self)
                (set-file-definition definition self))
              definitions)
    (when (context-setup? file)
      (clear-file-entry file)))


  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" file))))
  
  
  (method public virtual (print-catalog self first? output)
    (when (not first?)
      (format output "{%}{_ :v}" 2))
    (format output "~{{s} {s} {s} {s}"
            (category-name (class-of self))
            file
            digest
            dialect)
    (for-each (lambda (definition)
                (print-catalog definition 2 output))
              definitions)
    (format output "}"))
  
  
  (method package (context-setup self)
    (clear-file-entry file))


  ;;;
  ;;;; Access
  ;;;


  (method override (get-file self)
    file)


  (method override (get-moniker self)
    file)


  (method override (get-definitions self)
    definitions)


  (method override (get-location-name self)
    location-name)


  (method override (get-root-definition self)
    (and (not-null? definitions)
         (car definitions)))


  (method override (get-file-definition self)
    self)
  
  
  (method public (get-definition self name)
    (find-if (lambda (definition)
               (= (get-name definition) name))
             definitions))
  
  
  (method (cache-lines self) <vector>
    (or lines
        (let ((queue (new-queue)))
          (for-each-descendant-entry self
            (lambda (entry)
              (enqueue queue entry)))
          (let ((vec (list->vector (queue-list queue))))
            (set! lines vec)
            vec))))
  
  
  (method override (for-each-descendant-entry self proc)
    (let (iter (entry self))
      (loop (for child in (get-definitions entry))
            (proc child)
            (iter child))))
  
  
  ;; binary search
  (method package (line->entry self line)
    (let ((lines (cache-lines self)))
      (let ((len (length lines)))
        (and (> len 0)
             (let (iter (head <fx> 0)
                        (pos <fx> (fxround/ len 2))
                        (tail <fx> (- len 1)))
               (let ((entry (vector-ref lines pos)))
                 (let ((entry-line (get-line entry)))
                   ;; quicky to better understand later
                   (and entry-line
                        (cond ((and (>= line entry-line)
                                    (let ((next-pos (+ pos 1)))
                                      (or (> next-pos tail)
                                          (let ((next-entry (vector-ref lines next-pos)))
                                            (< line (get-line next-entry))))))
                               entry)
                              ((< line entry-line)
                               (let ((new-pos (fxfloor/ (+ head pos) 2)))
                                 (and (/= new-pos pos)
                                      (iter 0 new-pos pos))))
                              (else
                               (let ((new-pos (fxceiling/ (+ pos tail) 2)))
                                 (and new-pos
                                      (iter pos new-pos tail)))))))))))))


  ;;;
  ;;;; Present
  ;;;
  
  
  (method override (get-image self)
    {Image-Resource "File"})


  (method override (present-detailed self)
    (get-name file))


  ;;;
  ;;;; Indexes
  ;;;
  
  
  (method override (update-indexes self definitions-index references-index)
    (for-each (lambda (definition)
                (update-indexes definition definitions-index references-index))
              definitions))))
