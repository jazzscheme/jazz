;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Indexed Entry
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.catalog.entry.Indexed-Entry jazz


(import (jazz.catalog))


(class undocumented Indexed-Entry extends Catalog-Entry
  
  
  (slot name)
  (slot line)
  (slot column)
  (slot end-line)
  (slot end-column)
  (slot definitions)
  (slot references getter generate)
  
  
  (method override (initialize self name line column end-line end-column definitions references)
    (nextmethod self)
    (set! self.name name)
    (set! self.line line)
    (set! self.column column)
    (set! self.end-line end-line)
    (set! self.end-column end-column)
    (set! self.definitions definitions)
    (set! self.references references)
    (for-each (lambda (definition)
                (set-parent definition self))
              definitions))


  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name))))
  
  
  (method public virtual (print-catalog self level output)
    (format output "{%}{_ :v}~{{s} {s} {s} {s}"
            (* level 2)
            (category-name (class-of self))
            name line column)
    (print-definitions self level output)
    (format output "}"))
  
  
  (method package (print-definitions self level output)
    (for-each (lambda (definition)
                (print-catalog definition (+ level 1) output))
              definitions))


  ;;;
  ;;;; Access
  ;;;


  (method override (get-file self)
    (get-file file-definition))


  (method override (get-moniker self)
    (get-moniker (or root-definition file-definition)))


  (method override (get-title self)
    name)


  (method override (get-location-name self)
    (get-location-name (or root-definition file-definition)))


  (method override (get-name self)
    name)


  (method override (get-line self)
    line)


  (method override (get-column self)
    column)
  
  
  (method override (get-start self)
    (new Cell line column))
  
  
  (method override (get-end-line self)
    end-line)
  
  
  (method override (get-end-column self)
    end-column)
  
  
  (method override (get-end self)
    (new Cell end-line end-column))
  
  
  (method override (get-range self)
    (new Range$Cell$ (get-start self) (get-end self)))
  
  
  (method override (get-definitions self)
    definitions)
  
  
  (method public (find-definition self name)
    (find-if (lambda (def)
               (equal? (get-name def) name))
             definitions))
  
  
  (method public (child self path)
    (if (null? path)
        self
      (let ((def (find-definition self (symbol->string (car path)))))
        (if def
            (child def (cdr path))
          #f))))
  
  
  (method public (add-definition self definition)
    (set! definitions (append! definitions (list definition))))


  (method public (add-reference self reference)
    (unless (member? reference references)
      (set! references (cons reference references))))


  (method public (get-catalog self)
    (get-catalog file-definition))


  (method override (set-root-definition self def)
    (nextmethod self def)
    (for-each (lambda (definition)
                (set-root-definition definition def))
              definitions))


  (method override (set-file-definition self def)
    (nextmethod self def)
    (for-each (lambda (definition)
                (set-file-definition definition def))
              definitions))
  
  
  (method override (for-each-descendant-entry self proc (include-self?: include-self? #t))
    (when include-self?
      (proc self))
    (let (iter (entry self))
      (loop (for child in (get-definitions entry))
            (proc child)
            (iter child))))
  
  
  ;;;
  ;;;; Indexes
  ;;;
  
  
  (method override (update-indexes self definitions-index references-index)
    (update-index self definitions-index name)
    (for-each (lambda (definition)
                (update-indexes definition definitions-index references-index))
              definitions))


  (method protected (update-index self index key)
    (when key
      (table-add index key self)))


  ;;;
  ;;;; Present
  ;;;


  (method override (present self)
    (present-name self)
    @old
    (case mode
      ((definitions) (get-location-name))
      ((references) (format "{a}.{a}" (get-location-name) (present-name)))))


  (method override (present-name self)
    (or (get-name self) (present-noname self)))
  
  
  (method protected virtual (present-noname self)
    "<static>")))
