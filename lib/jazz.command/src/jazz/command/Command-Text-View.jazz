;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Command Text-View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.command.Command-Text-View jazz


(import (jazz.action)
        (jazz.application)
        (jazz.feedback)
        (jazz.graphic)
        (jazz.ide)
        (jazz.io)
        (jazz.jml)
        (jazz.preferences)
        (jazz.project)
        (jazz.search)
        (jazz.ui)
        (jazz.ui.view))


(class Command-Text-View extends IDE-Text-View
  
  
  (slot mode             initialize #f accessors generate)
  (slot client           initialize #f getter generate)
  (slot client-selection initialize #f getter generate)
  (slot client-position  initialize #f getter generate)
  (slot client-restore   initialize #f accessors generate)
  (slot previous-mode    initialize #f accessors generate)
  
  
  (form
    (<install>
      focus-selects-all?: #t
      show-unfocused-selection?: #f
      accepts-returns?: #f
      left-padding: 2
      top-padding: 2
      tooltip?: #t
      tooltip-text: "Command Shell"))
  
  
  (method override (preferences)
    (get-preferences '(documents command-text)))
  
  
  (method override (install rest)
    (nextmethod rest)
    (set-propagate-styles?~ model #f)
    (let ((pref (preferences)))
      (add-style 'Separator (get-separator-style~ pref))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set! mode (default-mode)))
  
  
  (method override (component-alias name)
    (case name
      ((:mode) mode)
      (else (nextmethod name))))
  
  
  (method (status-label)
    (child~ creator 'status))
  
  
  (method (set-status title)
    (set-title~ (status-label) title))
  
  
  (method (clear-status)
    (set-status ""))
  
  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method (setup-client)
    (set! client (get-focus))
    (set! client-selection #f))
  
  
  (method (setup-search-client explicit-client)
    (define (determine-client)
      (let ((focus (get-focus)))
        (when (and (is? focus Text-View) (not (in-cell-edit? focus)))
          focus)))
    
    (set! client (or explicit-client (determine-client)))
    (set! client-selection (and client (get-selection~ client)))
    (set! client-position (and client (get-scrolled-position~ client))))
  
  
  (method (cleanup-client)
    (set! client #f)
    (set! client-selection #f))
  
  
  (method (restore-client)
    (when client-restore
      (client-restore)
      (set! client-restore #f)))
  
  
  (method (preserve-mode)
    (set! previous-mode mode))
  
  
  ;;;
  ;;;; Focus
  ;;;
  
  
  (method override (focus-lose)
    (nextmethod)
    (cleanup~ mode #t)
    (cleanup-client)
    (set-mode-default)
    (clear-user-message)
    (clear-status)
    (focus-empty))
  
  
  (method override (focus-actions)
    (cons (mode-actions~ mode)
          (nextmethod)))
  
  
  (method protected virtual (focus-empty)
    (empty-text~ model))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (key-press evt)
    (nextmethod evt)
    (key-press~ mode (get-key~ evt)))
  
  
  (method override (backspace-press evt (word?: word? #f))
    (nextmethod evt word?: word?)
    (backspace-press~ mode evt word?: word?))
  
  
  (method override (tab-press evt)
    (navigate-field (get-shift?~ evt)))
  
  
  (method override (next-page evt)
    (next-page~ mode))
  
  
  (method override (prior-page evt)
    (prior-page~ mode))
  
  
  (method override (get-constituent-test)
    (or (get-constituent-test~ mode)
        (nextmethod)))
  
  
  (method override (auto-complete?)
    (auto-complete?~ mode))
  
  
  (method override (auto-complete-char? key last)
    (let ((value (auto-complete-char?~ mode key last)))
      (if (eq? value 'default)
          (nextmethod key last)
        value)))


  (method override (favorite-completions?)
    (let ((value (favorite-completions?~ mode)))
      (if (eq? value 'default)
          (nextmethod)
        value)))
  
  
  (method override (complete-name? size)
    (let ((value (complete-name?~ mode size)))
      (if (eq? value 'default)
          (nextmethod size)
        value)))


  (method override (completion-name pos)
    (or (completion-name~ mode pos)
        (nextmethod pos)))
    
  
  (method override (symbol-completions name)
    (or (symbol-completions~ mode name)
        (nextmethod name)))

  
  ;;;
  ;;;; Fields
  ;;;
  
  
  (method (navigate-field backward?)
    (let ((fields (collect-fields)))
      (let ((rank (caret-rank fields)))
        (let ((next (modulo ((if backward? - +) rank 1) (length fields))))
          (goto-field (element fields next))))))
  
  
  (method (first-field)
    (first (collect-fields)))
  
  
  (method (second-field)
    (let ((fields (collect-fields)))
      (when (>= (length fields) 2)
        (second fields))))
  
  
  (method (last-field)
    (last (collect-fields)))
  
  
  (method (first-content)
    (field-content (first-field)))
  
  
  (method (second-content)
    (let ((field (second-field)))
      (when field
        (field-content field))))
  
  
  (method (last-content)
    (field-content (last-field)))
  
  
  (method (field-content field)
    (substring (get-string~ (first-paragraph))
               (get-start~ field)
               (get-end~ field)))
  
  
  (method (field-range field)
    (new Range$Cell$
      (new Cell 0 (get-start~ field))
      (new Cell 0 (get-end~ field))))
  
  
  (method (goto-field field)
    (set-selection (field-range field)))
  
  
  (method (goto-first-field)
    (goto-field (first-field)))
  
  
  (method (goto-second-field)
    (let ((field (second-field)))
      (if field
          (goto-field field)
        (set-caret (text-ending))
        (insert-separator))))
  
  
  (method (collect-fields)
    (let ((paragraph (first-paragraph))
          (fields (new-queue)))
      (define (forward-style pos predicate)
        (let ((card (get-length~ paragraph)))
          (let (iterate (pos pos))
            (if (>= pos card)
                pos
              (let ((next (+ pos 1)))
                (let ((run (get-run~ paragraph next)))
                  (if (or (not run) (predicate (get-style~ run)))
                      (iterate next)
                    pos)))))))
      
      (define (text-style? style)
        (not (separator-style? style)))
      
      (define (separator-style? style)
        (inherits?~ style 'Separator))
      
      (define (new-field start end)
        (new Range start end))
      
      (let (iter (pos 0))
        (let ((text-end (forward-style pos text-style?)))
          (enqueue fields (new-field pos text-end))
          (let ((separator-end (forward-style text-end separator-style?)))
            (when (/= separator-end text-end)
              (iter separator-end)))))
      (queue-list fields)))
  
  
  (method (field-rank fields pos)
    (let (iter (fields fields) (rank 0))
      (let ((field (car fields)))
        (if (between? pos (get-start~ field) (get-end~ field))
            rank
          (iter (cdr fields) (+ rank 1))))))
  
  
  (method (caret-rank fields)
    (let ((pos (get-col~ (get-caret))))
      (field-rank fields pos)))
  
  
  (method (insert-separator)
    (insert-styled "/" 'Separator))

  
  ;;;
  ;;;; Mode
  ;;;
  
  
  (method public (select-search direction range initial (search-result: search-result #f) (client: client #f) (highlight?: highlight? #f) (again?: again? #f))
    (set-mode-search (if highlight? Highlight-Mode Search-Mode) direction)
    (setup-search-client client)
    (push-focus self)
    (prepare-search~ mode range)
    (let ((search-result (or search-result (make-text-search-result initial (get-client) #f #f))))
      (set-search-result~ mode search-result)
      (set-text~ model (get-target~ (search-context~ model (get-find~ (get-search~ search-result))))))
    (select-all)
    (search-client~ mode direction initial?: #t again?: again?))
  
  
  (method (set-mode-search class direction)
    (set! mode (new class self direction))
    (user-message "Search..."))
  
  
  (method public (select-extended)
    (set-mode-extended)
    (setup-client)
    (push-focus self))
  
  
  (method (set-mode-extended)
    (set! mode (new Extended-Mode self))
    (user-message "Extended..."))
  
  
  (method public (select-execute)
    (set-mode-execute)
    (setup-client)
    (push-focus self))
  
  
  (method (set-mode-execute)
    (set! mode (new Execute-Mode self))
    (user-message "Execute..."))
  
  
  (method public (select-mode md)
    (set! mode md)
    (setup-client)
    (push-focus self))
  
  
  (method (set-mode-default)
    (set! mode (default-mode)))
  
  
  (method protected virtual (default-mode)
    (new Execute-Mode self)))


;;;
;;;; Command-Mode
;;;


(class Command-Mode extends Object
  
  
  (slot text)
  
  
  (method override (initialize text)
    (nextmethod)
    (set! text~self text))
  
  
  (method (get-client)
    (get-client~ text))
  
  
  (method (get-client-selection)
    (get-client-selection~ text))
  
  
  (method (get-client-position)
    (get-client-position~ text))
  
  
  (method virtual (mode-actions)
    )
    
  
  (method virtual (key-press evt)
    )
  
  
  (method virtual (backspace-press evt (word?: word? #f))
    )
  
  
  (method virtual (next-page)
    )
  
  
  (method virtual (prior-page)
    )
  
  
  (method virtual (get-constituent-test)
    #f)

  
  (method virtual (auto-complete?)
    #f)
  
  
  (method virtual (auto-complete-char? key last)
    'default)
  
  
  (method virtual (favorite-completions?)
    'default)
  
  
  (method virtual (complete-name? size)
    'default)


  (method virtual (completion-name pos)
    #f)
  
  
  (method virtual (symbol-completions name)
    #f)
  
  
  (method virtual (finish)
    (cleanup))
  
  
  (method virtual (cancel)
    (cleanup))
  
  
  (method virtual (cleanup (lose-focus? #f))
    (restore-client~ text)
    (let ((client (get-client)))
      (when client
        (unless lose-focus?
          (pop-focus))))
    (clear-user-message)))


;;;
;;;; Search-Mode
;;;


(class Search-Mode extends Command-Mode
  
  
  (slot direction       initialize #f)
  (slot search-result   initialize #f)
  (slot last-range      initialize #f)
  (slot last-found?     initialize #t)
  (slot prefixes        initialize (make-table test: equal?))
  (slot found-style     initialize #f)
  (slot highlight-style initialize #f)
  (slot replace-style   initialize #f)
  (slot boundary-style  initialize #f)
  
  
  (method override (initialize text direction)
    (nextmethod text)
    (set! direction~self direction))
  
  
  (method (prepare-search range)
    (let ((client (get-client))
          (client-selection (get-client-selection)))
      (let ((model (and (is? client Text-View) (get-model~ client))))
        (set! last-range (if model (or range client-selection) #f))
        (set! found-style (if model (add-style~ model 'Found (new Text-Style base: 'Base domain: (get-styles-domain~ model) highlight: {Color Found})) #f))
        (set! highlight-style (if model (add-style~ model 'Highlight (new Text-Style base: 'Base domain: (get-styles-domain~ model) highlight: {Color Highlight})) #f))
        (set! replace-style (if model (add-style~ model 'Replace (new Text-Style base: 'Base domain: (get-styles-domain~ model) highlight: {Color Light-Red} frame: {Color Replace})) #f))
        (set! boundary-style (if model (add-style~ model 'Boundary (new Text-Style base: 'Base domain: (get-styles-domain~ model) highlight: {Color Boundary})) #f)))))
  
  
  (method override (mode-actions)
    (find-actions 'search-mode))
  
  
  (method override (key-press evt)
    (search-client direction))
  
  
  (method override (backspace-press evt (word?: word? #f))
    (search-client direction prefix?: #t))
  
  
  (method override (next-page)
    (search-client 'forward again?: #t))
  
  
  (method override (prior-page)
    (search-client 'backward again?: #t))
  
  
  (method (set-search-result result)
    (set! search-result result))
  
  
  (method (set-boundaries range)
    (set-beginning~ search-result (and range (get-start~ range)))
    (set-ending~ search-result (and range (get-end~ range)))
    (rehighlight-search))
  
  
  (method (get-search)
    (get-search~ search-result))
  
  
  (method (get-context)
    ;; fow now get first one
    (car (get-contexts~ (get-find~ (get-search)))))
  
  
  (method (set-context context)
    (set-find~ (get-search) context))
  
  
  (method (get-whole-words?)
    (get-whole-words?~ (get-context)))
  
  
  (method (select-whole-words? whole-words?)
    (set-whole-words?~ (get-context) whole-words?))
  
  
  (method (get-ignore-case?)
    (get-ignore-case?~ (get-context)))
  
  
  (method (select-ignore-case? ignore-case?)
    (set-ignore-case?~ (get-context) ignore-case?))
  
  
  (method (get-regexp?)
    (get-regexp?~ (get-context)))
  
  
  (method (select-regexp? regexp?)
    (set-regexp?~ (get-context) regexp?))
  
  
  (method (on-whole-words evt)
    (select-whole-words? (not (get-whole-words?)))
    (user-message "Search whole-words {a}" (if (get-whole-words?) "on" "off"))
    (rehighlight-search))
  
  
  (method (on-ignore-case evt)
    (select-ignore-case? (not (get-ignore-case?)))
    (user-message "Search ignore-case {a}" (if (get-ignore-case?) "on" "off"))
    (rehighlight-search))
  
  
  (method (on-regexp evt)
    (select-regexp? (not (get-regexp?)))
    (user-message "Search regexp {a}" (if (get-regexp?) "on" "off"))
    (rehighlight-search))
  
  
  (method (on-forward evt)
    (let ((again? (not (select-empty-search))))
      (search-client 'forward again?: again?)))
  
  
  (method (on-backward evt)
    (let ((again? (not (select-empty-search))))
      (search-client 'backward again?: again?)))
  
  
  (method (select-empty-search)
    (let ((context (get-context)))
      (when (empty-string? (get-target~ context))
        (let ((previous (get-previous-mode~ text)))
          (when (is? previous Search-Mode)
            (let ((previous-context (get-context~ previous)))
              (set-previous-mode~ text #f)
              (set-context previous-context)
              (set-string-content~ text (get-target~ previous-context))
              (set-caret~ text (text-ending~ text)))
            #t)))))
  
  
  (method (on-current evt)
    (cancel))
  
  
  (method (on-widen evt)
    (let ((client (get-client)))
      (let ((model (get-model~ client))
            (selection (get-selection~ client)))
        (let ((end (get-end~ selection)))
          (let ((extend (next-word~ model end)))
            (let ((add (range-first-string~ client (new Range$Cell$ end extend))))
              (if (empty-string? add)
                  (bell)
                (insert-string~ text add)
                (search-client direction))))))))
  
  
  (method (on-outer-boundaries evt)
    (let ((client (get-client)))
      (let ((range (or (get-boundaries~ search-result)
                       (get-selection~ client))))
        (let ((outer (outer-range~ client range)))
          (if (not outer)
              (bell)
            (set-boundaries outer)
            (user-message "Boundaries set to range"))))))
  
  
  (method (on-declaration-boundaries evt)
    (let ((client (get-client)))
      (let ((range (or (get-boundaries~ search-result)
                       (get-selection~ client))))
        (let ((outer (outer-declaration-range~ client range)))
          (if (not outer)
              (bell)
            (receive (range name declaration-name) outer
              (set-boundaries range)
              (user-message "Boundaries set to ({a} {a})" name declaration-name)))))))
  
  
  (method (on-buffer-boundaries evt)
    (set-boundaries #f)
    (user-message "Boundaries set to buffer"))
  
  
  (method (on-center-caret evt)
    (center-caret~ (get-client)))
  
  
  (method (on-prior evt)
    (prior-page~ (get-client) #f))
  
  
  (method (on-next evt)
    (next-page~ (get-client) #f))
  
  
  (method (on-home evt)
    (on-home~ (get-client) evt)
    (finish))
  
  
  (method (on-end evt)
    (on-end~ (get-client) evt)
    (finish))
  
  
  (method (on-highlight evt)
    (highlight-current))
  
  
  (method (on-persist-highlights evt)
    (persist-highlights))
  
  
  (method protected virtual (highlight-current)
    (persist-highlights)
    (cancel))
  
  
  (method protected virtual (persist-highlights)
    (let ((client (get-client)))
      (let ((model (get-model~ client)))
        (let ((reference-style (locate-style~ model 'Reference)))
          (for-each (lambda (highlight)
                      (when (or (inherits?~ (get-style~ highlight) found-style)
                                (inherits?~ (get-style~ highlight) highlight-style))
                        (push-style~ model (get-range~ highlight) 'Reference)))
                    (get-highlights~ client))))))
  
  
  (method (on-goto-find evt)
    (goto-first-field~ text))
  
  
  (method (on-goto-replace evt)
    (goto-second-field~ text))
  
  
  (method (reset-search range)
    (set! last-range range)
    (set! last-found? #t)
    (set! prefixes (make-table test: equal?)))


  (method (on-find-forward evt)
    (let ((client (get-client)))
      (if (find-forward~ search-result client: client follow?: #f)
          (begin
            (reset-search (get-selection~ client))
            (rehighlight-search))
        (unless (follow-forward~ search-result)
          (bell)))))


  (method (on-find-backward evt)
    (let ((client (get-client)))
      (if (find-backward~ search-result client: client follow?: #f)
          (begin
            (reset-search (get-selection~ client))
            (rehighlight-search))
        (unless (follow-backward~ search-result)
          (bell)))))
  
  
  (method (on-replace evt)
    (unimplemented-replace)
    (select-replace)
    (with-rehighlight-search
      (lambda ()
        (unless (replace-selection~ search-result client: (get-client))
          (bell)))))
  
  
  (method (on-replace&find-forward evt)
    (unimplemented-replace)
    (select-replace)
    (with-rehighlight-search
      (lambda ()
        (unless (replace-selection&find-forward~ search-result client: (get-client))
          (bell)))))
  
  
  (method (on-replace-all evt)
    (unimplemented-replace)
    (select-replace)
    (with-rehighlight-search
      (lambda ()
        (replace-all~ search-result client: (get-client)))))
  
  
  (method (unimplemented-replace)
    (message-box "Unimplemented functionality")
    (signal-cancel))
  
  
  (method (select-replace)
    (let ((replace (second-content~ text)))
      (when replace
        (set-replace~ (get-search) (new Replace (list replace))))))
  
  
  (method (on-finish evt)
    (let ((fields (collect-fields~ text)))
      (when (= (caret-rank~ text fields) 1)
        (replace-all (second-content~ text))))
    (finish))
  
  
  (method (on-cancel evt)
    (cancel))

  
  (method (search-client direction (initial?: initial? #f) (prefix?: prefix? #t) (again?: again? #f))
    (define (setup-context target)
      (let ((context (get-context)))
        (set-target~ context target)
        (set-regexp~ context #f)))
    
    (define (search-target model beginning ending target again?)
      (let ((context (get-context))
            (range (or (and prefix? (table-ref prefixes target #f)) last-range)))
        (let ((pos (case direction
                     ((forward) (if again? (get-end~ range) (get-start~ range)))
                     ((backward) (if again? (get-start~ range) (nu+ (get-start~ range) (cardinality target))))))
              (reversed? (eq? direction 'backward)))
          (let ((found (text-regular-search~ model context start: pos end: (if reversed? beginning ending) reversed?: reversed?)))
            (define (add-prefix range)
              (reset-prefixes)
              (table-set! prefixes target range))
            
            (define (reset-prefixes)
              (let ((card (cardinality target)))
                (iterate-table-safe prefixes
                  (lambda (prefix range)
                    (when (>= (cardinality prefix) card)
                      (table-clear prefixes prefix))))))
            
            (when found
              (bind (from . to) found
                (let ((range (new Range$Cell$ from to)))
                  (add-prefix range)
                  (set! last-range range)))
              found)))))
    
    (define (highlight-boundary client beginning ending)
      (define (add-boundary-highlight range)
        (add-highlight~ client (new Text-Highlight client range boundary-style)))
      
      (when beginning
        (add-boundary-highlight (new Range$Cell$ beginning (next-cell~ client beginning))))
      (when ending
        (add-boundary-highlight (new Range$Cell$ (previous-cell~ client ending) ending))))
    
    (define (highlight-find client all)
      (when all
        (for-each (lambda (found)
                    (bind (start . end) found
                      (let ((range (new Range$Cell$ start end)))
                        (unless (nu=? range last-range)
                          (add-highlight~ client (new Text-Highlight client range highlight-style))))))
                  all))
      (add-highlight~ client (new Text-Highlight client last-range found-style))
      (set-selection~ client last-range))
    
    (define (highlight-replace client model beginning ending repl)
      (unless (empty-string? repl)
        (let ((target (make-search-context repl whole-words?: #t)))
          (for-each (lambda (found)
                      (bind (start . end) found
                        (let ((range (new Range$Cell$ start end)))
                          (add-highlight~ client (new Text-Highlight client range replace-style)))))
                    (text-regular-search-all~ model target start: beginning end: ending)))))
    
    (let ((client (get-client)))
      (when (is? client Text-View)
        (let ((model (get-model~ client))
              (content (first-content~ text))
              (repl (second-content~ text))
              (context (get-context))
              (beginning (get-beginning~ search-result))
              (ending (get-ending~ search-result)))
          (setup-context content)
          (let ((all (and (not (empty-string? (get-target~ context))) (text-regular-search-all~ model context start: beginning end: ending))))
            (let ((found #f))
              (set! found (search-target model beginning ending content #f))
              (when again?
                (set! found (search-target model beginning ending content #t)))
              (if found
                  (set! last-found? #t)
                (when (or last-found? again?)
                  (set! last-found? #f)
                  (bell)))
              (if (not all)
                  (clear-status~ text)
                (let ((rank (and last-range (find all (cons (get-start~ last-range)
                                                            (get-end~ last-range))
                                              test: (lambda (x y)
                                                      (and (cell=? (car x) (car y))
                                                           (cell=? (cdr x) (cdr y)))))))
                      (total (length all)))
                  (set-status~ text (if rank
                                        (format "{a} of {a}" (+ rank 1) total)
                                      (format "{a}" total)))))
              (remove-highlights)
              (highlight-boundary client beginning ending)
              (highlight-find client all)
              (when repl
                (highlight-replace client model beginning ending repl))
              found))))))
  
  
  (method (replace-all repl)
    (let ((context (get-context))
          (client (get-client))
          (beginning (get-beginning~ search-result))
          (ending (get-ending~ search-result)))
      (let ((model (get-model~ client)))
        (with-atomic-undo~ (get-undoer~ model)
          (lambda ()
            (for-each-reversed (lambda (found)
                                 (bind (start . end) found
                                   (let ((range (new Range$Cell$ start end)))
                                     (replace~ client range (list repl)))))
                               (text-regular-search-all~ model context start: beginning end: ending)))))))
  
  
  (method (rehighlight-search)
    (search-client direction))
  
  
  (method (with-rehighlight-search proc)
    (remove-highlights)
    (proc)
    (reset-search (get-selection~ (get-client)))
    (rehighlight-search))
  
  
  (method (remove-highlights)
    (let ((client (get-client)))
      (when (is? client Text-View)
        (remove-all-highlights~ client))))
  
  
  (method override (finish)
    (cleanup))
  
  
  (method override (cancel)
    (let ((client (get-client))
          (client-selection (get-client-selection)))
      (when client-selection
        (set-selection~ client client-selection ensure-displayed?: #f)
        (scroll-to~ client (get-client-position))))
    (cleanup))
  
  
  (method override (cleanup (lose-focus? #f))
    (restore-client~ text)
    (preserve-mode~ text)
    (let ((client (get-client)))
      (when client
        (remove-highlights)
        (unless lose-focus?
          (pop-focus))))
    (clear-user-message)))


;;;
;;;; Highlight-Mode
;;;


(class Highlight-Mode extends Search-Mode
  
  
  (method override (mode-actions)
    (find-actions 'highlight-mode))
  
  
  (method override (highlight-current)
    (cancel))
  
  
  (method override (finish)
    (persist-highlights)
    (cleanup)))


;;;
;;;; Extended-Mode
;;;


(class Extended-Mode extends Command-Mode
  
  
  (method override (mode-actions)
    (find-actions 'extended-mode))
  
  
  (method (on-find-file evt)
    (let ((model (get-model~ text)))
      (set-mode~ text (new Find-File-Mode text))
      (set-text~ model (standardize-filename (current-directory)))
      (set-caret~ text (text-ending~ text))
      (refresh-focus-actions text)
      (user-message "Find file...")))
  
  
  (method (on-find-filename evt)
    (let ((model (get-model~ text)))
      (set-mode~ text (new Find-Filename-Mode text))
      (set-text~ model "")
      (refresh-focus-actions text)
      (user-message "Find filename...")))
  
  
  (method (on-find-buffer evt)
    (set-mode~ text (new Find-Buffer-Mode text))
    (refresh-focus-actions text)
    (user-message "Find buffer..."))
  
  
  (method (on-save-buffer evt)
    (finish)
    (let ((guest (current-document)))
      (if (not guest)
          (bell)
        (on-save~ guest evt))))
  
  
  (method (on-save-buffer-as evt)
    (finish)
    (let ((guest (current-document)))
      (if (not guest)
          (bell)
        (on-save-as~ guest evt))))
  
  
  (method (on-save-all evt)
    (finish)
    (let ((appl (get-application)))
      (on-save-all~ appl evt)))
  
  
  (method (on-revert-buffer evt)
    (finish)
    (let ((guest (current-document)))
      (if (not guest)
          (bell)
        (on-revert-saved~ guest evt))))
  
  
  (method (on-kill-buffer evt)
    (let ((appl (get-application)))
      (on-close~ appl evt)))
  
  
  (method (on-delete-stage evt)
    (delete-stage)
    (finish))
  
  
  (method (on-delete-other-stages evt)
    (delete-other-stages)
    (finish))
  
  
  (method (on-split-stage-vertically evt)
    (split-stage-vertically)
    (finish))
  
  
  (method (on-split-stage-horizontally evt)
    (split-stage-horizontally)
    (finish))
  
  
  (method (on-quit evt)
    (let ((appl (get-application)))
      (on-quit-application~ appl evt)))
  
  
  (method (on-start-recording evt)
    (finish)
    (let ((recorder (get-recorder~ (get-application))))
      (on-start-recording~ recorder evt)))
  
  
  (method (on-stop-recording evt)
    (finish)
    (let ((recorder (get-recorder~ (get-application))))
      (on-stop-recording~ recorder evt)))
  
  
  (method (on-play-recording evt)
    (finish)
    (let ((recorder (get-recorder~ (get-application))))
      (on-play-recording~ recorder evt)))
  
  
  (method (on-downcase-selection evt)
    (finish)
    (let ((text (current-document)))
      (if (is-not? text Text-View)
          (bell)
        (on-downcase-selection~ text evt))))
  
  
  (method (on-upcase-selection evt)
    (finish)
    (let ((text (current-document)))
      (if (is-not? text Text-View)
          (bell)
        (on-upcase-selection~ text evt))))
  
  
  (method (on-select-all evt)
    (finish)
    (let ((view (current-document)))
      (if (not view)
          (bell)
        (on-select-all~ view evt))))
  
  
  (method (on-finish evt)
    (finish))
  
  
  (method (on-cancel evt)
    (cancel)))


;;;
;;;; Find
;;;


(class Find-Mode extends Command-Mode
  
  
  (method override (mode-actions)
    (find-actions 'find-mode))
  
  
  (method override (get-constituent-test)
    alphanumeric?)
  
  
  (method override (auto-complete?)
    #t)
  
  
  (method override (auto-complete-char? key last)
    (memv? key '(#\/ #\- #\_ #\.)))

  
  (method override (favorite-completions?)
    #f)
  
  
  (method override (complete-name? size)
    (>= size 1))


  (method override (completion-name pos)
    (get-string~ text (new Range$Cell$ {Cell 0 0} pos)))
  
  
  (method (on-finish evt)
    (finish))
  
  
  (method (on-cancel evt)
    (cancel))
  
  
  (method override (cancel)
    (cleanup))
  
  
  (method override (cleanup (lose-focus? #f))
    (set-string-content~ text "")
    (restore-client~ text)
    (let ((client (get-client)))
      (when client
        (unless lose-focus?
          (pop-focus))))
    (clear-user-message)))


;;;
;;;; Find-File
;;;


(class Find-File-Mode extends Find-Mode
  
  
  (method override (symbol-completions name)
    (define (split-filename filename)
      (let ((pos (find filename #\/ reversed?: #t)))
        (if (not pos)
            (values #f filename)
          (values (substring filename 0 (+ pos 1))
                  (substring filename (+ pos 1) (string-length filename))))))
    
    (receive (dir filename) (split-filename name)
      (when (or (not dir)
                (and (file-exists? dir)
                     (eq? (pathname-type dir) 'directory)))
        (collect (lambda (name)
                   (when (compare-n filename name (string-length filename) test: char-ci=?)
                     (let ((path (standardize-filename (string-append (or dir "") name))))
                       (if (and (file-exists? path)
                                (eq? (pathname-type path) 'directory))
                           (string-append path "/")
                         path))))
                 (directory-content (list path: (or dir (current-directory)) ignore-hidden: 'dot-and-dot-dot))))))
  
  
  (method override (finish)
    (let ((filename (first-content~ text)))
      (if (and (file-exists? filename)
               (eq? (pathname-type filename) 'regular))
          (let ((file (anchorize~ (new File (tokenise-filename (pathname-normalize filename))))))
            (set-current-directory (get-parent~ file))
            (edit-document~ (get-application) file)
            (cleanup))
        (bell)))))


;;;
;;;; Find-Filename
;;;


(class Find-Filename-Mode extends Find-Mode
  
  
  (slot filenames)
  
  
  (method override (initialize text)
    (define (collect-filenames)
      (let ((workbench (get-workbench))
            (table (make-table test: equal?)))
        (for-each-project-file~ workbench
          (lambda (file)
            (let ((source (get-source~ file)))
              (when source
                (let ((name (downcase (get-name~ source))))
                  (table-set! table name source))))))
        table))
    
    (nextmethod text)
    (set! filenames (collect-filenames)))
  
  
  (method override (symbol-completions name)
    (let ((queue (new-queue)))
      (iterate-table filenames
                     (lambda (filename file)
                       (when (compare-n name filename (string-length name) test: char-ci=?)
                         (enqueue queue filename))))
      (queue-list queue)))
  
  
  (method override (finish)
    (define (find-file name)
      (continuation-capture
        (lambda (return)
          (let ((len (string-length name)))
            (iterate-table filenames
                           (lambda (filename file)
                             (when (compare-n name filename len test: char-ci=?)
                               (let ((remaining (substring filename len (string-length filename))))
                                 (when (or (empty-string? remaining)
                                           (and (eqv? (string-ref remaining 0) #\.)
                                                (not (find remaining #\. start: 1))))
                                   (continuation-return return file)))))))
          #f)))
    
    (let ((filename (first-content~ text)))
      (let ((file (find-file filename)))
        (if file
            (edit-document~ (get-application) file)
          (bell))))))


;;;
;;;; Find-Buffer
;;;


(class Find-Buffer-Mode extends Find-Mode
  
  
  (method override (symbol-completions name)
    (collect (lambda (view)
               (let ((presentation (present-title view)))
                 (when (compare-n name presentation (string-length name) test: char-ci=?)
                   presentation)))
             (all-views)))
  
  
  (method override (finish)
    (define (find-view name)
      (find-if (lambda (view)
                 (string-ci=? (present-title view) name))
               (all-views)))
    
    (let ((name (first-content~ text)))
      (if (empty-string? name)
          (let ((views (all-frames)))
            (if (>= (length views) 2)
                (begin
                  (send-to-back~ (first views))
                  (focus-host~ (current-frame)))
              (bell)))
        (let ((view (find-view name)))
          (if view
              (begin
                (focus-host~ view)
                (cleanup))
            (bell))))))
  
  
  (definition (present-title window/view)
    (let ((title (typecase window/view
                   ((Window)
                    (get-title~ window/view))
                   ((View)
                    (if (is? window/view Host-View)
                        (let ((guest (get-guest~ window/view)))
                          (if (is? guest Document)
                              (name-status~ (get-document-controller~ guest))
                            (get-title~ window/view)))
                      (get-title~ window/view))))))
      (if (or (null? title) (empty-string? title))
          "<Untitled>"
        title)))
  
  
  (definition (all-views)
    (append (collect-frames)
            (collect-palettes))))


;;;
;;;; Execute-Mode
;;;


(class Execute-Mode extends Command-Mode
  
  
  (method override (mode-actions)
    (find-actions 'execute-mode))
  
  
  (method override (auto-complete?)
    #t)
  
  
  (method (on-finish evt)
    (finish))
  
  
  (method (on-cancel evt)
    (cancel)))


;;;
;;;; Utilities
;;;


(definition (standardize-filename filename)
  (let ((home (pathname-normalize (home-directory))))
    (if (starts-with? filename home)
        (string-append "~/" (substring filename (cardinality home) (cardinality filename)))
      filename))))
