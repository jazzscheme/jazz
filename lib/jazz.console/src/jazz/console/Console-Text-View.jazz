;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Console Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.console.Console-Text-View jazz


(import (jazz.action)
        (jazz.console)
        (jazz.event)
        (jazz.exemplar)
        (jazz.ide)
        (jazz.locale)
        (jazz.markup)
        (jazz.menu)
        (jazz.preferences)
        (jazz.resource)
        (jazz.text))


(class undocumented Console-Text-View extends IDE-Text-View


  (slot inserted-rank  initialize #f)
  (slot inserted-where initialize #f)
  (slot banner         initialize #f accessors generate)
  (slot banner-prompt  initialize #f accessors generate)
  (slot console-port   initialize #f accessors generate)
  (slot console-pump   initialize #f accessors generate)
  (slot partial-line   initialize #f)

  
  (method meta override (external-name self . rest)
    'console-text)


  (method override (default-tool? self)
    #t)


  (method override (default-console? self)
    #t)
  
  
  (method override (new-model self)
    (new Text-Model base-style: {Text-Style Code-Base} wrap?: #t left-padding: 2))

  
  (method override (install self rest)
    (nextmethod self rest)
    (set-propagate-styles? model #f)
    (set-colorize? syntax #f)
    (let ((pref (preferences self)))
      (add-style self 'Banner  (get-banner-style pref))
      (add-style self 'Prompt  (get-prompt-style pref))
      (add-style self 'Entry   (get-entry-style pref))
      (add-style self 'Input   (get-input-style pref))
      (add-style self 'Output  (get-output-style pref))
      (add-style self 'Comment (get-comment-style pref))))

  
  (method override (preferences self)
    (current-preferences '(documents console-text)))


  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Console"})
  
  
  (method meta override (host-title self)
    "Console")

  
  ;;;
  ;;;; Console
  ;;;
  
  
  (method package (get-console self)
    (get-parent (get-parent self)))


  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions self)
    (cons (find-actions 'console-text)
          (nextmethod self)))


  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-fresh-prompt self evt)
    (fresh-prompt self))
  
  
  (method (on-console-evaluate self evt)
    (let ((block (block-at self (get-end self))))
      (cond ((empty-block? block)
             (set-caret self (text-ending self)))
            ((nu=? block (last-block self))
             (console-evaluate self))
            (else
             (set-caret self (text-ending self))
             (insert-string self (remove-enclosing #\newline (get-string self block)))))))
  
  
  (method (on-insert-previous self evt)
    (insert-previous-block self))
  
  
  (method (on-insert-next self evt)
    (insert-next-block self))


  (method override (tab-press self evt)
    (cond ((has-completion? self)
           (end-completion self))
          ((not (get-auto-complete? (preferences self)))
           (complete-at self (get-start self) auto-complete?: #t))
          ((get-shift? evt)
           (insert-next-block self))
          (else
           (insert-previous-block self))))
  
  
  (method override (delete-char self)
    (define (send-end-of-file)
      (close-port console-port))
    
    (if (at-end? self)
        (send-end-of-file)
      (nextmethod self)))
  
  
  (method override (scrollee-width self)
    (get-display-width self))


  (method override (context-menu self evt)
    (let ((pos (get-position evt)))
      (receive (shift? control? alt? meta?) (get-down evt)
        (if (and (not control?) (not shift?) (not alt?))
            (begin
              (context-menu-click self pos)
              (track-popup-menu self (new Console-Menu) pos))
          (nextmethod self evt)))))
  
  
  (method override (set-selection self range . rest)
    (apply nextmethod self range rest)
    (set! inserted-where #f))


  ;;;
  ;;;; Eval
  ;;;
  
  
  (method (console-evaluate self)
    (set-caret self (text-ending self))
    (eval-from-prompt self))

  
  (method protected virtual (eval-from-prompt self)
    (insert-newline self)
    (let ((range (current-command self)))
      (let ((command (get-string self range)))
        (unless (empty-string? command)
          (display command console-port)
          (force-output console-port)
          (stylize-range model range (locate-style self 'Input))
          (reset-inserted self)))))
  
  
  (method (current-command self)
    (select-style self (text-ending self)
      (lambda (style)
        (inherits? style 'Entry))))


  ;;;
  ;;;; Text
  ;;;


  (method override (get-modified? self)
    #f)
  
  
  (method override (get-tooltip-context self pos)
    #f)
  
  
  (method override (home-column self row col)
    (let ((str (paragraph-string self row)))
      (let ((pos (find str #\>)))
        (if pos
            (let ((end (+ pos 2)))
              (if (<= end (string-length str))
                  (let ((header (substring str 0 pos)))
                    (if (numeric? header)
                        end
                      0))
                0))
          0))))
  
  
  ;;;
  ;;;; Text Formatted
  ;;;
  
  
  (method override (insert-formatted-line self line)
    (let ((line (if partial-line (string-append partial-line line) line)))
      (nextmethod self
        (call-with-input-string line
          (lambda (input)
            (let (iterate (pos 0)
                          (insert-end #f)
                          (depth 0))
              (let ((c (read-char input)))
                (if (not (eof-object? c))
                    (ecase c
                      ((formatted-start-delimiter)
                       (iterate (+ pos 1) (or insert-end pos) (+ depth 1)))
                      ((formatted-end-delimiter)
                       (iterate (+ pos 1) (and (> depth 1) insert-end) (- depth 1)))
                      (else
                       (iterate (+ pos 1) (and (> depth 0) insert-end) depth)))
                  (if insert-end
                      (begin
                        (set! partial-line (substring line insert-end pos))
                        (substring line 0 insert-end))
                    (set! partial-line #f)
                    line)))))))))

  
  ;;;
  ;;;; History
  ;;;
  
  
  (method override (current-history-item self)
    #f)
  

  ;;;
  ;;;; Formatted
  ;;;

  
  (method override (process-formatted-directive self directive input)
    (case directive
      ((banner)
       (with-formatted-range self input
         (lambda (range)
           (stylize-range model range (locate-style self 'Banner))
           (set! banner (get-string self range)))))
      ((banner-prompt)
       (with-formatted-range self input
         (lambda (range)
           (stylize-range model range (locate-style self 'Prompt))
           (set! banner-prompt (get-string self range)))))
      ((prompt)
       (with-formatted-range self input
         (lambda (range)
           (stylize-range model range (locate-style self 'Prompt)))))
      (else
       (nextmethod self directive input))))


  ;;;
  ;;;; Style
  ;;;
  
  
  (method package (backward-style self pos predicate)
    (let ((explorer (new (explorer-class self) model start: pos end: pos)))
      (backward-style explorer predicate)
      (get-range explorer)))
  
  
  (method package (forward-style self pos predicate)
    (let ((explorer (new (explorer-class self) model start: pos end: pos)))
      (forward-style explorer predicate)
      (get-range explorer)))
  
  
  (method package (select-style self pos predicate)
    (let ((explorer (new (explorer-class self) model start: pos end: pos)))
      (select-style explorer predicate)
      (get-range explorer)))
    

  ;;;
  ;;;; Banner
  ;;;

  
  (method public virtual (display-banner self banner)
    (when banner
      (insert-styled self banner 'Banner)))
  
  
  (method public virtual (determine-banner self)
    (let ((range (forward-style self (text-beginning self) (lambda (style)
                                                   (or (inherits? style 'Banner)
                                                       (inherits? style 'Prompt)
                                                       (inherits? style 'Output))))))
      (when range
        (get-string self range))))
  
  
  (method public (clear-console self)
    (clear-all self)
    (when banner
      (display-banner self banner)
      (when banner-prompt
        (insert-styled self banner-prompt 'Prompt))))


  ;;;
  ;;;; Prompt
  ;;;
  
  
  (method public (fresh-prompt self)
    (stylize-range model (block-at self (get-end self)) (locate-style self 'Comment))
    (let ((end (text-ending self)))
      (set-caret self end)
      (let ((prompt-range (last-prompt self)))
        (when (and prompt-range (nu/=? (get-end prompt-range) end))
          (let ((prompt (get-string self prompt-range)))
            (when (/= (get-col end) 0)
              (insert-newline self)
              (insert-styled self prompt 'Prompt)))))))
  
  
  ;;;
  ;;;; Navigation
  ;;;
  
  
  (method (reset-inserted self)
    (set! inserted-rank #f)
    (set! inserted-where #f))
  
  
  (definition (text-style? style)
    (or (inherits? style 'Banner)
        (inherits? style 'Prompt)
        (inherits? style 'Output)))
  
  
  (definition (user-style? style)
    (not (text-style? style)))
  
  
  (method package (block-at self pos)
    (new-range self
      (get-start (backward-style self pos user-style?))
      (get-end (forward-style self pos user-style?))))
  
  
  (method (next-block self block)
    (let* ((next-start (get-end (forward-style self (get-end block) text-style?)))
           (next-end (get-end (forward-style self next-start user-style?))))
      (new-range self next-start next-end)))
  
  
  (method (previous-block self block)
    (let* ((previous-end (get-start (backward-style self (get-start block) text-style?)))
           (previous-start (get-start (backward-style self previous-end user-style?))))
      (new-range self previous-start previous-end)))
  
  
  (method package (last-block self)
    (let ((ending (text-ending self)))
      (backward-style self ending user-style?)))
  
  
  (method (last-prompt self)
    (define (prompt-style? style)
      (inherits? style 'Prompt))
    
    (define (prompt range)
      (if (empty? range)
          #f
        (let ((start (get-start range))
              (end (get-end range)))
          (if (= (get-line start)
                 (get-line end))
              range
            (new Range$Cell$ (new Cell (get-line end) 0) end)))))
    
    (let ((ending (text-ending self)))
      (let ((range (backward-style self ending (lambda (style) (not (prompt-style? style))))))
        (prompt (backward-style self (get-start range) (lambda (style) (prompt-style? style)))))))
  
  
  (method (find-block self rank)
    (let (iterate (block (last-block self)) (n rank))
      (if (<= n 0)
          block
        (iterate (previous-block self block) (- n 1)))))
  
  
  (definition (empty-block? block)
    (nu=? (get-start block)
          (get-end block)))

  
  (method (insert-previous-block self)
    (let* ((current (or inserted-rank 0))
           (previous (+ current 1))
           (block (find-block self previous)))
      (unless (empty-block? block)
        (when inserted-where
          (set-selection self inserted-where))
        (let ((start (get-start self)))
          (insert-string self (remove-enclosing #\newline (get-string self block)))
          (set! inserted-rank previous)
          (set! inserted-where (new-range self start (get-start self)))))))

  
  (method (insert-next-block self)
    (let* ((current (or inserted-rank 0))
           (next (- current 1)))
      (when (> next 0)
        (let ((block (find-block self next)))
          (unless (empty-block? block)
            (when inserted-where
              (set-selection self inserted-where))
            (let ((start (get-start self)))
              (insert-string self (remove-enclosing #\newline (get-string self (find-block self next))))
              (set! inserted-rank next)
              (set! inserted-where (new-range self start (get-start self))))))))))


;;;
;;;; Console-Menu
;;;


(class Console-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item>     name: browse-class     title: "Browse Class" icon: {Image-Resource "Class"} action-handler: {Action ide browse-class})
      (<Label-Item>     name: browse-method    title: "Browse Method" icon: {Image-Resource "Method"} action-handler: {Action ide browse-method})
      (<Separator-Item>)
      (<Label-Item>     name: edit-definitions title: "Find Definitions" icon: {Image-Resource "Edit"} action-handler: {Action code-text edit-definitions})
      (<Label-Item>     name: edit-references  title: "Find References" action-handler: {Action code-text edit-references})
      (<Separator-Item>)
      (<Label-Item>     name: console-evaluate title: "Evaluate" icon: {Image-Resource "Import"} action-handler: {Action console-text console-evaluate})
      (<Label-Item>     name: fresh-prompt     title: "Fresh Prompt" action-handler: {Action console-text fresh-prompt})
      (<Separator-Item>)
      (<Label-Item>     name: cut              title: {Locales english: "Cut" french: "Couper"} icon: {Image-Resource "Cut"} action-handler: {Action view cut})
      (<Label-Item>     name: copy             title: {Locales english: "Copy" french: "Copier"} icon: {Image-Resource "Copy"} action-handler: {Action view copy})
      (<Label-Item>     name: paste            title: {Locales english: "Paste" french: "Coller"} icon: {Image-Resource "Paste"} action-handler: {Action view paste})
      (<Separator-Item>)
      (<Label-Item>     name: insert-previous  title: "Insert Previous" icon: {Image-Resource "FindBackward"} action-handler: {Action console-text insert-previous})
      (<Label-Item>     name: insert-next      title: "Insert Next" icon: {Image-Resource "FindForward"} action-handler: {Action console-text insert-next})))))
