;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; System types
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.database.provider.Systype jazz


(import (jazz.database))


(class Systype extends Object
  
  
  (slot name          getter generate)
  (slot size          getter generate)
  (slot precision     getter generate)
  (slot scale         getter generate)
  (slot default-value getter generate)
  
  
  @convert-literal
  (method meta (construct-literal context name size precision scale default-value)
    (assert (or (integer? size) (null? size)))
    (assert (or (integer? precision) (null? precision)))
    (assert (or (integer? scale) (null? scale)))
    (assert (or (is? default-value String) (integer? default-value) (null? default-value) (= default-value 'read-only)))
    (new self name size precision scale default-value))
    
    
  (method override (initialize self name size precision scale default-value)
    (nextmethod self)
    (set! self.name name)
    (set! self.size size)
    (set! self.precision precision)
    (set! self.scale scale)
    (set! self.default-value default-value))


  (method override (compare-object self target)
    (cond ((is-not? target Systype)
           :incompatible)
          ((and (= name (get-name target))
                (= size (get-size target))
                (= precision (get-precision target))
                (= scale (get-scale target))
                (= default-value (get-default-value target)))
           :equal)
          (else
           :not-equal)))
  
  
  (method override (print self output readably)
    (format output "~{{a} {s} {s} {s} {s} {s}}"
            (category-name (class-of self))
            name
            size
            precision
            scale
            default-value))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-sizeable? self)
    (null? size))
  
  
  (method public (get-fixed? self)
    (not-null? size))
  
  
  (method public (get-precisionable? self)
    (not-null? precision))
  
  
  (method public (get-scalable? self)
    (not-null? scale))
  
  
  (method public (get-defaultable? self)
    (not-null? default-value))
  
  
  ;;;
  ;;;; Presentation
  ;;;
  
  
  (method public (present self)
    (format "{a}" name))))
