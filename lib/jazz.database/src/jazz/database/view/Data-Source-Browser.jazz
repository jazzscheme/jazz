;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Data-Source Browser
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.database.view.Data-Source-Browser jazz


(import (jazz.database)
        (jazz.handler)
        (jazz.jml)
        (jazz.ui)
        (jazz.view))


(class undocumented Data-Source-Browser extends View


  (property provider initialize #f accessors explicit)
  

  (form
    (<install>                             size: {Dimension 500 206}
      (<Label-View>  name: provider-label  title: "Provider:" position: {Point 0 0} size: {Dimension 50 20})
      (<Border-View> name: provider-border position: {Point 67 1} size: {Dimension 146 19} border-type: edge
        (<Combo-Box> name: provider        content-change-handler: {Event-Handler :form on-provider-change}
          (<!>       name: scroller
            (<!>     name: content
              (<!>   name: body            left-padding: 2 top-padding: 2)))))
      (<Layout-View> name: source          position: {Point 0 33} size: {Dimension 450 160})))

  
  (method override (install self rest)
    (nextmethod self rest)
    (setup-providers self))
  

  ;;;
  ;;;; Access
  ;;;


  (method public (get-provider self)
    provider)


  (method public (set-provider self value)
    (set! provider value)
    (provider-update self))
  
  
  (method public (get-data-source self)
    (let ((browser (get-data-source-browser self)))
      (when browser
        (get-data-source browser))))
  
  
  (method public (set-data-source self source)
    (let ((combo (locate self 'provider)))
      (if (null? source)
          (set-entry combo #f)
        (set! provider (external-name (get-provider source)))
        (set-value combo provider)
        (provider-update self)
        (set-data-source (get-data-source-browser self) source))))
  
  
  (method public (setup-username/password-mode self)
    (let ((browser (get-data-source-browser self)))
      (when browser
        (setup-username/password-mode browser))))
  

  ;;;
  ;;;; Events
  ;;;
  
  
  (method public (set-default-focus self)
    )
  
  
  (method (on-provider-change self evt)
    (let ((combo (locate self 'provider)))
      (set! provider (get-value combo))
      (provider-update self)))
  
  
  (method override (on-return-press self evt)
    (when return-press-handler
      (invoke-handler self return-press-handler evt)))

  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout self)
    (if (null? provider)
        (begin
          (set-visible? (locate self 'provider-label) #t)
          (set-visible? (locate self 'provider-border) #t)
          (set-position (locate self 'source) {Point 0 33})
          (set-size (locate self 'source) (- (get-size self) {Point 0 33})))
      (set-visible? (locate self 'provider-label) #f)
      (set-visible? (locate self 'provider-border) #f)
      (set-position (locate self 'source) {Point 0 0})
      (set-size (locate self 'source) (get-size self))))

  
  ;;;
  ;;;; Modal
  ;;;
  
  
  (method override (validate-content self)
    (validate-content (locate self 'provider))
    (let ((browser (get-data-source-browser self)))
      (when browser
        (validate-content browser))))
  

  ;;;
  ;;;; Provider
  ;;;


  (method (setup-providers self)
    @cant-find-get-providers
    (let ((combo (locate self 'provider))
          (choices
           (sort < (map (lambda (provider)
                          (list (external-name provider) (present provider)))
                        (get-providers Provider))
                 key: second)))
      (set-choices combo choices)
      @not-sure
      (set-entry combo "<Select provider>")))
  

  ;;;
  ;;;; Source
  ;;;
  
  
  (method (get-data-source-browser self)
    (first-child (locate self 'source)))


  (method (provider-update self)
    (let ((source (locate self 'source)))
      (close-children source)
      (when provider
        (new (browser-class (get-provider Provider provider)) parent: source))
      (layout-view source)
      (layout-view self)))))
