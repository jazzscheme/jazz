;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Debuggee Process
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debuggee.jazz.Jazz-Debuggee-Process jazz


(import (jazz.component (cond (not ios)))
        (jazz.debuggee)
        (jazz.debuggee.autoload)
        (jazz.debuggee.jazz)
        (jazz.debuggee.stub)
        (jazz.debuggee.jazz.autoload)
        (jazz.debuggee.jazz.autoload-external)
        (jazz.debugger.stub)
        (jazz.evaluate)
        (jazz.event (cond (not ios)))
        (jazz.inspector)
        (jazz.presence)
        (jazz.process)
        (jazz.runtime)
        (jazz.tracker)
        (profiler))


(class Jazz-Debuggee-Process extends Debuggee-Process
  
  
  (slot threads)
  
  
  (method override (get-id self)
    #f)
  
  
  (method override (get-title self)
    (current-process-present))
  
  
  (method override (get-traits self)
    (current-process-traits))
  
  
  (method override (get-icon self)
    (current-process-icon))
  
  
  ;;;
  ;;;; Control
  ;;;
  
  
  (method override (detach-process self)
    (detach-from-controller))
  
  
  (method override (reload-process self)
    (reload-process (current-process)))
  
  
  (method override (attach-to-debugger self controller-register debugger-cookie)
    (thread-post (primordial-thread) 'attach-to-debugger
      (lambda ()
        (attach-debuggee-to-controller controller-register focus?: #t)
        (start-repl-thread (current-thread) #f (current-console-port))
        (select-debugger-console)
        (ready-to-controller debugger-cookie))))
  
  
  (method override (quit-process self)
    (detach-from-controller)
    (quit))
  
  
  ;;;
  ;;;; Console
  ;;;
  
  
  (method override (console-input self thread-proxy str)
    (let ((thread (get-thread (get-object thread-proxy))))
      (let ((port (thread-console-pump-port thread)))
        (if (not str)
            (close-port port)
          (display str port)
          (force-output port)))))
  
  
  (method override (console-close self thread-proxy)
    (let ((thread (get-thread (get-object thread-proxy))))
      (close-thread-console thread)))
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method override (get-toplevel-hwnd self)
    @convert
    (get-handle (current-toplevel)))
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method override (get-threads self)
    ;; gc protect
    (set! threads
          (map (lambda (thread)
                 (new Debuggee-Thread-Local-Proxy (require-presence 'debugging) (new Jazz-Debuggee-Thread thread)))
               (top-threads)))
    threads)
  
  
  ;;;
  ;;;; Restart
  ;;;
  
  
  (method override (restart-stop self restart-proxy)
    (let ((debuggee-restart (get-object restart-proxy)))
      (let ((thread (get-thread debuggee-restart))
            (restart (get-restart debuggee-restart)))
        (thread-post thread 'restart-stop
          (lambda ()
            (invoke-restart restart))))))
  
  
  ;;;
  ;;;; Thread
  ;;;
  
  
  (method override (break-thread self thread-proxy)
    (let ((thread (get-thread (get-object thread-proxy))))
      (thread-int! thread
        (lambda ()
          (break)))))
  
  
  (method override (repl-thread self thread-proxy)
    (let ((thread (get-thread (get-object thread-proxy))))
      (thread-int! thread
        (lambda ()
          (jazz.platform:terminal-repl)))))
  
  
  (method override (terminate-thread self thread-proxy preserve-console?)
    (let ((thread (get-thread (get-object thread-proxy))))
      (if (eq? thread (primordial-thread))
          (quit-process self)
        (catch-exception-filter
          inactive-thread-exception?
          (lambda (exception) #f)
          (lambda () (exit-thread thread)))
        (unless preserve-console?
          (close-thread-console thread)))))
  
  
  (method override (kill-thread self thread-proxy)
    (let ((thread (get-thread (get-object thread-proxy))))
      (if (eq? thread (primordial-thread))
          (quit-process self)
        (catch-exception-filter
          inactive-thread-exception?
          (lambda (exception) #f)
          (lambda () (thread-terminate! thread)))
        (close-thread-console thread))))
  
  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method override (continue-stop self stop-proxy)
    (let ((stop (get-object stop-proxy)))
      (let ((thread (get-thread stop))
            (continuation (get-continuation stop)))
        (thread-post thread 'continue-stop
          (lambda ()
            (continuation-return continuation #f))))))
  
  
  (method override (step-stop self stop-proxy command)
    (let ((stop (get-object stop-proxy)))
      (let ((thread (get-thread stop))
            (stepper (get-stepper stop)))
        (thread-post thread 'step-stop
          (lambda ()
            (stepper command))))))
  
  
  ;;;
  ;;;; Runtime
  ;;;
  
  
  (method override (transmit-runtime-information self what)
    (thread-start!
      (new-safe-thread
        (lambda (exception)
          #t)
        (lambda ()
          (let ((queue (new-queue)))
            (define (add-unit unit)
              (let ((resource (find-unit-src unit error?: #f)))
                (when resource
                  (enqueue queue (resource-pathname resource)))))
            
            (let ((process (current-process)))
              (let ((properties (runtime-properties process)))
                (if (eq? (effective-transmit-runtime-what process what) 'properties)
                    (transmit-information '() '() properties)
                  ;; immediately transmit properties before doing
                  ;; the potentially long computation of filenames
                  (transmit-information #f #f properties)
                  (when kernel-source-access?
                    (when (remote-proxy? (get-controller-debugger))
                      (let ((product (runtime-product process)))
                        (when product
                          (for-each add-unit (product-units product))))
                      (let ((products (runtime-products process)))
                        (for-each (lambda (product)
                                    (for-each add-unit (product-units product)))
                                  products)))
                    (for-each add-unit (runtime-units process)))
                  (let ((filenames (queue-list queue))
                        (processes (evaluation-processes process)))
                    (transmit-information filenames processes properties)))))))
        'runtime-information)))
  
  
  (method override (transmit-product-runtime-information self what product)
    (thread-start!
      (new-thread
        (lambda ()
          (let ((queue (new-queue)))
            (define (add-unit unit)
              (let ((resource (find-unit-src unit error?: #f)))
                (when resource
                  (enqueue queue (resource-pathname resource)))))
            
            (if (eq? what 'properties)
                (transmit-information '() '() '())
              ;; immediately transmit properties before doing
              ;; the potentially long computation of filenames
              (transmit-information #f #f '())
              (when kernel-source-access?
                (when (remote-proxy? (get-controller-debugger))
                  (for-each add-unit (product-units product))))
              (let ((filenames (queue-list queue))
                    (processes (list (get-local-process))))
                (terminal filenames processes)
                (transmit-information filenames processes '())))))
        'runtime-information)))
  
  
  ;;;
  ;;;; Evaluate
  ;;;
  
  
  (method override (evaluate-expressions self forms syntax str container line col evaluation-mode walk-debugger walk-cookie)
    (thread-call (primordial-thread) 'evaluate-expressions
      (lambda ()
        (let ((evaluate-hook (get-evaluate-forms-hook)))
          (when (or (not evaluate-hook)
                    (not (evaluate-hook forms syntax str container line col evaluation-mode walk-debugger walk-cookie)))
            (catch (Walk-Problems exc
                     (report-walk-problems walk-debugger (get-local-process) (exception-reason exc) (exception-detail exc) walk-cookie)
                     (unless (delay-reporting?)
                       (throw exc)))
              ;; quick hack for non-ide debuggers
              (let ((local-debugger (get-local-debugger (current-process))))
                (if local-debugger
                    (jazz.debugger.jazz:evaluating-code local-debugger
                      (lambda ()
                        (evaluate-forms syntax str container line col evaluation-mode)))
                  (evaluate-forms syntax str container line col evaluation-mode)))
              (hook-evaluate forms syntax str container line col)))))))
  
  
  ;;;
  ;;;; Profiler
  ;;;
  
  
  (method override (profiler-start self)
    (let ((profile (find-selected-profile)))
      (when profile
        (active-profile profile)
        (start-profiler profile))))
  
  
  (method override (profiler-stop self)
    (let ((profile (find-selected-profile)))
      (when profile
        (stop-profiler profile)
        (active-profile #f))))


  (method override (profiler-profiles self)
    (get-profile-names))
  
  
  (method override (profiler-selected-profile self)
    (get-selected-profile))
  
  
  (method override (profiler-selected-profile-set! self name)
    (set-selected-profile name))
  
  
  (method override (profiler-profile self)
    (define (package-frame frame)
      (or (and frame
               (bind (creator locat) frame
                 (and creator
                      (let ((name (if (symbol? creator) creator (get-procedure-name creator))))
                        (list name locat)))))
          frame))
    
    (define (merge-calls call-table calls)
      (iterate-table calls
        (lambda (proc count/duration)
          (let ((key (map package-frame proc)))
            (let ((local-count/duration (table-ref calls key #f)))
              (if local-count/duration
                  (table-set! call-table key (list (+ (first count/duration) (first local-count/duration))
                                                   (+ (second count/duration) (second local-count/duration))))
                (table-set! call-table key count/duration)))))))
    
    (define (profile-add profile remote-profile proc)
      (if remote-profile
          (+ (proc profile) (proc remote-profile))
        (proc profile)))
    
    (let ((profile (find-selected-profile)))
      (and profile
           (let ((remote-profile (and (current-application) (get-remote-profile (current-application) (profile-label profile)))))
             (let ((frames-count (profile-add profile remote-profile profile-frames-count))
                   (frames-duration (profile-add profile remote-profile profile-frames-duration))
                   (calls-count (profile-add profile remote-profile profile-calls-count))
                   (calls-duration (profile-add profile remote-profile profile-calls-duration))
                   (calls (let ((call-table (make-table test: equal?)))
                            (merge-calls call-table (profile-calls profile))
                            (when remote-profile
                              (merge-calls call-table (profile-calls remote-profile)))
                            call-table)))
               (list (profile-label profile)
                     (profiler-type (profile-profiler profile))
                     (profile-depth profile)
                     frames-count
                     frames-duration
                     calls-count
                     calls-duration
                     (profile-process-info profile)
                     (table->list calls)))))))
  
  
  (method override (profile-reset self profiler depth all?)
    (if all?
        (reset-profiles)
      (let ((profile (find-selected-profile)))
        (when profile
          (reset-profile profile (and profiler (require-service profiler)) depth)))))
  
  
  ;;;
  ;;;; Tracker
  ;;;
  
  
  (method override (tracker-start self)
    (start-tracker))
  
  
  (method override (tracker-stop self)
    (stop-tracker))


  (method override (tracker-trackings self)
    (get-tracking-names))
  
  
  (method override (tracker-selected-tracking self)
    (get-selected-tracking))
  
  
  (method override (tracker-selected-tracking-set! self name)
    (set-selected-tracking name))
  
  
  (method override (tracker-tracking self)
    (define (package-frame frame)
      (or (and frame
               (bind (creator locat) frame
                 (and creator
                      (let ((name (if (symbol? creator) creator (get-procedure-name creator))))
                        (list name locat)))))
          frame))
    
    (declare (proper-tail-calls))
    (let ((tracking (find-selected-tracking)))
      (and tracking
           (let ((allocations (extract-allocations tracking)))
             (list (get-label tracking)
                   (get-all tracking)
                   (map (lambda (allocation)
                          (bind (object file line stack) allocation
                            (list (package-info object) file line (and stack (map package-frame stack)))))
                        allocations))))))
  
  
  (method override (tracking-reset self all?)
    (if all?
        (reset-trackings)
      (let ((tracking (find-selected-tracking)))
        (when tracking
          (reset-tracking tracking)))))
  
  
  ;;;
  ;;;; Inspector
  ;;;
  
  
  (method override (inspect-serial self serial max-content)
    (inspect-value (serial->object serial) max-content: max-content))
  
  
  (method override (inspect-continuation self serial)
    (let ((k (serial->object serial)))
      (assert (continuation? k))
      (thread-start! (make-thread (lambda ()
                                    ;; Prevent problems with continuation-checkpoints
                                    (continuation-graft-no-winding k break))
                                  (format "Continuation #{a}" serial)
                                  debugged-continuations-thread-group))))
  
  
  (method override (present-serial self serial)
    (let ((port (open-output-string)))
      (display (serial->object serial) port)
      (get-output-string port)))
  
  
  ;;;
  ;;;; Designer
  ;;;
  
  
  (method override (try-out self class)
    (cond-expand
      (ios)
      (else
    (let ((class (resolve-runtime-reference (deserialize-runtime-reference class))))
      (when (subtype? class Component)
        (delay-event
          (lambda ()
            (try-out class))))))))))
