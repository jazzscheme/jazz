;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Debuggee Stop
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debuggee.jazz.Jazz-Debuggee-Stop jazz


(import (jazz.debuggee)
        (jazz.debuggee.autoload)
        (jazz.debuggee.stub)
        (jazz.debuggee.jazz.autoload)
        (jazz.presence))


(class Jazz-Debuggee-Stop extends Debuggee-Stop
  
  
  (slot thread       accessors generate)
  (slot kind)
  (slot reason)
  (slot detail)
  (slot location)
  (slot exception    accessors generate)
  (slot continuation accessors generate)
  (slot frames)
  (slot restarts)
  (slot stack        accessors generate)
  (slot stepper      accessors generate)
  
  
  (method override (initialize self thread kind reason detail exc continuation restarts locat stepper)
    (define max-depth
      1000)
    
    (define (collect-continuation-stack cont)
      (map (lambda (cont)
             (list cont (get-continuation-name cont) (boolean (hidden-frame? (continuation-parent cont)))))
           (get-continuation-stack cont max-depth)))
    
    (define (stop-locat stack)
      (let ((found (find-if (lambda (data) (not (third data))) stack)))
        (and found (exception-locat exc (first found)))))
    
    (nextmethod self)
    (let ((stack (collect-continuation-stack continuation)))
      (let ((location (locat->container/line/col (or locat (stop-locat stack)))))
        (set! self.thread thread)
        (set! self.kind kind)
        (set! self.reason reason)
        (set! self.detail detail)
        (set! self.location location)
        (set! exception exc)
        (set! self.continuation continuation)
        (set! self.stepper stepper)
        (set! self.restarts restarts)
        (set! self.stack stack))))
  
  
  (method override (get-id self)
    (object->serial self))
  
  
  (method override (get-kind self)
    kind)
  
  
  (method override (get-reason self)
    reason)
  
  
  (method override (get-location self)
    location)
  
  
  (method override (get-detail self)
    detail)
  
  
  (method override (get-restarts self)
    restarts)
  
  
  ;;;
  ;;;; Frames
  ;;;
  
  
  (method override (get-frames self)
    (define (make-frame id frame)
      (bind (cont name hidden?) frame
        ;; to test the stepper
        (let ((location
                (and stepper (= id 0)
                  location)))
        (let ((frame (new Jazz-Debuggee-Frame id location (object->serial cont) name hidden?)))
          (new Debuggee-Frame-Local-Proxy (require-presence 'debugging) frame)))))
    
    ;; gc protect
    (set! frames
          (map (lambda (id frame)
                 (make-frame id frame))
               (naturals 0 (length stack))
               stack))
    frames)))
