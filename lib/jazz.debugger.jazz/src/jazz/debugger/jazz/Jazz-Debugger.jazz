;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Debugger
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.debugger.jazz.Jazz-Debugger jazz


(import (jazz.console)
        (jazz.debuggee)
        (jazz.debugger)
        (jazz.io)
        (jazz.editor.jazz)
        (jazz.jrm)
        (jazz.media)
        (jazz.network)
        (jazz.system)
        (jazz.ui.view)
        (jazz.ui.window)
        (jazz.ui.workspace))


(class Jazz-Debugger extends Debugger

  
  (slot processes initialize '())
  
  
  ;;;
  ;;;; Debugger
  ;;;
  
  
  (method override (start-debugger)
    )
  
  
  (method override (stop-debugger)
    )
  
  
  (method override (get-processes (validate-liveness?: validate-liveness? #f))
    (when validate-liveness?
      (validate-processes))
    processes)
  
  
  (method override (detach-processes)
    (validate-processes)
    (let ((local-process (get-local-process)))
      (for-each (lambda (process-proxy)
                  (when (not (proxy=? process-proxy local-process))
                    (detach-process~ process-proxy)))
                processes)))
  
  
  (method public (validate-processes)
    (let ((valid (new-queue)))
      (for-each (lambda (process)
                  (if (live-process? process)
                      (enqueue valid process)
                    (close-process-debugger-resources (new Debugged-Process self process process))))
                processes)
      (set! processes (queue-list valid))))
  
  
  (method (live-process? process-proxy)
    (or (is? process-proxy Local-Proxy)
        (live?~ process-proxy)))
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method override (process=? x y)
    (proxy-server=? x y))
  
  
  (method override (process-live? process-proxy)
    (live?~ process-proxy))
  
  
  (method override (process-title process-proxy)
    (get-title~ process-proxy))
  
  
  (method override (process-icon process-proxy)
    (get-icon~ process-proxy))
  
  
  (method override (process-threads process-proxy)
    (get-threads~ process-proxy))
  
  
  (method override (present-process process-proxy)
    (let ((title (either (get-title~ process-proxy) "Process")))
      (if (local-proxy? process-proxy)
          title
        (let ((host (get-host~ (get-ior~ process-proxy))))
          (if (host=? host (listening-host~ (get-remote-listener)))
              title
            (format "{a} at {a}" title (host->string host)))))))
  
  
  (method override (reload-process process-proxy)
    (reload-process~ process-proxy))
  
  
  (method override (evaluate-expressions process-proxy syntax str container line col)
    (evaluate-expressions~ process-proxy syntax str container line col))
  
  
  (method override (inspect-serial process-proxy serial)
    (inspect-serial~ process-proxy serial))
  
  
  (method override (process-detach process-proxy)
    (detach-process~ process-proxy))
  
  
  (method override (process-exit process-proxy)
    (exit-process~ process-proxy))
  
  
  ;;;
  ;;;; Thread
  ;;;
  
  
  (method override (thread=? x-process x y-process y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (thread-name process-proxy thread-proxy)
    (get-name~ thread-proxy))
  
  
  (method override (thread-state process-proxy thread-proxy)
    (get-state~ thread-proxy))
  
  
  (method override (thread-stops process-proxy thread-proxy)
    (get-stops~ thread-proxy))
  
  
  (method override (thread-active-stop process-proxy thread-proxy)
    (let ((stops (thread-stops process-proxy thread-proxy)))
      (if (null? stops)
          {}
        (last stops))))
  
  
  (method override (break-thread process-proxy thread-proxy)
    (break-thread~ process-proxy thread-proxy))
  
  
  (method override (set-repl-frame process-proxy thread-proxy frame-proxy)
    (set-repl-frame~ thread-proxy frame-proxy))
    
  
  ;;;
  ;;;; Restart
  ;;;
  
  
  (method override (restart=? x-process x y-process y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (restart-name process-proxy restart-proxy)
    (get-name~ restart-proxy))
  
  
  (method override (restart-message process-proxy restart-proxy)
    (get-message~ restart-proxy))

  
  (method override (restart-stop process-proxy restart-proxy)
    (restart-stop~ process-proxy restart-proxy))

  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method override (stop=? x-process x y-process y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (stop-kind process-proxy stop-proxy)
    (get-kind~ stop-proxy))
  
  
  (method override (stop-reason process-proxy stop-proxy)
    (get-reason~ stop-proxy))
  
  
  (method override (stop-detail process-proxy stop-proxy)
    (get-detail~ stop-proxy))
  
  
  (method override (stop-location process-proxy stop-proxy)
    (get-location~ stop-proxy))
  
  
  (method override (stop-restarts process-proxy stop-proxy)
    (get-restarts~ stop-proxy))
  
  
  (method override (stop-frames process-proxy stop-proxy)
    (get-frames~ stop-proxy))
  
  
  (method override (continue-stop process-proxy stop-proxy)
    (continue-stop~ process-proxy stop-proxy))
  
  
  ;;;
  ;;;; Frame
  ;;;
  
  
  (method override (frame=? x-process x y-process y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (frame-name process-proxy frame-proxy)
    (get-name~ frame-proxy))
  
  
  (method override (frame-interpreted? process-proxy frame-proxy)
    (get-interpreted?~ frame-proxy))
  
  
  (method override (frame-location process-proxy frame-proxy)
    (get-location~ frame-proxy))
  
  
  (method override (frame-variables process-proxy frame-proxy kind)
    (get-variables~ frame-proxy kind))
  
  
  ;;;
  ;;;; Inspect
  ;;;
  
  
  (method public (inspect process-proxy info)
    (post-event
      (lambda ()
        (bind (serial presented more? mutable? kind) info
          (let ((title (format "Inspector - {a}" presented))
                (client (cons info :remote)))
            (new-frame Inspector-View host-title: title process: process-proxy client: client))))))
  
  
  ;;;
  ;;;; Process
  ;;;


  (method (attach-process process-proxy cookie select?)
    (unless (member? process-proxy processes test: process=?)
      (execute-event
        (lambda ()
          (set! processes (append! processes (list process-proxy)))
          (attach-process~ (get-debugger-manager) (new Debugged-Process self process-proxy process-proxy) select?)
          (when cookie
            (let ((proc (get-registered-cookie cookie)))
              (when proc
                (proc cookie))))))))
  
  
  (method (update-process process-proxy)
    (when (remote-proxy? process-proxy)
      (let ((actual (find-in processes process-proxy test: process=?)))
        (when (and actual (remote-proxy? actual))
          (set-values~ actual (get-values~ process-proxy))
          (execute-event
            (lambda ()
              (update-processes~ (get-processes-view))
              (update-focused-process~ (find-status-bar~ (get-application)))))))))


  (method (detach-process process-proxy)
    (when (member? process-proxy processes test: process=?)
      (execute-event
        (lambda ()
          (close-process-debugger-resources (new Debugged-Process self process-proxy process-proxy))
          (set! processes (remove! process-proxy processes test: process=?))
          (let ((process (new Debugged-Process self process-proxy process-proxy))
                (best-process (unless (proxy=? process-proxy (get-local-process))
                                (new Debugged-Process self (get-local-process) (get-local-process)))))
            (detach-process~ (get-debugger-manager) process best-process))))))
  
  
  ;;;
  ;;;; Console
  ;;;
  
  
  (method public (register-console process-proxy thread-proxy select?)
    (call-event
      (lambda ()
        (let ((process (new Debugged-Process self process-proxy process-proxy))
              (text (register-debugger-console process-proxy thread-proxy select?)))
          (let ((console (new Debugger-Resource
                           process
                           text
                           (lambda ()
                             (unregister-debugger-console text)))))
            (register-debugger-resource process console)
            (object->serial console))))))
  
  
  (method public (unregister-console resource)
    (close-resource~ (serial->object resource)))
  
  
  (method public (select-console resource)
    (let ((text (get-reference~ (serial->object resource))))
      (let ((console (get-console~ text)))
        (select-console~ (get-console-manager) console))))
  
  
  (method public (console-output resource str)
    (let ((text (get-reference~ (serial->object resource))))
      (debugger-console-output text str)))
  
  
  ;;;
  ;;;; Status
  ;;;
  
  
  (method public (status-message message)
    (user-message message))
  
  
  ;;;
  ;;;; Window
  ;;;
  
  
  (method public (get-toplevel-hwnd)
    (get-handle~ (get-toplevel)))
  
  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method public (register-stop process-proxy thread-proxy stop-proxy)
    (let ((process (new Debugged-Process self process-proxy process-proxy))
          (thread (new Debugged-Thread self process-proxy thread-proxy))
          (stop (new Debugged-Stop self process-proxy stop-proxy)))
      (execute-event
        (lambda ()
          (register-stop~ (get-debugger-manager) process thread stop)))))
  
  
  (method public (unregister-stop process-proxy thread-proxy stop-proxy)
    (let ((process (new Debugged-Process self process-proxy process-proxy))
          (thread (new Debugged-Thread self process-proxy thread-proxy))
          (stop (new Debugged-Stop self process-proxy stop-proxy)))
      (execute-event
        (lambda ()
          (unregister-stop~ (get-debugger-manager) process thread stop)))))))
