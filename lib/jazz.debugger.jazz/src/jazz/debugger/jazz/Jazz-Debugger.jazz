;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Debugger
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debugger.jazz.Jazz-Debugger jazz


(import (jazz.application)
        (jazz.console)
        (jazz.debuggee)
        (jazz.debugger)
        (jazz.editor.jazz)
        (jazz.event)
        (jazz.feedback)
        (jazz.io)
        (jazz.jrm)
        (jazz.network)
        (jazz.ui.view))


(class Jazz-Debugger extends Debugger

  
  (slot processes            initialize '())
  (slot process-filenames    initialize (make-table test: process=? hash: process-hash))
  (slot process-processes    initialize (make-table test: process=? hash: process-hash))
  (slot process-properties   initialize (make-table test: process=? hash: process-hash))
  (slot process-information? initialize (make-table test: process=? hash: process-hash))
  
  
  ;;;
  ;;;; Debugger
  ;;;
  
  
  (method override (start-debugger)
    )
  
  
  (method override (stop-debugger)
    )
  
  
  (method override (get-processes (validate-liveness?: validate-liveness? #f))
    (when validate-liveness?
      (validate-processes))
    processes)
  
  
  (method override (validate-processes)
    (define (live-process? process)
      (or (is? process Local-Proxy)
          (live?~ process)))
    
    (let ((valid (new-queue)))
      (for-each (lambda (process)
                  (if (live-process? process)
                      (enqueue valid process)
                    (clear-runtime-information process)
                    (detach-process~ (get-debugger-manager) (new Debugged-Process self process process))))
                processes)
      (set! processes (queue-list valid))))
  
  
  (method override (detach-processes)
    (validate-processes)
    (let ((local-process (get-local-process)))
      (for-each (lambda (process)
                  (when (not (proxy=? process local-process))
                    (detach-process~ process)))
                processes)))
  
  
  (method override (standard-process-image)
    #f)
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method override (process=? x y)
    (and (proxy-server=? x y)
         (equal? (get-id~ x) (get-id~ y))))
  
  
  (method override (process-live? process)
    (live?~ process))
  
  
  (method override (process-title process)
    (get-title~ process))
  
  
  (method override (process-icon process)
    (get-icon~ process))
  
  
  (method override (process-threads process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-threads~ process))))
  
  
  (method override (present-process process)
    (let ((title (present-process-title/traits (get-title~ process) (get-traits~ process))))
      (if (local-proxy? process)
          title
        (let ((host (get-host~ (get-ior~ process))))
          (if (host=? host (listening-host~ (get-remote-listener)))
              (format "{a} @" title)
            (format "{a} @ {a}" title (host->string host)))))))
  
  
  (method override (reload-process process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (reload-process~ process))))
  
  
  (method override (process-focusable? process)
    #t)
  
  
  (method override (transmit-runtime-information process what)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (transmit-runtime-information~ process what))))
  
  
  (method override (evaluate-expressions process forms syntax str container line col evaluation-mode walk-debugger walk-cookie)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (evaluate-expressions~ process forms syntax str container line col evaluation-mode walk-debugger walk-cookie))))
  
  
  (method override (profiler-start process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profiler-start~ process))))
  
  
  (method override (profiler-stop process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profiler-stop~ process))))
  
  
  (method override (profiler-profiles process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profiler-profiles~ process))))
  
  
  (method override (profiler-selected-profile process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profiler-selected-profile~ process))))
  
  
  (method override (profiler-selected-profile-set! process name)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profiler-selected-profile-set!~ process name))))
  
  
  (method override (profiler-profile process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profiler-profile~ process))))
  
  
  (method override (profile-reset process profiler depth all?)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (profile-reset~ process profiler depth all?))))
  
  
  (method override (inspect-serial process serial max-content)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (inspect-serial~ process serial max-content))))
  
  
  (method override (present-serial process serial)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (present-serial~ process serial))))
  
  
  (method override (try-out process class)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (try-out~ process class))))
  
  
  (method override (process-detach process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (detach-process~ process))))
  
  
  (method override (process-exit process)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (exit-process~ process))))
  
  
  ;;;
  ;;;; Thread
  ;;;
  
  
  (method override (thread=? x y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (thread-name process thread)
    (get-name~ thread))
  
  
  (method override (thread-state process thread)
    (get-state~ thread))
  
  
  (method override (thread-stops process thread)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-stops~ thread))))
  
  
  (method override (thread-active-stop process thread)
    (let ((stops (thread-stops process thread)))
      (if (null? stops)
          #f
        (last stops))))
  
  
  (method override (break-thread process thread)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (break-thread~ process thread))))
  
  
  (method override (terminate-thread process thread preserve-console?)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (terminate-thread~ process thread preserve-console?))))
  
  
  (method override (kill-thread process thread)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (kill-thread~ process thread))))
  
  
  (method override (set-repl-frame process thread frame)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (set-repl-frame~ thread frame))))
    
  
  ;;;
  ;;;; Restart
  ;;;
  
  
  (method override (restart=? x y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (restart-name process restart)
    (get-name~ restart))
  
  
  (method override (restart-message process restart)
    (get-message~ restart))

  
  (method override (restart-stop process restart)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (restart-stop~ process restart))))

  
  (method override (thread-debugged-continuation? thread)
    (get-debugged-continuation?~ thread))
  
  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method override (stop=? x y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (stop-kind process stop)
    (get-kind~ stop))
  
  
  (method override (stop-reason process stop)
    (get-reason~ stop))
  
  
  (method override (stop-detail process stop)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-detail~ stop))))
  
  
  (method override (stop-location process stop)
    (get-location~ stop))
  
  
  (method override (stop-restarts process stop)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-restarts~ stop))))
  
  
  (method override (stop-frames process stop)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-frames~ stop))))
  
  
  (method override (continue-stop process stop)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (continue-stop~ process stop))))
  
  
  (method override (step-stop process stop command)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (step-stop~ process stop command))))
  
  
  ;;;
  ;;;; Frame
  ;;;
  
  
  (method override (frame=? x y)
    (and (proxy-server=? x y)
         (= (get-id~ x) (get-id~ y))))
  
  
  (method override (frame-name process frame)
    (get-name~ frame))
  
  
  (method override (frame-interpreted? process frame)
    (get-interpreted?~ frame))
  
  
  (method override (frame-hidden? process frame)
    (get-hidden?~ frame))
  
  
  (method override (frame-location process frame)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-location~ frame))))
  
  
  (method override (frame-variables process frame kind)
    (with-jrm-exception-handler (new Debugged-Process self process process)
      (lambda ()
        (get-variables~ frame kind))))
  
  
  ;;;
  ;;;; Inspect
  ;;;
  
  
  (method public (inspect process info)
    (post-event
      (lambda ()
        (let ((debugged-process (new Debugged-Process self process process)))
          (inspect-info debugged-process info)))))
  
  
  (method public (inspect-info debugged-process info)
    (let ((client (cons info :remote)))
      (new-frame Inspector-View process: debugged-process client: client)))
  
  
  (method public (inspect-continuation debugged-process serial)
    (with-jrm-exception-handler debugged-process
      (lambda ()
        (inspect-continuation~ (get-process~ debugged-process) serial))))
  
  
  ;;;
  ;;;; Process
  ;;;


  (method (attach-process process focus?)
    (unless (member? process processes test: process=?)
      (execute-event
        (lambda ()
          (set! processes (append! processes (list process)))
          (attach-process~ (get-debugger-manager) (new Debugged-Process self process process) focus?)))))
  
  
  (method (ready-process process cookie)
    (execute-event
      (lambda ()
        (when cookie
          (invoke-registered-cookie-procedures (new Debugged-Process self process process) cookie)))))
  
  
  (method (update-process process)
    (when (remote-proxy? process)
      (let ((actual (find-in processes process test: process=?)))
        (when (and actual (remote-proxy? actual))
          (set-values~ actual (get-values~ process))
          (execute-event
            (lambda ()
              (update~ (get-processes-view))
              (update-focused-process~ (find-status-bar~ (get-application)))
              (update-console-buttons~ (get-console-manager))))))))


  (method (detach-process process)
    (when (member? process processes test: process=?)
      (execute-event
        (lambda ()
          (set! processes (remove! process processes test: process=?))
          (clear-runtime-information process)
          (detach-process~ (get-debugger-manager) (new Debugged-Process self process process))))))
  
  
  ;;;
  ;;;; Filenames
  ;;;
  
  
  (method (unavailable-runtime-information process)
    (table-set! process-filenames process '())
    (table-set! process-processes process '())
    (table-set! process-properties process '())
    (table-set! process-information? process #f)
    (process-evaluate-always~ (get-debugger-manager) (new Debugged-Process self process process))
    (let ((processes-view (get-processes-view)))
      (when processes-view
        (execute-event
          (lambda ()
            (update~ processes-view))))))
  
  
  (method (receive-runtime-information process filenames processes properties)
    (table-set! process-filenames process filenames)
    (table-set! process-processes process processes)
    (table-set! process-properties process properties)
    (table-set! process-information? process #t)
    (let ((processes-view (get-processes-view)))
      (when processes-view
        (execute-event
          (lambda ()
            (update~ processes-view))))))
  
  
  (method override (received-runtime-filenames? process)
    (table-ref process-filenames process #f))
  
  
  (method override (add-filename-processes queue filename)
    ;; quick hack for mac/windows parallels debugging
    (define (mac/windows-filename=? mac windows)
      (let ((effective-mac (cond ((starts-with? mac "/Volumes/C")
                                  (string-append "c:" (substring mac (string-length "/Volumes/C") (string-length mac))))
                                 ((starts-with? windows "z:")
                                  (let ((pos (search mac "/Users/")))
                                    (if pos
                                        (let ((pos (search mac "/" start: (string-length "/Users/"))))
                                          (if pos
                                              (string-append "z:" (substring mac pos (string-length mac)))
                                            filename))
                                      filename)))
                                 (else
                                  filename))))
        (filename=? effective-mac windows)))
    
    (define (add-process process)
      (let ((filenames (table-ref process-filenames process #f)))
        (when (and filenames
                   (member? filename filenames test: mac/windows-filename=?))
          (enqueue queue (new Debugged-Process self process process)))))
    
    (for-each add-process processes))
  
  
  (method override (clear-runtime-information process)
    (table-clear process-filenames process)
    (table-clear process-processes process)
    (table-clear process-properties process)
    (table-clear process-information? process))
  
  
  ;;;
  ;;;; Processes
  ;;;
  
  
  (method override (determine-process-processes process)
    (or (table-ref process-processes process #f) '()))
  
  
  (method override (determine-process-child? process child)
    (member? child (determine-process-processes process) test: process=?))
  
  
  ;;;
  ;;;; Properties
  ;;;
  
  
  (method override (determine-process-properties process)
    (table-ref process-properties process '()))
  
  
  (method override (determine-process-property process property default)
    (getf (determine-process-properties process) property not-found: default))
      
  
  ;;;
  ;;;; Information
  ;;;
  
  
  (method override (determine-process-information? process)
    (table-ref process-information? process #f))

  
  ;;;
  ;;;; Problems
  ;;;
  
  
  (definition last-reported-cookie
    #f)
  
  
  (method (report-walk-problems process detail cookie)
    (unless (eqv? cookie last-reported-cookie)
      (set! last-reported-cookie cookie)
      (let ((problems-view (get-problems-view)))
        (when problems-view
          (execute-event
            (lambda ()
              (let ((code (message-box (walk-problems-message detail)
                                       type: 'question
                                       title: "Problems"
                                       yes: "Highlight"
                                       no: "Ignore")))
                (case code
                  ((yes)
                   (report-problems~ problems-view detail highlight?: #t)
                   (ensure-focusable~ problems-view))))))))))
  
  
  ;;;
  ;;;; Console
  ;;;
  
  
  (method public (register-console process thread select?)
    (call-event
      (lambda ()
        (let ((actual (or (find-in processes process test: process=?) process)))
          (let ((debugged-process (new Debugged-Process self actual actual))
                (debugged-thread (new Debugged-Thread self actual thread)))
            (let ((text (register-console-text debugged-process debugged-thread select?)))
              (let ((console (new Debugger-Resource
                               debugged-process
                               debugged-thread
                               text
                               (lambda ()
                                 (unregister-console-text text)))))
                (register-debugger-resource debugged-process console)
                (object->serial console))))))))
  
  
  (method protected virtual (register-console-text debugged-process debugged-thread select?)
    (register-debugger-console debugged-process debugged-thread select?))
  
  
  (method protected virtual (unregister-console-text text)
    (unregister-debugger-console text))
  
  
  (method public (unregister-console resource)
    (close-resource~ (serial->object resource)))
  
  
  (method public virtual (select-console resource)
    (let ((text (get-reference~ (serial->object resource))))
      (let ((console (get-console~ text)))
        (select-console~ (get-console-manager) console))))
  
  
  (method public (persist-console resource)
    (persist-resource~ (serial->object resource)))
  
  
  (method public virtual (console-output resource str)
    (let ((text (get-reference~ (serial->object resource))))
      (debugger-console-output text str)))
  
  
  (method public virtual (clear-console resource)
    (let ((text (get-reference~ (serial->object resource))))
      (clear-console~ text)))
  
  
  ;;;
  ;;;; Status
  ;;;
  
  
  (method public (status-message message)
    (user-message message))
  
  
  ;;;
  ;;;; Window
  ;;;
  
  
  (method public (get-toplevel-hwnd)
    (get-handle~ (get-toplevel)))
  
  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method public (register-stop process thread stop (step?: step? #f))
    (let ((debugged-process (new Debugged-Process self process process))
          (debugged-thread (new Debugged-Thread self process thread))
          (debugged-stop (new Debugged-Stop self process stop)))
      (execute-event
        (lambda ()
          (register-stop~ (get-debugger-manager) debugged-process debugged-thread debugged-stop step?: step?)))))
  
  
  (method public (unregister-stop process thread stop)
    (let ((debugged-process (new Debugged-Process self process process))
          (debugged-thread (new Debugged-Thread self process thread))
          (debugged-stop (new Debugged-Stop self process stop)))
      (execute-event
        (lambda ()
          (unregister-stop~ (get-debugger-manager) debugged-process debugged-thread debugged-stop)))))))
