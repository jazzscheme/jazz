;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Projects
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.debugger.jazz.Jazz-Project jazz


(import (jazz.debugger)
        (jazz.designer)
        (jazz.jrm)
        (jazz.library)
        (jazz.network)
        (jazz.system)
        (jazz.ui.dialog)
        (jazz.ui.view)
        (jazz.utilities)
        (jedi)
        (jedi.builder))


(class Jazz-Project extends Project
  
  
  (property product      initialize {}  accessors generate)
  (property destination  initialize {}  accessors generate)
  (property repositories initialize '() accessors generate)


  ;;;
  ;;;; Concrete
  ;;;
  
  
  (method (concrete?)
    #t)


  ;;;
  ;;;; Run
  ;;;
  
  
  (method (project-run (profile: profile {}) (command-action: command-action {}) (command-arguments: command-arguments {}) (show-console?: show-console? #f) (wait?: wait? #f))
    (define (determine-executable)
      (let ((executable (project-executable)))
        (if (exists?~ executable)
            executable
          (project-kernel))))
      
    (if product
        (launch
          executable: (determine-executable)
          debugger?: #f
          profile: profile
          command-action: command-action
          command-arguments: command-arguments
          show-console?: show-console?
          wait?: wait?)
      (message-box "This project doesn't define a product property")))


  ;;;
  ;;;; Debug
  ;;;
  
  
  (method (project-debug (executable: executable {}) (debugger-interactive?: debugger-interactive? #f) (profile: profile {}) (command-action: command-action {}) (command-arguments: command-arguments {}) (show-console?: show-console? #f) (wait?: wait? #f))
    (if product
        (launch
          executable: executable
          debugger?: #t
          debugger-interactive?: debugger-interactive?
          profile: profile
          command-action: command-action
          command-arguments: command-arguments
          show-console?: show-console?
          wait?: wait?)
      (message-box "This project doesn't define a product property")))


  ;;;
  ;;;; Launch
  ;;;
  
  
  (method (launch (executable: executable {}) (debugger?: debugger? #f) (debugger-cookie: debugger-cookie {}) (debugger-interactive?: debugger-interactive? #f) (profile: profile {}) (command-action: command-action {}) (command-arguments: command-arguments {}) (show-console?: show-console? #f) (wait?: wait? #f))
    (define (determine-kernel)
      (cond ((eq? executable :choose) (choose-kernel))
            (executable executable)
            (else (project-kernel))))
    
    (define (choose-kernel)
      (choose-file
        title: "Choose Kernel"
        extensions: '("exe")))
    
    (let ((ide (get-process)))
      (when debugger?
        (start-remote-listener)
        (start-jazz-debugger~ ide))
      (let* ((executable               (determine-kernel))
             (runtime-arg              (list @wait "-:m250000,h1000000"))
             (product-arg              (list "-run" (->string product)))
             (debugger-arg             (if debugger? (list "-debugger" (encode-host/port (listening-host~ (get-remote-listener)) (listening-port~ (get-remote-listener)))) '()))
             (debugger-cookie-arg      (if debugger? (list "-debugger-cookie" (->string (either debugger-cookie (register-debugger-cookie (function (cookie) (clear-user-message)))))) '()))
             (debugger-interactive-arg (if (and debugger? debugger-interactive?) (list "-debugger-interactive" "true") '()))
             (profile                  (either profile (project-profile)))
             (profile-arg              (if profile (list "-profile" (->string profile)) '()))
             (action-arg               (if command-action (list command-action) '()))
             (arguments-arg            (if command-arguments command-arguments '()))
             (arguments                (append runtime-arg action-arg product-arg debugger-arg debugger-cookie-arg debugger-interactive-arg profile-arg arguments-arg)))
        (save-all~ ide)
        (when debugger?
          (user-message "Debugging {a}..." (get-presentation)))
        (launch-executable executable
          arguments: arguments
          show-console?: show-console?))))
  
  
  (method protected virtual (project-profile)
    {})
  
  
  (method public (project-kernel)
    (let ((executable (if destination
                          (find-kernel destination)
                        (find-actual-kernel))))
      (if (and executable (exists?~ executable))
          executable
        (message-box (format "Unable to find kernel{?~: {a}~}" destination))
        (throw-cancel))))
  
  
  (method (project-executable)
    (let ((kernel (project-kernel)))
      (new-brother~ kernel (format "{a}{a}" product (executable-extension kernel-platform)))))


  ;;;
  ;;;; Build
  ;;;


  (method (project-build)
    (if product
        (begin
          ;; should be only the project's files...
          (save-all~ (get-application))
          (let ((kernel (project-kernel)))
            (launch-executable kernel arguments: (list "-make" (->string product)) show-console?: #t)))
      (message-box "This project doesn't define a product property")))


  ;;;
  ;;;; Designer
  ;;;
  
  
  (method meta (get-class-image)
    {Bitmap-Resource "ConcreteProject"})
  
  
  (method meta (property-presentation property)
    (case (field-name property)
      ((product) "Product")
      ((destination) "Destination")
      ((repositories) "Repositories")
      (else (nextmethod property))))
  
  
  (method (get-categorized-properties)
    '(title
      include-extensions
      catalog? catalog-extensions catalog-heuristics
      product
      destination
      repositories))
  
  
  (method (get-row-instance property)
    (case (field-name property)
      ((destination) (new String-Row nullable?: #t))
      ((repositories) (new Repositories-Row))
      (else (nextmethod property)))))


;;;
;;;; Repositories-Row
;;;


(class Repositories-Row extends Value-Row
  
  
  ;;;
  ;;;; Presentation
  ;;;
  
  
  (method (present names)
    (if (null? names)
        "{}"
      (format "{l}" names)))
  
  
  ;;;
  ;;;; Edition
  ;;;
  
  
  (method (get-edited-value)
    (let ((content (get-string-content~ editor)))
      (if (or (empty-string? content) (string=? content "{}"))
          '()
        (read-string-content content))))))
