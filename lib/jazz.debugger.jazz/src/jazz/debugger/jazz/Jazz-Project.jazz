;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Projects
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debugger.jazz.Jazz-Project jazz


(import (jazz.debugger)
        (jazz.debugger.gambit)
        (jazz.designer)
        (jazz.io)
        (jazz.jml)
        (jazz.jrm)
        (jazz.project)
        (jazz.network)
        (jazz.system)
        (jazz.ui.dialog)
        (jazz.ui.view))


(class Jazz-Project extends Gambit-Project
  
  
  (property directory        initialize #f accessors generate)
  (property configuration    initialize #f accessors generate)
  (property product          initialize #f accessors generate)
  (property profile          initialize #f accessors generate)
  (property arguments        initialize #f accessors generate)
  (property debug-script     initialize #f accessors generate)
  (property test-unit        initialize #f accessors generate)
  (property validate-script  initialize #f accessors generate)
  (property script-arguments initialize #f accessors generate)


  ;;;
  ;;;; Run
  ;;;
  
  
  (method override (project-run (profile: profile #f) (command-action: command-action #f) (command-arguments: command-arguments #f) (show-console?: show-console? #f) (wait?: wait? #f))
    (if product
        (launch
          executable: (project-kernel)
          debugger?: #f
          profile: profile
          command-action: command-action
          command-arguments: command-arguments
          show-console?: show-console?
          wait?: wait?
          invite: "Running")
      (message-box "This project doesn't define a product property")))


  ;;;
  ;;;; Debug
  ;;;
  
  
  (method override (project-debug (executable: executable #f) (debugger-interactive?: debugger-interactive? #f) (debugger-cookie: debugger-cookie #f) (profile: profile #f) (command-action: command-action #f) (command-arguments: command-arguments #f) (show-console?: show-console? #f) (wait?: wait? #f))
    (if debug-script
        (parameterize ((current-script-arguments (cons self (or script-arguments '()))))
          (load-script (parse~ debug-script)))
      (if product
          (launch
            executable: executable
            debugger?: #t
            debugger-interactive?: debugger-interactive?
            debugger-cookie: debugger-cookie
            profile: profile
            command-action: command-action
            command-arguments: command-arguments
            show-console?: show-console?
            wait?: wait?)
        (message-box "This project doesn't define a product property"))))


  ;;;
  ;;;; Test
  ;;;
  
  
  (method override (project-test (profile: profile #f) (command-action: command-action #f) (command-arguments: command-arguments #f) (show-console?: show-console? #f) (wait?: wait? #f))
    (cond (test-unit
           (load-unit test-unit))
          (product
           (launch
             executable: (project-kernel)
             action: "-test"
             debugger?: #t
             profile: profile
             command-action: command-action
             command-arguments: command-arguments
             show-console?: show-console?
             wait?: wait?
             invite: "Testing"))
          (else
           (message-box "This project doesn't define a product property"))))


  ;;;
  ;;;; Validate
  ;;;
  
  
  (method override (project-validate (executable: executable #f) (debugger-interactive?: debugger-interactive? #f) (debugger-cookie: debugger-cookie #f) (profile: profile #f) (command-action: command-action #f) (command-arguments: command-arguments #f) (show-console?: show-console? #f) (wait?: wait? #f))
    (if validate-script
        (parameterize ((current-script-arguments (cons self (or script-arguments '()))))
          (load-script (parse~ validate-script)))
      (if product
          (launch
            executable: executable
            debugger?: #t
            debugger-interactive?: debugger-interactive?
            debugger-cookie: debugger-cookie
            validate?: #t
            profile: profile
            command-action: command-action
            command-arguments: command-arguments
            show-console?: show-console?
            wait?: wait?
            invite: "Validating")
        (message-box "This project doesn't define a product property"))))


  ;;;
  ;;;; Launch
  ;;;
  
  
  (method override (launch (executable: executable #f) (directory: directory #f) (kernel-arguments: kernel-arguments #f) (action: action #f) (debugger?: debugger? #f) (debugger-cookie: debugger-cookie #f) (debugger-interactive?: debugger-interactive? #f) (validate?: validate? #f) (profile: profile #f) (command-action: command-action #f) (command-arguments: command-arguments #f) (show-console?: show-console? #f) (wait?: wait? #f) (invite: invite #f))
    (define (determine-kernel)
      (cond ((eq? executable :choose) (choose-kernel))
            (executable executable)
            (else (project-kernel))))
    
    (define (choose-kernel)
      (choose-file
        title: "Choose Kernel"
        extensions: (list (executable-extension kernel-platform))))
    
    (let ((ide (get-process))
          (cookie (or debugger-cookie (register-debugger-cookie))))
      (when debugger?
        (start-remote-server~ ide)
        (start-debuggers~ ide))
      (register-debugger-cookie-procedure cookie
        (lambda (process cookie)
          (clear-user-message)))
      (let* ((executable               (determine-kernel))
             (runtime-arg              (list @wait "-:m250000,h1000000"))
             (product-arg              (list (or action "-run") (->string product)))
             (kernel-arguments-arg     (if kernel-arguments kernel-arguments '()))
             (debugger-arg             (if debugger? (list "-debugger" (encode-host/port (listening-host~ (get-remote-listener)) (listening-port~ (get-remote-listener)))) '()))
             (debugger-cookie-arg      (if debugger? (list "-debugger-cookie" (->string cookie)) '()))
             (debugger-interactive-arg (if (and debugger? debugger-interactive?) (list "-debugger-interactive" "true") '()))
             (process-title-arg        (list "-process-title" (project-title)))
             (process-traits-arg       (let ((traits (get-default-traits))) (if traits (list "-process-traits" (present-process-traits traits)) '())))
             (validate-arg             (if validate? (list "-validate" "true") '()))
             (profile                  (or profile (project-profile)))
             (profile-arg              (if profile (list "-profile" (->string profile)) '()))
             (action-arg               (if command-action (list command-action) '()))
             (arguments-arg            (if command-arguments command-arguments '()))
             (project-arg              (if arguments arguments '()))
             (toplevel-arg             (let ((pref (get-preferences 'debug)))
                                         (let ((toplevel-state (get-toplevel-state~ pref))
                                               (toplevel-monitor (get-toplevel-monitor~ pref))
                                               (toplevel-position (get-toplevel-position~ pref))
                                               (toplevel-size (get-toplevel-size~ pref)))
                                           (append (if toplevel-state (list "-toplevel-state" (->string toplevel-state)) '())
                                                   (if toplevel-monitor (list "-toplevel-monitor" (->string toplevel-monitor)) '())
                                                   (if toplevel-position (list "-toplevel-position" (point->argument toplevel-position)) '())
                                                   (if toplevel-size (list "-toplevel-size" (dimension->argument toplevel-size)) '())))))
             (arguments                (append runtime-arg action-arg product-arg kernel-arguments-arg debugger-arg debugger-cookie-arg debugger-interactive-arg process-title-arg process-traits-arg validate-arg profile-arg arguments-arg project-arg toplevel-arg)))
        (save-all~ ide)
        (when debugger?
          (user-message "{a} {a}..." (or invite "Debugging") (get-presentation-property)))
        @debug
        (debug arguments)
        (launch-executable executable
          directory: (or directory directory~self)
          arguments: arguments
          show-console?: show-console?))))
  
  
  (method protected virtual (project-profile)
    profile)
  
  
  (method public (project-kernel)
    (let ((executable (if directory
                          (let ((kernel-name (add-extension "kernel" (executable-extension kernel-platform))))
                            (cond ((directory? configuration)
                                   (new-directory~ configuration kernel-name))
                                  ((string? configuration)
                                   (new-file~ (new-directory~ directory configuration) kernel-name))
                                  ((not configuration)
                                   (find-actual-kernel))
                                  (else
                                   (error "Ill-formed configuration: {s}" configuration))))
                        (find-actual-kernel))))
      (if (and executable (exists?~ executable))
          executable
        (message-box (format "Unable to find kernel{?~: {a}~}" executable))
        (signal-cancel))))
  
  
  (method (project-executable)
    ;; quick hack
    (define (filename-heuristic build)
      (let ((info (car build)))
        (if (symbol? info)
            (symbol->string info)
          (symbol->string (car info)))))
    
    (receive (package descriptor) (get-product-descriptor product)
      (let ((build (product-descriptor-build descriptor)))
        (if build
            (let ((kernel (project-kernel))
                  (filename (filename-heuristic build)))
              (new-brother~ kernel (add-extension filename (executable-extension kernel-platform))))
          (message-box "This project doesn't define a build descriptor")
          (signal-cancel)))))


  ;;;
  ;;;; Build
  ;;;


  (method override (project-build)
    (if product
        (begin
          ;; should be only the project's files...
          (save-all~ (get-application))
          (let* ((kernel           (project-kernel))
                 (repositories-arg (let ((projects (find-repository 'Projects))) (if projects (list "-repositories" (format "\"{a}\"" (repository-directory projects))) '())))
                 (make-arg         (list "-make" (->string product)))
                 ;; until truncation bug fixed in parallel build
                 (debug-arg        (list "-debug"))
                 (arguments        (append repositories-arg make-arg debug-arg)))
            (launch-executable kernel arguments: arguments show-console?: #t)))
      (message-box "This project doesn't define a product property")))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Jazz-Project-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Jazz-Project-Descriptor extends Gambit-Project-Descriptor
  
  
  (form
    (<install>
      (<properties~>
        (<Property-Descriptor> name: directory
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: configuration
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: product
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: profile
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: arguments
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: debug-script
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: validate-script
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: script-arguments
          (<Value-Domain>      name: domain           nullable?: #t))
        (<Property-Descriptor> name: test-unit
          (<Value-Domain>      name: domain           nullable?: #t))))))))
