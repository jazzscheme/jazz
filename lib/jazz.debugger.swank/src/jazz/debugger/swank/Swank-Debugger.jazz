;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Swank Debugger
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debugger.swank.Swank-Debugger jazz


(import (jazz.console)
        (jazz.debugger)
        (jazz.debugger.jazz)
        (jazz.event)
        (jazz.inspector)
        (jazz.resource)
        (jazz.text)
        (jazz.view))


(class Swank-Debugger extends Debugger)


@wait
(class Swank-Debugger extends Debugger
  
  
  (slot processes initialize '())
  (slot stops     initialize '())
  
  
  ;;;
  ;;;; Debugger
  ;;;
  
  
  (method override (start-debugger self)
    )
  
  
  (method override (stop-debugger self)
    )
  
  
  (method override (get-processes self (validate-liveness?: validate-liveness? #f))
    processes)
  
  
  (method override (validate-processes self)
    )
  
  
  (method override (detach-processes self)
    )
  
  
  (method override (standard-process-image self)
    {Image-Resource "Interface"})
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method override (process=? self x y)
    (eq? x y))
  
  
  (method override (process-live? self process)
    #t)
  
  
  (method override (process-title self process)
    (get-title process))
  
  
  (method override (process-icon self process)
    "Grab")
  
  
  (method override (process-threads self process)
    (collect (lambda (stop)
               (let ((thread (get-thread stop)))
                 (and (process=? self (get-process thread) process)
                      thread)))
             stops))
  
  
  (method override (present-process self process)
    (process-title self process))
  
  
  (method override (reload-process self process)
    )
  
  
  (method override (process-focusable? self process)
    #t)
  
  
  (method override (evaluate-expressions self process forms syntax str container line col evaluation-mode walk-debugger walk-cookie)
    )
  
  
  (method override (inspect-serial self process serial max-content)
    '())
  
  
  (method override (present-serial self process serial)
    (unimplemented))
  
  
  (method override (try-out self process class)
    (unimplemented))
  
  
  (method override (process-detach self process)
    )
  
  
  (method override (process-quit self process)
    (swank-disconnect self process)
    (detach-process self process))
  
  
  ;;;
  ;;;; Thread
  ;;;
  
  
  (method override (thread=? self x y)
    (and (process=? self (get-process x) (get-process y))
         (= (get-id x) (get-id y))))
  
  
  (method override (get-thread-name self process thread)
    "Thread")
  
  
  (method override (get-thread-state self process thread)
    #f)
  
  
  (method override (get-thread-priority self process thread)
    #f)
  
  
  (method override (get-thread-id self process thread)
    #f)
  
  
  (method override (get-thread-stops self process thread)
    (collect-if (lambda (stop)
                  (thread=? self (get-thread stop) thread))
                stops))
  
  
  (method override (get-thread-active-stop self process thread)
    (let ((stops (get-thread-stops self process thread)))
      (if (null? stops)
          #f
        (last stops))))
  
  
  (method override (get-thread-virtual-stop self process thread)
    #f)
  
  
  (method override (break-thread self process thread)
    )
  
  
  (method override (repl-thread self process thread)
    )
  
  
  (method override (terminate-thread self process thread preserve-console?)
    )
  
  
  (method override (set-repl-frame self process thread frame)
    )
  
  
  (method override (thread-debugged-continuation? self thread)
    #f)
  
  
  ;;;
  ;;;; Restart
  ;;;
  
  
  (method override (restart=? self x y)
    (eq? x y))
  
  
  (method override (restart-name self process restart)
    (get-title restart))
  
  
  (method override (restart-message self process restart)
    (format "[{a}] {a}" (get-title restart) (get-message restart)))
  
  
  (method override (restart-stop self process restart)
    (swank-restart self process (get-rank restart)))
  
  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method override (stop=? self x y)
    (and (thread=? self (get-thread x) (get-thread y))
         (= (get-id x) (get-id y))))
  
  
  (method override (stop-kind self process stop)
    'exception)
  
  
  (method override (stop-reason self process stop)
    (car (get-condition stop)))
  
  
  (method override (stop-detail self process stop)
    #f)
  
  
  (method override (stop-location self process stop)
    #f)
  
  
  (method override (stop-restarts self process stop)
    (get-restarts stop))
  
  
  (method override (stop-frames self process stop)
    (get-frames stop))
  
  
  (method override (continue-stop self process stop)
    )
  
  
  (method override (step-stop self process stop command)
    )
  
  
  ;;;
  ;;;; Frame
  ;;;
  
  
  (method override (frame=? self x y)
    (eq? x y))
  
  
  (method override (frame-name self process frame)
    (cadr frame))
  
  
  (method override (frame-interpreted? self process frame)
    #t)
  
  
  (method override (frame-hidden? self process frame)
    #f)
  
  
  (method override (frame-location self process frame)
    #f)
  
  
  (method override (frame-variables self process frame kind)
    (map (lambda (variable)
           (let ((name (getf variable :name))
                 (value (getf variable :value))
                 (mutable? #f))
             (cons name (package-info value kind: :raw mutable?: mutable?))))
         (swank-frame-variables self process (car frame) kind)))
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method package (attach-process self process focus?)
    (unless (member? process processes test: (~ process=? self))
      (call-event
        (lambda ()
          (set! processes (append! processes (list process)))
          (let ((debugged-process (new Debugged-Process self process process)))
            (attach-process (get-debugger-manager) debugged-process focus?)
            debugged-process)))))
  
  
  (method package (detach-process self process)
    (when (member? process processes test: (~ process=? self))
      (send-event
        (lambda ()
          (set! processes (remove! process processes test: (~ process=? self)))
          (detach-process (get-debugger-manager) (new Debugged-Process self process process))))))
  
  
  ;;;
  ;;;; Swank
  ;;;
  
  
  (definition swank-port-number
    4005)
  
  
  (method (swank-connect self)
    (let ((port (open-tcp-client (list port-number: swank-port-number))))
      (let ((process (new Swank-Process port)))
        (let ((info (swank-connection-info self process)))
          (let ((implementation (getf info :lisp-implementation))
                (package (getf info :package)))
            (let ((type (getf implementation :type))
                  (version (getf implementation :version)))
              (set-title process type)
              (set-process-id process (getf info :pid))
              (set-package-name process (and package (getf package :name)))
              (swank-create-repl self process)
              (attach-process self process #f)
              (let ((text (register-console self process type #t)))
                (set-console process text)
                (when (and type version)
                  (console-write self process (format "{a} {a}{%}{%}" type version))))))))))
  
  
  (method (swank-disconnect self process)
    (let ((console (get-console process)))
      (close-port (get-port process))
      (unregister-console self console)))
  
  
  (method package (register-console self process title select?)
    (define (setup-debugger-console-ports text)
      (receive (head tail) (open-string-pipe (list permanent-close: #f))
        (set-console-port text head)
        tail))
    
    (let ((manager (get-console-manager-guest)))
      (let ((text (register-console manager title select?)))
        (let ((port (setup-debugger-console-ports text))
              (resource (new Debugger-Resource
                             process
                             #f
                             text
                             (lambda ()
                               (unregister-console self text)))))
          (register-debugger-resource process resource)
          (start-swank-repl self process port)
          (set-console process text)
          (set-console-resource process resource)
          (when select?
            (acquire-focus text))
          text))))
  
  
  (method package (unregister-console self text)
    (unregister-console (get-console-text-manager text) text))
  
  
  (method (console-write self process str)
    (let ((text (get-console process)))
      (send-event
        (lambda ()
          (set-caret text (text-ending text))
          (insert-formatted-line text str)))))
  
  
  (method (start-swank-repl self process port)
    (define (swank-repl)
      (declare (proper-tail-calls))
      (let (loop)
        (console-write self process (prompt 0))
        (let ((expr (read port)))
          (if (eof-object? expr)
              (begin
                (swank-disconnect self process)
                (console-write self process "disconnected"))
            (swank-eval self process expr)
            (loop)))))
    
    (define (prompt level)
      (if (> level 0)
          (format "{a}> " level)
        "> "))
    
    (thread-start!
      (new-thread
        (lambda ()
          (swank-repl))
        'swank-repl)))
  
  
  (method package (register-stop self process thread stop)
    (let ((debugged-process (new Debugged-Process self process process))
          (debugged-thread (new Debugged-Thread self process thread))
          (debugged-stop (new Debugged-Stop self process stop)))
      (send-event
        (lambda ()
          (set! stops (cons stop stops))
          (register-stop (get-debugger-manager) debugged-process debugged-thread debugged-stop)))))
  
  
  (method package (unregister-stop self process thread stop)
    (let ((debugged-process (new Debugged-Process self process process))
          (debugged-thread (new Debugged-Thread self process thread))
          (debugged-stop (new Debugged-Stop self process stop)))
      (send-event
        (lambda ()
          (set! stops (remove! stop stops test: (~ stop=? self)))
          (unregister-stop (get-debugger-manager) debugged-process debugged-thread debugged-stop)))))
  
  
  (method (swank-connection-info self process)
    (let ((id (swank-unique-id self process)))
      (send-swank-message self process `(:emacs-rex (swank:connection-info) "COMMON-LISP-USER" t ,id))
      (wait-swank-response self process id)))
  
  
  (method (swank-create-repl self process)
    (let ((package (get-package-name process))
          (id (swank-unique-id self process)))
      (send-swank-message self process `(:emacs-rex (swank:create-repl nil) ,package t ,id))
      (wait-swank-response self process id)))
  
  
  (method (swank-eval self process expr)
    (let ((package (get-package-name process))
          (id (swank-unique-id self process))
          (str (format "{s}" expr)))
      (send-swank-message self process `(:emacs-rex (swank:listener-eval ,str) ,package :repl-thread ,id))
      (wait-swank-response self process id)))
  

  (method (swank-frame-variables self process frame kind)
    (let ((package (get-package-name process))
          (id (swank-unique-id self process)))
      (send-swank-message self process `(:emacs-rex (swank:frame-locals-and-catch-tags ,frame) ,package :repl-thread ,id))
      (bind (locals catch-tags) (wait-swank-response self process id)
        (append (swank-decode self locals)
                ;; need to understand the format of catch-tags
                '()))))
  
  
  (method (swank-decode self expr)
    (if (eq? expr 'nil)
        '()
      expr))
  
  
  (method (swank-restart self process n)
    (let ((package (get-package-name process))
          (id (swank-unique-id self process)))
      (send-swank-message self process `(:emacs-rex (swank:invoke-nth-restart-for-emacs 1 ,n) ,package 0 ,id))
      (wait-swank-response self process id)))
  
  
  (method (swank-debug-return self process thread level)
    (let ((thread (new Swank-Thread process thread)))
      (let ((stop (find-if (lambda (stop)
                             (and (thread=? self (get-thread stop) thread)
                                  (= (get-level stop) level)))
                           stops)))
        (when stop
          (unregister-stop self process thread stop)))))
  
  
  (method (swank-unique-id self process)
    (generate-unique-id process))
  
  
  (method (send-swank-message self process expr)
    (define (encode-length message)
      (format "{x width: 6 justify: :right padding: :v}" (cardinality message) #\0))
    
    (let ((port (get-port process))
          (message (->string expr :reader)))
      (let ((message-length (encode-length message)))
        (display message-length port)
        (display message port)
        (force-output port))))
  
  
  (method (receive-swank-response self process)
    (let ((port (get-port process))
          (header (make-string 6)))
      (and (= (read-substring header 0 6 port) 6)
           (let ((count (string->number header 16)))
             (let ((response (make-string count)))
               (and (= (read-substring response 0 count port) count)
                    (read-string-element response)))))))
  
  
  (method (receive-swank-response-non-blocking self process)
    (let ((port (get-port process))
          (header (make-string 6)))
      (input-port-timeout-set! port 0.1)
      (and (= (read-substring header 0 6 port) 6)
           (let ((count (string->number header 16)))
             (let ((response (make-string count)))
               (input-port-timeout-set! port 0.1)
               (and (= (read-substring response 0 count port) count)
                    (read-string-element response)))))))
  

  (method (wait-swank-response self process target-id)
    (define (create-restarts restarts)
      (map (lambda (restart rank)
             (let ((title (car restart))
                   (message (cadr restart)))
               (new Swank-Restart title message rank)))
           restarts
           (naturals 0 (length restarts))))
    
    ;; quick try
    (define (process-events)
      (declare (proper-tail-calls))
      (let (loop)
        (let ((response (receive-swank-response-non-blocking self process)))
          (when response
            (process-swank-event self process response)
            (loop)))))
    
    (declare (proper-tail-calls))
    (let (loop)
      (let ((response (receive-swank-response self process)))
        (if (not response)
            #f
          (let ((kind (car response)))
            (case kind
              ((:return)
               (let ((id (caddr response)))
                 (if (= target-id id)
                     (case (car (cadr response))
                       ((:ok)
                        ;(process-events)
                        (cadr (cadr response)))
                       ((:abort)
                        ;(process-events)
                        (process-swank-abort self process response)))
                   (process-swank-problem self process response)
                   (loop))))
              ((:debug)
               (bind (kind thread level condition restarts frames ids) response
                 (if (memv target-id ids)
                     (let ((thread (new Swank-Thread process thread)))
                       (let ((stop (new Swank-Stop thread target-id level condition (create-restarts restarts) frames)))
                         (register-stop self process thread stop)
                         ;(process-events)
                         response))
                   (process-swank-problem self process response)
                   (loop))))
              (else
               (process-swank-event self process response)
               (loop))))))))
  
  
  (method (process-swank-abort self process abort)
    @debug
    (debug 'swank 'abort abort)
    #f)
  
  
  (method (process-swank-problem self process problem)
    @debug
    (debug 'swank 'problem problem))
  
  
  (method (process-swank-event self process event)
    (let ((kind (car event)))
      (case kind
        ((:write-string)
         (console-write self process (cadr event)))
        ((:new-features)
         )
        ((:indentation-update)
         )
        ((:debug-return)
         (swank-debug-return self process (cadr event) (caddr event)))
        (else
         @debug
         (debug 'swank 'event process event)))))
  
  
  ;;;
  ;;;; Processes
  ;;;
  
  
  (method override (determine-process-processes self process)
    '())
  
  
  (method override (determine-process-child? self process child)
    #f)
  
  
  ;;;
  ;;;; Properties
  ;;;
  
  
  (method override (determine-process-properties self process)
    '())
  
  
  (method override (determine-process-property self process property default)
    default)
      
  
  ;;;
  ;;;; Information
  ;;;
  
  
  (method override (determine-process-information? self process)
    #f)))
