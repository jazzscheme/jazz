;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Debugger Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debugger.debugger.Debugger-Manager jazz


(import (jazz.debuggee)
        (jazz.debugger)
        (jazz.graphic)
        (jazz.io)
        (jazz.media)
        (jazz.system.access)
        (jazz.ui)
        (jazz.ui.view))


(constant stopped-frame-color     {Color name: Dark-Red})
(constant visited-highlight-color {Color red: 255 green: 205 blue: 205})


(class Debugger-Manager extends Object
  
  
  (slot active-stops    initialize (make-table test: debugged=? hash: debugged-hash))
  (slot unvisited-stops initialize (make-table test: debugged=? hash: debugged-hash))
  (slot focused-process initialize #f getter generate)
  
  (slot process         initialize #f getter generate)
  (slot thread          initialize #f getter generate)
  (slot frame           initialize #f getter generate)
  
  
  (method public (debugging?)
    (/= 0 (table-length active-stops)))
  
  
  ;;;
  ;;;; Status
  ;;;
  
  
  (method public (visit-debugged debugged)
    (unless (debugged-visited? debugged)
      (table-set! unvisited-stops debugged)
      (update-status)
      #t))
  
  
  (method public (update-status)
    (let ((toolbar (find-toolbar~ (get-application))))
      (when toolbar
        (let ((button (find-component~ toolbar 'debugger))
              (visited? (> (table-length unvisited-stops) 0))
              (stopped? (> (table-length active-stops) 0)))
          (when button
            (notify~ button visited? stopped?))))))
  
  
  (method public (get-stopped-frame-color debugged)
    (when (debugged-stopped? debugged)
      stopped-frame-color))
  
  
  (method public (get-visited-highlight-color debugged)
    (unless (debugged-visited? debugged)
      visited-highlight-color))
  
  
  (method (debugged-stopped? debugged)
    (not-null? (table-ref active-stops debugged '())))
  
  
  (method (debugged-visited? debugged)
    (not (table-ref unvisited-stops debugged #f)))
  
  
  (method public (get-active-stops debugged)
    (table-ref active-stops debugged '()))
  
  
  ;;;
  ;;;; Focused
  ;;;
  
  
  (method public (require-focused-process)
    (or (get-focused-process)
        (begin
          (message-box "There is no focused process")
          (throw-cancel))))
  
  
  (method public (focus-process process)
    (unless (debugged=? focused-process process)
      (set-focus-process process)
      (when (get-processes-view)
        (update~ (get-processes-view)))))
  
  
  (method (set-focus-process process)
    (set! focused-process process)
    (let ((focus-status-view (find-status-bar~ (get-application))))
      (when focus-status-view
        (update-focused-process~ focus-status-view))))
  
  
  ;;;
  ;;;; Processes
  ;;;
  
  
  (method public (attach-process process focus?)
    (when (and focus? (focusable?~ process))
      (set-focus-process process))
    (when (get-processes-view)
      (update~ (get-processes-view))))
  
  
  (method public (detach-process process)
    (define (local-debugged-process)
      (let ((local-debugger (get-local-debugger~ (get-application)))
            (local-process (get-local-process)))
        (new Debugged-Process local-debugger local-process local-process)))
    
    (define (cleanup-stops table)
      (for-each (lambda (key)
                  (when (shared-process?~ process key)
                    (table-clear table key)))
                (table-keys table)))
    
    (when (debugged=? focused-process process)
      (let ((best-process (local-debugged-process)))
        (unless (debugged=? best-process process)
          (set-focus-process best-process))))
    (close-process-debugger-resources process)
    (cleanup-stops active-stops)
    (cleanup-stops unvisited-stops)
    (let ((view (get-processes-view)))
      (when view
        (update~ view)
        (unless (selected-process~ view)
          (process-changed #f))))
    (update-status))
  
  
  (method public (for-each-process proc)
    (for-each (lambda (debugger)
                (for-each (lambda (process)
                            (let ((debugged-process (new Debugged-Process debugger process process)))
                              (let ((live? (live?~ debugged-process))
                                    (focused? (debugged=? focused-process debugged-process)))
                                (proc debugger debugged-process live? focused?))))
                          (get-processes~ debugger)))
              (get-debuggers)))
  
  
  (method public (process-changed new-process)
    (let ((new-process (when (and new-process (get-alive?~ new-process)) new-process)))
      (unless (debugged=? process new-process)
        (catch (Cancel-Signal exc
                 (when (get-processes-view)
                   (update~ (get-processes-view))))
          (set! process new-process)
          (when (visit-debugged process)
            (update-processes-highlight process))
          (let ((view (get-threads-view)))
            (when view
              (process-changed~ view process)
              (unless (selected-thread~ view)
                (thread-changed #f))))))))
  
  
  (definition (update-processes-highlight process)
    (when (get-processes-view)
      (update-highlight~ (get-processes-view) process)))
  
  
  ;;;
  ;;;; Threads
  ;;;
  
  
  (method public (for-each-thread proc (detailed?: detailed? #f))
    (define system-threads
      '(message-pump
         debuggee-console-pump
         debugger-console-pump
         remote-listener
         remote-connection
         catalog
         autorepeat
         autoscroll
         caret
         hovering))
    
    (define (system-thread? thread)
      (let ((name (get-name~ thread)))
        (or (memq? name system-threads)
            (and (symbol? name)
                 (starts-with? (symbol->string name) "repl")))))
    
    (when process
      (let ((debugger (get-debugger~ process)))
        (for-each (lambda (thread)
                    (when (or detailed? (not (system-thread? thread)))
                      (let ((stop (get-active-stop~ thread)))
                        (proc thread stop detailed?))))
                  (with-jrm-exception-handler
                    (lambda ()
                      (get-threads~ process)))))))
  
  
  (method public (thread-changed new-thread)
    (unless (debugged=? thread new-thread)
      (set! thread new-thread)
      (when (and (get-threads-view) (view-visible?~ (get-threads-view)))
        (when (visit-debugged thread)
          (update-threads-highlight thread)))
      (when (get-frames-view)
        (thread-changed~ (get-frames-view) thread))
      (when (get-restarts-view)
        (thread-changed~ (get-restarts-view) thread))
      (when (get-exception-view)
        (thread-changed~ (get-exception-view) thread))))
  
  
  (definition (update-threads-highlight thread)
    (when (get-threads-view)
      (update-highlight~ (get-threads-view) thread)))
  
  
  ;;;
  ;;;; Frame
  ;;;

  
  (method public (for-each-frame proc)
    (define (frames stop)
      (with-jrm-exception-handler
        (lambda ()
          (get-frames~ stop))))
    
    (when (and process thread)
      (let ((stop (get-active-stop~ thread)))
        (when stop
          (for-each proc (frames stop))))))
  
  
  (method public (frame-changed new-frame (user-origin?: user-origin? #f))
    (set! frame new-frame)
    (when (and (get-frames-view) (or (not frame) (view-visible?~ (get-frames-view))))
      (when (get-variables-view)
        (frame-changed~ (get-variables-view) frame))
      (when (or user-origin? (not frame))
        (edit-frame frame)))
    (when (and thread (get-active-stop~ thread))
      (with-jrm-exception-handler
        (lambda ()
          (set-repl-frame~ thread (and frame (get-reference~ frame)))))))
  
  
  (definition *frame-indicator*
    (new Frame-Indicator color: {Color name: Light-Green}))
  
  
  (definition (edit-frame frame)
    (let ((location (and frame (get-location~ frame))))
      (edit~ *frame-indicator* location)))
  
  
  ;;;
  ;;;; Stop
  ;;;
  
  
  (method public (register-stop process thread stop (step?: step? #f)) ;; to test the stepper
    (define (update-process)
      (let ((empty? (not process~self))
            (view (get-processes-view)))
        (when empty?
          (if view
              (set-selected-process~ view process)
            (process-changed process)))
        (update-processes-highlight process)))
    
    (define (update-thread)
      (when (debugged=? process process~self)
        (let ((empty? (not thread~self))
              (view (get-threads-view)))
          (when view
            (process-changed~ view process~self))
          (when empty?
            (if view
                (set-selected-thread~ view thread)
              (thread-changed process)))
          (when (and view (view-visible?~ view))
            (when (visit-debugged thread)
              (update-threads-highlight thread))))))
    
    (define (update-others)
      (when (debugged=? thread thread~self)
        (when (get-frames-view)
          (thread-changed~ (get-frames-view) thread))
        (when (get-restarts-view)
          (thread-changed~ (get-restarts-view) thread))
        (when (get-exception-view)
          (thread-changed~ (get-exception-view) thread)))
      (update-status))
    
    (define (add-active-stop debugged stop)
      (table-add active-stops debugged stop))
    
    (define (add-unvisited-stops debugged)
      (table-set! unvisited-stops debugged #t))
    
    (add-active-stop process stop)
    (add-active-stop thread stop)
    (add-unvisited-stops process)
    (add-unvisited-stops thread)
    (unless step?
      (error-sound))
    (update-process)
    (update-thread)
    (update-others))
  
  
  (method public (unregister-stop process thread stop)
    (define (update-others)
      (when (debugged=? thread thread~self)
        (when (get-frames-view)
          (thread-changed~ (get-frames-view) thread))
        (when (get-restarts-view)
          (thread-changed~ (get-restarts-view) thread))
        (when (get-exception-view)
          (thread-changed~ (get-exception-view) thread)))
      (update-status))
    
    (define (remove-active-stop debugged stop)
      (let ((stops (remove! stop (table-ref active-stops debugged '()) test: debugged=?)))
        (if (not-null? stops)
            (table-set! active-stops debugged stops)
          (table-set! active-stops debugged))))
    
    (define (remove-unvisited-stops debugged)
      (unless (and (table-ref unvisited-stops debugged #f)
                   (some? (lambda (stop)
                            (table-ref unvisited-stops debugged #f))
                          (table-ref active-stops debugged '())))
        (table-set! unvisited-stops debugged)))
    
    (remove-active-stop process stop)
    (remove-active-stop thread stop)
    (remove-unvisited-stops process)
    (remove-unvisited-stops thread)
    (update-processes-highlight process)
    (let ((view (get-threads-view)))
      (when view
        (process-changed~ view process~self)))
    (update-others))))
