;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Processes View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.debugger.debugger.Processes-View jazz


(import (jazz.application)
        (jazz.debuggee)
        (jazz.debugger)
        (jazz.event)
        (jazz.feedback)
        (jazz.graphic)
        (jazz.handler)
        (jazz.io)
        (jazz.layout)
        (jazz.markup)
        (jazz.menu)
        (jazz.network)
        (jazz.outline)
        (jazz.presence)
        (jazz.process)
        (jazz.resource)
        (jazz.text)
        (jazz.tree)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.view)
        (jazz.workspace))


(class Processes-View extends Layout-View
  
  
  (property client initialize #f accessors explicit)
  

  (form
    (<install>                                     layout-type: border
      (<Layout-View>            name: tools
        (<Push-Tool>            name: open         size: {Dimension 22 22} tooltip?: #t tooltip-text: "Open Snapshot" resource: {Image-Resource "Open"} portfolio: :images action-handler: {Event-Handler :guest on-open-snapshot})
        (<Separator>                               size: {Dimension 6 0})
        (<Push-Tool>            name: attach       size: {Dimension 22 22} tooltip?: #t tooltip-text: "Attach Process" resource: {Image-Resource "Spy"} portfolio: :images action-handler: {Event-Handler :guest on-attach-process})
        (<Separator>                               size: {Dimension 2 0})
        (<Toggle-Tool>          name: focused-mode size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Focused Mode" resource: {Image-Resource "Exception"} portfolio: :images action-handler: {Event-Handler :guest on-focused-mode})
        (<Separator>                               size: {Dimension 2 0})
        (<Push-Tool>            name: update       size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Refresh" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :guest on-update}))
      (<Scroller-View>          name: scroller     location: center vscroll?: #t style: tool
        (<!>                    name: content      layout-type: fill
          (<Tree-Header>        name: header       shown?: #f
            (<!>                name: content
              (<Processes-Tree> name: processes    multiple-selection?: #f selection-mode: row selection-handler: {Selection-Handler :form on-select-process} context-menu-handler: {Event-Handler :form on-context-menu} double-click-handler: {Event-Handler :form on-double-click})))))))

  
  (method meta override (external-name self . rest)
    'Processes)
  
  
  (method override (finish self initargs)
    (nextmethod self initargs)
    (set-selected? (locate self 'focused-mode) (get-focused-mode? (get-debugger-manager))))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Process"})
  
  
  (method meta override (host-title self)
    "Processes")
  
  
  (method override (client-activate self)
    (let ((guest (current-document)))
      (if (is? guest Text-View)
          (set-client self guest)
        (set-client self #f)))
    (let ((selected-process (selected-process self)))
      (when selected-process
        (update-highlight self selected-process))))
  
  
  (method override (client-deactivate self)
    (set-client self #f))
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (get-tools-view self)
    (locate self 'tools))
  
  
  (method override (focus-default self)
    (let ((tree (locate self 'processes)))
      (acquire-focus tree)))

  
  ;;;
  ;;;; Component
  ;;;


  (method override (install self rest)
    (nextmethod self rest)
    (update self))

  
  ;;;
  ;;;; Events
  ;;;


  (method override (resize self dh dv)
    (nextmethod self dh dv)
    (size-figure (locate self 'scroller) dh dv))
  
  
  (method (on-context-menu self evt)
    (let* ((sender (get-sender evt))
           (pos (get-position evt))
           (acquired (acquire self sender pos)))
      (with-context-click sender pos
        (lambda ()
          (let ((menu (new-context-menu self (selected-object self))))
            (when menu
              (track-popup-menu self menu acquired)))))))
  
  
  (method (on-double-click self evt)
    (let ((process (selected-process self)))
      (when (and process (focusable? process))
        (focus-selected-process self))))
  
  
  (method (on-attach-process self evt)
    (let ((address (request-string title: "Enter process address")))
      (attach-process-at self address)))
  
  
  (method (attach-process-at self address)
    (start-remote-server (current-process))
    (receive (host service alternate-service) (parse-host/service address)
      (let ((remote-debuggee (connect-remote-reference host service 'debuggee purpose: 'debugging))
            (cookie (register-debugger-cookie)))
        (register-debugger-cookie-procedure cookie
          (lambda (process cookie)
            (clear-user-message)
            (transmit-runtime-information (get-debugger process) (get-reference process) 'all)))
        (user-message "Attaching {a}..." address)
        (attach-to-debugger remote-debuggee (require-object (get-local-register 'debugging) 'debugger) cookie))))
  
  
  (definition Snapshot-Extensions
    '(("Snapshot Files" . "snapshot")))
  
  
  (method (on-open-snapshot self evt)
    (with-cursor :wait
      (lambda ()
        (let ((file (with-preserved-current-directory
                      (lambda ()
                        (choose-file directory: {Directory Settings} extensions: Snapshot-Extensions)))))
          (set-selected-process self (open-snapshot self file))
          (acquire-focus (locate self 'processes))))))


  (method (on-select-process self evt)
    (process-change self))
  
  
  (method (on-focused-mode self evt)
    (toggle-focused-mode self))
  
  
  (method (on-update self evt)
    (for-each validate-processes (get-debuggers))
    (update self user-origin: #t))
  
  
  (method (process-change self)
    (process-changed (get-debugger-manager) (selected-process self)))
  
  
  ;;;
  ;;;; Client
  ;;;


  (method public (get-client self)
    client)

 
  (method public (set-client self clt)
    (when (neq? clt client)
      (set! client clt)
      (update self)))

  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method public (update self (user-origin: user-origin #f))
    (define (process-image debugger process live? processes manager)
      (cond ((standard-process-image debugger))
            ((not live?)
             {Image-Resource "ProcessDead"})
            ((not (process-received-filenames? manager process))
             {Image-Resource "Link"})
            ((member? process processes test: debugged=?)
             {Image-Resource "Script"})
            (else
             (or (get-icon process)
                 {Image-Resource "Process"}))))
    
    (define (effective-processes manager)
      (define (client-filename)
        (and client
             (let ((moniker (get-moniker client)))
               (and moniker
                    (parse moniker)))))
      
      (let ((filename (client-filename)))
        (if (not filename)
            (collect-always-processes manager)
          (effective-filename-processes manager filename))))
    
    (define (update-processes tree)
      (let ((manager (get-debugger-manager)))
        (let ((processes (effective-processes manager)))
          (for-each-process manager
            (lambda (debugger process live? focused?)
              (let ((title (present process))
                    (image (process-image debugger process live? processes manager))
                    (font (and (get-focused-mode? manager) focused? {Font Link-Small}))
                    (frame-color (get-stopped-frame-color manager process))
                    (background (get-visited-highlight-color manager process)))
                (set-alive? manager process live?)
                (add-row tree children: (list (new Tree-Node title: title font: font image: image frame-color: frame-color background: background)) user-data: process)))
            live-test?: user-origin))))
    
    (with-system-exception-debugger
      (lambda ()
        (let ((tree (locate self 'processes)))
          (with-preserved-selection tree
            (lambda ()
              (remove-every-row tree)
              (with-update-locked tree
                (lambda ()
                  (update-processes tree))))
            use: 'data
            test: debugged=?
            generate-event?: user-origin)))))
  
  
  (method public (update-focused-mode self focused-mode?)
    (set-selected? (locate self 'focused-mode) focused-mode?))

  
  ;;;
  ;;;; Highlight
  ;;;
  
  
  (method public (update-highlight self process)
    (let ((selected-process (selected-process self)))
      (when (debugged=? process selected-process)
        (visit-debugged (get-debugger-manager) process)))
    (let ((tree (locate self 'processes)))
      (let ((line (process-row tree process)))
        (when line
          (let ((cell (new Cell (row->line tree line) 0)))
            (let ((data (get-cell-data tree cell))
                  (manager (get-debugger-manager)))
              (set-frame-color data (get-stopped-frame-color manager process))
              (set-background data (get-visited-highlight-color manager process)))
            (invalidate-cell tree cell))))))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method public (selected-process self)
    (selected-object self))
  
  
  (method public (selected-object self)
    (let ((row (selected-row self)))
      (and row
           (get-user-data row))))
  
  
  (method (selected-row self)
    (let* ((tree (locate self 'processes))
           (n (get-single-selection tree)))
      (and n
           (get-row tree n))))
  
  
  (method public (set-selected-process self process)
    (let ((tree (locate self 'processes)))
      (let ((row (process-row tree process)))
        (when row
          (set-single-selection tree (row->line tree row))))))
  
  
  (definition (process-row tree process)
    (assert process)
    (continuation-capture
      (lambda (return)
        (for-each-row tree
          (lambda (row rank)
            (when (debugged=? (get-user-data row) process)
              (continuation-return return row))))
        #f)))
  
  
  ;;;
  ;;;; Menu
  ;;;
  
  
  (method (new-context-menu self object)
    (define (new-process-menu)
      (let ((manager (get-debugger-manager))
            (menu (new Process-Menu client: self)))
        (let ((alive? (get-alive? manager object))
              (evaluate (process-evaluate-when manager object))
              (focusable? (focusable? object)))
          (define (update-evaluate state evaluate)
            (set-selected? (locate menu state) (eq? state evaluate)))
          
          (unless (and focusable? alive?)
            (for-each (lambda (name)
                        (set-visible? (locate menu name) #f))
                      '(focus never known always refresh)))
          (when (and focusable? alive?)
            (set-title (locate menu 'quit) "Quit"))
          (when (not alive?)
            (set-title (locate menu 'quit) "Remove Process"))
          (update-evaluate 'never evaluate)
          (update-evaluate 'known evaluate)
          (update-evaluate 'always evaluate))
        (layout-menu menu)
        menu))
    
    (typecase object
      ((Debugged-Process) (new-process-menu))
      (else #f)))
  
  
  (method package (quit-selected-process self)
    (let ((manager (get-debugger-manager))
          (object (selected-object self)))
      (if (get-alive? manager object)
          (quit-process object)
        (detach object))
      (let ((tree (locate self 'processes)))
        (set-selection tree '()))))
  
  
  (method package (toggle-focused-mode self)
    (toggle-focused-mode (get-debugger-manager)))
  
  
  (method package (focus-selected-process self)
    (focus-process (get-debugger-manager) (selected-process self)))
  
  
  (method package (evaluate-never-process self)
    (process-evaluate-never (get-debugger-manager) (selected-process self))
    (update self))
  
  (method package (evaluate-known-process self)
    (process-evaluate-known (get-debugger-manager) (selected-process self))
    (update self))
  
  (method package (evaluate-always-process self)
    (process-evaluate-always (get-debugger-manager) (selected-process self))
    (update self))
  
  
  (method package (refresh-selected-process self)
    (let ((process (selected-process self)))
      (if (not process)
          (bell)
        (clear-runtime-information (get-debugger process) (get-reference process))
        (transmit-runtime-information process 'all))))
  
  
  (method public (open-snapshot self snapshot/file)
    (let ((snapshot (if (is? snapshot/file Markup-Element)
                        snapshot/file
                      (let ((snapshot (load-markup snapshot/file)))
                        (set-property snapshot 'file snapshot/file)
                        snapshot))))
      (add-snapshot self snapshot)))
  
  
  (method public (add-snapshot self snapshot)
    (attach-process (get-snapshot-debugger (current-application)) snapshot #f)))


;;;
;;;; Processes-Tree
;;;


(class Processes-Tree extends Tree-View
  
  
  (form
    (<install>            portfolio: :images
      (<Tree-Node-Column> title: "Process" width: 431 toplevel-controls?: #f))))


;;;
;;;; Process-Menu
;;;


(class Process-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item>     name: focus   title: "Focus" default?: #t icon: {Image-Resource "Script"} action-handler: {Event-Handler :form on-focus})
      (<Separator-Item>)
      (<Radio-Item>     name: never   title: "Evaluate Never" icon: {Image-Resource "ServerStopped"} action-handler: {Event-Handler :form on-evaluate-never})
      (<Radio-Item>     name: known   title: "Evaluate Known" icon: {Image-Resource "ServerClosing"} action-handler: {Event-Handler :form on-evaluate-known})
      (<Radio-Item>     name: always  title: "Evaluate Always" icon: {Image-Resource "ServerRunning"} action-handler: {Event-Handler :form on-evaluate-always})
      (<Separator-Item>)
      (<Label-Item>     name: refresh title: "Refresh Information" icon: {Image-Resource "Refresh"} action-handler: {Event-Handler :form on-refresh})
      (<Separator-Item>)
      (<Label-Item>     name: quit    title: "Close" action-handler: {Event-Handler :form on-quit})))
  
  
  (method (on-focus self evt)
    (focus-selected-process client))
  
  
  (method (on-evaluate-never self evt)
    (evaluate-never-process client))
  
  (method (on-evaluate-known self evt)
    (evaluate-known-process client))
  
  (method (on-evaluate-always self evt)
    (evaluate-always-process client))
  
  
  (method package (on-refresh self evt)
    (refresh-selected-process client))
  
  
  (method (on-quit self evt)
    (quit-selected-process client))))
