;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Grid
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Jeremie Lasalle Ratelle.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.designer.ui.grid.tree jazz


(import (jazz.action)
        (jazz.application)
        (jazz.component)
        (jazz.event)
        (jazz.graphic)
        (jazz.handler)
        (jazz.jml)
        (jazz.text)
        (jazz.tree)
        (jazz.ui))


(definition public (call-with-row/column-data row column proc)
  (proc (get-user-data~ row) (get-provider-data~ column)))


;;;
;;;; Data-Provider
;;;


(interface Data-Provider
  
  
  (method public virtual abstract (get-domain row-data column-data))
  (method public virtual abstract (get-data row-data column-data fail succeed))
  (method public virtual abstract (set-data row-data column-data value))
  (method public virtual abstract (read-only? row-data column-data))
  (method public virtual abstract (get-violation row-data column-data))
  (method public virtual abstract (set-violation row-data column-data violation)))


(class Provider-Data extends Component
  
  
  (method override (default-name)
    'provider-data))



(class Provider-Violation extends Component

  
  (property violation initialize #f accessors generate)
  (property presentation initialize #f accessors generate))


;;;
;;;; Grid-Controller
;;;


(interface Grid-Controller
  
  
  (method public virtual abstract (get-focus-actions))
  (method public virtual abstract (focus-update-actions))
  (method public virtual abstract (selection-cell? row column))
  (method public virtual abstract (get-context-menu))
  (method public virtual abstract (on-selection-change evt))
  (method public virtual abstract (get-tab-stops))
  (method public virtual abstract (copy-selection))
  (method public virtual abstract (cut-selection))
  (method public virtual abstract (delete-selection))
  (method public virtual abstract (paste-clipboard))
  (method public virtual abstract (can-copy?))
  (method public virtual abstract (can-cut?))
  (method public virtual abstract (can-delete?))
  (method public virtual abstract (can-paste?))
  (method public virtual abstract (get-draggable?))
  (method public virtual abstract (active-background row-data column-data))
  (method public virtual abstract (inactive-title row-data column-data))
  (method public virtual abstract (inactive-background row-data column-data))
  (method public virtual abstract (get-frame-color))
  (method public virtual abstract (cell-editable? row column))
  (method public virtual abstract (merge? master-row-data row-data column-data))
  (method public virtual abstract (client-rows-datas))
  (method public virtual abstract (drop? source cell))
  (method public virtual abstract (make-columns))
  (method public virtual abstract (receive-drop sender pos drag-drop)))
  

;;;
;;;; Grid-Column
;;;


(class Grid-Tree-Column extends Tree-Label-Column
  
  
  (form
    (<install> justification: (1 1)))
  
  
  (method (get-provider-data)
    (child 'provider-data))
  
  
  (method override (draw-data surface context row data width height)
    (let ((provider (get-provider~ parent)))
      (call-with-row/column-data row self
        (lambda (row-data column-data)
          (get-data~ provider row-data column-data
            (lambda ()
              (nextmethod surface context row data width height))
            (lambda (value)
              (let ((domain (get-domain~ provider row-data column-data)))
                (draw-data~ domain surface context value self row data width height)))))))))


;;;
;;;; Grid-Tree
;;;


(class Grid-Tree extends Tree-View
  
  
  (form
    (<install> default-row-height: 15 row-spacing: 1 column-spacing: 1 background: {Color Medium-Gray} empty-background: {Color Medium-Gray} multiple-selection?: #t filled-column: #f selection-mode: cell selection-handler: {Event-Handler :self on-selection-change}))
  
  
  (method (get-provider)
    (child 'provider))
  
  
  (method (get-controller)
    (child 'controller))
  
  
  (method virtual (get-widget)
    (find-ascendant Grid-Tree-Widget))
  
  
  (method override (focus-actions)
    (let ((actions (get-focus-actions~ (get-controller))))
      (if actions
          (cons actions (nextmethod))
        (nextmethod))))
  
  
  (method override (focus-update-actions)
    (focus-update-actions~ (get-controller)))
  
  
  (method (selected-cells-data)
    (let ((cells (remove-duplicates (get-selection) test: cell=?)))
      (values (map (compose* get-user-data~ get-row get-line~) cells)
              (map (compose* get-provider-data~ get-column get-col~) cells))))
  
  
  (method (cell-data-values cell)
    (receive (row column) (cell-values cell)
      (values (get-user-data~ row) (get-provider-data~ column))))
  
  
  ;;;
  ;;;; Handlers
  ;;;
  
  
  (method override (mouse-down evt)
    (let ((cell (view->tree (get-position~ evt))))
      (when cell
        (save-edition)
        (end-edition)
        (receive (row column) (cell-values cell)
          (if (and (or (null? selection)
                       (receive (row column) (cell-values (car selection))
                         (selection-cell?~ (get-controller) row column)))
                   (selection-cell?~ (get-controller) row column))
              (nextmethod evt)
            (select-cell cell))))))
  
  
  (method override (context-menu evt)
    (let ((pos (get-position~ evt)))
      (save-edition)
      (end-edition)
      (with-context-click pos
        (lambda ()
          (let ((menu (get-context-menu~ (get-controller))))
            (when menu
              (track-popup-menu menu pos)))))))
  
  
  (method (on-selection-change evt)
    (on-selection-change~ (get-controller) evt)
    (update-focus-actions~ (get-application))
    (update-rows-highlight~ (get-controller) (get-old~ evt) (get-selection~ evt))
    (invalidate-view))
  
  
  (method (editor-arrow-press tab-view evt)
    (let ((text? (is? tab-view Text-View)))
      (let ((edited-cell (get-edited-cell))
            (next-move (dispatch (class-of tab-view) (if text? 'arrow-move 'arrow-press)))
            (direction (get-property~ evt direction:))
            (internal? (if text? (get-property~ evt word?:) (get-control?~ evt)))
            (evt (if text?
                     (make-event Action-Event (get-kind~ evt) (get-sender~ evt) word?: #f extend?: (get-property~ evt extend?:) direction: (get-property~ evt direction:))
                   evt)))
        (unless internal?
          (save-edition))
        (case direction
          ((left)  (if internal? (next-move tab-view evt) (select-left edited-cell)))
          ((right) (if internal? (next-move tab-view evt) (select-right edited-cell)))
          ((up)    (select-up edited-cell))
          ((down)  (select-down edited-cell))))))
  
  
  (method override (arrow-press evt)
    (define (select-direction next-cell)
      (let ((cell (next-cell (get-single-selection))))
        (when cell
          (select-cell cell))))
    
    (let ((direction (get-property~ evt direction:)))
      (select-direction (case direction
                          ((left)  cell-left)
                          ((right) cell-right)
                          ((up)    cell-up)
                          ((down)  cell-down)))))
  
  
  (method (editor-return-press tab-view evt)
    (on-tab-press~ tab-view evt))
  
  
  (method (editor-tab-press tab-view evt)
    (if (and (is? tab-view Text-View) (has-ambiguous-completion?~ tab-view))
        (end-completion~ tab-view)
      (navigate-view~ tab-view (if (get-shift?~ evt) #t #f))))
  
  
  (method override (navigate-tab from backward?)
    (save-edition)
    (let ((stops (get-tab-stops)))
      (if (not-null? stops)
          (if (eq? from (get-creator))
              (let ((cell (if backward?
                              (last stops)
                            (first stops))))
                (when cell
                  (when (eq? cell from)
                    (set! cell (navigated-view cell backward?)))
                  (if (is? cell Cell)
                      (select-cell cell)
                    (navigate-tab~ cell self backward?))))
            (let ((sel (navigated-view (car (get-selection)) backward? test: cell=?)))
              (if (and sel (is? sel Cell))
                  (select-cell sel)
                (navigate-tab~ sel self backward?))))
        (nextmethod from backward?))))
  
  
  (method override (get-tab-stops)
    (get-tab-stops~ (get-controller)))
  
  
  ;;;
  ;;;; Copy
  ;;;
  
  
  (method override (copy-selection)
    (copy-selection~ (get-controller)))
  
  
  (method override (cut-selection)
    (cut-selection~ (get-controller)))
  
  
  (method override (delete-selection)
    (delete-selection~ (get-controller)))
  
  
  (method override (paste-clipboard)
    (paste-clipboard~ (get-controller)))
  
  
  (method override (can-copy?)
    (can-copy?~ (get-controller)))
  
  
  (method override (can-cut?)
    (can-cut?~ (get-controller)))
  
  
  (method override (can-paste?)
    (can-paste?~ (get-controller)))
  
  
  (method override (can-delete?)
    (can-delete?~ (get-controller)))
  
  
  (method override (get-draggable?)
    (get-draggable?~ (get-controller)))
  
  
  ;;;
  ;;;; Drag
  ;;;
  
  
  (method override (highlight-drop drag-drop pos)
    )
  
  
  (method override (get-tree-target drag-drop pos)
    (call-with-view->tree pos
      (lambda (cell v height)
        (let ((row (and cell (get-row (get-line~ cell))))
              (col (and cell (get-col~ cell)))
              (source (get-source~ drag-drop))
              (current (get-target~ drag-drop)))
          (when (or (neq? (get-view~ source) self) (neq? (get-row~ source) row) (not (equal? (get-col~ source) col)))
            (if (and current (eq? self (get-view~ current)) (eq? row (get-row~ current)) (equal? col (get-col~ current)))
                current
              (and (drop?~ (get-controller) source cell) (new Tree-Drop-Target view: self row: row col: col))))))))
  
  
  (method override (receive-drop sender pos drag-drop)
    (receive-drop~ (get-controller) sender pos drag-drop))
  
  
  ;;;
  ;;;; Edition
  ;;;
  
  
  (method (prepare-and-focus-entry entry)
    (let ((view-to-focus (tab-view~ entry)))
      (set-arrow-press-handler~ view-to-focus (new Event-Handler execute: (lambda (evt) (editor-arrow-press view-to-focus evt))))
      (set-return-press-handler~ view-to-focus (new Event-Handler execute: (lambda (evt) (editor-return-press view-to-focus evt))))
      (set-tab-press-handler~ view-to-focus (new Event-Handler execute: (lambda (evt) (editor-tab-press view-to-focus evt))))
      (acquire-focus~ view-to-focus)
      (select-all~ view-to-focus)))
  
  
  (method virtual (new-editor)
    (new Border-View border-type: 'edge style: 'entry))
  
  
  (method virtual (setup-entry editor domain component)
    (let ((entry (setup-entry~ domain editor (list component))))
      (setup-for-grid~ entry)
      entry))
  
  
  (method (save-edition)
    (let ((provider (get-provider)))
      (when edited-site
        (let ((cell (site->cell edited-site))
              (entry (first-child~ (first-child~ cell-editor))))
          (receive (row-data column-data) (cell-data-values cell)
            (when (get-modified?~ entry)
              (let ((text (get-presentation~ entry))
                    (violation (presentation-violation~ entry)))
                (set-violation~ provider row-data column-data (and violation (new Provider-Violation violation: violation presentation: text)))
                (if violation
                    (update-cell cell)
                  (set-data~ provider row-data column-data (get-value~ entry))))))))))
  
  
  (method override (select-cell cell)
    (let ((provider (get-provider)))
      (receive (row-data column-data) (cell-data-values cell)
        (acquire-focus)
        (set-single-selection cell)
        (unless (read-only?~ provider row-data column-data)
          (get-data~ provider row-data column-data
            (const #f)
            (lambda (value)
              (let ((domain (get-domain~ provider row-data column-data))
                    (editor (new-editor))
                    (component (element (get-hierarchy~ row-data) (get-component-offset~ column-data))))
                (let ((entry (setup-entry editor domain component)))
                  (let ((provider-violation (get-violation~ provider row-data column-data)))
                    (if provider-violation
                        (set-presentation~ entry (get-presentation~ provider-violation))
                      (set-value~ entry value)))
                  (edit-cell cell editor: editor)
                  (prepare-and-focus-entry entry)))))))))
  
  
  (method (populate)
    (let ((scrolled-position (get-scrolled-position)))
      (remove-every-row)
      (remove-columns)
      (for-each (lambda (column)
                  (set-parent~ column self))
                (make-columns~ (get-controller)))
      (let ((datas (client-rows-datas~ (get-controller))))
        (for-each (lambda (row-data row-labels)
                    (add-row children: row-labels user-data: row-data))
                  datas
                  (map make-row-labels datas)))
      (merge-grid-cells)
      (update-highlight)
      (scroll-to scrolled-position)))
  
  
  ;;;
  ;;;; Labels
  ;;;
  
  
  (method (make-row-label row-data column-data)
    (let ((label (new Tree-Label)))
      (update-label row-data column-data label #f)
      label))
  
  
  (method (make-row-labels row-data)
    (map (lambda (column)
           (make-row-label row-data (get-provider-data~ column)))
         (get-columns)))
  
  
  (method public (update-label row-data column-data label selected?)
    (define (update-inactive-element)
      (set-title~ label (inactive-title~ (get-controller) row-data column-data))
      (set-background~ label (inactive-background~ (get-controller) row-data column-data selected?)))
    
    (define (update-active-element val)
      (let ((domain (get-domain~ (get-provider) row-data column-data)))
        (set-background~ label (active-background~ (get-controller) row-data column-data selected?))
        (let ((violation (get-violation~ (get-provider) row-data column-data)))
          (if violation
              (set-title~ label (get-presentation~ violation))
            (set-title~ label (present~ domain val))))))
    
    (get-data~ (get-provider) row-data column-data update-inactive-element update-active-element))
  
  
  (method public (update-editor row-data column-data editor)
    (let ((entry (first-child~ (get-editor~ editor))))
      (unless (or (get-modified?~ entry)
                  (get-violation~ (get-provider) row-data column-data))
        (get-data~ (get-provider) row-data column-data
          (const #f)
          (~ set-value entry)))))
  
  
  ;;;
  ;;;; Highlight
  ;;;
  
  
  (method virtual (update-frame-highlight)
    (set-border-color~ (get-widget) (get-frame-color~ (get-controller))))
  
  
  (method virtual (update-cell-highlight tree-data cell)
    (receive (row-data column-data) (cell-data-values cell)
      (let ((current-color (get-frame-color~ tree-data))
            (color (get-cell-color~ (get-controller) row-data column-data)))
        (unless (eq? current-color color)
          (set-frame-color~ tree-data color)
          (invalidate-cell cell)))))
  
  
  (method virtual (get-editor-widget editor)
    (find-ascendant~ editor Border-View include-self?: #t))
  
      
  (method virtual (update-editor-highlight tree-data cell)
    (update-cell-highlight (get-preserved-data~ tree-data) cell)
    (let ((border (get-editor-widget (get-editor~ tree-data))))
      (receive (row-data column-data) (cell-data-values cell)
        (let ((current-color (get-border-color~ border))
              (color (get-editor-color~ (get-controller) row-data column-data)))
          (unless (eq? current-color color)
            (set-border-color~ border color))))))
  
  
  (method (update-cells-highlight)
    (for-each-visible-row
      (lambda (row line)
        (for-each-column
          (lambda (column col)
            (let ((tree-data (get-child~ row col))
                  (cell (new Cell line col)))
              (typecase child
                ((Tree-Label)
                 (update-cell-highlight tree-data cell))
                ((Tree-Data-View)
                 (update-editor-highlight tree-data cell)))))))))
          
  
  (method (update-highlight)
    (update-frame-highlight)
    (update-cells-highlight))
  
  
  ;;;
  ;;;; Merge cells
  ;;;
  
  (method (merge-column-cells column col)
    (let (iterate-master-rows (master (get-next~ (get-root-row))))
      (when master
        (let (iterate-rows (current (get-next~ master)) (to-merge 0))
          (if (and current (merge?~ (get-controller) (get-user-data~ master) (get-user-data~ current) (get-provider-data~ column)))
              (iterate-rows (get-next~ current) (+ to-merge 1))
            (let ((cell (new Cell (row->line master) col)))
              (merge-cells cell (+ to-merge 1) 1)
              (iterate-master-rows current)))))))
  
  
  (method (merge-grid-cells)
    (for-each-column merge-column-cells))
  
  
  (method (update-cell cell)
    (let* ((edited-cell (get-edited-cell))
           (editor (and edited-cell (cell=? cell edited-cell) cell-editor))
           (label (if editor (get-preserved-data~ cell-editor) (get-cell-data cell))))
      (receive (row column) (cell-values cell)
        (call-with-row/column-data row column
          (lambda (rd cd)
            (let ((selected-lines (remove-duplicates (map get-line~ selection)))
                  (cell-line (get-line~ cell)))
              (update-label rd cd label (memv? cell-line selected-lines)))
            (update-cell-highlight label cell)
            (when editor
              (update-editor rd cd editor)
              (update-editor-highlight editor cell))
            (invalidate-cell cell)))))))


(class Grid-Tree-Widget extends Border-View
  
  
  (form
    (<install>                                         border-type: edge style: entry background: {Color Medium-Gray}
      (<Scroller-View>              name: scroller     style: document vscroll?: #t hscroll?: #t
        (<content~>                                    layout-type: fill
          (<Tree-Header>            name: grid-header  style: document @a (background: {Color Medium-Gray} empty-background: {Color Medium-Gray})
            (<content~>
              (<Grid-Tree>          name: grid)))))))))
