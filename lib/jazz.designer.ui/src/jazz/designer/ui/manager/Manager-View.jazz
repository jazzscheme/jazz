;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Manager View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.designer.ui.manager.Manager-View jazz


(import (jazz.action)
        (jazz.designer)
        (jazz.designer.ui)
        (jazz.event)
        (jazz.handler)
        (jazz.layout)
        (jazz.markup)
        (jazz.resource)
        (jazz.tree)
        (jazz.ui)
        (jazz.view))


(class Manager-View extends Component-Editor


  (form
    (<install>                                                 size: {Dimension 650 400} layout-type: border
      (<Layout-View>                  name: toolbar            size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        (<Push-Tool>                  name: new                size: {Dimension 22 22} tooltip?: #t tooltip-text: "New" resource: {Image-Resource "New"} portfolio: :images action-handler: {Event-Handler :form on-new})
        (<Push-Tool>                  name: open               size: {Dimension 22 22} tooltip?: #t tooltip-text: "Open" resource: {Image-Resource "Open"} portfolio: :images action-handler: {Event-Handler :form on-open})
        (<Push-Tool>                  name: save               size: {Dimension 22 22} tooltip?: #t tooltip-text: "Save" resource: {Image-Resource "Save"} portfolio: :images action-handler: {Event-Handler :form on-save})
        (<Separator>                  name: end-open-group     size: {Dimension 16 0})
        (<Push-Tool>                  name: undo               size: {Dimension 22 22} tooltip?: #t tooltip-text: "Undo" resource: {Image-Resource "Undo"} portfolio: :images action-handler: {Event-Handler :form on-undo})
        (<Push-Tool>                  name: redo               size: {Dimension 22 22} tooltip?: #t tooltip-text: "Redo" resource: {Image-Resource "Redo"} portfolio: :images action-handler: {Event-Handler :form on-redo})
        (<Separator>                  name: end-undo-group     size: {Dimension 16 0})
        (<Push-Tool>                  name: edit-form          size: {Dimension 22 22} tooltip?: #t tooltip-text: "View" resource: {Image-Resource "Edit"} portfolio: :images action-handler: {Event-Handler :form on-edit-form}))
      (<Center-View>                  name: center             size: {Dimension 650 367} location: center
        (<Border-View>                name: traits-border      position: {Point 1 0} size: {Dimension 229 130} border-type: edge style: entry
          (<Scroller-View>            name: scroller           style: document hscroll?: #t vscroll?: #t style: tool
            (<!>                      name: content            layout-type: fill
              (<Tree-Header>          name: header             style: document
                (<!>                  name: content
                  (<Traits-Tree>      name: traits
                    (<!>              name: trait              width: 183)))))))
        (<Splitter>                   name: left               position: {Point 1 130} size: {Dimension 229 3} orientation: horz move-handler: {Event-Handler :form on-splitter-move})
        (<Border-View>                name: descendants-border position: {Point 1 133} size: {Dimension 229 233} border-type: edge style: entry
          (<Scroller-View>            name: scroller           style: document hscroll?: #t vscroll?: #t
            (<!>                      name: content            layout-type: fill
              (<Tree-Header>          name: header             style: document
                (<!>                  name: content
                  (<Descendants-Tree> name: descendants        tab-press-handler: {Event-Handler :form on-tab-press}
                    (<!>              name: descendant         width: 183)))))))
        (<Splitter>                   name: right              position: {Point 230 0} size: {Dimension 3 366} move-handler: {Event-Handler :form on-splitter-move})
        (<Layout-View>                name: card-contour       position: {Point 233 0} size: {Dimension 416 366} layout-type: fill
          (<Border-View>              name: card-border        border-type: edge style: entry
            (<Properties-Card>        name: card))))))
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Doc"})
  
  
  (method meta override (host-position self)
    {Point 100 80})
  
  
  (method meta override (host-size self)
    {Dimension 650 400})
  
  
  (method override (host-workspaces self)
    '(home))
  
  
  ;;;
  ;;;; Controller
  ;;;


  @convert
  (method override (new-document-controller)
    (new Manager-View-Controller self))
  
  
  @convert
  (class Manager-View-Controller extends Component-Editor-Controller
    
    
    (method override (name-status)
      (editor-title document)))


  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method override (install self rest)
    (nextmethod self rest)
    (set-editor (locate self 'traits) self)
    (set-editor (locate self 'descendants) self)
    (set-editor (locate self 'card) self))


  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions self)
    (cons (find-actions 'manager)
          (nextmethod self)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (size-change self size)
    (nextmethod self size)
    (let ((toolbar (locate-component self 'toolbar)))
      (when toolbar
        (set-size toolbar (new Dimension (get-width size) (get-height toolbar))))))
  
  
  (method package virtual (resize-center self dh dv)
    (size-figure (locate self 'descendants-border) 0 dv)
    (size-figure (locate self 'right) 0 dv)
    (size-figure (locate self 'card-contour) dh dv))
  
  
  (method package (on-splitter-move self evt)
    (let ((sender (get-sender evt))
          (delta (get-property evt delta:)))
      (case (get-name sender)
        ((left)
         (size-figure (locate self 'traits-border) 0 delta)
         (move-figure (locate self 'descendants-border) 0 delta)
         (size-figure (locate self 'descendants-border) 0 (- delta)))
        ((right)
         (size-figure (locate self 'traits-border) delta 0)
         (size-figure (locate self 'left) delta 0)
         (size-figure (locate self 'descendants-border) delta 0)
         (move-figure (locate self 'card-contour) delta 0)
         (size-figure (locate self 'card-contour) (- delta) 0)))
      (update-view self)))
  
  
  (method (on-new self evt)
    (new-form self))
  
  
  (method package (on-open self evt)
    (open-form self))
  
  
  (method (on-edit-form self evt)
    (edit-form self))
  
  
  (method override (on-tab-press self evt)
    (let ((tree (get-tree (locate self 'card))))
      (acquire-focus tree)))
  
  
  ;;;
  ;;;; Designer
  ;;;
  
  
  (method override (install-current-designer self component form)
    (nextmethod self component form)
    (set-title self (editor-title self))))


;;;
;;;; Center-View
;;;


(class Center-View extends View
  
  
  (method override (resize self dh dv)
    (nextmethod self dh dv)
    (resize-center creator dh dv))))
