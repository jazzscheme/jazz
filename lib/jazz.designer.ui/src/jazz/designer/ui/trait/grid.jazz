;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Trait Grid
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Jeremie Lasalle Ratelle.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.designer.ui.trait.grid jazz


(import (jazz.component)
        (jazz.descriptor)
        (jazz.designer)
        (jazz.designer.ui)
        (jazz.designer.ui.grid)
        (jazz.designer.ui.grid.tree)
        (jazz.graphic)
        (jazz.markup)
        (jazz.tree))


(class Trait-Row-Data extends Component
  
  
  (property trait initialize #f accessors generate))


(class Traits-Provider-Data extends Provider-Data
  
  
  (property path initialize '() accessors generate)
  (property prop initialize #f accessors generate))


(class Traits-Controller extends Component implements Grid-Controller
  
  (slot client accessors generate)
  (slot traits accessors generate)
  
  (method override (get-focus-actions self)
    #f)
  (method override (focus-update-actions self))
  (method override (selection-cell? self row column)
    #f)
  (method override (get-context-menu self)
    #f)
  (method override (on-selection-change self evt))
  (method override (get-tab-stops self)
    #f)
  (method override (copy-selection self))
  (method override (cut-selection self))
  (method override (delete-selection self))
  (method override (paste-clipboard self))
  (method override (can-copy? self) #f)
  (method override (can-cut? self) #f)
  (method override (can-delete? self) #f)
  (method override (can-paste? self) #f)
  (method override (get-draggable? self) #f)
  (method override (get-frame-color self)
    #f)
  (method override (active-background self row-data column-data)
    {Color Tree-Background})
  (method override (inactive-background self row-data column-data)
    {Color Gray})
  
  (method override (cell-editable? self row column) #f)
  (method override (merge? self master-row-data row-data column-data) #f)
  
  (method override (client-rows-datas self)
    (map (lambda (trait)
           (new Trait-Row-Data trait: (resolve-trait #f trait)))
         traits)))


(class Traits-Provider extends Component implements Data-Provider
  
  
  (slot client accessors generate)
  
  
  (method (get-grid self)
    (get-parent self))
  
  
  (method override (read-only? self row-data column-data)
    #t)
  
  
  (method override (get-violation self row-data column-data)
    #f)
  
  
  (method override (get-domain self row-data column-data)
    (let* ((component (find-path client (get-path column-data)))
           (field (and component (category-field (class-of component) (get-prop column-data)))))
      (and field (property-domain component field))))
  
  
  (method override (get-data self row-data column-data fail succeed)
    (let ((form (find-path (get-client-form (get-trait row-data)) (get-path column-data)))
          (not-found (list 'not-found)))
      (let ((value (if form (get-property form (get-prop column-data) not-found) not-found)))
        (if (eq? value not-found)
            (fail)
          (succeed value))))))


(class Grid-Traits-Editor extends Grid-Tree-Widget
  
  
  (slot client   initialize #f)
  (slot traits   initialize #f)
  
  
  (form
    (<install>
      (<grid~>
        (<Traits-Provider> name: provider)
        (<Traits-Controller> name: controller))))
  
  (method (get-grid self)
    (locate self 'grid))
  
  
  (method package (get-provider self)
    (locate self 'provider))
  
  
  (method (get-controller self)
    (locate self 'controller))
  
  
  (definition Default-Property-Width 100)
  
  
  (method public (setup self client traits)
    (set! self.traits traits)
    (set-client self client))
  
  
  (method public (set-client self clt)
    (when (neq? clt client)
      (set! client clt)
      (client-update self)))
  
  
  (method package (client-update self)
    (end-edition (get-grid self))
    (update-controller self)
    (update-provider self)
    (update self))
  
  
  (method (update-provider self)
    (set-client (get-provider self) client))
  
  
  (method (update-controller self)
    (set-client (get-controller self) client)
    (set-traits (get-controller self) traits))
  
  
  (method package (update self)
    (with-update-locked (get-grid self)
      (lambda ()
        (setup-columns self)
        (populate (get-grid self)))))
  
  (method package (add-column self path prop presentation)
    (let ((column (add-column (get-grid self) class: Grid-Tree-Column title: presentation width: Default-Property-Width)))
      (add-child column (new Traits-Provider-Data name: 'provider-data path: path prop: prop))))
  
  
  (method (setup-child-columns self parent child-descriptor)
    (let* ((path (list (get-target child-descriptor)))
           (subcomponent (find-path parent path))
           (class (class-of subcomponent)))
      (for-each (lambda (x)
                  (bind (name presentation domain) x
                    (add-column self path name presentation)))
                (classes-properties/presentations/domains (list class)))))
  
  
  (method (setup-client-columns self)
    (let ((client-class (class-of client)))
      (let ((client-properties/presentation/domains (classes-properties/presentations/domains (list client-class)))
            (child-descriptors (get-children (find-child (get-descriptor client-class) 'children))))
        (for-each (lambda (name presentation) (add-column self '() name presentation)) (map car client-properties/presentation/domains) (map cadr client-properties/presentation/domains))
        (for-each (lambda (desc) (setup-child-columns self client desc)) child-descriptors))))
        
  
  (method (setup-columns self)
    (remove-columns (get-grid self))
    (when client
      (setup-client-columns self))))
)
