;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Books View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.designer.book.Book-View jazz


(import (jazz.component)
        (jazz.designer)
        (jazz.document)
        (jazz.io)
        (jazz.outline)
        (jazz.ui)
        (jazz.view))


(class undocumented Book-View extends Layout-View implements Document
  

  (slot sections            initialize (make-table test: eq?))
  (slot book                initialize #f accessors generate)
  (slot document-controller initialize #f)
  
  
  (method override (default-extension self));;abstract
  (method override (extensions self));;abstract
  (method override (focus-document self));;abstract
  (method override (current-history-item self));;abstract
  (method override (compare-moniker self moniker));;abstract
  
  
  ;;;
  ;;;; Book-View
  ;;;
  
  
  (method public (get-version self)
    (get-version book))
  
  
  (method public (get-properties self)
    (get-properties book))
  
  
  (method public (get-description self)
    (get-description book))
  
  
  (method public (get-book-name self)
    (get-book-name book))

  
  ;;;
  ;;;; Sections
  ;;;
  
  
  (method public (add-section self name section)
    (when (get-client book name)
      (table-set! sections name section)))
  
  
  (method public (remove-section self name)
    (when (get-client book name)
      (table-set! sections name #f)))
  
  
  (method public (get-section self name)
    (or (table-ref sections name #f)
        (error "Unable to find section: {t}" name)))
  
  
  (method public (get-client self name)
    (get-client (get-section self name)))
  
  
  (method public (get-designer self name)
    (get-designer (get-section self name)))
  
  
  (method public (get-editor self name)
    (get-editor (get-section self name)))
  
  
  (method public (for-each-section self proc)
    (for-each (lambda (name)
                (proc name (get-section self name)))
              (get-names book)))

  
  ;;;
  ;;;; Client Forms
  ;;;
  
  
  (method public (set-client-from-forms self client-forms)
    (for-each-property (lambda (name form)
                         (let ((client (instantiate form book: book)))
                           (set-client book name client)))
                       client-forms)
    (clients-update self))
 
  
  (method public virtual (clients-update self)
    (for-each-client book
      (lambda (name client)
        (let ((section (get-section self name)))
          (set-client section client)))))
  
  
  (method (update-names self book-name)
    (for-each-section self
      (lambda (name section)
        (let ((client (get-client section))
              (designer (get-designer section)))
          (set-name designer client book-name trait: #f)))))

  
  ;;;
  ;;;; New
  ;;;
  
  
  @convert-guest
  (method meta override (new-event (host-visible?: host-visible? #t))
    (let* ((frame (apply new-frame host-visible?: #f rest))
           (book-view (get-guest frame))
           (book-name (unique-domain-name "New" (collect-names book-view)))
           (controller (get-document-controller book-view))
           (fact (new List-Factory)))
      (set-newname controller book-name)
      (update-status controller)
      (clients-update book-view)
      (update-names book-view book-name)
      (bring-to-front frame)
      (set-visible? frame host-visible?)
      frame))
  
  
  @convert-guest
  (method meta (collect-names book)
    (let ((model (class-of book)))
      (collect (lambda (frame)
                 (when (is? frame Internal-Frame-Host)
                   (let ((guest (get-guest frame)))
                     (when (and guest (neq? guest book) (is? guest model))
                       (get-book-name guest)))))
               (all-frames))))

  
  ;;;
  ;;;; Empty
  ;;;
  
  @not-used?
  (method public (empty-sections sections)
    (for-each (lambda (name)
                (let* ((section (get-section name))
                       (old-client (get-client section))
                       (old-name (get-name old-client))
                       (form (new Form textual?: #t action: 'new model: (get-model section)))
                       (client (instantiate form book: self)))
                  (set-client section client)
                  (let ((designer (get-designer section)))
                    (set-name designer client old-name)
                    (clients-update))))
              sections))
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method override (install-in-host self host)
    (nextmethod self host)
    (install-in-host (get-document-controller self) host))

  
  ;;;
  ;;;; Controller
  ;;;


  (method override (get-document-controller self)
    (unless document-controller
      (set! document-controller (new-document-controller self)))
    document-controller)


  (method override (new-document-controller self)
    (new Book-Controller self))
  
  
  (method override (save self)
    (save (get-document-controller self)))
  
  
  (method override (save-as self)
    (save-as (get-document-controller self)))
  
  
  (method override (save-to self moniker)
    (save-to (get-document-controller self) moniker))
  
  
  (method public virtual (save-all self)
    (save-all (get-document-controller self)))
  
  
  (method override (install-scroller? self)
    #f)


  (method package (get-moniker self)
    (get-moniker (get-document-controller self)))


  (method package (set-moniker self moniker)
    (set-moniker (get-document-controller self) moniker))
  
  
  (method override (verify-reload self)
    )
  
  
  (method override (reload-moniker self moniker)
    (when moniker
      (reload-content self moniker)
      (update-moniker-time self moniker)))
  
  
  (method (reload-content self moniker)
    (empty-content self)
    (load-content self moniker)
    (set-modified? self #f)
    (update-status self))
  
  
  (method protected virtual (empty-content self)
    )


  (method override (get-modified? self)
    (get-modified? (get-document-controller self)))


  (method override (set-modified? self state)
    (set-modified? (get-document-controller self) state))
  
  
  (method package (update-status self)
    (update-status (get-document-controller self)))
  
  
  (method package (update-moniker-time self moniker)
    (update-moniker-time (get-document-controller self) moniker))
  
  
  (method override (confirm-close self . rest)
    (confirm-close (get-document-controller self)))


  (method override (tool-update self tool?)
    )


  (method override (console-update self console?)
    )

  
  ;;;
  ;;;; Load
  ;;;


  (method override (load-content self moniker)
    (load-content book moniker)
    (clients-update self)
    (update-status self))

  
  ;;;
  ;;;; Save
  ;;;
  
  
  (method override (save-content self moniker)
    (with-cursor :wait
      (lambda ()
        (let ((base (get-base moniker)))
          (for-each-section self
            (lambda (name section)
              (let ((designer (get-designer section)))
                (set-origin (get-form designer) moniker)
                (set-name designer (get-client section) base trait: #f)
                (process-saving (get-editor section)))))
          (call-with-output-file (path-settings moniker char-encoding: 'UTF-8)
            (lambda (output)
              (let ((first? #t))
                (for-each-section self
                  (lambda (name section)
                    (let ((designer (get-designer section)))
                      (when first?
                        (output-header (get-description self) output)
                        (output-version self output)
                        (output-properties self output)
                        (set! first? #f))
                      (output-section (get-title section) output)
                      (format output "{a}{%}{%}" name)
                      (print-to designer -1 output))))))))
        (update-moniker-time self moniker)
        (set-modified? self #f)
        (update-status self)
        (for-each-section self
          (lambda (name section)
            (process-saved (get-editor section)))))))
  
  
  (method (output-version self output)
    (format output "{%}{%}version: {s}{%}" (get-version self)))
  
  
  (method override (output-properties self output)
    (let ((properties (get-properties self)))
      (format output "{%}{%}properties: {a}{%}"
              (if (null? properties)
                  "()"
                (format "{s}" properties)))))))
