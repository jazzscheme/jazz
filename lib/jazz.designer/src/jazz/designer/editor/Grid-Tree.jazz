;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Grid Tree
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Jeremie Lasalle Ratelle.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.designer.editor.Grid-Tree jazz


(import (jazz.graphic)
        (jazz.designer)
        (jazz.designer.view)
        (jazz.jml)
        (jazz.library)
        (jazz.locale)
        (jazz.ui)
        (jazz.ui.view))


(definition (get-lineage-from component ancestor)
  (let* ((root (get-root-parent~ component))
         (ancestry (cons root (get-ancestry~ component root))))
    (memq ancestor ancestry)))


(definition (get-class-categorized-properties class)
  (let* ((properties-descriptor (get-properties-descriptor class))
         (categorized (get-categorized~ properties-descriptor)))
    (either categorized #f)))


(definition (present-property-value component property)
  (let ((value (read-property component property)))
    (present-property~ component property value)))


(definition (follow-component-path parent-component path fail succeed)
  (let ((component (find-component-path parent-component (butlast path))))
    (if (not component)
        (fail)
      (let* ((base (class-of component))
             (property (category-field base (last path))))
        (if (not property)
            (fail)
          (succeed component base property))))))


(definition (find-component-path component path)
  (cond ((not component) #f)
        ((null? path) component)
        (else (find-component~ component path))))


(definition (iota count (start 0) (step 1))
  (if (zero? count)
      '()
    (cons start (iota (- count 1) (+ start step) step))))


(definition (target-classes-union target-classes)
  (cond ((null? target-classes)
         '())
        ((null? (cdr target-classes))
         target-classes)
        (else
         (bind (target title classes) (car target-classes)
           (receive (same rest) (partition-binary (cdr target-classes)
                                                  (lambda (el)
                                                    (equal? target (car el))))
             (cons (list target title (apply union test: eq? classes (map second same)))
                   (target-classes-union rest)))))))


(definition (class-properties class)
  (let* ((categorized (get-class-categorized-properties class))
         (keep-property?
           (lambda (property)
             (let ((category (get-name~ (get-category~ class property))))
               (and (eq? category 'properties)
                    (if categorized
                        (memq (get-name~ property) categorized)
                      (let ((property-descriptor (get-property-descriptor class (get-name~ property))))
                        (and property-descriptor (get-domain~ property-descriptor)))))))))
    (collect-if keep-property? (get-properties~ class))))


(definition (make-class-cell-domain classes)
  (new Enumeration-Domain choices: (cons (list #f "") 
                                         (zip classes
                                              (map get-class-presentation~
                                                   (map resolve-runtime-reference classes))))))


(definition (normalize-descriptor-target descriptor)
  (let ((target (get-target~ descriptor)))
    (cond
      ((not target)
       (error "Child-Descriptor has no target: {s}" descriptor))
      ((symbol? target)
       (list target))
      (else target))))


(definition (validate-descriptor-title descriptor)
  (or (get-title~ descriptor)
      (error "Child-Descriptor has not title: {s}" descriptor)))



;;;
;;;; Grid-Column
;;;


(class Grid-Column extends Tree-Label-Column
  
  
  (property class-column? initialize #f accessors generate)
  (property read-only? initialize #f accessors generate)
  
  
  (method public (get-path)
    (get-user-data))
  
  
  (method override (draw-data surface context row data width height)
    (let ((component (get-component~ row))
          (path (get-path)))
      (follow-component-path component path
        (lambda ()
          (nextmethod surface context row data width height))
        (lambda (child-component base property)
          (let ((domain (property-domain~ child-component property))
                (value (read-property child-component property)))
            (draw-data~ domain surface context value self row data width height)))))))


;;;
;;;; Grid-Row
;;;


(class Grid-Row extends Tree-Row
  
  
  (method public (get-component)
    (get-user-data)))


;;;
;;;; Grid-Tree
;;;


(class Grid-Tree extends Components-Tree
  
  
  (form
    (<install> reorderable?: #t multiple-selection?: #t selection-mode: cell selection-handler: {Event-Handler :self on-selection-change}))
  
  
  (definition Default-Class-Width 150)
  (definition Default-Property-Width 100)
  
  
  (method override (on-return-press evt)
    (end-edition))
  
  
  ;;;
  ;;;; Classes
  ;;;


  (method override (categorized-addable-classes component)
    (or (component-categorized-toolbox-addable-classes component)
        (nextmethod component)))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method override (on-selection-change evt)
    (let ((selection (get-selection~ evt)))
      (unless (null? selection)
        (select-cell (car selection))))
    (nextmethod evt))
  
  
  (method (cells->lines cells)
    (sort < (remove-duplicates (map get-line~ cells))))
  
  
  (method override (selected-components)
    (map get-selection-component
         (map (lambda (line)
                (new Cell line 0))
              (cells->lines (get-selection)))))
  
  
  ;;;
  ;;;; Editor
  ;;;
  
  
  (method (determine-client component)
    (let ((editor-client (get-client~ editor))
          (parent (get-parent~ component)))
      (cond ((or (not parent) (not-null? (addable-classes component)))
             (values component #f))
            ((not-null? (addable-classes parent))
             (values parent component))
            ((eq? parent editor-client)
             (values parent #f))
            (else
             (determine-client parent)))))
  
  
  (method override (selection-changed selection old origin force-parent? focus?)
    (unless (or (not origin) (eq? origin self))
      (if (null? selection)
          (begin
            (set-client (get-client~ editor))
            (set-selection '() generate-event?: #f))
        (let ((component (car selection)))
          (receive (client selected) (determine-client component)
            (set-client client)
            (if selected
                (let ((row (component-row selected)))
                  (when row
                    (set-selection (list (new Cell (row->line row) 0)) generate-event?: #t)))
              (set-selection '() generate-event?: #f)))))))
  
  
  ;;;
  ;;;; Select Cell
  ;;;
  
  
  (method (prepare-and-focus-entry entry)
    (let ((view-to-focus (tab-view~ entry)))
      (set-return-press-handler~ view-to-focus (new Event-Handler execute: on-return-press))
      (acquire-focus~ view-to-focus)
      (select-all~ view-to-focus)))
  
  
  (method (select-class-cell cell editor row-component path)
    (let ((parent-component (if (null? path) client (find-component-path row-component (butlast path))))
          (classes (if (null? path)
                       (addable-classes client)
                     (let* ((row-descriptor (get-descriptor (class-of row-component)))
                            (descriptor (find-key~ (find-child~ row-descriptor 'children) normalize-descriptor-target path)))
                       (when descriptor
                         (map get-runtime-reference~ (get-classes~ descriptor)))))))
      (when (and parent-component classes)
        (let ((old-component (if (null? path) row-component (find-child~ parent-component (last path)))))
          (let* ((domain (make-class-cell-domain classes))
                 (entry (setup-entry~ domain editor #f))
                 (end-edit (lambda (cell)
                             (when (get-modified?~ entry)
                               (let* ((ref (get-value~ entry))
                                      (import (second (serialize-runtime-reference ref)))
                                      (class (and ref (resolve-runtime-reference ref)))
                                      (new-component (and class (new class))))
                                 (when old-component
                                   (remove-child~ designer old-component))
                                 (when new-component
                                   (add-child~ designer new-component parent-component name: (and (not-null? path) (last path)) import: import)))))))
            (set-value~ entry (and old-component (class-of old-component)))
            (edit-cell cell editor: editor close-editor?: #f after-end: end-edit)
            (prepare-and-focus-entry entry))))))
  
  
  (method (select-property-cell cell editor row-component path)
    ;; No property can be edited until a row-component exists
    (when row-component
      (follow-component-path row-component path 
        (lambda () #f)
        (lambda (component base property)
          (let* ((domain   (property-domain~ component property))
                 (entry    (setup-entry~ domain editor #f)))
            (set-value~ entry (read-property component property))
            (unless (get-read-only?~ (get-column (get-col~ cell)))
              (let ((end-edit (lambda (cell)
                                (when (get-modified?~ entry)
                                  (let* ((value (get-value~ entry))
                                         (title (present-property~ component property value)))
                                    (set-title~ (get-cell-data cell) title)
                                    (safe-set-property~ designer component property value))))))
                (edit-cell cell editor: editor close-editor?: #f after-end: end-edit)
                (prepare-and-focus-entry entry))))))))
  
  
  (method override (select-cell cell)
    ;; Because changes in some cells could make other cells inactive
    (end-edition)
    (let ((editor (new Layout-View layout-type: 'fill))
          (column (get-column (get-col~ cell)))
          (row    (get-row (get-line~ cell))))
      (let ((path (get-path~ column))
            (row-component (get-component~ row)))
        (if (get-class-column?~ column)
            ;; Remove class-info from the path
            (select-class-cell cell editor row-component (butlast path))
          (select-property-cell cell editor row-component path)))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method override (client-update)
    (end-edition)
    (update))
  
  
  (method override (update)
    (with-update-locked
      (lambda ()
        (setup-columns)
        (insert-rows))))
  
  
  (method (add-class-column path title)
    (add-column class: Grid-Column title: title width: Default-Class-Width user-data: (append path '(class-info)) class-column?: #t))
  
  
  (method (add-property-column path title (read-only? #f))
    (add-column class: Grid-Column title: title width: Default-Property-Width user-data: path class-column?: #f read-only?: read-only?))
  
  
  (method (setup-client-columns)
    (define (setup-target-columns target title classes)
      (if (not-null? target)
          (add-class-column target title)
        (add-property-column '(name-info) "Name" #t)
        (when (> (length classes) 1)
          (add-class-column target title)))
      (let ((classes-properties (map (lambda (class)
                                       (map (lambda (property)
                                              (list class property))
                                            (class-properties class)))
                                     classes)))
        (apply for-each (lambda (class property)
                          (add-property-column
                            (append target (list (get-name~ property)))
                            (property-presentation~ class property)))
          (apply zip (apply union key: (lambda (class+property)
                                         (get-name~ (second class+property)))
                       classes-properties)))))
    
    (let ((classes (map resolve-runtime-reference (addable-classes client))))
      (when (not-null? classes)
        (let* ((addable-descriptors (map get-descriptor classes))
               (child-descriptors (append-map (lambda (descriptor)
                                                (get-children~ (find-child~ descriptor 'children)))
                                              addable-descriptors))
               (targets+titles+classes (apply zip (target-classes-union
                                                    (cons (list '() "Class" classes)
                                                          (map (lambda (descriptor)
                                                                 (list (normalize-descriptor-target descriptor)
                                                                       (validate-descriptor-title descriptor)
                                                                       (map resolve-reference~ (get-classes~ descriptor))))
                                                               child-descriptors))))))
          (apply for-each setup-target-columns targets+titles+classes)))))
  
  
  (method (setup-columns)
    (set-filled-column #f)
    (remove-columns)
    (when client
      (setup-client-columns)))
  
  
  (method override (row-class)
    Grid-Row)
  
  
  (method (insert-rows)
    (remove-every-row)
    (when client
      (insert-components client)))
  
  
  (method (insert-components component)
    (let ((components (get-components~ component)))
      (for-each (lambda (child)
                  (insert-component child))
                (if (sort-components?~ component)
                    (sort < components key: get-presentation-property~)
                  components))))
  
  
  (method (inactive-cell-background row-component column)
    (let ((component-path (butlast (get-path~ column))))
      (cond ((not (get-class-column?~ column))
             {Color Gray})
            ((null? component-path)
             {Color White})
            ((find-component-path row-component (butlast component-path))
             {Color White})
            (else
             {Color Gray}))))
  
  
  (method (make-row-element row-component column)
    (let ((path (get-path~ column)))
      (let ((make-inactive-element
              (lambda ()
                (let ((background (inactive-cell-background row-component column)))
                  (new Tree-Label title: "" background: background))))
            (make-active-element
              (lambda (component base property)
                (let ((title (present-property-value component property)))
                  (new Tree-Label title: title background: {Color Tree-Background})))))
        (follow-component-path row-component path make-inactive-element make-active-element))))
  
  
  (method (call-with-row+column cell proc)
    (proc (get-row (get-line~ cell))
          (get-column (get-col~ cell))))
  
  
  (method (update-cell cell)
    (let* ((editor (get-cell-editor))
           (label (if editor (get-preserved-data~ editor) (get-cell-data cell))))
      (call-with-row+column cell
        (lambda (row column)
          (let ((row-component (get-component~ row))
                (path (get-path~ column)))
            (let ((update-inactive-element
                    (lambda ()
                      (let ((background (inactive-cell-background row-component column)))
                        (set-title~ label "")
                        (set-background~ label background))))
                  (update-active-element
                    (lambda (component base property)
                      (set-title~ label (present-property-value component property))
                      (set-background~ label {Color Tree-Background}))))
              (follow-component-path row-component path update-inactive-element update-active-element)
              (invalidate-cell cell)))))))
  
  
  (method protected virtual (insert-component component)
    (let ((labels (map (lambda (column)
                         (make-row-element component column))
                       (get-columns))))
      (add-row children: labels user-data: component)))
  
  
  ;;;
  ;;;; Designer
  ;;;
  
  
  (method override (on-designer evt)
    (case (get-kind~ evt)
      ((:name-change)     (name-change (get-property~ evt component:) (get-property~ evt name:) (get-property~ evt old-name:)))
      ((:child-add)       (child-add (get-property~ evt child:) (get-property~ evt parent:)))
      ((:child-remove)    (child-remove (get-property~ evt child:) (get-property~ evt parent:)))
      ((:property-change) (property-change (get-property~ evt component:) (get-property~ evt property:)))))
  
  
  (method (child-add child parent)
    (if (eq? parent client)
        (insert-component child)
      (call-with-parsed-lineage child update-component-paths)))
  
  
  (method (child-remove child parent)
    (if (eq? parent client)
        (remove-row (component-row child))
      (call-with-parsed-lineage child update-component-paths)))
  
  
  (method (for-each-column proc)
    (for-each proc (get-columns) (iota (get-columns-count))))
  
  
  (method (super-path? path1 path2)
    (cond ((null? path1) #t)
          ((eq? (car path1)
                (car path2))
           (super-path? (cdr path1) (cdr path2)))
          (else #f)))
  
  
  (method (update-component-paths row-component . paths)
    (for-each-row
      (lambda (row row-rank)
        (when (eq? row-component (get-component~ row))
          (for-each-column
            (lambda (column column-rank)
              (let ((column-path (get-path~ column)))
                (for-each (lambda (path)
                            (when (super-path? path column-path)
                              (update-cell (new Cell row-rank column-rank))))
                          paths))))))))
  
  
  (method (call-with-parsed-lineage component proc)
    (let* ((lineage-from-client (get-lineage-from component client))
           (lineage-from-row (and lineage-from-client (cdr lineage-from-client))))
      (when (and lineage-from-row (not-null? lineage-from-row)
        (proc (car lineage-from-row)
              (map get-name~ (cdr lineage-from-row)))))))
  
  
  (method (name-change component new-name old-name)
    (if (eq? client (get-parent~ component))
        (update-component-paths component '(name-info))
      (call-with-parsed-lineage component
        (lambda (row-component change-path)
          (let ((old-path (append (butlast change-path) (list old-name)))
                (new-path (append (butlast change-path) (list new-name))))
            (update-component-paths row-component old-path new-path))))))
  
  
  (method (property-change component property)
    (call-with-parsed-lineage component
      (lambda (row-component change-path)
        (update-component-paths row-component (append change-path (list (field-name property)))))))))
