(module protected jazz.designer.manager.Style-Editor jazz


(import (jazz.designer)
        (jazz.event)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.handler)
        (jazz.jml)
        (jazz.layout)
        (jazz.resource)
        (jazz.text)
        (jazz.ui)
        (jazz.view)
        (jazz.workspace))


(class Style-Editor extends Layout-View
  
  
  (slot client accessors generate)
  (slot selection)
  
  
  (form
    (<install>                                 layout-type: flow layout-insets: {Rect 4 5 0 0} background: {Color Medium}
      (<Border-View>     name: style-border    size: {Dimension 120 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
        (<Border-View>                         border-color: {Color red: 200 green: 188 blue: 166}
          (<Style-Combo> name: style           enabled?: #f editable?: #t)))
      (<Border-View>     name: font-border     size: {Dimension 140 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
        (<Border-View>                         border-color: {Color red: 200 green: 188 blue: 166}
          (<Font-Combo>  name: font            enabled?: #f editable?: #t)))
      (<Border-View>     name: size-border     size: {Dimension 40 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
        (<Border-View>                         border-color: {Color red: 200 green: 188 blue: 166}
          (<Size-Combo>  name: size            enabled?: #f editable?: #t)))
      (<Separator>)
      (<Toggle-Tool>     name: bold            size: {Dimension 22 22} tooltip?: #t tooltip-text: "Bold" resource: {Image-Resource "Bold"} portfolio: :images enabled?: #f action-handler: {Event-Handler :form on-bold})
      (<Toggle-Tool>     name: italic          size: {Dimension 22 22} tooltip?: #t tooltip-text: "Italic" resource: {Image-Resource "Italic"} portfolio: :images enabled?: #f action-handler: {Event-Handler :form on-italic})
      (<Toggle-Tool>     name: underline       size: {Dimension 22 22} tooltip?: #t tooltip-text: "Underline" resource: {Image-Resource "Underline"} portfolio: :images enabled?: #f action-handler: {Event-Handler :form on-underline})
      (<Separator>)
      (<Color-Chooser>   name: text-color      size: {Dimension 22 22} enabled?: #f color: #f portfolio: :images resource: {Image-Resource "TextColor"} tooltip-text: "Select Text Color" action-handler: {Event-Handler :form on-pick-color})
      (<Color-Chooser>   name: highlight-color size: {Dimension 22 22} enabled?: #f color: #f portfolio: :images resource: {Image-Resource "HighlightColor"} tooltip-text: "Select Highlight Color" action-handler: {Event-Handler :form on-pick-color})
      (<Color-Chooser>   name: frame-color     size: {Dimension 22 22} enabled?: #f color: #f portfolio: :images resource: {Image-Resource "FrameColor"} tooltip-text: "Select Frame Color" action-handler: {Event-Handler :form on-pick-color})))
  
  
  (method meta override (external-name self . rest)
    'Style)
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Document"})
  

  (method meta override (host-title self)
    "Style")
  
  
  ;;;
  ;;;; Editor
  ;;;
  
  
  (method (on-editor self evt)
    (case (get-kind evt)
      ((:selection-changed)
       (let ((selection (and (not-null? (get-selection evt)) (car (get-selection evt)))))
         (set! self.selection selection)
         (selection-update self)))))
         
  
  (method (on-designer self evt)
    (case (get-kind evt)
      ((:property-change)
       (when (eq? (get-effective-property self)
                  (get-name (get-property evt property:)))
         (selection-update self)))))
  
  
  (method (enable-combo self name client enable?)
    (let ((component (locate self name)))
      (set-enabled? component enable?)
      (set-client component client)
      (selection-change component)))
  
  
  (method (enable-toggler self name enable? selected?)
    (let ((component (locate self name)))
      (set-enabled? component enable?)
      (set-selected? component selected?)))
  
  
  (method (enable-color-button self name enable? color)
    (let ((component (locate self name)))
      (set-enabled? component enable?)
      (set-color component color)))
  
  
  (method virtual (get-effective-property self)
    'style)
  
  
  (method virtual (get-effective-view self)
    (and (is? selection View) selection))
  
  
  (method package virtual (get-effective-style self view)
    (get-style view))
  
  
  (method package (selection-update self)
    (let* ((view (get-effective-view self))
           (style (and view (get-effective-style self view)))
           (valid-style? (or (not style) (is? style Style)))
           (font (and style valid-style? (get-font style))))
      (enable-combo self 'style view (and view valid-style?))
      (enable-combo self 'font view (and view valid-style?))
      (enable-combo self 'size view font)
      (enable-toggler self 'bold font (and font (get-bold? font)))
      (enable-toggler self 'underline font (and font (get-underline? font)))
      (enable-toggler self 'italic font (and font (get-italic? font)))
      (enable-color-button self 'text-color font (and style valid-style? (get-color style)))
      (enable-color-button self 'highlight-color font (and style valid-style? (get-highlight style)))
      (enable-color-button self 'frame-color font (and style valid-style? (get-frame style)))))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-bold self evt)
    (toggle-bold client))
  
  
  (method (on-italic self evt)
    (toggle-italic client))
  
  
  (method (on-underline self evt)
    (toggle-underline client))
  
  
  (method (on-pick-color self evt)
    (let* ((sender (get-sender evt))
           (position (nu+ (get-position sender) (new Point 0 (get-height sender)))))
      (popup self (new Color-Picker colors: #f geometry: {Cell 3 8} action-handler: (new Event-Handler execute: (lambda (evt) (on-color self sender evt)))) position initiator: sender)))
  
  
  (method (on-color self sender evt)
    (close-popups self)
    (let ((color (get-property evt color:)))
      (case (get-name sender)
        ((text-color)
         (change-text-color client color))
        ((highlight-color)
         (change-highlight-color client color))
        ((frame-color)
         (change-frame-color client color)))))
  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method override (client-activate self)
    (let ((guest (current-document)))
      (let ((client (and (is? guest View-Editor) (not (previewing? guest)) guest)))
        (set! self.client client)
        (when client
          (set-editor (locate self 'font) self)
          (set-editor (locate self 'size) self)
          (set-editor (locate self 'style) self)
          (add-editor-listener client (new Event-Handler target: self method-name: 'on-editor))
          (add-designer-listener (get-designer client) (new Event-Handler target: self method-name: 'on-designer))))))
  
  
  (method override (client-deactivate self)
    (when client
      (remove-editor-listener client self)
      (and (get-designer client) (remove-designer-listener (get-designer client) self)))
    (set! client #f)
    (set! selection #f)
    (selection-update self)))


(class Style-Combo extends Text-Style-Combo
  
  
  (slot editor accessors generate)
  
  
  (method override (client-update self)
    (set-choices self (map (lambda (name)
                        (list (new Text-Style base: name) (->string name)))
                      (sort string-ci<? (symbolic-styles) key: ->string))))
  
  
  (method override (selection-change self)
    (let* ((style (and client (get-effective-style editor client)))
           (base (and (is? style Text-Style) (get-base style)))
           (name (and base (not-null? base) (last base))))
      (set-content self (and name (->string name)))))
  
  
  (method override (combo-choice self)
    (let ((val (get-value self)))
      (on-style (get-client editor) val))))


(class Font-Combo extends Text-Font-Combo
  
  
  (slot editor accessors generate)
  
  
  (method override (selection-change self)
    (let* ((style (and client (get-effective-style editor client)))
           (font (and (is? style Text-Style) (get-font style))))
      (if font
          (set-value self (get-font-name font))
        (set-choice self '("" #f)))))
  
  
  (method override (combo-choice self)
    (let ((val (get-value self)))
      (on-font-name (get-client editor) val))))


(class Size-Combo extends Text-Size-Combo
  
  
  (slot editor accessors generate)
  
  
  (method override (selection-change self)
    (let* ((style (and client (get-effective-style editor client)))
           (font (and (is? style Text-Style) (get-font style))))
        (if font
            (set-value self (get-point-size font))
          (set-choice self '("" #f)))))
  
  
  (method override (combo-choice self)
    (let ((val (get-value self)))
      (on-font-size (get-client editor) val))))


(class Color-Chooser extends Push-Tool
  
  
  (property color  initialize #f accessors explicit)
  
  
  (method public (get-color self)
    color)


  (method public (set-color self value)
    (set! color value)
    (invalidate-view self))
  
  
  (method override (draw-tool self surface context)
    (nextmethod self surface context)
    (when color
      (let* ((offset (if (eq? state 'pushed) 2 1))
             (rect (offset-rect (new Rect 2 14 18 18) offset offset)))
        (fill-rect surface rect (if (view-enabled? self) color {Color Dark})))))))
