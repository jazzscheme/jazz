;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Toolbox Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.designer.toolbox.Toolbox-Manager jazz


(import (jazz.designer)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.library.component)
        (jazz.system)
        (jazz.system.process)
        (jazz.ui)
        (jazz.ui.view))


;;;
;;;; Read
;;;


(definition (read-toolbox)
  (define (load-toolbox file)
    (if (exists?~ file)
        (instantiate~ (read-form file))
      (new Toolbox)))
  
  (with-safe-execution "loading the toolbox"
    (lambda ()
      (let ((appl (get-application)))
        (let ((toolbox (load-toolbox (toolbox-file~ appl))))
          (install-context~ appl toolbox)
          toolbox)))
    continue-handler:
    (lambda (err)
      )))


;;;
;;;; Toolbox
;;;


(class undocumented Toolbox-Manager extends Component-Editor implements Exit-Listener


  (form
    (<install>                            layout-type: border
      (<Layout-View>    name: tools
        (<Push-Tool>    name: add         size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Add" resource: {Image-Resource "Added"} portfolio: :images action-handler: {Event-Handler :guest on-add})
        (<Push-Tool>    name: new         size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "New" resource: {Image-Resource "NewTrait"} portfolio: :images action-handler: {Event-Handler :guest on-new})
        (<Push-Tool>    name: edit        size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Edit" resource: {Image-Resource "EditResult"} portfolio: :images action-handler: {Event-Handler :guest on-edit})
        (<Push-Tool>    name: sync        size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Sync" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :guest on-sync}))
      (<Toolbox-Editor> name: descendants location: center
        (<!>            name: scroller    style: tool
          (<!>          name: content
            (<!>        name: header
              (<!>      name: content
                (<!>    name: tree        highlight-edited?: #f auto-expand/collapse?: #t double-click-handler: {Event-Handler :form on-double-click}
                  (<!>  name: descendant  title: "Model" width: 291)))))))))
  
  
  (method meta override (external-name . rest)
    'toolbox-manager)

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon)
    {Image-Resource "Toolbox"})
  

  (method meta override (host-title)
    "Toolbox")
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (get-tools-view)
    (locate 'tools))
  
  
  (method override (focus-default)
    (let ((tree (locate '(descendants tree))))
      (acquire-focus~ tree)))
  
  
  ;;;
  ;;;; Controller
  ;;;


  (method override (new-controller)
    (new Toolbox-Manager-Controller self))

  
  ;;;
  ;;;; Component
  ;;;


  (method override (install rest)
    (nextmethod rest)
    (set-shown?~ (locate-component '(descendants header)) #f)
    (set-editor~ (locate 'descendants) self)
    (set-client (read-toolbox))
    (add-exit-listener~ (get-application) self))
  
  
  (method override (prepare-exit)
    (when (and designer (file? (get-origin~ (get-form~ designer))))
      (save~ designer)))
  
  
  ;;;
  ;;;; Tree
  ;;;
  
  
  (method (get-tree)
    (locate '(descendants tree)))
  
  
  ;;;
  ;;;; Document
  ;;;
    
  
  (method (document-status)
    "Toolbox")
  
  
  ;;;
  ;;;; Modules
  ;;;
  
  
  (method (get-dynamic-toolboxes)
    (collect-if (lambda (cp) (is? cp Toolbox-Dynamic))
                (collect-family~ (get-client))))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-add evt)
    (add-new))
  
  
  (method (on-new evt)
    (edit-new))
  
  
  (method (on-edit evt)
    (edit))
  
  
  (method (on-sync evt)
    (sync-dynamic-toolbox-groups))
  
  
  (method (on-double-click evt)
    (add-new))
  
  
  (method (call-with-selected-toolbox-model func)
    (let ((selected-model (get-single-selected-data~ (get-tree))))
      (and (is? selected-model Toolbox-Model)
           (func selected-model))))
  
  
  (method (selected-class)
    (call-with-selected-toolbox-model get-class~))
  
  
  (method (selected-class-name)
    (call-with-selected-toolbox-model get-model~))
  
  
  (method (editor-class component)
    (typecase component
      ((View) View-Editor)
      ((Component) Grid-Component-Editor)))
      
  
  (method (add-new)
    (let ((class (selected-class))
          (editor (current-document)))
      (when (and class (is? editor View-Editor))
        (add-new~ (get-surrogate~ (get-client~ editor)) class {Point 0 0} centered?: #f))))
  
  
  (method (edit)
    (let* ((class (selected-class))
           (client (new class)))
      (new-frame (editor-class client) client: client)))
  
  
  (method (sync-dynamic-toolbox-groups)
    (with-update-locked
      (lambda ()
        (let ((tree (get-tree)))
          (for-each (lambda (toolbox)
                      (let ((module-name (get-module-name~ toolbox)))
                        (for-each (lambda (class-name)
                                    (let ((model (new Toolbox-Model parent: toolbox name: class-name))
                                          (props (list unit: module-name model: class-name)))
                                      (install-new-component~ tree (get-designer) model toolbox (symbol->string class-name) '() props)))
                                  (get-missing-classes~ toolbox))))
                    (get-dynamic-toolboxes))))))
  
  
  (method (edit-new)
    (let ((class-name (selected-class-name))
          (class (selected-class)))
      (let* ((model-name (string->symbol (string-append "<" (symbol->string class-name) ">")))
             (form (construct-form (list model-name) (lambda (symbol)
                                                       (and (eq? symbol class-name) class))))
             (client (instantiate~ form)))
        (set-origin~ form #f)
        (new-frame (editor-class client) client: client)))))


;;;
;;;; Controller
;;;


(class Toolbox-Manager-Controller extends Component-Editor-Controller
  
  
  (method override (document-status)
    "Toolbox")))
