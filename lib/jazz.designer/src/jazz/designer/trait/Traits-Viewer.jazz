;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Traits Viewer
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.designer.trait.Traits-Viewer jazz


(import (jazz.descriptor)
        (jazz.handler)
        (jazz.markup)
        (jazz.resource)
        (jazz.tree)
        (jazz.ui)
        (jazz.workspace))


(class undocumented Traits-Viewer extends Layout-View


  (form
    (<install>                              layout-type: fill
      (<Scroller-View>       name: scroller hscroll?: #t vscroll?: #t style: tool
        (<!>                 name: content  layout-type: fill
          (<Tree-Header>     name: header   shown?: #f
            (<!>             name: content
              (<Viewer-Tree> name: tree
                (<!>         name: trait    width: 183))))))))
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Traits"})
  

  (method meta override (host-title self)
    "Traits")
  
  
  (method override (client-activate self)
    (set-client self (current-document)))
  
  
  (method override (client-deactivate self)
    (set-client self #f))
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (focus-default self)
    (let ((tree (locate self 'tree)))
      (acquire-focus tree)
      (select-first-row tree)))
    
  
  ;;;
  ;;;; Hosting
  ;;;
  
  
  (method override (install-in-host self host)
    (nextmethod self host)
    (set-layout-type (get-root host) 'fill)
    (set-layout-insets (get-root host) {Rect 0 2 0 0}))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-tree self)
    (locate self 'tree))

  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method public (get-client self)
    (get-client (get-tree self)))
  
  
  (method public (set-client self client)
    (set-client (get-tree self) client)))


;;;
;;;; Viewer-Tree
;;;


(class Viewer-Tree extends Tree-View
  
  
  (form
    (<install>                        multiple-selection?: #f portfolio: :images selection-handler: {Event-Handler :self on-selection-change}
      (<Tree-Node-Column> name: trait title: "Trait" width: 157)))
  
  
  (slot client initialize #f)
  
  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method public (get-client self)
    client)
  
  
  (method public (set-client self clt)
    (when (neq? clt client)
      (set! client clt)
      (update-tree self)))
  
  
  (method (update-tree self)
    (remove-every-row self)
    (when client
      (let ((node (insert-client self)))
        (for-each (lambda (trait)
                    (insert-trait self node trait))
                  (child-traits client)))))
  
  
  (method (insert-client self)
    (let ((title (get-presentation-property client))
          (image (component-image self client)))
      (add-row self state: 'expanded children: (list (new Tree-Node title: title image: image)) user-data: client)))
  
  
  (method (insert-trait self node trait)
    (let ((children (get-children trait))
          (title    (get-presentation-property trait))
          (image    (component-image self trait)))
      (let ((handle (add-row self father: node state: 'expanded children: (list (new Tree-Node title: title image: image)) user-data: trait)))
        (for-each (lambda (child)
                    (insert-trait self handle child))
                  children))))
  
  
  (method package virtual (component-image self component)
    (get-component-image component))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method package (on-selection-change self evt)
    (select-trait self (or (selected-trait self) client)))
  
  
  (method public (selected-trait self)
    (let ((selection (get-single-selection self)))
      (when selection
        (get-user-data (get-row self selection)))))
  
  
  (method package (select-trait self trait)
    (when trait
      (set-active-trait client trait)))))
