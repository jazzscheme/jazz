;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Deterministic Profiler
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.detprof jazz


(export (jazz.detprof.syntax))


(import (jazz.detprof.syntax)
        (jazz.jml)
        (jazz.system)
        (profiler))


;;;
;;;; Advice
;;;


(definition public (profiling-advice profile (time?: time? #f))
  (lambda (field locator original set)
    (let ((original (original)))
      (when (procedure? original)
        (set (lambda rest
               (define (call-original)
                 (with-profiling profile
                   (lambda ()
                     (apply original rest))))
               
               (if time?
                   (time (call-original))
                 (call-original))))))))


(definition public (profile-advice field locator original set)
  (let ((original (original)))
    (when (procedure? original)
      (set (lambda rest
             (profile-code original
               (lambda ()
                 (apply original rest))))))))


;;;
;;;; Profile
;;;


(define time-spent-in-children
  (make-parameter 0))


(definition public (profile-code original proc)
  (let ((profile (active-profile))
        (original-call (and original (list original (jazz.locat->file/line/col (jazz.procedure-locat original))))))
    (define (profile-stack)
      (continuation-capture
        (lambda (cont)
          (if original-call
              (cons original-call (identify-call cont (- (profile-depth profile) 1) (profile-profiler profile)))
            (identify-call cont (profile-depth profile) (profile-profiler profile))))))
    
    (if profile
        (let ((outer-start-time (profiler-performance-counter)))
          (let ((result (parameterize ((time-spent-in-children 0))
                          (let ((inner-start-time (profiler-performance-counter)))
                            (let ((result (proc)))
                              (let ((total-elapsed-time (- (profiler-performance-counter) inner-start-time (get-inner-epsilon))))
                                (profile-register-call profile (profile-stack) (- total-elapsed-time (time-spent-in-children))))
                              result)))))
            (let ((total-elapsed-time (- (profiler-performance-counter) outer-start-time (get-outer-epsilon))))
              (time-spent-in-children (+ (time-spent-in-children) total-elapsed-time)))
            result))
      (proc))))


;;;
;;;; Epsilon
;;;


(define outer-epsilon
  #f)

(define inner-epsilon
  #f)


(define (get-outer-epsilon)
  (when (not outer-epsilon)
    (setup-epsilon))
  outer-epsilon)

(define (get-inner-epsilon)
  (when (not inner-epsilon)
    (setup-epsilon))
  inner-epsilon)


(define (setup-epsilon)
  (define (estimate-outer-epsilon)
    (let ((outer-start-time (profiler-performance-counter)))
      (time-spent-in-children (+ (time-spent-in-children) 1))
      (unroll 99
              (profiler-performance-counter)
              (time-spent-in-children (+ (time-spent-in-children) 1)))
      (quotient (- (profiler-performance-counter) outer-start-time) 100)))
  
  (define (estimate-inner-epsilon)
    (let ((inner-start-time (profiler-performance-counter)))
      (apply test-proc '(1 2 3))
      (unroll 99
              (profiler-performance-counter)
              (apply test-proc '(1 2 3)))
      (quotient (- (profiler-performance-counter) inner-start-time) 100)))
  
  (parameterize ((time-spent-in-children 0))
    (set! outer-epsilon (estimate-outer-epsilon))
    (set! inner-epsilon (estimate-inner-epsilon))))


(define (test-proc . rest)
  rest)


;;;
;;;; JML
;;;


(definition public (profile->jml profile)
  (map (lambda (profile)
         (bind (name count . duration) profile
           (new JML-Element #f 'profile properties: (list 'name name 'count count 'duration duration))))
       (sort > (table->list profile) key: cddr)))


;;;
;;;; Detprof
;;;


(definition public detprof
  (make-profiler 'detprof #f #f 1))


(profiler-ignore-module detprof 'jazz.detprof)


(register-service 'detprof (lambda () detprof)))
