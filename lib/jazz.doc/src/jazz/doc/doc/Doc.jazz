;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Documentation Entry
;;;
;;;  Filename: Doc.jazz
;;;  Copyright: Guillaume Cartier
;;;


(module protected jazz.doc.doc.Doc jazz


(import (jazz.component)
        (jazz.catalog)
        (jazz.jml))


(class Doc extends Component
  
  
  (property title       initialize #f accessors generate)
  (property description initialize #f accessors generate)
  
  
  (method override (install-child self form creator creator-branch restore-form)
    (case (get-model form)
      ((description) (set-description self form))
      (else (nextmethod self form creator creator-branch restore-form))))


  ;;;
  ;;;; Access
  ;;;
  
  
  (method public virtual (get-catalog self)
    (get-catalog parent))
  
  
  (method public virtual (set-catalog self cat)
    )


  ;;;
  ;;;; Title
  ;;;
  
  
  (method public (get-effective-title self)
    (or title (->string name)))


  ;;;
  ;;;; Catalog
  ;;;
  
  
  (method public virtual (update-indexes self definitions-index references-index)
    (when (not-null? name)
      (table-add definitions-index name self)))
  
  
  (method public virtual (print-catalog self level output)
    (format output "{%}{_ :v}簖簖簖í戾鲥博ㄣ狒彗矧钺礤ㄣ灬篌镦箦戽┅钺礤溴筱蜷痿轱瞟换换换娘沲礤铘狒轱换礤翳镤瘐忪殂ㄧ弭滹沲礤铘狒轱箦戽簌礅镬戾è汜翎祜ㄧ弭汜翎祜箦戽┅ㄦ轭洵殒灬礅溽ㄤ镢ㄥ窨ㄧ弭疳蝈铘滹悌箦戽┅ㄧ弭溴骈铋糸镱汜翎祜簌礅镬┅┅换换换序弩孱翎糸镱换礤翳镤瘐忪殂鲩螋踽痱弩孱箦戽溴筱蜷痿轱瞟礤翳镤瘐忪殂鲩螋踽痱弩孱舡翦箦戽麒孱溴筱蜷痿轱ㄢ轭翎疳蜥珧狃梵ō娟盱ㄦ矧憝绢镤溴筱蜷痿轱瞟啜俭疳罹括殒铛祆疳蜥珧狃梵Ж疳蜥珧狃梵┅┅礤翳镤痱雉邈翦痱弩孱舡疳蜥礤翦蝮箦戽疳蜥礤翦蝮ㄤ彐轭痱弩孱舡疳蜥礤翦疳蜥礤翦颟ㄩ簌礅镬疳蜥礤翦颟ō倔趄轭疳蜥礤翦颟ㄦ矧磲Ⅺ猃猃ㄣ狎疳蜥礤翦颟ㄣ徜疳蜥礤翦颟┅ㄣ镱è铛祆疳蜥礤翦蝮è篝蜷铉疳蜥礤翦蝮疳蜥礤翦蝮ㄥ祗戾è痫箝糸镱犰ㄧ弭疳蜥礤翦蝮痫箝糸镱犰铒舡骘躅浜Ж┅镳糸镱犰ㄧ弭疳蜥礤翦蝮镳糸镱犰铒舡骘躅浜Ж┅钺礤ㄧ弭疳蜥礤翦蝮钺礤浜铒舡骘躅浜Ж┅蝈篝ㄧ弭疳蜥礤翦蝮蝈篝铒舡骘躅浜ｆ┅戾è犰ㄡ痧孱磲灬礅溽疳蜥礤翦颟痱弩孱舡疳蜥礤翦疳蜥礤翦颟痫箝糸镱犰磲灬礅溽疳蜥礤翦颟ㄦ矧磲③猃茛痱弩孱舡疳蜥礤翦疳蜥礤翦颟┅镳糸镱犰磲灬礅溽疳蜥礤翦颟ㄦ矧磲猃" (present-parameter parameter)))
                                  named)
                             (if rest
                                 (list (format ". {a}" (present-parameter rest)))
                               '()))))
               (if (null? all)
                   ""
                 (join all " ")))))))
  
  
  (method protected (present-result-type self result-type)
    (if result-type
        `(" "
          (<span> class: "parameter" ,(->string result-type)))
      '()))


  ;;;
  ;;;; HTML
  ;;;
  
  
  (constant Red-Ball
    "images/red-ball.gif")
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor self (context: context #f))
    (new jazz.doc.descriptors:Doc-Descriptor))))
