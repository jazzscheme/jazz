;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Enumeration Domains
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.domain.Enumeration-Domain jazz


(import (jazz.domain)
        (jazz.locale)
        (jazz.markup)
        (jazz.reference))


(class Enumeration-Domain extends Domain
  
  
  (form
    (<install> nullable?: #t))
  
  
  (property choices                     initialize '() accessors generate)
  (property choices-handler             initialize #f  accessors generate)
  (property condensed?                  initialize #f  accessors generate)
  (property strict?                     initialize #f  accessors generate)
  (property limit                       initialize #f  accessors generate)
    
  
  ;;;
  ;;;; Validation
  ;;;
  
  
  (method override (validate-value self value (validation-context #f))
    (or (nextmethod self value validation-context)
        (boolean choices-handler)
        (let ((values (map car choices)))
          (if strict?
              (unless (member? value values)
                (add-violation self "must be one of: {a}" (join (map ->string (if nullable? (cons #f values) values)) ", ")))
            (when (and limit (< limit (string-length (->string value))))
              (add-violation self "{a}" (contextual-locales {Locales english: "This field can't contain more than {limit} characters" french: "Ce champ ne peut contenir plus de {limit} caractÃ¨res"} (local-context limit))))))))
  
  
  ;;;
  ;;;; Presentation
  ;;;
  
  
  (method override (present-value self value)
    (let ((pair (assq value choices)))
      (if pair
          (cadr pair)
        (->string value))))
  
  
  ;;;
  ;;;; Entry
  ;;;
  
  
  (method protected (collect-choices self client)
    (if choices-handler
        (let* ((choices ((reference-resolve choices-handler) client))
               (sorted-choices (sort string<=? (if nullable? (cons #f choices) choices) key: (compose ->string localize))))
          (zip sorted-choices
               (map (lambda (value)
                      (if value (->string value) (localize (get-nil-presentation self))))
                    sorted-choices)))
      (let ((choices (map (lambda (pair)
                            (list (car pair) (localize (cadr pair))))
                          choices)))
        (if nullable?
            (cons (list #f (localize (get-nil-presentation self))) choices)
          choices))))
    
  
  (method override (setup-entry self view client)
    (let ((entry (new jazz.designer.ui:Enumeration-Entry parent: view size: (get-size view) domain: self)))
      (let ((combo (locate entry 'combo)))
        (set-condensed? combo condensed?)
        (set-choices combo (collect-choices self client)))
      entry))))
