;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; CLike Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.clike.text.CLike-Text-View jazz


(import (jazz.editor.c)
        (jazz.library.automata)
        (jazz.system)
        (jazz.ui))


(class CLike-Text-View extends Code-Text-View
  
  
  (method override (install rest)
    (nextmethod rest)
    (let ((pref (preferences))
          (default (get-name~ default-style)))
      (add-style-preference 'Title            default (get-title-style~ pref))
      (add-style-preference 'Definition       default (get-definition-style~ pref))
      (add-style-preference 'Line-Comment     default (get-line-comment-style~ pref))
      (add-style-preference 'Balanced-Comment default (get-balanced-comment-style~ pref))
      (add-style-preference 'Keyword          default (get-keyword-style~ pref))
      (add-style-preference 'Profile-Macro    default (get-profile-macro-style~ pref))
      (add-style-preference 'Profile-Keyword  default (get-profile-keyword-style~ pref))
      (add-style-preference 'Profile-Type     default (get-profile-type-style~ pref))))
  
  
  (method override (new-syntax)
    (new C-Syntax))


  ;;;
  ;;;; Match
  ;;;


  ;; temporarely...
  (method override (flash-character? char)
    #f)

  
  ;;;
  ;;;; Tabulation
  ;;;


  (method override (return-press key)
    (nextmethod key)
    (tabulate))

  
  ;;;
  ;;;; Completion
  ;;;
  
  
  (method override (commented? pos)
    (let ((style (get-style-at pos)))
      (and style
           (or (inherits?~ style 'Title)
               (inherits?~ style 'Line-Comment)
               (inherits?~ style 'Balanced-Comment)))))

  
  ;;;
  ;;;; Keywords
  ;;;
  
  
  (method protected virtual (get-keywords)
    )
      
  
  ;;;
  ;;;; Profile
  ;;;

  
  (method protected virtual (get-profile-macros profile)
    )
  
  
  (method protected virtual (get-profile-keywords profile)
    )
  
  
  (method protected virtual (get-profile-types profile)
    )

  
  ;;;
  ;;;; Mouse Copy
  ;;;

  
  (method override (mouse-insert copy)
    (let ((start (get-start))
          (end (get-end)))
      @implement-with-c-logic-ie-capitalized-variables
      (when (/= start (text-beginning))
        (let ((sc (get-char (previous-cell start))))
          (when (and (text-constituent?~ syntax (element copy 0)) (text-constituent?~ syntax sc) (not (memq? sc '(#\minus #\colon))))
            (set! copy (concatenate " " copy)))))
      @implement-with-c-logic-ie-capitalized-variables
      (when (and (/= end (text-ending)) (text-constituent?~ syntax (last copy)) (text-constituent?~ syntax (get-char end)))
        (set! copy (concatenate copy " ")))
      (insert-string copy)))

  
  ;;;
  ;;;; Coloring
  ;;;
  
  
  (method override (compute-syntax-targets)
    (let ((profile (get-profile~ (get-application))))
      (append (list (list "/*" 'balanced-comment #f))
              (list (list "*/" 'balanced-comment-backward #f))
              (list (list "//" 'line-comment #f))
              (map (lambda (str) (list str 'keyword)) (get-keywords))
              (map (lambda (str) (list str 'profile-macro)) (get-profile-macros profile))
              (map (lambda (str) (list str 'profile-keyword)) (get-profile-keywords profile))
              (map (lambda (str) (list str 'profile-type)) (get-profile-types profile)))))
  
  
  (method override (compute-syntax-automata)
    (build-search-automata (syntax-targets) #t #f c-constituent?))

  
  (method override (colorize-type found type start end colorizer recolorize?)
    (case type
      ((balanced-comment)          (colorize-balanced-comment          found start end colorizer 'Balanced-Comment))
      @irregex-to-convert
      ((balanced-comment-backward) (colorize-balanced-comment-backward found start end colorizer 'Balanced-Comment recolorize?))
      ((line-comment)              (colorize-line-comment              found start end colorizer 'Line-Comment 'Title #\slash))
      ((keyword)                   (colorize-keyword                   found start end colorizer 'Keyword))
      ((profile-macro)             (colorize-keyword                   found start end colorizer 'Profile-Macro))
      ((profile-keyword)           (colorize-keyword                   found start end colorizer 'Profile-Keyword))
      ((profile-type)              (colorize-keyword                   found start end colorizer 'Profile-Type))))


  ;;;
  ;;;; Explorer
  ;;;


  (method override (explorer-class)
    C-Explorer)
 
  
  ;;;
  ;;;; Expr
  ;;;
  
  
  (method override (get-context)
    '(c))


  (method override (current-name)
    (name-at (current-expr-range)))
  
  
  (method (name-at range)
    (when range
      (let ((name (get-string range)))
        (if (eqv? (element name 0) #\open-brace)
            (read-string-element name)
          name))))


  (method override (current-expr)
    (current-name))
  
  
  (method override (current-expr-range)
    (if (and (not completion-active?) (has-selection?))
        (get-selection)
      (expr-range (get-end))))
  
  
  (method override (expr-at pos)
    (name-at (expr-range pos)))

  
  ;;;
  ;;;; Chapters
  ;;;
  
  
  (constant headers
    (list
      (concatenate "///" (string #\tab))
      (concatenate "///" (string #\space))))
  
  
  (method override (get-headers)
    headers)))
