;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; JavaScript Syntax
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.javascript.text.JavaScript-Syntax jazz


(import (jazz.automata)
        (jazz.editor)
        (jazz.editor.clike)
        (jazz.editor.javascript)
        (jazz.exemplar)
        (jazz.preferences)
        (jazz.text))


(class JavaScript-Syntax extends Code-Syntax
  
  
  ;;;
  ;;;; Preferences
  ;;;

  
  (method override (preferences self)
    (current-preferences '(documents javascript-text)))
  
  
  ;;;
  ;;;; Constituent
  ;;;
      
  
  (method override (get-constituent-test self)
    javascript-constituent?)

  
  ;;;
  ;;;; Completion
  ;;;
  
  
  (method override (favorite-completions self)
    (get-completions (preferences self)))
  
  
  (method override (symbol-completions self name)
    '())


  ;;;
  ;;;; Explorer
  ;;;


  (method override (explorer-class self)
    JavaScript-Explorer)
 
  
  ;;;
  ;;;; Expression
  ;;;
  
  
  (method override (name-at self range)
    (when range
      (let ((name (get-string model range)))
        (when (> (cardinality name) 0)
          (if (eqv? (element name 0) #\open-brace)
              (read-string-element name)
            name)))))

  
  ;;;
  ;;;; Chapters
  ;;;
  
  
  (constant headers
    (list
      "/// "))
  
  
  (method override (get-headers self)
    headers)
  
  
  (method override (get-first-special? self)
    #f)

  
  ;;;
  ;;;; Definitions
  ;;;
  
  
  (constant Definitions
    '())
  
  
  (method package (get-definitions self)
    Definitions)

  
  ;;;
  ;;;; Keywords
  ;;;
  
  
  (constant Keywords
    '("new"
      "delete"
      "this"
      "if"
      "else"
      "while"
      "for"
      "do"
      "case"
      "default"
      "break"
      "continue"
      "return"
      "try"
      "catch"
      "throw"
      "true"
      "false"
      "null"
      "var"
      "function"
      "let"))
  
  
  (method package virtual (get-keywords self)
    Keywords)

  
  ;;;
  ;;;; Coloring
  ;;;
  
  
  (definition Syntax-Targets
    #f)
  
  (definition Syntax-Automata
    #f)
  
  
  (method override (syntax-targets self)
    (unless Syntax-Targets
      (set! Syntax-Targets (compute-syntax-targets self)))
    Syntax-Targets)
  
  
  (method override (syntax-automata self)
    (unless Syntax-Automata
      (set! Syntax-Automata (compute-syntax-automata self)))
    Syntax-Automata)
  
  
  (method override (compute-syntax-targets self)
    (append (list (list "/*" 'balanced-comment #f))
            (list (list "*/" 'balanced-comment-backward #f))
            (list (list "//" 'line-comment #f))
            (map (lambda (str) (list str 'definition)) (get-definitions self))
            (map (lambda (str) (list str 'keyword)) (get-keywords self))))
        
  
  (method override (compute-syntax-automata self)
    (build-search-automata (syntax-targets self) #t #t javascript-constituent?))
  
  
  (method override (colorize-type self found type start end colorizer recolorize?)
    (case type
      ((balanced-comment)          (colorize-balanced-comment self          found start end colorizer 'Balanced-Comment))
      ((balanced-comment-backward) (colorize-balanced-comment-backward self found start end colorizer 'Balanced-Comment recolorize?))
      ((line-comment)              (colorize-line-comment self              found start end colorizer 'Line-Comment 'Title #\slash))
      ((definition)                (colorize-definition self                found start end colorizer 'Keyword 'Definition))
      ((keyword)                   (colorize-keyword self                   found start end colorizer 'Keyword))))
  
  
  (method (colorize-definition self found start end colorizer keyword-style definition-style)
    (apply-coloring self colorizer start end keyword-style)
    (let* ((line (get-line end))
           (col (get-col end))
           (str (paragraph-string model line))
           (len (paragraph-length model line))
           (open (search str "{" start: col))
           (close (search str "}" start: col)))
      (when (and open close (> close open))
        (apply-coloring self colorizer (new Cell line (+ open 1)) (new Cell line close) definition-style)))
    (cdr found))
  
  
  ;;;
  ;;;; Styles
  ;;;
  
  
  (definition Comment-Styles
    '(Title Line-Comment))
  
  
  (method override (comment-style? self style)
    (some? (lambda (style-name)
             (inherits? style style-name))
           Comment-Styles))))
