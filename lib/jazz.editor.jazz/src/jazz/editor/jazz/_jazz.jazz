;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Language
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.editor.jazz jazz


(import (jazz.editor.jazz.autoload)
        (jazz.system))


(export (jazz.editor.jazz.autoload))


(definition public (call-with-saved-text file func)
  (call-with-text file
    (lambda (text)
      (let ((result (func text)))
        (save~ text)
        result))))


(definition public (call-with-text file func)
  (let* ((frame (edit-document~ (get-application) file force-new?: #t class: Jazz-Text-View visible?: #f))
         (text (get-guest~ frame))
         (result (func text)))
    (close~ frame)
    result))


(definition public (insert-imports text imports)
  (define (insert-import-declaration initial-import)
    (let ((explorer (new Jazz-Explorer (get-model~ text))))
      (forward-namespace~ explorer)
      (unless (enter-import~ explorer)
        (let ((explorer (new Jazz-Explorer (get-model~ text))))
          (forward-namespace~ explorer)
          (set-caret~ text (get-end~ explorer))
          (insert-string~ text (format "{%}{%}{%}(import ({a}))" initial-import))))))
  
  (define (insert-import unit)
    (let ((explorer (new Jazz-Explorer (get-model~ text))))
      (forward-namespace~ explorer)
      (enter-import~ explorer)
      (receive (found? first?) (find-import-invoice~ explorer unit #t)
        (unless found?
          (set-caret~ text (get-end~ explorer))
          (cond (first?
                  (insert-string~ text (format " ({a})" unit))
                  (insert-newline~ text)
                  (tabulate~ text))
                (else
                 (insert-newline~ text)
                 (tabulate~ text)
                 (insert-string~ text (format "({a})" unit))))))))
  
  (insert-import-declaration (car imports))
  (for-each insert-import (let ((explorer (new Jazz-Explorer (get-model~ text))))
                            (filter-existing-import-invoices~ explorer (remove-duplicates (cdr imports)))))))
