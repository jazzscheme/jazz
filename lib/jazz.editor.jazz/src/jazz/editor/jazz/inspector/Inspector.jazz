;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Object Inspector
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.jazz.inspector.Inspector jazz


(import (jazz.action)
        (jazz.application)
        (jazz.clipboard)
        (jazz.debugger)
        (jazz.debugger.jazz)
        (jazz.event)
        (jazz.feedback)
        (jazz.handler)
        (jazz.inspector)
        (jazz.markup)
        (jazz.menu)
        (jazz.outline)
        (jazz.process)
        (jazz.resource)
        (jazz.tree)
        (jazz.view))


(constant inspector-max-content
  256)


(class Inspector extends Tree-View
  
  
  (property process  initialize #f          accessors generate)
  (property client   initialize #f          accessors explicit)
  (property filter   initialize #f          accessors explicit)
  (property ordering initialize 'alphabetic accessors generate)
  
  
  (form
    (<install>                         filled-column: value multiple-selection?: #f selection-mode: row portfolio: :images context-menu-handler: {Event-Handler :form on-context-menu}
      (<Tree-Node-Column>  name: name  title: "Name")
      (<Tree-Label-Column> name: value title: "Value")))
  
  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method public (get-client self)
    client)
  
  
  (method public (set-client self clt (force?: force? #f))
    (when (or force? (neq? clt client))
      (set! client clt)
      (inspect-client self client)))
  
  
  (method public (refresh-client self)
    (inspect-client self client))
  
  
  ;;;
  ;;;; Filter
  ;;;
  
  
  (method public (get-filter self)
    filter)
  
  
  (method public (set-filter self flt)
    (set! filter flt)
    (filter-update self))
  
  
  (method protected (filter-update self)
    (when filter
      (filter-rows self
        (lambda (title)
          (search title filter)))))
  
  
  ;;;
  ;;;; Tree
  ;;;
  
  
  (method override (update-row self row)
    (bind (name serial class presentation more? mutable? kind) (get-user-data row)
      (inspect-content self row (safe-inspect-serial self serial inspector-max-content) (calculate-forced-ordering self class))))
  
  
  ;;;
  ;;;; Present
  ;;;
  
  
  (method protected (safe-calculate-forced-ordering self data)
    (and (pair? data)
         (bind (serial class presentation more? mutable? kind) data
           (calculate-forced-ordering self class))))
  
  
  (method protected (calculate-forced-ordering self class-symbol)
    (and (or (eq? class-symbol (category-identifier Pair))
             (eq? class-symbol (category-identifier Vector))
             (eq? class-symbol (category-identifier Axis)))
         'natural))
  
  
  (method protected (inspect-client self client)
    (remove-every-row self)
    (with-update-locked self
      (lambda ()
        (when client
          (bind (data . kind) client
            (inspect-content self #f (inspect-object self data kind inspector-max-content) (safe-calculate-forced-ordering self data)))))))
  
  
  (method (inspect-content self row values forced-ordering)
    (define (insert-info father info)
      (bind (key serial class presentation more? mutable? kind) info
        (bind (category name rank) (backward-compatible-key key)
          (let ((title (or name "..."))
                (state (if more? 'dynamic 'collapsed)))
            (add-row self father: father state: state
              children: (list (new Tree-Node title: title image: {Image-Resource "Variable"}) (new Tree-Label title: presentation))
              user-data: info)))))
    
    (with-update-locked self
      (lambda ()
        (case (or forced-ordering ordering)
          ((alphabetic)
           (for-each (lambda (info)
                       (insert-info row info))
                     (sort di<? values key: (lambda (info) (->string (second (car info)))))))
          ((natural)
           (let ((partition (partition values key: caar)))
             (if (and (= 1 (length partition))
                      (not (caar partition)))
                 (for-each (lambda (info)
                             (insert-info row info))
                           values)
               (for-each (lambda (partition rank)
                           (bind (key . elements) partition
                             (let ((father (add-row self father: row state: 'expanded children: (list (new Tree-Node title: key image: {Image-Resource "BlueRed"}) (new Tree-Label title: "")) user-data: key)))
                               (for-each (lambda (info)
                                           (insert-info father info))
                                         elements))))
                         partition
                         (naturals 0 (length partition)))))))
        (filter-update self))))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions self)
    (cons (find-actions 'inspector)
          (nextmethod self)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-context-menu self evt)
    (let* ((sender (get-sender evt))
           (pos (get-position evt))
           (cell (view->tree self pos))
           (acquired (acquire self sender pos)))
      (with-context-click sender pos
        (lambda ()
          (when cell
            (let ((info (get-single-selected-data self)))
              (when (pair? info)
                (bind (key serial class presentation more? mutable? kind) info
                  (bind (category name rank) (backward-compatible-key key)
                    (if (not name)
                        (let ((menu (new Missing-Menu client: self)))
                          (track-popup-menu self menu acquired))
                      (let ((menu (new Value-Menu client: self)))
                        (when (and (eq? class 'jazz:Continuation)
                                   (neq? kind :raw))
                          (set-visible? (locate menu 'inspect) #t))
                        (when (not more?)
                          (set-visible? (locate menu 'refresh) #f))
                        (layout-menu menu)
                        (track-popup-menu self menu acquired))))))))))))
  
  
  (method (on-copy-text self evt)
    (let ((row (get-selected-row self)))
      (let ((data (get-child row 1)))
        (set-clipboard-text (get-title data))
        (user-message "Text copied to clipboard"))))
  
  
  (method (on-copy-content self evt)
    (let ((info (get-single-selected-data self)))
      (if (not (pair? info))
          (bell)
        (bind (key serial class presentation more? mutable? kind) info
          (let ((presentation (present-serial process serial)))
            (set-clipboard-text presentation)))
        (user-message "Content copied to clipboard"))))
  
  
  (method (on-inspect self evt)
    (let ((info (get-single-selected-data self)))
      (if (not (pair? info))
          (bell)
        (bind (key serial class presentation more? mutable? kind) info
          (when (eq? class 'jazz:Continuation)
            (inspect-continuation (get-local-debugger (current-application)) process serial))))))
  
  
  (method package (on-open self evt)
    (let ((info (get-single-selected-data self)))
      (if (not (pair? info))
          (bell)
        (inspect-info (get-local-debugger (current-application)) process (cdr info)))))
  
  
  (method package (on-refresh self evt)
    (let ((row (get-selected-row self)))
      (if (or (not row) (not (pair? (get-user-data row))))
          (bell)
        (let ((actual-state (get-state row)))
          (collapse self row)
          (remove-every-son row)
          (set-state row 'dynamic)
          (when (eq? actual-state 'expanded)
            (expand/collapse self row))))))
  
  
  (method (on-retrieve-all self evt)
    (with-cursor :wait
      (lambda ()
        (retrieve-all self))))
  
  
  (method package (retrieve-all self)
    (define (determine-father)
      (let ((father (get-father (get-selected-row self))))
        (if (eq? father root-row)
            #f
          father)))
    
    (let ((father (determine-father)))
      (if (not father)
          (begin
            (remove-every-row self)
            (bind (data . kind) client
              (inspect-content self #f (inspect-object self data kind #f) (safe-calculate-forced-ordering self data))))
        (remove-every-son father)
        (bind (name serial class presentation more? mutable? kind) (get-user-data father)
          (inspect-content self father (safe-inspect-serial self serial #f) (calculate-forced-ordering self class))))))
  
  
  ;;;
  ;;;; Inspect
  ;;;
  
  
  (method public virtual (inspect-object self data kind max-content)
    (case kind
      ((:value)
       (inspect-value data))
      ((:remote)
       (bind (serial class presentation more? mutable? kind) data
         (if more?
             (safe-inspect-serial self serial max-content)
           '())))))
  
  
  (method (safe-inspect-serial self serial max-content)
    (with-remote-exception-handler process
      (lambda ()
        (inspect-serial process serial max-content)))))


;;;
;;;; Missing-Menu
;;;


(class Missing-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item> name: retrieve-all title: "Retrieve All" icon: {Image-Resource "Import"} action-handler: {Event-Handler (:form :client) on-retrieve-all}))))

;;;
;;;; Value-Menu
;;;


(class Value-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item>     name: copy       title: "Copy" icon: {Image-Resource "Copy"} action-handler: {Event-Handler (:form :client) on-copy-text})
      (<Label-Item>     name: copy-value title: "Copy Content" action-handler: {Event-Handler (:form :client) on-copy-content})
      (<Label-Item>     name: inspect    title: "Inspect Continuation" action-handler: {Event-Handler (:form :client) on-inspect} visible?: #f)
      (<Separator-Item>)
      (<Label-Item>     name: open       title: "Open in New Window" icon: {Image-Resource "Window"} action-handler: {Event-Handler (:form :client) on-open})
      (<Separator-Item>)
      (<Label-Item>     name: refresh    title: "Refresh" icon: {Image-Resource "Refresh"} action-handler: {Event-Handler (:form :client) on-refresh})))))
