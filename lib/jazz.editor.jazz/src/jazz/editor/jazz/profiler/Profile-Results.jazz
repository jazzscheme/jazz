;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Profile Results
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.jazz.profiler.Profile-Results jazz


(import (jazz.debugger)
        (jazz.detprof)
        (jazz.editor.jazz)
        (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (profiler)
        (statprof))


(class Profile-Results extends Layout-View
  
  
  (form
    (<install>                               size: {Dimension 600 400} layout-type: border position: {Point 1 0}
      (<Layout-View>          name: tools
        (<Push-Tool>          name: start    size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Start Profile" resource: {Image-Resource "Run"} portfolio: :images action-handler: {Event-Handler :guest on-start-profiler})
        (<Push-Tool>          name: stop     size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Stop Profile" resource: {Image-Resource "Stop"} portfolio: :images action-handler: {Event-Handler :guest on-stop-profiler})
        (<Separator>                         size: {Dimension 6 0})
        (<Push-Tool>          name: refresh  size: {Dimension 22 22} tooltip?: #t tooltip-text: "Refresh Profile" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :guest on-refresh-profile})
        (<Separator>                         size: {Dimension 4 0})
        (<Push-Tool>          name: reset    size: {Dimension 22 22} tooltip?: #t tooltip-text: "Reset Profile" resource: {Image-Resource "Clear"} portfolio: :images action-handler: {Event-Handler :guest on-reset-profile}))
      (<Layout-View>                         layout-type: border location: center background: {Color Medium}
        (<Layout-View>                       size: {Dimension 100 22} location: north layout-type: flow
          (<Push-Tool>        name: view     size: {Dimension 22 22} tooltip?: #t tooltip-text: "View Profile..." resource: {Image-Resource "Edit"} portfolio: :images action-handler: {Event-Handler :form on-choose-profile})
          (<Separator>                       size: {Dimension 4 0})
          (<Label-View>       name: profile  size: {Dimension 250 23} auto-size?: width)
          (<Label-View>                      title: "Kind:" size: {Dimension 31 23} location: tail)
          (<Layout-View>                     size: {Dimension 85 22} layout-type: fill layout-insets: {Rect 0 2 0 2} location: tail
            (<Border-View>                   border-color: {Color Dark}
              (<Combo-Box>    name: profiler content-change-handler: {Event-Handler :form on-change-profiler})))
          (<Separator>                       size: {Dimension 12 0} location: tail)
          (<Label-View>                      title: "Depth:" size: {Dimension 40 23} location: tail)
          (<Layout-View>                     size: {Dimension 30 22} layout-type: fill layout-insets: {Rect 0 2 0 2} location: tail
            (<Border-View>                   border-color: {Color Dark}
              (<Combo-Box>    name: depth    content-change-handler: {Event-Handler :form on-change-depth}))))
        (<Scroller-View>                     location: center style: document hscroll?: #t vscroll?: #t
          (<content!>                        layout-type: fill
            (<Tree-Header>                   style: document
              (<content!>
                (<Calls-Tree> name: calls    selection-handler: {Selection-Handler :form on-select-location} return-press-handler: {Event-Handler :form on-return-press}))))))))
  
  
  (method override (finish initargs)
    (nextmethod initargs)
    (set-choices~ (locate 'profiler)
      '((statprof "Statistical")
        (detprof "Deterministic")))
    (set-value~ (locate 'profiler) 'statprof)
    (set-choices~ (locate 'depth)
      (loop (for d from 0 to 8)
            (collect (list d (->string d)))))
    (set-value~ (locate 'depth) 2))
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon)
    {Image-Resource "Constructor"})
  
  
  (method meta override (host-title)
    "Profile Results")
  
  
  (method meta override (host-position)
    {Point 28 14})
  
  
  (method meta override (host-size)
    {Dimension 408 608})
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (get-tools-view)
    (locate 'tools))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'calls))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-start-profiler evt)
    (user-feedback "Profiler started")
    (profiler-start~ (require-focused-process~ (get-debugger-manager))))
  
  
  (method (on-stop-profiler evt)
    (profiler-stop~ (require-focused-process~ (get-debugger-manager)))
    (view-profile)
    (user-message "Profiler stopped"))


  (method (on-choose-profile evt)
    (define (present-profile profile)
      (or profile "<default>"))
    
    (let ((process (require-focused-process~ (get-debugger-manager))))
      (let ((profiles (profiler-profiles~ process))
            (profile (profiler-selected-profile~ process)))
        (profiler-selected-profile-set!~ process (request-list (sort string<? profiles key: present-profile) title: "Choose Profile" invite: "Select a profile from the list" present: present-profile selected: profile))
        (view-profile))))
  
  
  (method (on-change-profiler evt)
    (reset-profile (get-value~ (get-sender~ evt)) #f))
  
  
  (method (on-change-depth evt)
    (reset-profile #f (get-value~ (get-sender~ evt))))
  
  
  (method (on-reset-profile evt)
    (reset-profile #f #f))
  
  
  (method (reset-profile profiler depth)
    (profile-reset~ (require-focused-process~ (get-debugger-manager)) profiler depth)
    (view-profile))
  
  
  (definition public (track-history sender initiator)
    )
  
  
  (method (on-refresh-profile evt)
    (view-profile))
  
  
  (method (on-select-location evt)
    (edit-source))
  
  
  ;;;
  ;;;; Edition
  ;;;
  
  
  (definition *frame-indicator*
    (new Location-Indicator {Color red: 255 green: 146 blue: 94}))
  
  
  (method (edit-source)
    (edit-location~ *frame-indicator* (selected-location)))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method (selected-frame)
    (let ((tree (get-tree)))
      (let ((rank (get-single-selection~ tree)))
        (if (not rank)
            #f
          (get-user-data~ (get-row~ tree rank))))))
  
  
  (method (selected-location)
    (let ((frame (selected-frame)))
      (if (not frame)
          #f
        (second frame))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-profile)
    (let ((profile (profiler-profile~ (require-focused-process~ (get-debugger-manager))))
          (tree (get-tree)))
      (when profile
        (bind (label profiler depth calls-count calls-duration (user-time sys-time gc-time bytes-allocated) calls) profile
          (define (present-profile)
            (format "{?{a}~} Program: {a}ms Kernel: {a}ms GC: {a}ms Memory allocated: {a}KiB" label user-time sys-time gc-time (floor (/ bytes-allocated 1024))))
            
          (define (make-children frame count duration)
            (let ((duration (if count (* count (/ user-time calls-count)) #f)))
             (list (new Tree-Node
                        title: (present-frame frame)
                        image: {Image-Resource "Link"})
                   (new Tree-Label title: (if count (->string count) ""))
                   (new Tree-Label title: (if duration (format "{r precision: 3}" duration)))
                   (new Tree-Label title: (if duration (format "{r precision: 2}" (* 100.0 (/ duration user-time))))))))
            
          (define (present-frame frame)
            (if (string? frame)
                frame
                (let ((name (and frame (car frame))))
                  (if name
                      (present-reference name)
                      "(interaction)"))))
            
          (set-title~ (locate 'profile) (present-profile))
          (set-value~ (locate 'profiler) profiler)
          (set-value~ (locate 'depth) depth)
          (remove-every-row~ tree)
          (let ((calls calls))
            
           (with-update-locked~ tree
                                (lambda ()
                                  (for-each (lambda (call)
                                              (bind (stack count . duration) call
                                                (if (null? stack)
                                                    (add-row~ tree children: (make-children "(interaction)" count duration) user-data: #f)
                                                    (bind (head-frame . frames) stack
                                                      (let ((row (add-row~ tree children: (make-children head-frame count duration) user-data: head-frame)))
                                                        (for-each (lambda (frame)
                                                                    (add-row~ tree father: row children: (make-children frame #f #f) user-data: frame))
                                                                  frames))))))
                                            (sort > calls key: second))))))))))


;;;
;;;; Calls-Tree
;;;


(class Calls-Tree extends Tree-View
  
  
  (form
    (<install>                              portfolio: :images filled-column: procedure
      (<Tree-Node-Column>  name: procedure  title: "Procedure" width: 250)
      (<Tree-Label-Column> name: count      title: "Count" width: 50)
      (<Tree-Label-Column> name: duration   title: "Milliseconds" width: 75)
      (<Tree-Label-Column> name: percentage title: "Percentage" width: 75)))))
