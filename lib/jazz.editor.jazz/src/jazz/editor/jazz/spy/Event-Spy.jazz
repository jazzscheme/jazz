;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Event Spy
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.jazz.spy.Event-Spy jazz


(import (jazz.editor.jazz)
        (jazz.event)
        (jazz.markup)
        (jazz.outline)
        (jazz.resource)
        (jazz.tree))


;;;
;;;; Constants
;;;


(constant Event-Categories
  '((general   . {Image-Resource "MsgGeneral"})
    (component . {Image-Resource "MsgComponent"})
    (window    . {Image-Resource "MsgWindow"})
    (paint     . {Image-Resource "MsgPaint"})
    (action    . {Image-Resource "MsgAction"})
    (mouse     . {Image-Resource "MsgMouse"})
    (keyboard  . {Image-Resource "MsgKeyboard"})
    (clipboard . {Image-Resource "MsgClipboard"})
    (focus     . {Image-Resource "MsgFocus"})
    (menu      . {Image-Resource "MsgMenu"})
    (unknown   . {Image-Resource "MsgUnknown"})))


(constant Event-Classes
  '((Action-Event     component)
    (Break-Event      general)
    (Component-Event  component)
    (Focus-Event      focus)
    (Keyboard-Event   focus)
    (Mouse-Event      mouse)
    (Print-Event      paint)
    (Selection-Event  window)))


;;;
;;;; Event-Spy
;;;


(class Event-Spy extends Spy-View


  (form
    (<install>
      (<!>                             name: tree-border
        (<!>                           name: tree-scroller
          (<!>                         name: content
            (<!>                       name: tree-header
              (<!>                     name: content
                (<!>                   name: tree       filled-column: properties
                  (<Tree-Node-Column>  name: event      title: "Event" width: 140 toplevel-controls?: #f)
                  (<Tree-Label-Column> name: kind       title: "Kind" width: 140)
                  (<Tree-Label-Column> name: properties title: "Properties" width: 226)
                  (<Tree-Label-Column> name: component  title: "Component" width: 213)))))))))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-title self)
    "Event Spy")
  
  
  (method meta override (host-size self)
    {Dimension 736 339})

  
  ;;;
  ;;;; Spy
  ;;;
  
  
  (method override (spy-domain self)
    'views)


  (method override (spy self client)
    (when (is? client Event-Consumer)
      (set! old-consumer (get-event-consumer client))
      (set-event-consumer client (new Spyer self client))))


  (method override (unspy self client)
    (when (is? client Event-Consumer)
      (set-event-consumer client old-consumer)))


  (method package (incoming-event self level window client evt)
    (let* ((type (category-name (class-of evt)))
           (info (find-in Event-Classes type key: car))
           (cat  (if info (second info) 'unknown)))
      (when (or (eq? category 'all) (eq? category cat))
        (let* ((tree (get-tree self))
               (image (cdr (assq cat Event-Categories)))
               (properties (format "{l}" (get-properties evt)))
               (c0 (new Tree-Node  title: type image: image))
               (c1 (new Tree-Label title: (get-kind evt)))
               (c2 (new Tree-Label title: properties))
               (c3 (new Tree-Label title: (format "{a}{s}" (make-string (* level 5) #\space) window)))
               (item (get-visible-count tree)))
          (add-row tree children: (list c0 c1 c2 c3))
          (ensure-at-end tree)
          (update-view self)
          (when slowdown?
            (sleep slowdown-quantum))
          item))))


  (method (incoming-result self level item result)
    ))


;;;
;;;; Spyer
;;;


(class Spyer extends Object implements Event-Consumer
  
  
  (slot level initialize 0)
  (slot spy-window)
  (slot client)
  
  
  (method override (get-event-consumer self));;abstract
  (method override (set-event-consumer self consumer));;abstract
  
  
  (method override (initialize self wnd clt)
    (nextmethod self)
    (set! spy-window wnd)
    (set! client clt))
  
  
  (method override (deliver-event self handler target proc evt)
    (incoming-event spy-window level client target evt)
    (increase! level)
    (proc target evt)
    (decrease! level))))
