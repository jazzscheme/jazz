;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Exploring Jazz Code
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2012
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.jazz.text.Jazz-Explorer jazz


(require (jazz.editor.scheme.forms))
(require (jazz.editor.jazz.forms))

(import (jazz.editor.jazz)
        (jazz.editor.lisp)
        (jazz.editor.scheme))


(proclaim (warn optimizations))


(class Jazz-Explorer extends Scheme-Explorer
  
  
  (method override (expression-comment? mark)
    (define (get-first-char range)
      (get-char (get-start~ range)))
    
    (or (char=? (get-first-char (get-range~ mark)) #\at)
        (nextmethod mark)))


  ;;;
  ;;;; Location
  ;;;
  
  
  (method public (forward-location location <Object> (toplevel-location: toplevel-location #f))
    (define (forward-category-keywords)
      (let ((keyword #f)
            (previous (get-end)))
        (while (and (forward-expr)
                    (begin
                      (set! keyword (find-marked-symbol))
                      (or (expression-syntax-namespace? keyword)
                          (memq? keyword '(unit module class interface))
                          (memq? keyword '(metaclass extends implements))))
                    (forward-expr))
          (set! previous (get-end)))
        (set-start previous)
        (set-end previous)))
    
    (define (forward-new-form (toplevel-location: toplevel-location #f))
      (forward-category-keywords)
      (set-end (get-start)))
    
    (define (forward-form (toplevel-location: toplevel-location #f))
      (while (not (memq? (form-expr) '(form)))
        (when (not (forward-valid-expr))
          (signal (new Location-Error kind: 'unknown location: (or toplevel-location 'form))))))
    
    (define (forward-declaration name <Object> (toplevel-location: toplevel-location #f))
      (while (or (not (declaration-expr?))
                 (neq? (declaration-name) name))
        (when (not (forward-valid-expr))
          (signal (new Location-Error kind: 'unknown location: (or toplevel-location name))))))
    
    (define (forward-numeric n <fx> (toplevel-location: toplevel-location #f))
      (while (>= n 0)
        (when (not (forward-valid-expr))
          (signal (new Location-Error kind: 'unknown location: (or toplevel-location n))))
        (decrease! n)))
    
    (define (forward-valid-expr)
      (let ((kind (forward-expr)))
        (cond ((eq? kind 'named-comment)
               (forward-expr)
               (forward-expr))
              (else
               kind))))
    
    (let ((scan location))
      (while (not-null? scan)
        (let ((ref (car scan)))
          (set-end (get-start))
          (forward-whites)
          (let ((c0 (char-forward 0)))
            (cond ((eqv? c0 #\backquote)
                   (forward)
                   (forward-expr)
                   (set! scan (cdr scan)))
                  ((and (eqv? c0 #\comma) (eqv? (char-forward 1) #\at))
                   (forward)
                   (forward)
                   (forward-expr)
                   (set! scan (cdr scan)))
                  ((eqv? c0 #\comma)
                   (forward)
                   (forward-expr)
                   (set! scan (cdr scan)))
                  ((not (enter-composite))
                   (signal (new Location-Error kind: 'bad location: (or toplevel-location location))))
                  (else
                   (unless (eq? ref 'meta)
                     (cond ((eq? ref :new-form)
                            (forward-new-form toplevel-location: toplevel-location))
                           ((eq? ref :form)
                            (forward-form toplevel-location: toplevel-location))
                           ((symbol? ref)
                            (forward-declaration ref toplevel-location: toplevel-location))
                           (else
                            (forward-numeric ref toplevel-location: toplevel-location))))
                   (set! scan (cdr scan)))))))))


  ;;;
  ;;;; Namespace
  ;;;


  (method public (namespace-declaration-range (include-invoices?: include-invoices? #f))
    (let ((s (copy start))
          (e (copy end)))
      (forward-namespace include-invoices?: include-invoices?)
      (prog1 (new Range$Cell$ s (copy end))
        (set-start s)
        (set-end e))))
  
  
  (method public (forward-namespace (include-invoices?: include-invoices? #f))
    (set-end start)
    (enter-list)
    (forward-expr)
    (let ((type (intern-marked-symbol)))
      (case type
        ((unit) (forward-unit))
        ((module) (forward-module include-invoices?))
        ((submodule) (forward-submodule include-invoices?))
        ((class) (forward-class))
        ((interface) (forward-interface)))))
  
  
  (method (forward-unit)
    (list 'unit
          (forward-modifiers (expression-syntax-modifiers 'unit))
          (forward-name)))
  
  
  (method (forward-module include-invoices?)
    (list 'module
          (forward-modifiers (expression-syntax-modifiers 'module))
          (forward-name)
          (forward-name)
          (when include-invoices?
            (forward-invoices))))
  
  
  (method (forward-submodule include-invoices?)
    (list 'submodule
          (forward-modifiers (expression-syntax-modifiers 'submodule))
          (forward-name)
          (when include-invoices?
            (forward-invoices))))
  
  
  (method (forward-class)
    (list 'class
          (forward-modifiers (expression-syntax-modifiers 'class))
          (forward-name)
          (forward-keywords (expression-syntax-keywords 'class))))
  
  
  (method (forward-interface)
    (list 'interface
          (forward-modifiers (expression-syntax-modifiers 'interface))
          (forward-name)
          (forward-keywords (expression-syntax-keywords 'interface))))
  
  
  (method (forward-name)
    (forward-expr)
    (intern-marked-symbol))
  
  
  (method (forward-keywords keywords)
    (let (iterate (finish (copy end))
                  (result '()))
      (define (done)
        (set-end finish)
        result)
      
      (forward-expr)
      (if (eq? kind 'symbol)
          (let ((expr (intern-marked-symbol)))
            (if (memq? expr keywords)
                (begin
                  (forward-expr)
                  (let ((value (read-marked)))
                    (iterate (copy end) (append result (list expr value)))))
              (done)))
        (done))))
  
  
  (method (forward-invoices)
    (while (forward-invoice)
      ))
  
  
  (method (forward-invoice)
    (let ((s (copy start))
          (e (copy end)))
      (define (reset)
        (set-start s)
        (set-end e))
      
      (define (forward)
        (when (enter-list)
          (forward-expr)
          (when (and (eq? kind 'symbol)
                     (memq? (intern-marked-symbol) '(export import)))
            (exit-list)
            (read-marked))))
      
      (let ((invoices (forward)))
        (or invoices
            (begin
              (reset)
              #f)))))
  
  
  (method (find-namespace-insertion-range)
    (let ((point (find-namespace-insertion-point)))
      (new Range$Cell$ (get-end) (get-end))))
  
  
  (method (find-namespace-insertion-point)
    (forward-namespace)
    (let (loop)
      (let* ((last-expression? (not (forward-expr)))
             (composite? (and (not last-expression?)
                              (enter-composite)))
             (submodule? (and composite?
                              (eq? (get-kind) 'list)
                              (forward-expr)
                              (eq? (read-marked) 'submodule))))
        (when last-expression?
          (backward-expr))
        (when composite?
          (exit-composite))
        (when submodule?
          (forward-expr)
          (backward-expr)
          (backward-expr))
        (unless (or last-expression? submodule?)
          (loop))))
    (get-end))
  
  
  ;;;
  ;;;; Imports
  ;;;
  
  
  (method (filter-existing-import-invoices units)
    (collect-if (lambda (unit)
                  (goto-beginning)
                  (forward-namespace)
                  (enter-import)
                  (receive (found? first? none?) (find-import-invoice unit #f)
                    (not found?)))
                units))
  
  
  (method (find-import)
    (enter-import)
    (exit-list)
    (set-start (get-end))
    (backward-expr))
  
  
  (method (enter-import)
    (let (iterate)
      (when (forward-expr)
        (let ((pos (copy (get-end))))
          (if (eq? kind 'list)
              (let ((s (get-start))
                    (e (get-end)))
                (set-end s)
                (enter-list)
                (forward-expr)
                (if (eq? kind 'symbol)
                    (let ((expr (intern-marked-symbol)))
                      (if (eq? expr 'import)
                          #t
                        (set-end pos)
                        (iterate)))
                  (set-end pos)
                  (iterate)))
            (iterate))))))
  
  
  (method (find-import-invoice unit ordered-insert? (parameter #f))
    (let ((target (symbol->string unit))
          (target-parameter (if parameter (object->string parameter) "")))
      (let (iterate (first? #t))
        (let ((pos (copy (get-end))))
          (if (forward-expr)
              (begin
                (set-end start)
                (enter-list)
                (forward-expr)
                (let ((name (get-marked))
                      (parameter (if (forward-expr) (get-marked) "")))
                  (cond ((and (string=? name target)
                              (string=? parameter target-parameter))
                         (exit-list)
                         (set-start pos)
                         (values #t first? #f))
                        ((and ordered-insert? (string>? name target))
                         (set-end pos)
                         (values #f first? #f))
                        (else
                         (set-end pos)
                         (forward-expr)
                         (iterate #f)))))
            (values #f first? first?))))))
  
  
  (method public (insert-import-invoice unit (parameter #f))
    (forward-namespace)
    (if (not (enter-import))
        (error "Unable to find import declaration")
      (receive (found? first? none?) (find-import-invoice unit #t parameter)
        (unless found?
          (with-atomic-undo~ (get-undoer~ model)
            (lambda ()
              (let ((text (single-client~ model))
                    (copy (get-end)))
                (assert text)
                (set-caret~ text (get-end))
                (cond (first?
                        (insert-string~ text (format " ({a}{? {a}~})" unit parameter))
                        (unless none?
                          (insert-newline~ text))
                        (tabulate~ text))
                      (else
                       (insert-newline~ text)
                       (tabulate~ text)
                       (insert-string~ text (format "({a}{? {a}~})" unit parameter)))))))))))
  
  
  (method public (remove-import-invoice unit)
    (forward-namespace)
    (if (not (enter-import))
        (error "Unable to find import declaration")
      (receive (found? first? none?) (find-import-invoice unit #f)
        (when found?
          (with-atomic-undo~ (get-undoer~ model)
            (lambda ()
              (let ((text (single-client~ model)))
                (assert text)
                (delete-range~ model (get-range))
                (when first?
                  (remove-spaces~ text (get-start) keep: 'one)))))))))


  ;;;
  ;;;; Declarations
  ;;;
  
  
  (method override (find-named-walker name)
    (case name
      ((:proclaim)        walk-proclaim)
      ((:properties)      walk-properties)
      ((:state)           walk-state)
      ((:attributes)      walk-attributes)
      ((:native)          walk-native)
      ((:definition)      walk-definition)
      ((:specialize)      walk-specialize)
      ((:class)           walk-class)
      ((:interface)       walk-interface)
      ((:remotable-stub)  walk-remotable-stub)
      ((:submodule)       walk-submodule)
      ((:method)          walk-method)
      ((:slot)            walk-slot)
      ((:jazzdoc)         walk-jazzdoc)
      ((:property)        walk-property)
      ((:form)            walk-form)
      ((:global)          walk-global)
      ((:constant)        walk-constant)
      ((:enumeration)     walk-enumeration)
      ((:c-constant)      walk-c-constant)
      ((:c-enumeration)   walk-c-enumeration)
      ((:c-named-declare) walk-c-named-declare)
      ((:c-include)       walk-c-include)
      ((:c-type)          walk-c-type)
      ((:c-structure)     walk-c-structure)
      ((:c-definition)    walk-c-define)
      ((:c-external)      walk-c-external)
      ((:com-external)    walk-com-external)
      ((:datacontainer)   walk-datacontainer)
      ((:define-handler)  walk-define-handler)
      (else               (nextmethod name))))
  
  
  (method (walk-proclaim context symbol form declaration keyword expression doc)
    (walk-anonymous context 'proclaim symbol form declaration keyword expression doc))

  
  (method (walk-properties context symbol form declaration keyword expression doc)
    (keyword context start end)
    (walk-expressions context expression))
  
  
  (method (walk-attributes context symbol form declaration keyword expression doc)
    (keyword context start end)
    (walk-expression context expression 'code #f)
    (forward-expr)
    (let (iterate)
      (when (enter-list)
        (forward-expr)
        (keyword context start end)
        (forward-expr)
        (when (eq? kind 'symbol)
          (let ((marked (get-marked)))
            (let ((entry (declaration context 'definition '() symbol '() #f start end marked (symbol-documented? (find-marked-symbol) doc))))
              (declaration context 'accessor '() 'getter '() #f start end (string-append "get-" marked) #f)
              (declaration context 'accessor '() 'setter '() #f start end (string-append "set-" marked) #f)
              (let ((keywords '()))
                (define (add-keyword keyword value)
                  (set! keywords (cons (cons keyword value) keywords)))
                
                (walk-keywords/properties context expression
                  (lambda (symbol)
                    (case symbol
                      ((initialize test)
                       (keyword context start end)
                       (when (forward-expr)
                         (add-keyword symbol (get-marked)))
                       #f)
                      (else
                       #f))))
                (when entry
                  (set-parameters~ entry (reverse! keywords))))
              (walk-expressions context expression))))
        (exit-list)
        (iterate))))
  
  
  (method (walk-state context symbol form declaration keyword expression doc)
    (keyword context start end)
    (walk-expression context expression 'code #f)
    (forward-expr)
    (let (iterate)
      (when (enter-list)
        (forward-expr)
        (keyword context start end)
        (forward-expr)
        (when (eq? kind 'symbol)
          (let ((marked (get-marked)))
            (let ((entry (declaration context 'definition '() symbol '() #f start end marked (symbol-documented? (find-marked-symbol) doc))))
              (let ((keywords '()))
                (define (add-keyword keyword value)
                  (set! keywords (cons (cons keyword value) keywords)))
                
                (walk-keywords/properties context expression
                  (lambda (symbol)
                    (case symbol
                      ((initialize test)
                       (keyword context start end)
                       (when (forward-expr)
                         (add-keyword symbol (get-marked)))
                       #f)
                      ((getter setter accessors)
                       (keyword context start end)
                       (when (forward-expr)
                         (let ((accessor (get-marked)))
                           (add-keyword symbol accessor)
                           (walk-slot-accessor context marked declaration doc symbol accessor))))
                      (else
                       #f))))
                (when entry
                  (set-parameters~ entry (reverse! keywords))))
              (walk-expressions context expression))))
        (exit-list)
        (iterate))))

  
  (method (walk-definition context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (forward-whites)
        (let ((list? (eqv? (char-forward 0) #\open-parenthesis)))
          (cond ((not list?)
                 (when (and (forward-expr) (eq? kind 'symbol))
                   (set! entry (declaration context 'definition modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc)))))
                (else
                 (enter-list)
                 (when (and (forward-expr) (eq? kind 'symbol))
                   (set! entry (declaration context 'definition modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
                 (walk-parameters entry declaration keyword expression doc)
                 (exit-list))))
        (walk-specifier entry expression 'code)
        (walk-expressions entry expression 'code #f)
        entry)))
  
  
  (method (walk-specialize context symbol form declaration keyword expression doc)
    (let ((entry context)
          (as #f))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (forward-whites)
        (when (not (eqv? (char-forward 0) #\open-parenthesis))
          (when (and (forward-expr) (eq? kind 'symbol) (eq? (find-marked-symbol) 'as))
            (keyword context start end)
            (forward-expr)
            (set! as (get-marked))
            (declaration context 'specialize modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))
            (forward-whites)))
        (let ((list? (eqv? (char-forward 0) #\open-parenthesis)))
          (define (mangle-as marked)
            (if as
                (list marked as)
              marked))
          
          (cond ((not list?)
                 (when (and (forward-expr) (eq? kind 'symbol))
                   (set! entry (declaration context 'specialize modifiers symbol '() #f start end (mangle-as (get-marked)) (symbol-documented? (find-marked-symbol) doc)))))
                (else
                 (enter-list)
                 (when (and (forward-expr) (eq? kind 'symbol))
                   (set! entry (declaration context 'specialize modifiers symbol '() #f start end (mangle-as (get-marked)) (symbol-documented? (find-marked-symbol) doc))))
                 (walk-parameters entry declaration keyword expression doc)
                 (exit-list))))
        (walk-expressions entry expression 'code #f)
        entry)))

  
  (method (walk-class context symbol form declaration keyword expression doc)
    (walk-namespace context 'class symbol form declaration keyword expression doc))

  (method (walk-interface context symbol form declaration keyword expression doc)
    (walk-namespace context 'interface symbol form declaration keyword expression doc))

  (method (walk-remotable-stub context symbol form declaration keyword expression doc)
    (walk-namespace context 'remotable-stub symbol form declaration keyword expression doc))

  (method (walk-submodule context symbol form declaration keyword expression doc)
    (walk-namespace context 'submodule symbol form declaration keyword expression doc))
  
  
  (method public (walk-method context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (enter-list)
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'method modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc)))
          (forward-whites)
          (walk-parameters entry declaration keyword expression doc)
          (exit-list)
          (walk-specifier entry expression 'code)
          (walk-expressions entry expression 'code #f))
        entry)))
  
  
  (method (walk-keywords/properties context expression proc)
    (while (forward-expr)
      (unless (and (eq? kind 'symbol)
                   (let ((marked-symbol (find-marked-symbol)))
                     (if marked-symbol
                         (proc marked-symbol)
                       #f)))
        (walk-expression context expression 'code #f))))
  
  
  (method (walk-slot-accessor context slot-name declaration doc symbol accessor)
    (define (declare-accessor accessor)
      (let ((name (string-append (case accessor ((getter) "get-") ((setter) "set-")) slot-name)))
        (declaration context 'accessor '() accessor '() #f start end name (symbol-documented? (find-marked-symbol) doc))
        #t))
    
    (when (and (eq? kind 'symbol) (string=? accessor "generate"))
      (case symbol
        ((getter)
         (declare-accessor 'getter))
        ((setter)
         (declare-accessor 'setter))
        ((accessors)
         (declare-accessor 'getter)
         (declare-accessor 'setter)))))
  
  
  (method (walk-slot/property-accessors context entry slot-name declaration keyword expression doc)
    (walk-specifier entry expression 'code)
    (let ((keywords '()))
      (define (add-keyword keyword value)
        (set! keywords (cons (cons keyword value) keywords)))
      
      (walk-keywords/properties context expression
        (lambda (symbol)
          (case symbol
            ((initialize)
             (keyword context start end)
             (when (forward-expr)
               (add-keyword symbol (get-marked)))
             #f)
            ((getter setter accessors)
             (keyword context start end)
             (when (forward-expr)
               (let ((accessor (get-marked)))
                 (add-keyword symbol accessor)
                 (walk-slot-accessor context slot-name declaration doc symbol accessor))))
            (else
             #f))))
      (when entry
        (set-parameters~ entry (reverse! keywords)))))
  
  
  (method (walk-slot context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (forward-whites)
        (when (and (forward-expr) (eq? kind 'symbol))
          (let ((marked (get-marked)))
            (set! entry (declaration context 'slot modifiers symbol '() #f start end marked (symbol-documented? (find-marked-symbol) doc)))
            (walk-slot/property-accessors context entry marked declaration keyword expression doc)))
        (walk-expressions entry expression)
        entry)))
  
  
  (method (walk-jazzdoc context symbol form declaration keyword expression doc)    
    (let ((entry context))
      (set! entry (declaration context 'jazzdoc '() symbol '() #f start end (get-marked) #f))
      (expression context start end 'code
        (if (specifier-marked?)
            'tag
          'jazzdoc))
      (let ((l (new List-Factory)))
        (while (forward-expr)
          (put~ l (get-constant)))
        (when entry
          (set-modifiers~ entry (get-output~ l))))
      entry))
  
  
  (method (walk-property context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (let ((marked (get-marked)))
            (set! entry (declaration context 'property modifiers symbol '() #f start end marked (symbol-documented? (find-marked-symbol) doc)))
            (walk-slot/property-accessors context entry marked declaration keyword expression doc)))
        (walk-expressions entry expression)
        entry)))
  
  
  (method (walk-form context symbol form declaration keyword expression doc)
    (walk-anonymous context 'form symbol form declaration keyword expression doc))
        
  
  (method (walk-global context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'global modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
        entry)))
    
  
  (method (walk-constant context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'constant modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
        (walk-expressions entry expression)
        entry)))
  
  
  (method (walk-enumeration context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'enumeration modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
        (walk-expressions entry expression)
        entry)))

  
  (method (walk-c-constant context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'c-constant modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
        (walk-expressions entry expression)
        entry)))
  
  
  (method (walk-c-enumeration context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'c-enumeration modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
        (walk-expressions entry expression)
        entry)))

  
  (method (walk-c-named-declare context symbol form declaration keyword expression doc)
    (keyword context start end)
    (walk-expressions context expression))

  
  (method (walk-c-include context symbol form declaration keyword expression doc)
    (keyword context start end)
    (walk-expressions context expression))
  
  
  (method (walk-c-type context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context 'c-type modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
        (walk-expressions entry expression)
        entry)))
  
  
  (method (walk-c-structure context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (forward-whites)
        (let ((list? (eqv? (char-forward 0) #\open-parenthesis)))
          (when list?
            (enter-list))
          (when (and (forward-expr) (eq? kind 'symbol))
            (set! entry (declaration context 'c-structure modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
          (when list?
            (exit-list)))
        (walk-expressions entry expression)
        entry)))

  
  (method (walk-c-external context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (let ((entered? (enter-list)))
          (when (and (forward-expr) (eq? kind 'symbol))
            (set! entry (declaration context 'external modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
          (when entered?
            (walk-parameters entry declaration keyword expression doc))
          (when entered?
            (exit-list)))
        (walk-specifier entry expression 'code)
        (walk-expressions entry expression 'code #f)
        entry)))

  
  (method (walk-com-external context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (let ((entered? (enter-list)))
          (when (and (forward-expr) (eq? kind 'symbol))
            (set! entry (declaration context 'com-external modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
          (when entered?
            (walk-parameters entry declaration keyword expression doc))
          (when entered?
            (exit-list)))
        (walk-specifier entry expression 'code)
        (walk-expressions entry expression 'code #f)
        entry)))
  
  
  (method (walk-datacontainer context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (when (forward-expr)
        (set! entry (declaration context 'datacontainer '() symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
      (walk-expressions entry expression)
      entry))
  
  
  (method (walk-define-handler context symbol form declaration keyword expression doc)
    (let ((entry context))
      (keyword context start end)
      (let ((modifiers (walk-modifiers context declaration keyword (get-modifiers~ form))))
        (forward-whites)
        (let ((list? (eqv? (char-forward 0) #\open-parenthesis)))
          (cond ((not list?)
                 (when (and (forward-expr) (eq? kind 'symbol))
                   (set! entry (declaration context 'definition modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc)))))
                (else
                 (enter-list)
                 (when (and (forward-expr) (eq? kind 'symbol))
                   (set! entry (declaration context 'definition modifiers symbol '() #f start end (get-marked) (symbol-documented? (find-marked-symbol) doc))))
                 (walk-parameters entry declaration keyword expression doc)
                 (exit-list))))
        (walk-expressions entry expression 'code #f)
        entry)))))
