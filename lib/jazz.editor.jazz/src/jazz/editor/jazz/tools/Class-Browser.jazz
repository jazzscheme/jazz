;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Class Browser
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.jazz.tools.Class-Browser jazz


(import (jazz.editor.jazz)
        (jazz.jml)
        (jazz.library)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view))


(class Class-Browser extends View
  
  
  (property text initialize {})


  (form
    (<install>                                             size: {Dimension 395 599}
      (<Border-View>                  name: name-border    position: {Point 2 9} size: {Dimension 250 19} border-type: edge style: entry
        (<Scroller-View>                                   hscroll?: #f vscroll?: #f
          (<!>                        name: content        layout-type: fill
            (<IDE-Text-View>          name: name           left-padding: 2 top-padding: 2 accepts-returns?: #f show-unfocused-selection?: #f focus-selects-all?: #t return-press-handler: {Event-Handler :form on-return-press}))))
      (<Push-Button>                  name: browse         title: "Browse Hierarchy" position: {Point 272 7} size: {Dimension 120 22} action-handler: {Event-Handler :form on-browse})
      (<Border-View>                  name: classes-border position: {Point 2 35} size: {Dimension 391 562} border-type: edge style: entry
        (<Scroller-View>                                   style: document hscroll?: #t vscroll?: #t
          (<!>                        name: content        layout-type: fill
            (<Tree-Header>                                 style: document
              (<!>                    name: content
                (<Tree-View>          name: classes        multiple-selection?: #t portfolio: :images double-click-handler: {Event-Handler :form on-double-click}
                  (<Tree-Node-Column>                      title: "Name" display-images?: #f width: 419)))))))))
  
  
  (method meta override (external-name . rest)
    'class-browser)

  
  ;;;
  ;;;; Host
  ;;;
    

  (method meta override (host-icon)
    {Image-Resource "Class"})
  
  
  (method meta override (host-title)
    "Class Browser")
  
  
  (method meta override (host-position)
    {Point 90 20})
  
  
  (method meta override (host-size)
    {Dimension 395 600})

  
  (method override (focus-default)
    (acquire-focus~ (locate 'name)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-text)
    (get-string-content~ (locate 'name)))
  
  
  (method public (set-text value)
    (set-string-content~ (locate 'name) (either value "")))
  
  
  ;;;
  ;;;; Actions
  ;;;
  
  
  
  (method override (guest-actions)
    (cons (find-actions 'class-browser)
          (nextmethod)))

  
  ;;;
  ;;;; Events
  ;;;


  (method override (resize dh dv)
    (nextmethod dh dv)
    (move-figure~ (locate 'browse) dh 0)
    (size-figure~ (locate 'classes-border) dh dv))
  
  
  (method (on-browse evt)
    (define (get-root-class)
      (let ((text (get-text)))
        (if (empty-string? text)
            Object
          (let ((name (string->symbol text)))
            (if (get-autoload name)
                (autoload name)
              (if (global-variable? name)
                  (let ((value (global-value name)))
                    (if (category? value)
                        value
                      {}))
                {}))))))
    
    (let ((root (get-root-class)))
      (cond ((not root)
             (message-box (format "Unable to find class: {a}" (get-string-content~ (locate 'name)))))
            ((not (class? root))
             (message-box (format "Can only browse classes: {s}" root)))
            (else
             (with-cursor :wait
               (lambda ()
                 (browse-inheritance-tree root: (get-root-class))))))))
  
  
  (method override (on-return-press evt)
    (on-browse evt))
  
  
  (method (on-double-click evt)
    (let* ((tree (get-sender~ evt))
           (selection (get-single-selection~ tree)))
      (when selection
        (let* ((row (get-row~ tree selection))
               (type (get-user-data~ row))
               (appl (get-application)))
          (edit-definitions~ appl (category-name type))))))

  
  ;;;
  ;;;; Browse
  ;;;
  
  
  (method (browse-inheritance-tree (root: root Object))
    (let ((tree (locate 'classes)))
      (define (upward type)
        (let* ((state 'expanded)
               (title (->string (category-identifier type)))
               (ascendant (class-ascendant type))
               (father (essay ascendant (upward ascendant))))
          (add-row~ tree father: father state: state children: (list (new Tree-Node title: title image: {Image-Resource "Class"})) user-data: type)))
      
      (define (downward type father)
        (let* ((state 'expanded)
               (title (->string (category-identifier type)))
               (father (add-row~ tree father: father state: state children: (list (new Tree-Node title: title image: {Image-Resource "Class"})) user-data: type)))
          (for-each (lambda (subtype)
                      (downward subtype father))
                    (sort string<? (category-descendants type) key: (lambda (type)
                                                                      (symbol->string (category-identifier type)))))))
        
      (with-update-locked~ tree
        (lambda ()
          (remove-every-row~ tree)
          (let ((ascendant (class-ascendant root)))
            (downward root (essay ascendant (upward ascendant))))))))

  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method (selected-categories)
    (let* ((tree (locate 'classes))
           (selection (get-selection~ tree)))
      (map (lambda (rank)
             (get-user-data~ (get-row~ tree rank)))
           selection)))))
