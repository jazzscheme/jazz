;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Lisp Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.lisp.text.Lisp-Text-View jazz


(import (jazz.application)
        (jazz.debugger)
        (jazz.editor.lisp)
        (jazz.jml)
        (jazz.library)
        (jazz.resource)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.search)
        (jazz.ui.view))


(constant Auto-Complete-Characters
  '(#\minus #\question-mark #\exclamation-mark #\period #\colon #\tilde #\>))


(class Lisp-Text-View extends Code-Text-View

  
  (method override (install rest)
    (nextmethod rest)
    (let ((pref (preferences)))
      (add-style 'Title               (get-title-style~ pref))
      (add-style 'Comment             (get-comment-style~ pref))
      (add-style 'Expr-Comment        (get-expr-comment-style~ pref))
      (add-style 'Named-Comment       (get-named-comment-style~ pref))
      (add-style 'Declaration-Keyword (get-declaration-keyword-style~ pref))
      (add-style 'Declaration         (get-declaration-style~ pref))
      (add-style 'Keyword             (get-keyword-style~ pref))
      (add-style 'Jazzdoc             (get-jazzdoc-style~ pref))
      (add-style 'Jazzdoc-Body        (get-jazzdoc-body-style~ pref))
      (add-style 'Tag                 (get-tag-style~ pref))
      (add-style 'Specifier           (get-specifier-style~ pref))
      (add-style 'Char                (get-char-style~ pref))
      (add-style 'Number              (get-number-style~ pref))
      (add-style 'Boolean             (get-boolean-style~ pref))
      (add-style 'String              (get-string-style~ pref))
      (add-style 'Delimited-String    (get-delimited-string-style~ pref))
      (add-style 'Syntax-String       (get-syntax-string-style~ pref))
      (add-style 'Named-String        (get-named-string-style~ pref))
      (add-style 'Quotation           (get-quotation-style~ pref))
      (add-style 'Feature             (get-feature-style~ pref))
      (add-style 'Reference           (get-reference-style~ pref))
      (add-style 'Warning             (get-warning-style~ pref))
      (add-style 'Fatal               (get-fatal-style~ pref))
      (add-style 'Break               (get-break-style~ pref))
      (add-style 'Clojure-Syntax      {Text-Style Syntax-String})
      (add-style 'CommonLisp-Syntax   {Text-Style Syntax-String})
      (add-style 'C-Syntax            {Text-Style Syntax-String})
      (add-style 'Jazz-Syntax         {Text-Style Syntax-String})
      (add-style 'JavaScript-Syntax   {Text-Style Syntax-String})
      (add-style 'Scheme-Syntax       {Text-Style Syntax-String})
      ;; CLike
      (add-style 'Definition          (get-definition-style~ pref))
      (add-style 'Line-Comment        (get-line-comment-style~ pref))
      (add-style 'Balanced-Comment    (get-balanced-comment-style~ pref))))
    
  
  (method override (new-syntax)
    (new Lisp-Syntax model))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon)
    {Image-Resource "Script"})

  
  ;;;
  ;;;; Controller
  ;;;
  
  
  (method public (get-locat-container)
    (let ((moniker (get-moniker)))
      (if moniker
          (parse~ moniker)
        #f)))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions)
    (cons (find-actions 'lisp-text)
          (nextmethod)))

  
  ;;;
  ;;;; Events
  ;;;


  (method override (context-menu evt)
    (let ((pos (get-position~ evt)))
      (receive (shift? control? alt? meta?) (get-down~ evt)
        (let ((move-caret
                (cond ((and (not control?) (not shift?) (not alt?)) 'outside-selection)
                      ((and (not control?) shift? (not alt?)) 'never)
                      ((and control? shift? (not alt?)) 'never)
                      (else 'always))))
          (context-menu-click pos move-caret: move-caret))
        (cond ((and (not control?) shift? (not alt?)) (edit-definitions pos))
              ((and control? shift? (not alt?)) (edit-references pos))
              (else (track-popup-menu (get-context-menu) pos))))))

  
  (method override (tab-press evt)
    (if (or (has-completion?)
            (not accepts-returns?))
        (nextmethod evt)
      (with-atomic-undo~ (get-undoer~ model)
        (lambda ()
          (tabulate)))))
  
  
  @wait
  ;; need a better solution as this can be really
  ;; annoying and slow when pasting a large clipboard
  (method override (paste-clipboard-string string)
    (with-atomic-undo~ (get-undoer~ model)
      (lambda ()
        (let ((start (get-start)))
          (nextmethod string)
          (tabulate-range (new-range start (get-end)))))))
  
  
  ;;;
  ;;;; Mouse Copy
  ;;;
  
  
  (method override (mouse-insert copy)
    (with-atomic-undo~ (get-undoer~ model)
      (lambda ()
        (let ((start (get-start)))
          (nextmethod copy)
          (tabulate-range (new-range start (get-end)))))))

  
  ;;;
  ;;;; Tabulation
  ;;;
  
  
  @cases
  (comment
  
  (method public virtual (foo)
    a)
  
  (foo
    a
    b)
  
  (for-each (lambda (x)
              x)
            list)
  
  (cond (foo
         a)))
  
  
  (method (on-tabulate evt)
    (with-atomic-undo~ (get-undoer~ model)
      (lambda ()
        (unless (tabulate)
          (when (not (get-auto-complete?~ (preferences)))
            (complete-at (get-start) auto-complete?: #t))))))


  (method override (return-press evt)
    (with-atomic-undo~ (get-undoer~ model)
      (lambda ()
        (nextmethod evt)
        (tabulate))))


  (method override (tabulate)
    (let ((range (get-selection)))
      (let ((range-syntax (syntax-at-range~ syntax range)))
        (if (eq? range-syntax syntax)
            (if (shift-down?)
                (insert-char #\tab)
              (tabulate-range range))
          (tabulate~ range-syntax self)))))
  
  
  (method protected (tabulate-range range)
    (let ((start (get-start~ range))
          (end (get-end~ range)))
      (with-update-locked
        (lambda ()
          (with-safe-explore
            (lambda ()
              (set-caret start)
              (receive (modified? last-delta <fx>) (tabulate-lines (get-line~ start) (get-line~ end))
                (set-caret (nu+ end last-delta))
                (when modified?
                  (set-modified? #t))
                modified?))
            action: 'ignore)))))
  
  
  (method protected (tabulate-lines start end)
    (let ((modified? #f)
          (last-delta #f))
      (loop (for line from start to end)
            (for delta = (tabulate-line line))
            (when (/= delta 0)
              (set! modified? #t))
            (set! last-delta delta))
      (values modified? last-delta)))
  
  
  (method (tabulate-line line)
    (define (tabulate-column line delta)
      (when (/= delta 0)
        (let ((actual (get-column line)))
          (replace-text (new Range$Cell$ (new Cell line 0) (new Cell line actual))
                        (list (make-string (+ actual delta) #\space))))))
    
    (let ((delta (tabulate-delta line)))
      (tabulate-column line delta)
      delta))
  
  
  (method public (tabulate-delta line)
    (define (count-modifiers expressions modifiers)
      (let ((total 0)
            (done? #f))
        (while (and (not-null? expressions) (not done?))
          (let ((symbol (find-symbol (get-string (get-range~ (car expressions))))))
            (if (or (not symbol) (not (memq? symbol modifiers)))
                (set! done? #t)
              (increase! total)
              (set! expressions (cdr expressions)))))
        total))
    
    (define (effective-symbol string)
      (if (ends-with? string "~")
          (string->symbol (subseq string 0 (- (cast <fx> (cardinality string)) 1)))
        (string->symbol string)))
    
    (define (first-on-last-line expressions count)
      (let* ((reversed (if (> count 1) (reverse (cdr expressions)) (reverse expressions)))
             (first (car reversed))
             (line (get-line~ (get-start~ (get-range~ first)))))
        (while (and (not-null? reversed) (= (get-line~ (get-start~ (get-range~ (car reversed)))) line))
          (set! first (car reversed))
          (set! reversed (cdr reversed)))
        first))
    
    (define (remove-specifiers lst)
      (if (not (pair? lst))
          '()
        (cons (car lst)
              (collect-if (lambda (mark)
                            (let ((range <Range$Cell$> (get-range~ mark)))
                              (or (not (eqv? (get-char (get-start~ range)) #\<))
                                  (not (eqv? (get-char (- (get-end~ range) 1)) #\>)))))
                          (cdr lst)))))
    
    (let* ((pos (new Cell line 0))
           (expl <Text-Explorer> (new (explorer-class) model start: pos))
           (expressions (remove-specifiers (remove-expression-comments~ expl (collect-backward-exprs~ expl))))
           (opening (- (get-start~ expl) 1))
           (count (length expressions))
           (rank (- count 1))
           (col #f))
      (if (null? expressions)
          (set! col (+ (get-col~ opening) 1))
        (let* ((first (car expressions))
               (first-string (get-string (get-range~ first)))
               (first-symbol (and (eq? (get-kind~ first) 'symbol) (effective-symbol first-string)))
               (first-tag? (tag? first-symbol))
               (first-call-with? (and first-symbol (starts-with? first-string "call-with-")))
               (first-with? (and first-symbol (or (starts-with? first-string "with-") (starts-with? first-string "jazz.with-"))))
               (first-without? (and first-symbol (starts-with? first-string "without-")))
               (first-ing? (and first-symbol (search first-string "ing-")))
               (first-dispatch? (and first-symbol (ends-with? first-string "~")))
               (starts-with-lambda? (starts-with? (substring (paragraph-string line) (get-column line) (paragraph-length line)) "(lambda "))
               (first-indent (if (or first-tag? first-call-with? first-with? first-without? first-ing? starts-with-lambda?) 1 0)))
          (if (or (not first-symbol) first-tag?)
              (set! col (+ (get-col~ (get-start~ (get-range~ first))) first-indent))
            (let ((info (let ((form (get-expression-syntax first-symbol)))
                          (and form
                               (let ((modifiers (get-modifiers~ form))
                                     (tabulate (get-tabulate~ form)))
                                 (and tabulate (list modifiers tabulate)))))))
              (if (not info)
                  (let ((first-last (first-on-last-line expressions count))
                        (first-line (get-line~ (get-start~ (get-range~ first)))))
                    (if (and (= (get-line~ (get-start~ (get-range~ first-last))) first-line) (or first-call-with? first-with? first-without? first-ing? starts-with-lambda? first-dispatch? (/= count 2)) (neq? first-symbol 'else))
                        (set! col (+ (get-col~ opening) 2))
                      (let ((proposed-col (+ (get-col~ (get-start~ (get-range~ first-last))) (if (= count 1) first-indent 0)))
                            (threshold (+ (get-col~ opening) 2 4))
                            (default-col (+ (get-col~ opening) 2)))
                        (if (and (> proposed-col threshold)
                                 (upper-case? (element first-string 0)))
                            (set! col default-col)
                          (set! col proposed-col)))))
                (bind (modifiers specials) info
                  (when (not-null? modifiers)
                    (decrease! rank (count-modifiers (cdr expressions) modifiers)))
                  (if (= specials -1)
                      (set! col (get-col~ opening))
                    (if (= specials 0)
                        (set! col (+ (get-col~ opening) 2))
                      (if (> rank specials)
                          (set! col (get-col~ (get-start~ (get-range~ (first-on-last-line expressions count)))))
                        (set! col (+ (get-col~ opening) (* (+ (- specials rank) 1) 2))))))))))))
      (- col (get-column line))))
  
  
  ;;;
  ;;;; Evaluation
  ;;;
  
  
  (method override (evaluate-selection)
    (with-safe-explore
      (lambda ()
        (let ((range (current-declaration-range)))
          (if (not range)
              (bell)
            (with-cursor :wait
              (lambda ()
                (sleep .1)
                (evaluate-range range))))))))
  
  
  (method override (evaluate-buffer)
    (with-cursor :wait
      (lambda ()
        (sleep .1)
        (evaluate-range (buffer-selection)))))
  
  
  (method override (evaluate-range range (evaluation-mode: evaluation-mode #f))
    (define (collect-declaration-names)
      (let ((start (get-start~ range))
            (end (get-end~ range)))
        (let ((expl (new (explorer-class) model start: start end: start))
              (queue (new-queue)))
          (when (forward-expr~ expl)
            (let (iterate)
              (let ((s (get-start~ expl))
                    (e (get-end~ expl)))
                (cond ((nu<=? e end)
                       (let ((form (read-text-form~ syntax (get-range~ expl))))
                         (let ((name (form-name~ expl form)))
                           (when name
                             (enqueue queue name))))
                       (when (forward-expr~ expl)
                         (iterate)))
                      ((nu<? s end)
                       (explore-error~ expl "Ill-balanced expressions" start end))))))
          (queue-list queue))))
    
    (define (evaluate info)
      (bind (filename forms syntax str container line col) info
        (let ((processes (evaluate-filename-expressions~ (get-debugger-manager) filename forms syntax str container line col evaluation-mode)))
          (if (or (not processes) (null? processes))
              (message-box "No process found for evaluation")
            (let ((names (collect-declaration-names)))
              (when (not-null? names)
                (let ((process-titles (map get-title~ processes)))
                  (evaluate-delayed)
                  (user-message "Evaluated {l detail: :human separator: \", \"} to {l detail: :human separator: \", \"}" names process-titles))))))))
    
    (define (evaluate-delayed)
      (let ((delayed (get-delayed-evaluate)))
        (when delayed
          (bind (filename forms syntax str container line col) delayed
            (evaluate-filename-expressions~ (get-debugger-manager) filename forms syntax str container line col evaluation-mode))
          (reset-delayed-evaluate))))
    
    (let ((moniker (get-moniker)))
      (if (not moniker)
          (bell)
        (when (find-setting 'jazz.evaluation-autosave? #f)
          (save))
        (let ((filename (parse~ moniker))
              (forms (read-text-forms~ syntax range)))
          (let ((info (cons filename (cons forms (prepare-expressions range)))))
            (if (delay-evaluate?)
                (begin
                  (set-delayed-evaluate info)
                  (user-message "Evaluation delayed"))
              (evaluate info)))))))
  
  
  (method protected virtual (prepare-expressions range)
    )
  

  ;;;
  ;;;; Match
  ;;;
  
  
  (method override (match-backwards? char)
    (memq? char '(#\close-parenthesis #\close-bracket #\close-brace)))
  
  
  (method override (match-forwards? char)
    (memq? char '(#\open-parenthesis #\open-bracket #\open-brace)))

  
  ;;;
  ;;;; Chapters
  ;;;
  
  
  (constant headers
    (list
      (string-append (make-string 4 #\semi-colon) " ")))
  
  
  (method override (get-headers)
    headers)
  
  
  ;;;
  ;;;; Definitions
  ;;;


  (method (current-declaration)
    (let ((range (current-declaration-range)))
      (when range
        (read-string-element (get-string range)))))
  
  
  (method (current-declaration-range)
    (or (effective-selection)
        (declaration-range (get-end))))
  
  
  (method public (declaration-range pos)
    (let ((expl <Text-Explorer> (new (explorer-class) model start: pos end: pos)))
      (let (iterate (range #f))
        (if (not (outer-expr~ expl uniform-style?: #t))
            range
          (let ((new-range (get-range~ expl))
                (name (form-expr~ expl)))
            (cond ((namespace-name?~ expl name)
                   (if (or ;; anywhere on first line
                           (= (get-line~ pos)
                              (get-line~ (get-start~ new-range)))
                           ;; exactly at end
                           (nu=? pos (get-end~ new-range)))
                       new-range
                     range))
                  ((and (declaration-name?~ expl name)
                        (not (inner-name?~ expl name)))
                   new-range)
                  (else
                   (iterate new-range))))))))
  
  
  (method public (outer-declaration-range range)
    (let ((expl <Text-Explorer> (new (explorer-class) model range: range)))
      (let (iterate (range range))
        (if (not (outer-expr~ expl))
            #f
          (let ((new-range (get-range~ expl))
                (name (form-expr~ expl)))
            (if (and (declaration-name?~ expl name)
                     (not (inner-name?~ expl name)))
                (values new-range name (declaration-name~ expl))
              (iterate new-range)))))))
  
  
  (method override (info-message)
    (define (collect-names)
      (let ((expl (new (explorer-class) model range: (or (current-declaration-range) (get-selection)))))
        (let (iter (names '()))
          (let ((names (if (declaration-expr?~ expl)
                           (cons (declaration-name~ expl) names)
                         names)))
            (if (outer-expr~ expl)
                (iter names)
              names)))))
    
    (let ((base-message (nextmethod)))
      (catch (Explore-Error err
               base-message)
        (let ((names (collect-names)))
          (concatenate base-message
                       (if (>= (length names) 2)
                           (format ", Declaration: {l separator: \".\"}" (cdr names))
                         ""))))))

  
  ;;;
  ;;;; Highlights
  ;;;
  
  
  (definition Highlight-Styles
    '(Reference Warning Fatal Break))
  
  
  (definition (highlight-style? style)
    (some? (lambda (style-name)
             (inherits?~ style style-name))
           Highlight-Styles))
  
  
  (definition (constant? expr)
    (or (number? expr)
        (boolean? expr)
        (char? expr)
        (symbol? expr)
        (keyword? expr)))
  
  
  (method (reference-search-context target)
    (search-context~ model (and target (standardize-form target (empty-effective-selection?)))
      whole-words?: #t))
  
  
  (method (on-highlight-selection evt)
    (let ((pos (get-start)))
      (let ((declaration-range (declaration-range pos))
            (expr-range (current-expr-range)))
        (if (or (not declaration-range) (not expr-range))
            (bell)
          (let ((constant (range-expr expr-range)))
            (if (not (constant? constant))
                (bell)
              (let ((target (reference-search-context constant))
                    (style (get-style-at~ model pos)))
                (if (highlight-style? style)
                    (unhighlight-expression declaration-range target)
                  (highlight-expression declaration-range target)))))))))
  
  
  (method (on-highlight-declaration evt)
    (define (single-line str)
      (let ((pos (find str #\newline)))
        (if (not pos)
            str
          (substring str 0 pos))))
    
    (let ((range (current-expr-range)))
      (if (not range)
          (bell)
        (let ((target (reference-search-context (single-line (get-string range)))))
          (highlight-declaration range target)))))
  
  
  (method public (highlight-declaration range target)
    (let ((declaration-range (declaration-range (get-start))))
      (if (not declaration-range)
          (begin
            (bell)
            (select-command-search~ (get-application) 'forward target))
        (let ((search-result (make-text-search-result target self (get-start~ declaration-range) (get-end~ declaration-range))))
          (select-command-highlight~ (get-application) 'forward range search-result self highlight?: #t)))))

  
  (method (on-remove-highlights evt)
    (let ((range (declaration-range (get-start))))
      (if (not range)
          (bell)
        (remove-highlights range))))
  
  
  (method public (find-expression range target)
    (continuation-capture
      (lambda (return)
        (iterate-expression-target range target
          (lambda (found)
            (continuation-return return found)))
        #f)))
  
  
  (method public (highlight-reference direction range search-result)
    (select-command-highlight~ (get-application) direction range search-result self))
  
  
  (method public (highlight-expression range target)
    (let ((ranges (new-queue)))
      (iterate-expression-target range target
        (lambda (found)
          (push-style~ model found 'Reference)
          (enqueue ranges found)))
      (queue-list ranges)))
  
  
  (method public (unhighlight-expression range target)
    (iterate-expression-target range target
      (lambda (found)
        (pop-style~ model found 'Reference))))
  
  
  (method public (remove-highlights range)
    (for-each-paragraph~ model
      (lambda (line <fx> paragraph <Paragraph>)
        (for-each (lambda (run)
                    (let ((style (get-style~ run)))
                      (when (highlight-style? style)
                        (set-style~ run (get-default-style~ model))
                        (invalidate-paragraph~ model paragraph))))
                  (get-runs~ paragraph)))
      start: (get-line~ (get-start~ range))
      end: (get-line~ (get-end~ range))))
  
  
  (method (iterate-expression-target range target proc)
    (let ((expl <Text-Explorer> (new (explorer-class) model start: (get-start~ range) end: (get-start~ range))))
      (iterate-expression~ expl
        (lambda (mode)
          (when (constant?~ expl)
            (let ((seq (get-marked~ expl)))
              (unless (eqv? (string-ref seq 0) #\')
                (let ((found (regular-search seq target)))
                  (when found
                    (let ((start (nu+ (get-start~ expl) (car found))))
                      (let ((end (nu+ start (- (cdr found) (car found)))))
                        (proc (new Range$Cell$ start end)))))))))))))))
