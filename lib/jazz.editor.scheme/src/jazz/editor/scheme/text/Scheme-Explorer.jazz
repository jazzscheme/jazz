;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Exploring Scheme Code
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.editor.scheme.text.Scheme-Explorer jazz


(require (jazz.editor.scheme.forms))

(import (jazz.editor.lisp)
        (jazz.editor.scheme))


(proclaim (warn optimizations))


(class Scheme-Explorer extends Lisp-Explorer
  
  
  (method (get-syntax)
    'scheme)
  
  
  (method (expression-comment? mark)
    (define (get-string range)
      (get-range-string (get-start~ range) (get-end~ range)))
    
    (or (string=? (get-string (get-range~ mark)) "#;")
        (nextmethod mark)))


  ;;;
  ;;;; Definitions
  ;;;


  (method (declaration-name? name)
    (or (define-type-of-symbol? name)
        (nextmethod name)))
  
  
  (definition (define-type-of-symbol? symbol)
    (and (symbol? symbol)
         (starts-with? (symbol->string symbol) "define-type-of-")))


  ;;;
  ;;;; Declarations
  ;;;
  
  
  (method (find-named-walker name)
    (if (define-type-of-symbol? name)
        walk-define-type-of
      (case name
        ((:cond-expand)      walk-cond-expand)
        ((:begin)            walk-begin)
        ((:namespace)        walk-namespace)
        ((:keyword)          walk-keyword)
        ((:declare)          walk-declare)
        ((:define)           walk-define)
        ((:generic/specific) walk-define-generic/specific)
        ((:define-macro)     walk-define-macro)
        ((:macro)            walk-macro)
        ((:define-class)     walk-define-class)
        ((:type)             walk-type)
        ((:native)           walk-native)
        ((:unknown)          walk-unknown)
        ((:c-declare)        walk-c-declare)
        ((:c-initialize)     walk-c-initialize)
        (else                (nextmethod name)))))

  
  (method (walk-modifiers context declaration keyword modifiers)
    (let ((old (copy end)))
      (let (iterate)
        (forward-whites)
        (set-start end)
        (let ((c0 (char-forward 0)))
          (when (and c0 (text-constituent? c0))
            (forward-constituent)
            (when (memq? (find-marked-symbol) modifiers)
              (keyword context start end)
              (set! old (copy end))
              (iterate)))))
      (set! end old)))
  
  
  (method (walk-keywords context declaration keyword expression keywords)
    (let ((old (copy end)))
      (let (iterate)
        (forward-whites)
        (set-start end)
        (let ((c0 (char-forward 0)))
          (when (and c0 (text-constituent? c0))
            (forward-constituent)
            (let ((marked-keyword (find-marked-symbol)))
              (when (memq? marked-keyword keywords)
                (keyword context start end)
                (forward-expr)
                (walk-expression context expression 'code #f)
                (set! old (copy end))
                (iterate))))))
      (set! end old)))
    
  
  (method (walk-begin context symbol form declaration keyword expression doc walk-expressions?)
    (walk-declarations context declaration keyword expression doc walk-expressions?))

  
  (method (walk-cond-expand context symbol form declaration keyword expression doc walk-expressions?)
    (let ((done? #f))
      (while (not done?)
        (let ((enter? (enter-list)))
          (if (not enter?)
              (set! done? #t)
            (forward-expr)
            (when (eq? (get-kind) 'symbol)
              (expression context start end 'code 'feature))
            (walk-declarations context declaration keyword expression doc walk-expressions?)
            (exit-list))))))
  
  
  (method (walk-namespace context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (walk-modifiers context declaration keyword (get-modifiers~ form))
      (when (and (forward-expr) (eq? kind 'symbol))
        (set! entry (declaration context symbol start end (get-marked) (symbol-documented? (find-marked-symbol) doc)))
        (walk-keywords entry declaration keyword expression (get-keywords~ form))
        (walk-declarations entry declaration keyword expression doc walk-expressions?))
      entry))
  
  
  (method (walk-keyword context symbol form declaration keyword expression doc walk-expressions?)
    (keyword context start end))
        
  
  (method (walk-declare context symbol form declaration keyword expression doc walk-expressions?)
    (keyword context start end))
  
  
  (method (walk-define-type-of context symbol form declaration keyword expression doc walk-expressions?)
    (walk-define context symbol (get-lisp-form 'define) declaration keyword expression doc walk-expressions?))
  
  
  (method (walk-define context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (forward-whites)
      (let ((list? (eqv? (char-forward 0) #\open-parenthesis)))
        (when list?
          (enter-list))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context symbol start end (get-marked) #f)))
        (when list?
          (walk-parameters entry declaration keyword expression doc))
        (when list?
          (exit-list)))
      (when walk-expressions?
        (walk-declarations entry declaration keyword expression doc walk-expressions?))
      entry))
  
  
  (method (walk-define-generic/specific context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (forward-whites)
      (enter-list)
      (when (and (forward-expr) (eq? kind 'symbol))
        (let ((start (get-start))
              (end (get-end))
              (name (get-marked)))
          (forward-whites)
          (enter-list)
          (forward-expr)
          (let ((typename (get-marked)))
            (set! entry (declaration context symbol start end (list name typename) #f)))
          (exit-list)))
      (walk-parameters entry declaration keyword expression doc)
      (exit-list)
      (when walk-expressions?
        (walk-declarations entry declaration keyword expression doc walk-expressions?))
      entry))

  
  (method (walk-macro context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (walk-modifiers context declaration keyword (get-modifiers~ form))
      (enter-list)
      (when (and (forward-expr) (eq? kind 'symbol))
        (set! entry (declaration context symbol start end (get-marked) (symbol-documented? (find-marked-symbol) doc)))
        (walk-parameters entry declaration keyword expression doc)
        (exit-list)
        (when walk-expressions?
          (walk-expressions entry expression)))
      entry))
  
  
  (method (walk-define-macro context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (forward-whites)
      (let ((list? (eqv? (char-forward 0) #\open-parenthesis)))
        (when list?
          (enter-list))
        (when (and (forward-expr) (eq? kind 'symbol))
          (set! entry (declaration context symbol start end (get-marked) #f)))
        (when list?
          (walk-expressions entry expression 'code #f))
        (when list?
          (exit-list)))
      (when walk-expressions?
        (walk-declarations entry declaration keyword expression doc walk-expressions?))
      entry))
  
  
  (method (walk-define-class context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (forward-whites)
      (when (and (forward-expr) (eq? kind 'symbol))
        (set! entry (declaration context symbol start end (get-marked) #f)))
      (when walk-expressions?
        (walk-expressions entry expression))
      entry))
  
  
  (method (walk-native context symbol form declaration keyword expression doc walk-expressions?)
    (define (parse-identifier str)
      (let ((pos (find str #\. reversed?: #t)))
        (if (not pos)
            str
          (substring str (+ pos 1) (string-length str)))))
    
    (let ((entry context))
      (keyword context start end)
      (walk-modifiers context declaration keyword (get-modifiers~ form))
      (forward-whites)
      (when (and (forward-expr) (eq? kind 'symbol))
        (let ((name (parse-identifier (get-marked))))
          (set! entry (declaration context symbol start end name (symbol-documented? name doc)))))
      (when walk-expressions?
        (walk-expressions entry expression))
      entry))
  
  
  (method (walk-type context symbol form declaration keyword expression doc walk-expressions?)
    (let ((entry context))
      (keyword context start end)
      (when (and (forward-expr) (eq? kind 'symbol))
        (set! entry (declaration context symbol start end (get-marked) #f)))
      (when walk-expressions?
        (walk-expressions entry expression))
      entry))

  
  (method (walk-c-initialize context symbol form declaration keyword expression doc walk-expressions?)
    (keyword context start end)
    (when walk-expressions?
      (walk-expressions context expression)))

  
  (method (walk-c-declare context symbol form declaration keyword expression doc walk-expressions?)
    (keyword context start end)
    (when walk-expressions?
      (walk-expressions context expression)))
  
  
  (method package (symbol-documentation symbol doc)
    (when (and symbol doc)
      (get-definition~ doc symbol)))
  
  
  (method package (symbol-documented? symbol doc)
    (symbol-documentation symbol doc))))
