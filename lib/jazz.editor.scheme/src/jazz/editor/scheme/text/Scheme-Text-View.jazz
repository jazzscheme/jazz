;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Scheme Text-View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.scheme.text.Scheme-Text-View jazz


(import (jazz.action)
        (jazz.editor)
        (jazz.editor.lisp)
        (jazz.editor.scheme)
        (jazz.locale)
        (jazz.markup)
        (jazz.menu)
        (jazz.preferences)
        (jazz.resource)
        (jazz.text))


(class Scheme-Text-View extends Lisp-Text-View

  
  (form
    (<install> tooltip?: #t))
  
  
  (method meta override (external-name self . rest)
    'scheme-text)

  
  (method override (preferences self)
    (current-preferences '(documents scheme-text)))
    
  
  (method override (new-syntax self)
    (new Scheme-Syntax model))
  

  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions self)
    (cons (find-actions 'scheme-text)
          (nextmethod self)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (get-context-menu self)
    (new Scheme-Text-Menu))
  
  
  ;;;
  ;;;; Evaluation
  ;;;
  
  
  (method override (prepare-expressions self range)
    (define (extract-library)
      (let ((first-line (paragraph-string self 0)))
        (let ((start (if (or (starts-with? first-line "#!")
                             (starts-with? first-line "@;"))
                         (new Cell 1 0)
                       (new Cell 0 0))))
          (let ((expl (new Scheme-Explorer model start: start)))
            (if (and (forward-expr expl)
                     (namespace-expr? expl))
                (let ((range (get-range expl)))
                  (let ((form (read-text-form syntax range)))
                    (if (and (pair? form)
                             (eq? (car form) 'library))
                        range
                      #f)))
              #f)))))
    
    (define (package-library library string)
      (let ((first-line (get-line (get-start library)))
            (first-col (get-col (get-start library))))
        (let ((line first-line)
              (col first-col)
              (output (open-output-string)))
          (define (fill rng)
            (let ((l (get-line (get-start rng)))
                  (c (get-col (get-start rng))))
              (when (>= l line)
                (let (iterate)
                  (if (= l line)
                      (when (>= c col)
                        (display (make-string (- c col) #\space) output)
                        (set! col c))
                    (display #\newline output)
                    (set! line (+ line 1))
                    (set! col 0)
                    (iterate))))))
          
          (display (get-string self library) output)
          (fill range)
          (display string output)
          (values (get-output-string output) first-line first-col))))
    
    (let ((container (get-locat-container self))
          (library (extract-library))
          (string (get-string self range))
          (line (get-line (get-start range)))
          (col (get-col (get-start range))))
      (if (not library)
          (list 'scheme string container line col)
        (receive (str line col) (package-library library string)
          (list 'scheme str container line col))))))


;;;
;;;; Scheme-Text-Menu
;;;


(class Scheme-Text-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item>     name: evaluate          title: "Evaluate" icon: {Image-Resource "Execute"} action-handler: {Action code-text evaluate})
      (<Separator-Item>)
      (<Label-Item>     name: edit-definitions  title: "Find Definitions" icon: {Image-Resource "Edit"} action-handler: {Action code-text edit-definitions})
      (<Label-Item>     name: edit-references   title: "Find References" action-handler: {Action code-text edit-references})
      (<Separator-Item>)
      (<Label-Item>     name: cut               title: {Locales english: "Cut" french: "Couper"} icon: {Image-Resource "Cut"} action-handler: {Action view cut})
      (<Label-Item>     name: copy              title: {Locales english: "Copy" french: "Copier"} icon: {Image-Resource "Copy"} action-handler: {Action view copy})
      (<Label-Item>     name: paste             title: {Locales english: "Paste" french: "Coller"} icon: {Image-Resource "Paste"} action-handler: {Action view paste})
      (<Separator-Item>)
      (<Label-Item>                             title: "Column"
        (<Column-Menu>))
      (<Label-Item>                             title: "Format"
        (<Format-Menu>))))))
