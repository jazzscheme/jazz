;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; SQL Console Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.sql.sql.SQL-Console-Text-View jazz


(import (jazz.console)
        (jazz.editor.sql)
        (jazz.editor.sql.view)
        (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view))


(class SQL-Console-Text-View extends Console-Text-View
  
  
  (slot current-request initialize #f getter generate)
  (slot current-table   initialize #f)
  (slot current-tables  initialize '())
  
  
  (form
    (<install> context: sql))

  
  (definition (sql-tool-preferences)
    (get-preferences '(tools sql)))
  
  
  (method override (get-context)
    '(sql))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set! banner (determine-banner))
    (set! banner-prompt "> ")
    (display-banner banner)
    (display-prompt))
    

  ;;;
  ;;;; Banner
  ;;;

  
  (method override (determine-banner)
    (format "SQL Console{%}{%}"))
    

  ;;;
  ;;;; Prompt
  ;;;
  
  
  (method (display-prompt)
    (insert-styled "> " 'Prompt))
  
  
  (method (evaluation-break)
    (set-caret (text-ending))
    (insert-newline))


  ;;;
  ;;;; Eval
  ;;;
  
  
  (definition Header-Font
    {Font Code})
  
  (definition Header-Font-Plain
    {Font Code-Bold})
  
  (definition Header-Color
    {Color Dark-Blue})
  
  (definition Header-Style
    (new Text-Style font: Header-Font color: Header-Color))
  
  (definition Header-Style-Plain
    (new Text-Style font: Header-Font-Plain color: Header-Color))
  
  (definition Overflow-Style
    {Text-Style font: {Font Code} color: {Color Dark-Red}})

  
  (method override (eval-from-prompt)
    (let ((range (current-command)))
      (let ((command (get-string range)))
        (unless (empty-string? command)
          (set-style range (get-style 'Input))
          (reset-inserted)
          (evaluate-statements (list command) #t))))
    (display-prompt))
  
  
  (method public (evaluate-statements statements console?)
    (let ((length (length statements)))
      (case length
        ((0) (bell))
        ((1) (evaluate (car statements) console?))
        (else
         (with-cursor :wait
           (lambda ()
             (let ((connection (get-connection))
                   (affected 0)
                   (timer (new Timer)))
               (with-transaction~ connection
                 (lambda ()
                   (for-each (lambda (statement)
                               (cond ((select-statement? statement)
                                      (let ((table (load-request~ connection statement)))
                                        (increase! affected (get-count~ table))))
                                     (else
                                      (increase! affected (execute~ connection statement)))))
                             statements)))
               (let ((duration (real-duration~ timer)))
                 (clear-table)
                 (when console?
                   (evaluation-break))
                 (display-result (format "{a} row{a} affected ({a} statements)" affected (format-plural affected) length) duration console?)))))))))
  
  
  (method public (evaluate command console?)
    (define (eval-command)
      (with-cursor :wait
        (lambda ()
          (let ((connection (get-connection)))
            (set! current-request command)
            (let ((timer (new Timer)))
              (let ((result (process-command connection)))
                (values result (real-duration~ timer))))))))
    
    (define (process-command connection)
      (if (and console? (not (shift-down?)))
          (process-sql connection command)
        (load-sql connection command)))
  
    (define (display-evaluation tables duration)
      (when console?
        (evaluation-break))
      (cond ((null? tables)
             (clear-table)
             (display-result "0 rows affected" duration console?))
            ((integer? tables)
             (display-result (format "{a} row{a} affected" tables (format-plural tables)) duration console?))
            (else
             (let ((max-display (get-max-display))
                   (first-table (car tables)))
               (if (not first-table)
                   (display-result "0 rows affected" duration console?)
                 (let* ((count (get-count~ first-table))
                        (missing? (get-missing?~ first-table))
                        (displayed (min count max-display))
                        (not-displayed (- count displayed)))
                   (display-tables tables max-display not-displayed)
                   (display-affected count missing? not-displayed duration)))))))
    
    (define (display-affected count missing? not-displayed duration)
      (display-result (format "{a} row{a} retrieved{a}{a}"
                              count
                              (format-plural count)
                              (if (not missing?)
                                  ""
                                (format " with some missing"))
                              (if (= not-displayed 0)
                                  ""
                                (format ", {a} not displayed" not-displayed)))
                      duration
                      console?))
    
    (receive (tables duration) (eval-command)
      (display-evaluation tables duration)))
  
  
  (definition (get-connection)
    (or (get-active-connection)
        (error "No active connection")))
  
  
  (method (process-sql connection command)
    (let* ((appl (get-application))
           (profile (get-profile~ appl))
           (table (process-sql~ profile connection command)))
      (if table
          (list table)
        (load-sql connection command))))
  
  
  (method (load-sql connection command)
    (let* ((pref (sql-tool-preferences))
           (max-load (get-max-load~ pref))
           (sql (expand-sql command)))
      (if (select-statement? sql)
          (load-multirequest~ connection sql max-load: max-load)
        (execute~ connection sql))))
  
  
  (definition (expand-sql command)
    (let* ((appl (get-application))
           (profile (get-profile~ appl)))
      (expand-sql~ profile command)))
  
  
  (definition (get-max-display)
    (let ((pref (sql-tool-preferences)))
      (get-max-display~ pref)))
  
  
  (method (display-result string duration console?)
    (let ((effective-string (format "{a} ({r precision: 3}s)" string duration)))
      (if (not console?)
          (user-message "{a}" effective-string)
        (clear-user-message)
        (let ((start (get-start)))
          (insert-styled effective-string 'Output)
          (insert-newline)))))
  
  
  (definition (present-value value)
    (if (not value)
        "NULL"
      (->string value)))
  
  
  (method public (clear-table)
    (let ((text (get-sql-output-text))
          (tree (get-sql-output-tree)))
      (clear-all~ text)
      (remove-columns~ tree)
      (clear-tables)))
  
  
  (method public (output-tables tables (name #f))
    (set! current-request name)
    (display-tables tables (get-max-display) 0)
    tables)
  
  
  (method public (clear-output)
    (clear-all~ (get-sql-output-text))
    (remove-columns~ (get-sql-output-tree))
    (clear-tables))
  
  
  (method public (previous-table)
    (decrease! current-table)
    (display-current-table (get-max-display) 0))
  
  
  (method public (next-table)
    (increase! current-table)
    (display-current-table (get-max-display) 0))
  
  
  (method (clear-tables)
    (set! current-table 0)
    (set! current-tables '())
    (let ((output (get-sql-tree-output)))
      (recordsets-update~ output current-table current-tables #f)))
  
  
  (method (display-tables tables max-display not-displayed)
    (set! current-tables tables)
    (let ((table-no 0))
      (for-each (lambda (table)
                  (set! current-table table-no)
                  (display-current-table max-display not-displayed)
                  (set! table-no (+ table-no 1)))
                tables)))
  
  
  (method (display-current-table max-display not-displayed)
    (display-table (element current-tables current-table) max-display not-displayed)
    (let ((output (get-sql-tree-output)))
      (recordsets-update~ output current-table current-tables not-displayed)))
  
  
  (method (display-table table max-display not-displayed)
    (define (compute-widths columns font max-char)
      (map (lambda (column)
             (min 250 (+ (max (text-width~ font (get-name~ column)) (* (get-prefered-width~ column) max-char)) 24)))
           columns))
    
    (define (display-text columns rows affected max-display not-displayed widths)
      (define (determine-max-list headers data)
        (map (lambda (n)
               (let ((max (length (element headers n))))
                 (for-each (lambda (row)
                             (let* ((data (element row n))
                                    (count (length data)))
                               (when (> count max)
                                 (set! max count))))
                           data)
                 max))
             (naturals 0 (length headers))))
      
      (define Spacing
        3)
      
      (define (join-data row max-list)
        (let ((output (open-output-string)))
          (while row
            (let ((data (car row))
                  (max (+ (car max-list) Spacing))
                  (last? (null? (cdr row))))
              (format output "{a}" data)
              (unless last?
                (format output "{_ :v}" (- max (length data))))
              (set! row (cdr row))
              (set! max-list (cdr max-list))))
          (get-output-string output)))
      
      (let* ((pref (sql-tool-preferences))
             (tabulate? (get-tabulate-text?~ pref))
             (output (get-sql-output-text))
             (style (intern-style (if tabulate? Header-Style Header-Style-Plain)))
             (stop 0))
        (when (= current-table 0)
          (clear-all~ output))
        (cond (tabulate?
                (let ((stops (map (lambda (width)
                                    (increase! stop width)
                                    stop)
                                  widths)))
                  (set-tabs~ output (append! stops (make-tabs-from stop 32 256))))
                (insert-styled~ output (join (map get-name~ columns) (string #\tab)) style)
                (insert-line~ output "")
                (insert-line~ output "")
                (for-each (lambda (row)
                            (insert-line~ output (join (map present-value (get-values~ row)) (string #\tab))))
                          (if (> affected max-display) (subseq rows 0 max-display) rows)))
              (else
               (let* ((headers (map get-name~ columns))
                      (data (map (lambda (row) (map present-value (get-values~ row))) rows))
                      (max-list (determine-max-list headers data)))
                 (insert-styled~ output (join-data headers max-list) style)
                 (insert-line~ output "")
                 (insert-line~ output "")
                 (for-each (lambda (row)
                             (insert-line~ output (join-data row max-list)))
                           (if (> affected max-display) (subseq data 0 max-display) data)))))
        (when (> not-displayed 0)
          (insert-line~ output "")
          (insert-styled~ output (format "{a} row{a} not displayed" not-displayed (format-plural not-displayed)) (intern-style Overflow-Style))
          (insert-line~ output ""))
        (scroll-to~ output {Point 0 0})))
    
    (define (display-tree columns rows affected max-display not-displayed widths)
      (let ((output (get-sql-output-tree)))
        (remove-columns~ output)
        (let ((first-column (car columns)))
          (for-each (lambda (column width)
                      (let* ((node? (eq? column first-column))
                             (column (if node?
                                         (add-column~ output class: Tree-Node-Column toplevel-controls?: #f title: (get-name~ column) width: width)
                                       (add-column~ output class: Tree-Label-Column title: (get-name~ column) width: width))))
                        (when node?
                          (set-display-images?~ column #f))))
                    columns
                    widths))
        (for-each (lambda (row)
                    (let* ((values (get-values~ row))
                           (first (car values))
                           (rest (cdr values)))
                      (add-row~ output children: (cons (new Tree-Node title: (present-value first)) (map (lambda (value) (new Tree-Label title: (present-value value))) rest)))))
                  (if (> affected max-display) (subseq rows 0 max-display) rows))))
    
    (let* ((columns (get-columns~ table))
           (rows (get-rows~ table))
           (affected (length rows))
           (text-max-char (get-max-char-width~ (get-metrics~ {Font Code})))
           (text-widths (compute-widths columns Header-Font text-max-char))
           (tree (get-sql-output-tree))
           (tree-font (effective-font~ tree))
           (tree-max-char (get-max-char-width~ (get-metrics~ tree-font)))
           (tree-widths (compute-widths columns tree-font tree-max-char)))
      (set-?t table)
      (set-?c columns)
      (set-?r rows)
      (display-text columns rows affected max-display not-displayed text-widths)
      (display-tree columns rows affected max-display not-displayed tree-widths)))))
