;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; SQL Tree Output
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.editor.sql.sql.SQL-Tree-Output jazz


(import (jazz.editor.sql)
        (jazz.editor.sql.view)
        (jazz.event)
        (jazz.graphic)
        (jazz.handler)
        (jazz.history)
        (jazz.ide.history)
        (jazz.jml)
        (jazz.layout)
        (jazz.outline)
        (jazz.resource)
        (jazz.schema)
        (jazz.tree)
        (jazz.ui))


(class SQL-Tree-Output extends Layout-View implements History-Provider
  
  
  (slot history-manager initialize (new History-Manager self))


  (form
    (<install>                                   size: {Dimension 650 400} background: {Color Dark} layout-type: border
      (<Layout-View>             name: tools
        (<Layout-View>                           size: {Dimension 200 22} location: tail layout-type: border layout-insets: {Rect 0 1 0 0}
          (<Label-View>          name: current   location: center font: {Font Title-User} justification: tail))
        (<Separator>                             size: {Dimension 2 0} location: tail)
        (<Push-Tool>             name: previous  size: {Dimension 22 22} location: tail enabled?: #f tooltip?: #t tooltip-text: "Previous Recordset" resource: {Image-Resource "AddLeft"} portfolio: :images action-handler: {Event-Handler :guest on-previous-recordset})
        (<Push-Tool>             name: next      size: {Dimension 22 22} location: tail enabled?: #f tooltip?: #t tooltip-text: "Next Recordset" resource: {Image-Resource "AddRight"} portfolio: :images action-handler: {Event-Handler :guest on-next-recordset})
        @convert
        (<Separator>                             size: {Dimension 2 0} location: tail)
        @convert
        (<Push-Combo>            name: backward  location: tail
          (<!>                   name: body      resource: {Image-Resource "Backward"} portfolio: :images tooltip?: #t tooltip-text: "Backward in History" action-handler: {Event-Handler :guest on-backward-history})
          (<!>                   name: pick      action-handler: {Event-Handler :guest on-backward-menu}))
        @convert
        (<Separator>                             size: {Dimension 1 0} location: tail)
        @convert
        (<Push-Combo>            name: forward   location: tail
          (<!>                   name: body      resource: {Image-Resource "Forward"} portfolio: :images tooltip?: #t tooltip-text: "Forward in History" action-handler: {Event-Handler :guest on-forward-history})
          (<!>                   name: pick      action-handler: {Event-Handler :guest on-forward-menu}))
        (<Separator>                             size: {Dimension 4 0} location: tail)
        (<Push-Tool>             name: clear     size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Clear" resource: {Image-Resource "Clear"} portfolio: :images action-handler: {Event-Handler :guest on-clear})
        (<Push-Tool>             name: displayed size: {Dimension 22 22} location: tail tooltip?: #t resource: {Image-Resource "Gray"} portfolio: :images action-handler: {Event-Handler :guest on-settings}))
      (<Scroller-View>           name: scroller  location: center vscroll?: #t hscroll?: #t style: tool
        (<!>                     name: content   layout-type: fill
          (<Tree-Header>         name: header    style: tool
            (<!>                 name: content
              (<SQL-Output-Tree> name: tree      background: {Color Dark} filled-column: #f selection-mode: cell multiple-selection?: #t row-spacing: 1 column-spacing: 1)))))))
  
  
  (method meta override (external-name self . rest)
    'SQL-Tree-Output)

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Info"})
  
  
  (method meta override (host-title self)
    "Tree Output")

  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (get-tools-view self)
    (locate self 'tools))
  
  
  (method override (focus-default self)
    (let ((tree (get-tree self)))
      (acquire-focus tree)))
  
  
  ;;;
  ;;;; Access
  ;;;

  
  (method public (get-tree self)
    (locate self 'tree))
  
  
  ;;;
  ;;;; Edition
  ;;;


  (method override (delete-selection self)
    (let ((tree (get-tree self)))
      (remove-every-row tree)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-previous-recordset self evt)
    (let ((console (get-sql-console-text)))
      (previous-table console)))
  
  
  (method (on-next-recordset self evt)
    (let ((console (get-sql-console-text)))
      (next-table console)))
  
  
  (method public (recordsets-update self current tables not-displayed)
    (let* ((card (length tables))
           (table (and (not-null? tables) (element tables current)))
           (name (and table (get-name table)))
           (count (and table (get-count table)))
           (missing? (and table (get-missing? table))))
      (set-title (locate self 'current) (if (= card 0) "" (format "{?{a} ~}{a} of {a}" name (+ current 1) card)))
      (set-enabled? (locate self 'previous) (> current 0))
      (set-enabled? (locate self 'next) (< current (- card 1)))
      (let ((displayed-view (locate self 'displayed))
            (resource (cond ((not not-displayed) {Image-Resource "Gray"})
                            ((and (not missing?) (= not-displayed 0)) {Image-Resource "Green"})
                            (else {Image-Resource "Red"})))
            (tooltip-text (cond ((not not-displayed) #f)
                                ((and (not missing?) (= not-displayed 0)) (format "{a} rows retrieved and displayed" count))
                                (else (format "{a}{a}{a}"
                                              (if missing? "Some rows not retrieved" "")
                                              (if (and missing? (/= not-displayed 0)) ", " "")
                                              (if (/= not-displayed 0) (format "{a}{a} not displayed" not-displayed (if missing? "" " rows")) ""))))))
        (set-resource displayed-view resource)
        (set-tooltip-text displayed-view tooltip-text))))
  
  
  (method package (on-clear self evt)
    (let ((console (get-sql-console-text)))
      (clear-table console)))
  
  
  (method (on-settings self evt)
    @convert
    (new-frame Preferences-Manager host-workspace: 'sql))
  
  
  ;;;
  ;;;; History
  ;;;
  
  
  (method package (on-add-history self evt)
    (add-current history-manager))
  
  
  (method package (on-backward-history self evt)
    (go-backward-history history-manager))
  
  
  (method package (on-forward-history self evt)
    (go-forward-history history-manager))

  
  (method (on-backward-menu self evt)
    (let* ((sender (get-sender evt))
           (initiator (get-combo sender)))
      (track-history sender initiator history-manager 'backward)))
  
  
  (method (on-forward-menu self evt)
    (let* ((sender (get-sender evt))
           (initiator (get-combo sender)))
      (track-history sender initiator history-manager 'forward)))
  
  
  (method public (add-history self item)
    (add-history history-manager item))
  
  
  (method override (current-history-item self)
    (let* ((console (get-sql-console-text))
           (request (get-current-request console)))
      (when request
        (new History-Request-Item request))))
  
  
  (method override (update-history-actions self)
    )))
