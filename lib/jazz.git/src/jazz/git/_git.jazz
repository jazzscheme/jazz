;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Git
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.git jazz


(import (jazz.feedback)
        (jazz.io))


(definition *depot*
  #f)


(definition public (git-depot)
  *depot*)


(definition public (git-depot-set! repo)
  (set! *depot* repo))


;;;
;;;; Pathname
;;;


(definition public (determine-file-depot file)
  (let (iter (dir (get-parent file)) (path (list (get-name file))))
    (if (not (directory? dir))
        (values #f #f)
      (if (depot-directory? dir)
          (values (new Git-Depot dir) (join path "/"))
        (iter (get-parent dir) (cons (get-name dir) path))))))


(definition public (depot-directory? dir)
  (or (exists? (new-directory dir ".git"))
      (exists? (new-file dir ".git"))))


;;;
;;;; Commands
;;;


(definition public (git-clone address destination)
  (let ((parent (get-parent destination))
        (name (get-name destination)))
    (create-directories parent)
    (git-command (list "clone" address name) parent)
    (new Git-Depot destination)))


(definition public (git-depot-add path)
  (git-add (git-depot) path))


(definition public (git-depot-remove)
  (git-remove (git-depot)))


(definition public (git-depot-rename)
  (git-rename (git-depot)))


(definition public (git-depot-export)
  (git-export (git-depot)))


(definition public (git-depot-import)
  (git-import (git-depot)))


(definition public (git-depot-branch)
  (git-branch (git-depot)))


(definition public (git-depot-checkout)
  (git-checkout (git-depot)))


(definition public (git-depot-commit)
  (git-commit (git-depot)))


(definition public (git-depot-diff)
  (git-diff (git-depot)))


(definition public (git-depot-log)
  (git-log (git-depot)))


(definition public (git-depot-status)
  (git-status (git-depot)))


(definition public (git-depot-rollback)
  (git-rollback (git-depot)))


(definition public (git-depot-tag)
  (git-tag (git-depot)))


(definition public (git-depot-merge)
  (git-merge (git-depot)))


(definition public (git-depot-pull)
  (git-pull (git-depot)))


(definition public (git-depot-push)
  (git-push (git-depot)))


(definition public (git-depot-history path)
  (git-history (git-depot) path))


;; to think about how to nicely do this
(definition public (git-depot-extract path)
  (git-extract (git-depot) path))


(definition public (git-depot-show object (output (current-output-port)))
  (git-show (git-depot) object output))


(definition (git-command arguments directory (output (current-output-port)))
  @wait-need-to-both-grab-output-and-validate-return-code
  (call-process (list path: "git" arguments: arguments directory: (parse directory) show-console: #f))
  (let ((port (open-process (list path: "git" arguments: arguments directory: (parse directory) show-console: #f))))
    (if (not output)
        port
      (pipe port output)
      (close-port port))))


;;;
;;;; Patch File
;;;


(definition public (parse-patch-file patch-file proc)
  (call-with-input-file (path-settings patch-file eol-encoding: 'cr-lf)
    (lambda (input)
      (define (read-diff line)
        (let (iter (line line))
          (unless (not line)
            (if (starts-with? line "diff ")
                (parse-diff line)
              (iter (read-line input))))))
      
      (define (parse-diff line)
        (let ((line (read-line input)))
          (receive (a-index b-index) (parse-index line)
            (let ((a-name (parse-name (read-line input)))
                  (b-name (parse-name (read-line input))))
              (if (and a-name b-name (not (equal? a-name b-name)))
                  (parse-error line)
                (let ((deltas (new-queue)))
                  (let iter-delta ()
                       (let ((range (read-line input)))
                         (if (not (starts-with? range "@@ "))
                             (read-diff range)
                           (receive (a-line a-count b-line b-count) (parse-range range)
                             (let ((a-cnt 0)
                                   (b-cnt 0)
                                   (lines (new-queue)))
                               (let iter-line ()
                                    (let ((c (read-char input))
                                          (l (read-line input)))
                                      (case c
                                        ((#\-)
                                         (enqueue lines (cons 'a l))
                                         (increase! a-cnt))
                                        ((#\+)
                                         (enqueue lines (cons 'b l))
                                         (increase! b-cnt))
                                        ((#\space)
                                         (enqueue lines (cons 'both l))
                                         (increase! a-cnt)
                                         (increase! b-cnt))))
                                    (when (or (< a-cnt a-count) (< b-cnt b-count))
                                      (iter-line)))
                               (let ((delta (list a-line a-count b-line b-count (queue-list lines))))
                                 (enqueue deltas delta))))
                           (iter-delta))))
                  (proc a-name a-index b-name b-index (queue-list deltas))))))))
      
      (define (parse-index line)
        (let ((line (if (starts-with? line "index ") line (read-line input))))
          (let* ((start 6)
                 (dotdot (search line ".." start: start))
                 (end (find line #\space start: (+ dotdot 2))))
            (values (substring line start dotdot)
                    (substring line (+ dotdot 2) (or end (length line)))))))
      
      (define (parse-name line)
        (let ((str (substring line 4 (length line))))
          (if (equal? str "/dev/null")
              #f
            (substring str 2 (cardinality str)))))
      
      (define (parse-range line)
        (let* ((a-start 4)
               (a-comma (find line #\comma start: a-start))
               (a-end   (find line #\space start: (+ a-comma 1)))
               (a-line  (parse-integer (substring line a-start a-comma)))
               (a-count (parse-integer (substring line (+ a-comma 1) a-end)))
               (b-start (+ a-end 2))
               (b-comma (find line #\comma start: b-start))
               (b-end   (find line #\space start: (+ b-comma 1)))
               (b-line  (parse-integer (substring line b-start b-comma)))
               (b-count (parse-integer (substring line (+ b-comma 1) b-end))))
          (values a-line a-count b-line b-count)))
      
      (define (parse-error line)
        (error "Invalid patch file: {s} at line: {a}" patch-file line))
      
      (read-diff (read-line input)))))


;;;
;;;; Depot
;;;


(class Git-Depot extends Object
  
  
  (slot directory getter generate)
  
  
  (method override (initialize self dir)
    (nextmethod self)
    (set! directory dir))
  
  
  (method override (print self output detail)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" (last (get-list directory))))))
  
  
  ;;;
  ;;;; Commands
  ;;;
  
  
  (method package (git self arguments (output (current-output-port)))
    (git-command arguments directory output))
  
  
  ;;;
  ;;;; Commit
  ;;;
  
  
  (method public (git-add self path)
    (git self (list "add" path)))
  
  
  (method public (git-remove self)
    #f)
  
  
  (method public (git-rename self)
    #f)
  
  
  (method public (git-commit self)
    #f)
  
  
  (method public (git-rollback self)
    (git self (list "reset" "--hard" "HEAD~1")))
  
  
  (method public (git-tag self)
    (git self (list "tag")))
  
  
  (method public (git-log self)
    (git self (list "log" "--decorate")))
  
  
  (method public (git-status self)
    (git self (list "status")))
  
  
  ;;;
  ;;;; Branch
  ;;;
  
  
  (method public (git-branch self)
    #f)
  
  
  (method public (git-checkout self)
    #f)
  
  
  ;;;
  ;;;; Merge
  ;;;
  
  
  (method public (git-import self)
    (git self (list "am")))
  
  
  (method public (git-export self)
    (git self (list "format-patch")))
  
  
  (method public (git-diff self)
    #f)
  
  
  (method public (git-merge self)
    #f)
  
  
  (method public (git-history self path)
    (let ((output (open-output-string)))
      (git self (list "rev-list" "HEAD" path) output)
      (let ((commits (get-output-string output)))
        (collect-if (lambda (str)
                      (not (empty-string? str)))
                    (split-lines commits)))))
  
  
  (method public (git-extract self path)
    (let ((history (git-history self path))
          (version 1))
      (for-each (lambda (commit)
                  (user-message "Extracting {a}..." commit)
                  (let ((base (filename-base path))
                        (extension (filename-extension path)))
                    (let ((file (new File (list 'Home "merge" (string-append base "_v" (format "{a width: 3 justify: :right padding: #\\0}" (number->string version)) "_" commit "." extension)))))
                      (create-directories file)
                      (call-with-output-file (path-settings file)
                        (lambda (output)
                          (git-show self (string-append commit ":" path) output)))))
                  (increase! version))
                (reverse history))
      (user-message "Done")))
  
  
  (method public (git-show self object (output (current-output-port)))
    (git self (list "show" object) output))
  
  
  (method public (prepare-merge self merge-dir)
    (iterate-directory merge-dir
      (lambda (file)
        (let ((ext (get-extension file)))
          (when (extension=? ext "patch")
            (parse-patch-file file
              (lambda (base-name base-index right-name right-index deltas)
                (let ((name (or base-name right-name)))
                  (let ((name-path (split name "/")))
                    (define (setup-base)
                      (when base-name
                        (let ((base-file (new-file merge-dir (cons "base" name-path))))
                          (unless (exists? base-file)
                            (user-message "Extracting base/{a}..." base-name)
                            (create-directories base-file)
                            (call-with-output-file (path-settings base-file)
                              (lambda (output)
                                (git-show self base-index output)))))))
                    
                    (define (setup-left)
                      (let ((working-file (new-file directory name-path)))
                        (if (not (exists? working-file))
                            (debug (format "Unable to find working file: {a}" name-path))
                          (let ((left-file (new-file merge-dir (cons "left" name-path))))
                            (unless (exists? left-file)
                              (user-message "Copying left/{a}..." name)
                              (create-directories left-file)
                              (duplicate working-file left-file))))))
                    
                    (define (setup-right)
                      (let ((right-file (new-file merge-dir (cons "right" name-path))))
                        (cond ;; deleted
                              ((not right-name)
                               (when (exists? right-file)
                                 (user-message "Deleting right/{a}..." name)
                                 (delete right-file)))
                              ;; new
                              ((not base-name)
                               (user-message "Adding right/{a}..." name)
                               (create-directories right-file)
                               (call-with-output-file (path-settings right-file)
                                 (lambda (output)
                                   (let ((lines (fifth (car deltas))))
                                     (for-each (lambda (info)
                                                 (bind (what . line) info
                                                   (write-line line output)))
                                               lines)))))
                              ;; modified
                              (else
                               (user-message "Modifying right/{a}..." name)
                               (create-directories right-file)
                               (let ((base-file (new-file merge-dir (cons "base" name-path))))
                                 ;; first time
                                 (when (not (exists? right-file))
                                   (duplicate base-file right-file))
                                 ;; apply diff
                                 (apply-diff right-file deltas))))))
                    
                    (define (apply-diff target-file deltas)
                      (let ((merged-file (new-brother target-file (let ((base (get-base target-file))
                                                                         (ext (get-extension target-file)))
                                                                     (format "{a}_merged.{a}" base ext)))))
                        (call-with-input-file (path-settings target-file eol-encoding: 'cr-lf)
                          (lambda (target-input)
                            (call-with-output-file (path-settings merged-file)
                              (lambda (merged-output)
                                (let ((target-rank 1))
                                  (for-each (lambda (delta)
                                              (bind (target-line target-count result-line result-count lines) delta
                                                ;; output target common lines
                                                (loop (repeat (- target-line target-rank))
                                                      (do (write-line (read-line target-input) merged-output)
                                                          (increase! target-rank)))
                                                ;; skip replaced target lines
                                                (loop (repeat target-count)
                                                      (do (read-line target-input)
                                                          (increase! target-rank)))
                                                ;; output new lines
                                                (for-each (lambda (info)
                                                            (bind (what . line) info
                                                              (when (or (eq? what 'b) (eq? what 'both))
                                                                (write-line line merged-output))))
                                                          lines)))
                                            deltas))
                                ;; output remaining
                                (let (iterate)
                                  (declare (proper-tail-calls))
                                  (let ((line (read-line target-input)))
                                    (when line
                                      (write-line line merged-output)
                                      (iterate))))))
                            (delete target-file)
                            (rename merged-file target-file)))))
                    
                    (setup-base)
                    (setup-left)
                    (setup-right))))))))
      files?: #t
      directories?: #f
      recursive?: #f
      sort?: #t))
  
  
  ;;;
  ;;;; Manage
  ;;;
  
  
  (method public (git-pull self)
    #f)
  
  
  (method public (git-push self)
    #f)))
