;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Cairo Regions
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.graphic.Region jazz


(import (jazz.cairo)
        (jazz.geometry)
        (jazz.graphic))


(class Region extends Object
  
  
  (slot handle getter generate)
  
  
  (method override (initialize self (rect: rect #f))
    (set! handle (if rect
                     (begin
                       @pixman-will-output-an-error-message-if-passed-a-bad-rectangle
                       (validate-rect rect)
                       (with-cairo-rectangle rect
                         (lambda (rectangle)
                           (cairo_region_create_rectangle rectangle))))
                   (cairo_region_create))))

  
  (method override (destroy self)
    (cairo_region_destroy handle)
    (nextmethod self))


  (method public (get-box self) <Rect>
    (with-cairo-rectangle #f
      (lambda (rectangle)
        (cairo_region_get_extents handle rectangle)
        (cairo-rectangle->rect rectangle))))
  
  
  (method public (iterate-rectangles self proc)
    (with-cairo-rectangle #f
      (lambda (rectangle)
        (loop (for n from 0 below (cairo_region_num_rectangles handle))
              (cairo_region_get_rectangle handle n rectangle)
              (proc (cairo-rectangle->rect rectangle))))))
  
  
  (method public (offset! self h v)
    (cairo_region_translate handle h v))
  
  
  (method public (combine-and! self region)
    (cairo_region_intersect handle (get-handle region)))


  (method public (combine-or! self region)
    (cairo_region_union handle (get-handle region)))
  
  
  (method public (combine-xor! self region)
    (cairo_region_xor handle (get-handle region)))
  
  
  (method public (combine-diff! self region)
    (cairo_region_subtract handle (get-handle region)))
  
  
  (method public (empty! self)
    (cairo_region_subtract handle handle))
  
  
  (method public (empty? self) <bool>
    (/= (cairo_region_is_empty handle) 0))
  
  
  (method public (intersects-rect? self rect) <bool>
    (/= (cairo_region_contains_rectangle handle rect) CAIRO_REGION_OVERLAP_OUT))))
