;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Graphic
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.graphic jazz


(export (jazz.graphic.syntax)
        (jazz.graphic.autoload))

(import (jazz.graphic.autoload)
        (jazz.geometry)
        (jazz.library.exemplar))


;;;
;;;; Register
;;;


(definition (registered-object table name)
  (let ((procedure/object (table-ref table name #f)))
    (if (procedure? procedure/object)
        (let ((object (procedure/object)))
          (table-set! table name object)
          object)
      procedure/object)))


(definition (for-each-object table proc)
  (iterate-table table
    (lambda (name value)
      (proc name (if (procedure? value)
                     (value)
                   value)))))


;;;
;;;; Color
;;;


(definition *colors*
  (new Exemplar-Domain
    name: 'Colors
    initer: (lambda ()
              (load-unit 'jazz.graphic.literals.colors))))


(definition public (colors-domain)
  *colors*)


(definition public (register-color name color)
  (register-exemplar name color *colors*))


(definition public (registered-color name (alpha #f))
  (locate~ *colors* name))


(definition public (symbolic-colors)
  (accumulate (get-content~ *colors*)
              get-name~))


(definition public (symbolic-color color)
  (if (symbol? color)
      (registered-color color)
    color))


(definition public (symbolize-color color (epsilon: epsilon 0))
  (symbolize~ *colors* color
    test: (lambda (x y)
            (color-epsilon-similar? x y epsilon))))


(definition public (for-each-color proc)
  (iterate (get-content~ *colors*) proc))


(definition public (color=? x y)
  (exemplar=? x y))


(definition public (color-similar? x y)
  (and (= (get-red~ x) (get-red~ y))
       (= (get-green~ x) (get-green~ y))
       (= (get-blue~ x) (get-blue~ y))
       (eqv? (get-alpha~ x) (get-alpha~ y))))


(definition public (color-epsilon-similar? x y epsilon)
  (and (near? (get-red~ x) (get-red~ y) epsilon)
       (near? (get-green~ x) (get-green~ y) epsilon)
       (near? (get-blue~ x) (get-blue~ y) epsilon)
       (eqv? (get-alpha~ x) (get-alpha~ y))))


(definition public (rgb->hsv r g b)
  (define (hsv h s v)
    (values (exact->inexact h)
            (exact->inexact s)
            (exact->inexact v)))
  
  (let ((r (exact->inexact (/ r 255)))
        (g (exact->inexact (/ g 255)))
        (b (exact->inexact (/ b 255))))
    (let ((min (min r g b))
          (max (max r g b)))
      (let ((v max)
            (delta (- max min)))
        (if (/= max 0)
            (let ((s (/ delta max)))
              (let ((h (cond ((= r max) (/ (- g b) delta))
                             ((= g max) (+ 2 (/ (- b r) delta)))
                             (else (+ 4 (/ (- r g) delta))))))
                (set! h (* h 60))
                (when (< h 0)
                  (increase! h 360))
                (hsv h s v)))
          (hsv -1 0 v))))))


(definition public (hsv->rgb h s v)
  (define (rgb r g b)
    (values (fxround (* r 255))
            (fxround (* g 255))
            (fxround (* b 255))))
  
  (let ((h (flmodulo (exact->inexact h) 360))
        (s (exact->inexact s))
        (v (exact->inexact v)))
    (if (= s 0)
        (rgb v v v)
      (let* ((h (/ h 60))
             (i (fxfloor h))
             (f (- h i))
             (p (* v (- 1 s)))
             (q (* v (- 1 (* s f))))
             (t (* v (- 1 (* s (- 1 f))))))
        (case i
          ((0) (rgb v t p))
          ((1) (rgb q v p))
          ((2) (rgb p v t))
          ((3) (rgb p q v))
          ((4) (rgb t p v))
          ((5) (rgb v p q)))))))


(definition public (color->rgb color)
  (values (get-red~ color)
          (get-green~ color)
          (get-blue~ color)))


(definition public (color->hsv color)
  (rgb->hsv (get-red~ color)
            (get-green~ color)
            (get-blue~ color)))


(definition public (rgb->color r g b)
  (new Color red: r green: g blue: b))


(definition public (hsv->color h s v)
  (receive (r g b) (hsv->rgb h s v)
    (new Color red: r green: g blue: b)))


(definition public (color-cycle base cycle subcycle)
  (let ((seq (naturals-cycle cycle subcycle))
        (cardinality (* cycle subcycle)))
    (let ((inc (/ 360 cardinality)))
      (receive (h s v) (color->hsv base)
        (map (lambda (n)
               (let ((h (+ h (round (* inc n)))))
                 (hsv->color (fxround h) s v)))
             seq)))))


;;;
;;;; Font
;;;


(definition *fonts*
  (new Exemplar-Domain
    name: 'Fonts
    initer: (lambda ()
              (load-unit 'jazz.graphic.literals.fonts))))


(definition public (fonts-domain)
  *fonts*)


(definition public (register-font name font)
  (register-exemplar name font *fonts*))


(definition public (registered-font name)
  (locate~ *fonts* name))


(definition public (symbolic-fonts)
  (accumulate (get-content~ *fonts*)
              get-name~))


(definition public (symbolic-font font)
  (if (symbol? font)
      (registered-font font)
    font))


(definition public (for-each-font proc)
  (iterate (get-content~ *fonts*) proc))


(definition public (font=? x y)
  (exemplar=? x y))


;;;
;;;; Region
;;;


(definition public (copy-region region <Region>) <Region>
  (let ((copy (new Region)))
    (combine!~ copy region 'or)
    copy))


(definition public (region-union x <Region> y <Region>) <Region>
  (let ((region (copy-region x)))
    (combine!~ region y 'or)
    region))


(definition public (region-intersection x <Region> y <Region>) <Region>
  (let ((region (copy-region x)))
    (combine!~ region y 'and)
    region))


(definition public (region-difference x <Region> y <Region>) <Region>
  (let ((region (copy-region x)))
    (combine!~ region y 'diff)
    region))


;;;
;;;; Debug
;;;


(definition public debug-paint?
  (make-parameter #f))

(definition public debug-painting?
  (make-parameter #f)))
