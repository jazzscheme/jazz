;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Images
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.graphic.image.Image jazz


(import (jazz.cairo)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.image))


(class Image extends Object


  (slot position   initialize #f accessors generate)
  (slot size       initialize #f accessors generate)
  (slot fill-mode  initialize #f accessors generate)
  (slot background initialize #f accessors generate)
  (slot portfolio  initialize #f accessors generate)
  (slot resource   initialize #f accessors generate)
  (slot moniker    initialize #f accessors explicit)
  (slot handle     initialize #f accessors generate)
  
  
  (method override (initialize self
                     (position: position {Point 0 0})
                     (size: size #f)
                     (fill-mode: fill-mode 'position)
                     (background: background #f)
                     (portfolio: portfolio #f)
                     (resource: resource #f)
                     (moniker: moniker #f)
                     (handle: handle #f))
    (nextmethod self)
    (set! self.position position)
    (set! self.size size)
    (set! self.fill-mode fill-mode)
    (set! self.background background)
    (set! self.portfolio portfolio)
    (set! self.resource resource)
    (cond (moniker (set-moniker self moniker))
          (handle (set-handle self handle))))
  
  
  (method override (destroy self)
    (when handle
      (cairo_surface_destroy handle)
      (set! handle #f))
    (nextmethod self))


  ;;;
  ;;;; Drawing
  ;;;


  (method public (draw-at self surface context h v (alpha: alpha #f))
    (define (portfolio-draw h v)
      (when resource
        (if background
            (draw-opaque-image portfolio surface context resource background h v alpha: alpha)
          (draw-image portfolio surface context resource h v alpha: alpha))))
    
    (define (surface-draw h v)
      (draw-surface surface handle h v alpha: alpha))
    
    (let ((h (+ (get-h position) h))
          (v (+ (get-v position) v)))
      (cond (portfolio (portfolio-draw h v))
            (handle (surface-draw h v)))))


  (method public (fill-at self surface context h v rect)
    (cond (portfolio (draw-at self surface context h v))
          (handle (fill-image surface rect self mode: fill-mode position: position size: size))))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-moniker self)
    moniker)
  
  
  (method public (set-moniker self mnk)
    (set! moniker mnk)
    (when moniker
      (set! handle (load-image mnk 'image))))
  
  
  ;;;
  ;;;; Image
  ;;;
  
  
  (method public (image-size self) <Dimension>
    (assert handle)
    (get-cairo-image-surface-size handle))
  
  
  ;;;
  ;;;; Fit
  ;;;


  (method public (fit-to self size mode)
    (let ((image-size (image-size self)))
      (let ((ratio (fit-dimensions image-size size mode)))
        (new Image handle: (resize-image handle (fxround (* (cast <fl> (get-width image-size)) ratio)) (fxround (* (cast <fl> (get-height image-size)) ratio)))))))))
