;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Regions
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.graphic.platform.windows.Region jazz


(import (jazz.graphic)
        (jazz.platform))


(class Region extends Base-Region
  
  
  (slot handle getter generate)
  
  
  (method override (initialize self (handle: handle #f) (rect: rect #f))
    (cond (handle
           (set! self.handle handle))
          (rect
           (set! self.handle (CreateRectRgn (get-left rect) (get-top rect) (get-right rect) (get-bottom rect))))
          (else
           (set! self.handle (CreateRectRgn 0 0 0 0))))
    (allocated handle))

  
  (method override (destroy self)
    (DeleteObject handle)
    (freed handle)
    (nextmethod self))
  
  
  (method override (offset! self h v)
    (OffsetRgn handle h v))
  
  
  (method override (combine-and! self region)
    (CombineRgn handle handle (get-handle region) RGN_AND))


  (method override (combine-or! self region)
    (CombineRgn handle handle (get-handle region) RGN_OR))
  
  
  (method override (combine-xor! self region)
    (CombineRgn handle handle (get-handle region) RGN_XOR))
  
  
  (method override (combine-diff! self region)
    (CombineRgn handle handle (get-handle region) RGN_DIFF))
  
  
  (method override (empty! self)
    (CombineRgn handle handle handle RGN_DIFF))
  
  
  (method override (empty? self) <bool>
    (= (get-box self) {Rect 0 0 0 0}))
  
  
  (method override (intersects-rect? self rect) <bool>
    (with ((region (new Region rect: rect)))
      (combine-and! region self)
      (not (empty? region))))


  (method override (get-box self) <Rect>
    (let ((rect (RECT-make)))
      (GetRgnBox handle rect)
      (prog1 (RECT->rect rect)
        (RECT-free rect))))))
