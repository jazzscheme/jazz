;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Comparing Texts
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Alain Marcotte
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.groupware.compare.Compare-Texts jazz


(import (jazz.application)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.groupware)
        (jazz.handler)
        (jazz.io)
        (jazz.markup)
        (jazz.resource)
        (jazz.text)
        (jazz.ui)
        (jazz.view))


(class Compare-Texts extends View

  
  (slot green)
  (slot blue)
  (slot compare-directory)
  (slot compare-directory-cookie)
  
  
  (form
    (<install>                                       size: {Dimension 795 499}
      (<Push-Tool>               name: next-diff     position: {Point 2 7} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Next Diff (F9)" resource: {Image-Resource "NextDiff"} portfolio: :images action-handler: {Event-Handler :form on-next-diff})
      (<Push-Tool>               name: previous-diff position: {Point 24 7} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Previous Diff (Shift+F9)" resource: {Image-Resource "PreviousDiff"} portfolio: :images action-handler: {Event-Handler :form on-previous-diff})
      (<Push-Tool>               name: keep-green    position: {Point 60 7} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Keep Green (F10)" resource: {Image-Resource "KeepGreen"} portfolio: :images action-handler: {Event-Handler :form on-keep-green})
      (<Push-Tool>               name: keep-blue     position: {Point 82 7} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Keep Blue (F11)" resource: {Image-Resource "KeepBlue"} portfolio: :images action-handler: {Event-Handler :form on-keep-blue})
      (<Push-Tool>               name: keep-both     position: {Point 104 7} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Keep Both (F12)" resource: {Image-Resource "KeepBoth"} portfolio: :images action-handler: {Event-Handler :form on-keep-both})
      (<Label-View>              name: green-label   title: "Green:" position: {Point 150 11} size: {Dimension 37 16} font: {Font Label})
      (<Border-View>             name: green-border  position: {Point 190 9} size: {Dimension 250 19} border-type: edge style: entry
        (<Moniker-Widget>        name: green         protocol: file return-press-handler: {Event-Handler :form on-return-press}))
      (<Label-View>              name: blue-label    title: "Blue:" position: {Point 460 11} size: {Dimension 32 16} font: {Font Label})
      (<Border-View>             name: blue-border   position: {Point 493 9} size: {Dimension 250 19} border-type: edge style: entry
        (<Moniker-Widget>        name: blue          protocol: file return-press-handler: {Event-Handler :form on-return-press}))
      (<Push-Tool>               name: compare       position: {Point 770 7} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Compare Files" resource: {Image-Resource "Compare"} portfolio: :images action-handler: {Event-Handler :form on-compare})
      (<Border-View>                                 position: {Point 2 35} size: {Dimension 791 462} border-type: edge
        (<Scroller-View>                             hscroll?: #t vscroll?: #t
          (<!>                   name: content
            (<Compare-Text-View> name: text          left-padding: 12))))))

  
  ;;;
  ;;;; Host
  ;;;
    

  (method meta override (host-icon self)
    {Image-Resource "CompareFiles"})
  
  
  (method meta override (host-title self)
    "Compare Files")
  
  
  (method meta override (host-position self)
    {Point 90 80})
  
  
  (method meta override (host-size self)
    {Dimension 795 500})
  
  
  (method override (host-workspaces self)
    '(groupware))
  
  
  (method override (focus-default self)
    (acquire-focus (locate self 'text)))

  
  ;;;
  ;;;; View
  ;;;
  
  
  (method override (get-text-view self)
    (locate self 'text))

  
  ;;;
  ;;;; Events
  ;;;


  (method override (resize self dh dv)
    (nextmethod self dh dv)
    (move-figure (locate self 'compare) dh 0)
    (size-figure (child (locate self 'text) 3) dh dv)
    (bind (first-dh second-dh) (resize-parts (get-width self) dh 2)
      (size-figure (locate self 'green-border) first-dh 0)
      (move-figure (locate self 'blue-label) first-dh 0)
      (move-figure (locate self 'blue-border) first-dh 0)
      (size-figure (locate self 'blue-border) second-dh 0)))
  
  
  (method (on-compare self evt)
    (with-cursor :wait
      (lambda ()
        (compare-monikers self (require-green self) (require-blue self))
        (acquire-focus (locate self 'text)))))
  
  
  (method override (on-return-press self evt)
    (on-compare self evt))
  
  
  (method (on-next-diff self evt)
    (goto-next-diff (locate self 'text))
    (acquire-focus (locate self 'text)))
  
  
  (method (on-previous-diff self evt)
    (goto-previous-diff (locate self 'text))
    (acquire-focus (locate self 'text)))
  
  
  (method (on-compare-next self evt)
    (compare-next self))
  
  
  (method (on-compare-previous self evt)
    (compare-previous self))
  
  
  (method (on-keep-green-file self evt)
    (keep-green-file self))
  
  
  (method (on-keep-blue-file self evt)
    (keep-blue-file self))
  
  
  (method (on-keep-green self evt)
    (keep-green (locate self 'text))
    (when (get-property evt next?:)
      (goto-next-diff (locate self 'text))))
  
  
  (method (on-keep-blue self evt)
    (keep-blue (locate self 'text))
    (when (get-property evt next?:)
      (goto-next-diff (locate self 'text))))
  
  
  (method (on-keep-both self evt)
    (keep-both (locate self 'text)))

  
  ;;;
  ;;;; Actions
  ;;;
  
  
  (method package virtual (compare-next self)
    (close (get-host self))
    (let ((guest (current-guest)))
      (if (is-not? guest Compare-Trees)
          (bell)
        (compare-next guest))))
  
  
  (method package virtual (compare-previous self)
    (close (get-host self))
    (let ((guest (current-guest)))
      (if (is-not? guest Compare-Trees)
          (bell)
        (compare-previous guest))))
  
  
  (method protected virtual (keep-green-file self)
    (close (get-host self))
    (let ((guest (current-guest)))
      (if (is-not? guest Compare-Trees)
          (bell)
        (copy-right guest)
        (if (not (goto-next-comparable guest))
            (bell)
          (merge-leaves guest)))))
  
  
  (method protected virtual (keep-blue-file self)
    (close (get-host self))
    (let ((guest (current-guest)))
      (if (is-not? guest Compare-Trees)
          (bell)
        (copy-left guest)
        (if (not (goto-next-comparable guest))
            (bell)
          (merge-leaves guest)))))
  
  
  ;;;
  ;;;; Tab
  ;;;
  
  
  (method override (get-tab-stops self)
    (list
      (get-text (locate self 'green))
      (get-text (locate self 'blue))))
  
  
  ;;;
  ;;;; Compare
  ;;;

  
  (method public (compare-monikers self green-moniker blue-moniker (ignore-whitespace?: ignore-whitespace? #f) (compare-directory: compare-directory #f) (compare-directory-cookie: compare-directory-cookie #f))
    (set! self.compare-directory compare-directory)
    (set! self.compare-directory-cookie compare-directory-cookie)
    (let* ((host (get-host self))
           (green (get-name green-moniker))
           (blue (get-name blue-moniker))
           (title (format "{a} - {a}" (host-title (class-of self)) (if (string=? green blue) green (format "{a} / {a}" green blue)))))
      (set-title host title))
    (install-monikers self green-moniker blue-moniker)
    (let ((compare (new-comparer self ignore-whitespace?: ignore-whitespace?))
          (text (locate self 'text)))
      (compare-monikers text compare green-moniker blue-moniker)))
  
  
  (method protected virtual (new-comparer self (ignore-whitespace?: ignore-whitespace? #f))
    (new Text-Comparer ignore-whitespace?: ignore-whitespace?))
  
  
  (method (require-green self)
    (or (get-green self)
        (error "Unable to get green moniker")))
  
  
  (method (require-blue self)
    (or (get-blue self)
        (error "Unable to get blue moniker")))
  
  
  (method package (get-green self)
    (let ((browser (locate self 'green)))
      (if (get-editable? browser)
          (get-moniker browser)
        green)))
  
  
  (method package (get-blue self)
    (let ((browser (locate self 'blue)))
      (if (get-editable? browser)
          (get-moniker browser)
        blue)))
  
  
  (method protected virtual (install-monikers self green-moniker blue-moniker)
    (set-moniker (locate self 'green) green-moniker)
    (set-moniker (locate self 'blue) blue-moniker)
    (set! green green-moniker)
    (set! blue blue-moniker))
  
  
  ;;;
  ;;;; Open
  ;;;
  
  
  (method (on-open-green self evt)
    (open-item green))
  
  
  (method (on-open-blue self evt)
    (open-item blue))
  
  
  (definition (open-item item)
    (let ((appl (get-application)))
      (if (is? item Moniker)
          (edit-document appl item workspace: 'groupware)
        (edit-document appl (get-source item) workspace: 'groupware))))
    
  
  ;;;
  ;;;; Save
  ;;;
    
  
  (method (on-save-compare self evt)
    (save-to-both self)
    (when compare-directory
      (merge-done compare-directory compare-directory-cookie))
    (close (get-host self)))

  
  (method (on-save-compare-as self evt)
    (save-as (locate self 'text)))
  
  
  (method (on-save-to-green self evt)
    (save-to-green self))
  
  
  (method (on-save-to-blue self evt)
    (save-to-blue self))
  
  
  (method (on-save-to-both self evt)
    (save-to-both self))
  
  
  (method (save-to-green self)
    (save-to self green))
  
  
  (method (save-to-blue self)
    (save-to self blue))
  
  
  (method (save-to-both self)
    (save-to self green)
    (save-to self blue)
    (when (and (is? green File) (is? blue File))
      @convert
      (set-modification-time blue (get-last-modification-time green))))
  
  
  (method override (save-to self item)
    (ensure-read/write item)
    (let ((text (locate self 'text)))
      (save-lines item (get-content (get-model text)))))))
