;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Comparing Trees
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Alain Marcotte
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.groupware.compare.Compare-Trees jazz


(import (jazz.action)
        (jazz.application)
        (jazz.feedback)
        (jazz.graphic)
        (jazz.groupware)
        (jazz.handler)
        (jazz.io)
        (jazz.jml)
        (jazz.menu)
        (jazz.preferences)
        (jazz.resource)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.view))


(class Compare-Trees extends Layout-View
  
  
  (property mode                                         accessors explicit)
  (property base-date                                    accessors explicit)
  (property left-moniker       initialize :uninitialized accessors explicit)
  (property right-moniker      initialize :uninitialized accessors explicit)
  (property left-content       initialize #t             accessors generate)
  (property right-content      initialize #t             accessors generate)
  (property ignored-nodes      initialize '()            accessors generate)
  (property ignored-leaves     initialize '()            accessors generate)
  (property ignored-extensions initialize '()            accessors generate)
  (property check-algorithm    initialize #f             accessors explicit)
  
  
  (slot left-root)
  (slot left-base)
  (slot right-root)
  (slot right-base)
  (slot root-entry)
  (slot installed-configuration initialize #f)


  (form
    (<install>                                               size: {Dimension 895 455}
      (<Group-Box>                     name: mode            title: "Mode" position: {Point 5 5} size: {Dimension 210 76} style: styled
        (<content~>
          (<Radio-Button>              name: backup          title: "Backup" position: {Point 13 15} size: {Dimension 60 16})
          (<Radio-Button>              name: merge           title: "Merge" position: {Point 13 35} size: {Dimension 60 16} selected?: #t)
          (<Border-View>                                     position: {Point 81 32} size: {Dimension 111 19} border-type: edge style: entry
            (<Scroller-View>                                 hscroll?: #f vscroll?: #f
              (<!>                     name: content         layout-type: fill
                (<Text-View>           name: base-date       font: {Font Label} accepts-returns?: #f left-padding: 2 top-padding: 2 return-press-handler: {Event-Handler :form on-return-press}))))))
      (<Group-Box>                     name: trees           title: "Trees" position: {Point 222 5} size: {Dimension 375 76} style: styled
        (<content~>
          (<Label-View>                name: green-label     title: "Green:" position: {Point 6 9} size: {Dimension 37 16} font: {Font Label})
          (<Border-View>               name: green-border    position: {Point 46 7} size: {Dimension 320 19} border-type: edge style: entry
            (<Moniker-Widget>          name: green           return-press-handler: {Event-Handler :form on-return-press}))
          (<Label-View>                name: blue-label      title: "Blue:" position: {Point 6 34} size: {Dimension 32 16} font: {Font Label})
          (<Border-View>               name: blue-border     position: {Point 46 32} size: {Dimension 320 19} border-type: edge style: entry
            (<Moniker-Widget>          name: blue            return-press-handler: {Event-Handler :form on-return-press}))))
      (<Group-Box>                     name: check-algorithm title: "Check" position: {Point 603 4} size: {Dimension 111 78} style: styled
        (<content~>
          (<Radio-Button>              name: timestamp       title: "Timestamp" position: {Point 13 5} size: {Dimension 82 16} selected?: #t)
          (<Radio-Button>              name: content         title: "Content" position: {Point 13 25} size: {Dimension 82 16})
          (<Radio-Button>              name: whitespace      title: "Whitespace" position: {Point 13 45} size: {Dimension 82 16})))
      (<Push-Button>                   name: compare         title: "Compare" position: {Point 810 7} size: {Dimension 80 24} action-handler: {Event-Handler :form on-compare} default?: #t)
      (<Push-Tool>                     name: copy-right      position: {Point 723 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Mark Left -> Right" resource: {Image-Resource "CopyRight"} portfolio: :images action-handler: {Event-Handler :form on-copy-right})
      (<Push-Tool>                     name: merge-leaves    position: {Point 745 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Merge Files" resource: {Image-Resource "Merge"} portfolio: :images action-handler: {Event-Handler :form on-merge-leaves})
      (<Push-Tool>                     name: copy-left       position: {Point 767 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Mark Left <- Right" resource: {Image-Resource "CopyLeft"} portfolio: :images action-handler: {Event-Handler :form on-copy-left})
      (<Push-Tool>                     name: no-action       position: {Point 789 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "No Action" resource: {Image-Resource "Empty"} portfolio: :images action-handler: {Event-Handler :form on-no-action})
      (<Push-Tool>                     name: transfer        position: {Point 869 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Transfer" resource: {Image-Resource "Execute"} portfolio: :images action-handler: {Event-Handler :form on-transfer})
      (<Border-View>                   name: results-border  position: {Point 2 90} size: {Dimension 891 363} border-type: edge style: outline
        (<Scroller-View>                                     style: document vscroll?: #t
          (<!>                         name: content         layout-type: fill
            (<Tree-Header>                                   style: document
              (<!>                     name: content
                (<Tree-View>           name: results         filled-column: name multiple-selection?: #t double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} context-menu-handler: {Event-Handler :form on-context-menu} selection-mode: row portfolio: :images
                  (<Tree-Node-Column>  name: name            title: "Name" width: 235)
                  (<Tree-Label-Column> name: left-value      title: "Left Value" width: 200)
                  (<Tree-Label-Column> name: right-value     title: "Right Value" width: 200)
                  (<Left-Column>                             title: "Left" width: 80 justification: center)
                  (<Action-Column>                           title: "Action" width: 75 justification: center)
                  (<Right-Column>                            title: "Right" width: 80 justification: center)))))))))

  
  ;;;
  ;;;; Constants
  ;;;
  
  
  (constant $Left-Value-Column  1)
  (constant $Right-Value-Column 2)
  (constant $Left-Column        3)
  (constant $Action-Column      4)
  (constant $Right-Column       5)
  
  
  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method override (install rest)
    (nextmethod rest)
    (load-session))
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method override (host-workspaces)
    '(groupware))
  
  
  (method override (focus-default)
    (acquire-focus~ (locate 'results)))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions)
    (cons (find-actions 'compare-trees)
          (nextmethod)))

  
  ;;;
  ;;;; Events
  ;;;


  (method override (resize dh dv)
    (nextmethod dh dv)
    (size-figure~ (locate 'trees) dh 0)
    (move-figure~ (locate 'check-algorithm) dh 0)
    (size-figure~ (locate 'green-border) dh 0)
    (size-figure~ (locate 'blue-border) dh 0)
    (move-figure~ (locate 'compare) dh 0)
    (move-figure~ (locate 'copy-right) dh 0)
    (move-figure~ (locate 'merge-leaves) dh 0)
    (move-figure~ (locate 'copy-left) dh 0)
    (move-figure~ (locate 'no-action) dh 0)
    @wait
    (move-figure~ (locate 'build-updater) dh 0)
    (move-figure~ (locate 'transfer) dh 0)
    (size-figure~ (locate 'results-border) dh dv)
    (invalidate-header~ (locate 'results)))
  
  
  (method (on-copy-right evt)
    (copy-right)
    (when (get-property~ evt next?:)
      (unless (goto-next-comparable)
        (bell))))
  
  
  (method protected (copy-right)
    (let ((tree (locate 'results)))
      (with-update-locked~ tree
        (lambda ()
          (for-each (lambda (n)
                      (let ((entry (get-user-data~ (get-row~ tree n))))
                        (unless (and (eq? (get-kind~ entry) 'node) (get-left~ entry) (get-right~ entry))
                          (set-action~ entry 'copy-right)
                          (let ((cell (new Cell n $Action-Column)))
                            (invalidate-cell~ tree cell)))))
                    (get-selection~ tree))))))
  
  
  (method (on-copy-left evt)
    (copy-left)
    (when (get-property~ evt next?:)
      (unless (goto-next-comparable)
        (bell))))
  
  
  (method protected (copy-left)
    (let ((tree (locate 'results)))
      (with-update-locked~ tree
        (lambda ()
          (for-each (lambda (n)
                      (let ((entry (get-user-data~ (get-row~ tree n))))
                        (unless (and (eq? (get-kind~ entry) 'node) (get-left~ entry) (get-right~ entry))
                          (let ((cell (new Cell n $Action-Column)))
                            (set-action~ entry 'copy-left)
                            (invalidate-cell~ tree cell)))))
                    (get-selection~ tree))))))
  
  
  (method (on-merge-leaves evt)
    (merge-leaves))
  
  
  (method protected (merge-leaves)
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree)))
      (when (not-null? selection)
        (if (> (length selection) 1)
            (bell)
          (let ((entry (get-user-data~ (get-row~ tree (car selection)))))
            (when (and (eq? (get-kind~ entry) 'leaf) (get-left~ entry) (get-right~ entry))
              (compare-leaves (get-left~ entry) (get-right~ entry) entry)))))))
  
  
  (method (on-no-action evt)
    (let ((tree (locate 'results)))
      (with-update-locked~ tree
        (lambda ()
          (for-each (lambda (n)
                      (let ((entry (get-user-data~ (get-row~ tree n))))
                        (unless (and (eq? (get-kind~ entry) 'node) (get-left~ entry) (get-right~ entry))
                          (set-action~ entry #f)
                          (let ((cell (new Cell n $Action-Column)))
                            (invalidate-cell~ tree cell)))))
                    (get-selection~ tree))))))
  
  
  (method (on-compare-next evt)
    (compare-next))
  
  
  (method (on-compare-previous evt)
    (compare-previous))
  
  
  (method protected (compare-next)
    (if (not (goto-next-comparable))
        (bell)
      (merge-leaves)))
  
  
  (method protected (compare-previous)
    (if (not (goto-previous-comparable))
        (bell)
      (merge-leaves)))
  
  
  @old-code
  (method (on-build-updater evt)
    (let ((file (choose-updater)))
      (build-updater file)
      (updater-built file)))
  
  
  (method protected virtual (choose-updater)
    (choose-new-file))
  
  
  (method (on-transfer evt)
    (transfer evt))
  
  
  (method protected virtual (transfer evt)
    (let* ((tree (locate 'results))
           (count (get-visible-count~ tree))
           (n 0))
      (with-update-locked~ tree
        (lambda ()
          (while (< n count)
            (let* ((row (get-row~ tree n))
                   (entry (get-user-data~ row))
                   (entry-father (get-user-data~ (get-father~ row)))
                   (left (get-left~ entry))
                   (right (get-right~ entry))
                   (action (get-action~ entry)))
              (when (memq? action '(copy-right copy-left))
                (case action
                  ((copy-right)
                   (if right
                       (if left
                           (copy-item left right)
                         (delete-item right))
                     (let ((new-right (make-item left right-root (get-right~ entry-father))))
                       (set-right~ entry new-right)
                       (add-item left new-right))))
                  ((copy-left)
                   (if left
                       (if right
                           (copy-item right left)
                         (delete-item left))
                     (let ((new-left (make-item right left-root (get-left~ entry-father))))
                       (set-left~ entry new-left)
                       (add-item right new-left)))))
                ;; When copying nodes, treatment is a bit different because they stay showed...
                (if (and (eq? (get-kind~ entry) 'node)
                         (or (and (eq? action 'copy-right) left (not right))
                             (and (eq? action 'copy-left) right (not left))))
                    (begin
                      (case action
                        ((copy-right)
                         (let ((cell (new Cell n $Left-Column)))
                           (invalidate-cell~ tree cell)))
                        ((copy-left)
                         (let ((cell (new Cell n $Right-Column)))
                           (invalidate-cell~ tree cell))))
                      (set-action~ entry #f)
                      (let ((cell (new Cell n $Action-Column)))
                        (invalidate-cell~ tree cell)))
                  (remove-row~ tree row)
                  (decrease! count)
                  (decrease! n))))
            (increase! n))))))
  
  
  (method (on-double-click evt)
    (let ((sender (get-sender~ evt)))
      (case (get-name~ sender)
        ((results)
          (on-merge-leaves evt)))))
  
  
  (method override (on-return-press evt)
    (let ((sender (get-sender~ evt)))
      (case (get-name~ sender)
        ((results)
          (on-merge-leaves evt))
        (else
          (on-compare evt)))))
  
  
  (method protected virtual (on-compare evt)
    (with-cursor :wait
      (lambda ()
        (compare-trees))))
    
  
  (method (on-context-menu evt)
    (let* ((sender (get-sender~ evt))
           (pos (get-position~ evt))
           (acquired (acquire sender pos)))
      (with-context-click~ sender pos
        (lambda ()
          (track-popup-menu (get-compare-menu) acquired)))))
  
  
  (method (on-next-comparable evt)
    (unless (goto-next-comparable)
      (bell)))
  
  
  (method (on-previous-comparable evt)
    (unless (goto-previous-comparable)
      (bell)))
  
  
  (method (on-next-uncomparable evt)
    (unless (goto-next-uncomparable)
      (bell)))
  
  
  (method (on-previous-uncomparable evt)
    (unless (goto-previous-uncomparable)
      (bell)))
  

  ;;;
  ;;;; Tab
  ;;;
  
  
  (method override (get-tab-stops)
    (list
      (locate 'base-date)
      (locate~ (locate 'green) 'body)
      (locate~ (locate 'blue) 'body)))
  

  ;;;
  ;;;; Session
  ;;;
  
  
  (definition (load-session)
    (let ((pref (get-preferences '(tools compare-directories))))
      (set! Base-Dates (get-base-dates~ pref))))
  
  
  (method override (save-guest designer session)
    (add-guest-preferences designer session
      mode:          (get-mode)
      base-date:     (get-base-date)
      left-moniker:  (get-left-moniker)
      right-moniker: (get-right-moniker)
      base-dates:    Base-Dates))
  

  ;;;
  ;;;; Configure
  ;;;
  
  
  (definition Base-Dates
    '())
  
  
  (method package (on-configure evt)
    (install-configuration (get-property~ evt configuration:)))
  
  
  (method package (on-register-base-date evt)
    (unimplemented)
    @convert-date
    (if (not installed-configuration)
        (error "You need an installed configuration to register a base date")
      (bind (name mode) installed-configuration
        (if (/= mode 'merge)
            (error "Base dates can only be registered in merge mode")
          (let ((date (present~ (universal-date)))
                (pair (assq name Base-Dates)))
            (if (not pair)
                (set! Base-Dates (cons (cons name date) Base-Dates))
              (set-cdr! pair date))
            (set-base-date date))))))
  
  
  (method package (on-reset-actions evt)
    (reset-actions))
  
  
  (method public (configure name)
    (install-configuration (child~ (get-preferences '(tools compare-directories)) name)))
  
  
  (method public (install-configuration config)
    (define (find-base-date name)
      (assq-value name Base-Dates #f))
    
    (define (install-monikers)
      (let ((left (get-left~ config))
            (right (get-right~ config)))
        (set-protocol~ (locate 'green) (determine-protocol left))
        (set-left-moniker left)
        (set-protocol~ (locate 'blue) (determine-protocol right))
        (set-right-moniker right)))
    
    (define (determine-protocol moniker)
      (cond ((is? moniker Directory-Group)
             'group)
            ((is? moniker Manifest)
             'manifest)
            (else
             'dir)))
    
    (set! installed-configuration config)
    (set-mode (get-mode~ config))
    (set-base-date (and (eq? (get-mode~ config) 'merge) (find-base-date (get-name~ config))))
    (install-monikers)
    (set-ignored-nodes (get-ignored-nodes~ config))
    (set-ignored-leaves (get-ignored-leaves~ config))
    (set-ignored-extensions (get-ignored-extensions~ config))
    (set-check-algorithm (get-check-algorithm~ config))
    (remove-every-row~ (locate 'results)))
  
  
  (method (on-open-green evt)
    (let* ((tree (locate 'results))
           (selection (get-single-selection~ tree)))
      (when selection
        (let ((entry (get-user-data~ (get-row~ tree selection))))
          (if (or (eq? (get-kind~ entry) 'node) (not (get-left~ entry)))
              (bell)
            (let ((appl (get-application)))
              (edit-document~ appl (get-left~ entry) workspace: 'groupware)))))))
  
  
  (method (on-open-blue evt)
    (let* ((tree (locate 'results))
           (selection (get-single-selection~ tree)))
      (when selection
        (let ((entry (get-user-data~ (get-row~ tree selection))))
          (if (or (eq? (get-kind~ entry) 'node) (not (get-right~ entry)))
              (bell)
            (let ((appl (get-application)))
              (edit-document~ appl (get-right~ entry) workspace: 'groupware)))))))
  
  
  ;;;
  ;;;; Interface
  ;;;
  
  
  (method (get-mode)
    (get-selected-element~ (get-content~ (locate 'mode))))
  
  
  (method (set-mode mode)
    (set-selected-element~ (get-content~ (locate 'mode)) mode))
  
  
  (method (get-check-algorithm)
    (get-selected-element~ (get-content~ (locate 'check-algorithm))))
  
  
  (method (set-check-algorithm algorithm)
    (set-selected-element~ (get-content~ (locate 'check-algorithm)) (or algorithm 'timestamp)))
  
  
  (method (get-base-date)
    #f
    @convert-date
    (let ((text (get-string-content~ (locate 'base-date))))
      (if (empty-string? text)
          #f
        (parse-string~ Date text))))
  
  
  (method (set-base-date date)
    @convert-date
    (let ((text (cond ((not date) "") ((is? date Date) (present~ date)) (else date))))
      (set-string-content~ (locate 'base-date) text)))
  
  
  (method protected virtual (get-left-moniker)
    (let ((browser (locate 'green)))
      (if (not (get-editable?~ browser))
          left-moniker
        (set! left-moniker (get-moniker~ browser))
        left-moniker)))
  
  
  (method protected virtual (set-left-moniker moniker)
    (set! left-moniker moniker)
    (set-moniker~ (locate 'green) moniker))
  
  
  (method protected virtual (get-right-moniker)
    (let ((browser (locate 'blue)))
      (if (not (get-editable?~ browser))
          right-moniker
        (set! right-moniker (get-moniker~ browser))
        right-moniker)))
  
  
  (method protected virtual (set-right-moniker moniker)
    (set! right-moniker moniker)
    (set-moniker~ (locate 'blue) moniker))
  
  
  (method override (select-all . rest)
    (select-all~ (locate 'results) user-origin: #t))
  
  
  ;;;
  ;;;; Compare
  ;;;
  
  
  (method protected virtual (new-comparer)
    (new Tree-Comparer))
  
  
  (method protected virtual (get-trees)
    (define (require-left-moniker)
      (or (get-left-moniker)
          (error "Unable to get green moniker")))
    
    (define (require-right-moniker)
      (or (get-right-moniker)
          (error "Unable to get blue moniker")))
    
    (values (moniker->tree (require-left-moniker))
            (moniker->tree (require-right-moniker))))
  
  
  (method protected virtual (moniker->tree moniker)
    moniker)
  
  
  (method public (compare-trees)
    (define (display-results entry)
      (let* ((mode (get-mode))
             (date (get-merge-date mode))
             (tree (locate 'results)))
        (set! root-entry entry)
        (remove-every-row~ tree)
        (with-update-locked~ tree
          (lambda ()
            (display-entry mode date tree #f entry 0)
            (set-selection~ tree '())))))
    
    (define (display-entry mode date tree father entry level)
      (let* ((kind (get-kind~ entry))
             (left (get-left~ entry))
             (right (get-right~ entry))
             (action (get-action~ entry))
             (ref (or left right))
             (lvl? (and (eq? kind 'node) left right))
             (name (if (< level 2) (present-root left right) (present-item ref)))
             (image (entry-image entry ref))
             (c0 (new Tree-Node title: name image: image))
             (c1 (new Tree-Label title: (present-value kind (get-left-value~ entry))))
             (c2 (new Tree-Label title: (present-value kind (get-right-value~ entry))))
             (c3 entry)
             (c4 entry)
             (c5 entry)
             (row (add-row~ tree father: father state: (if lvl? 'collapsed 'expanded) children: (list c0 c1 c2 c3 c4 c5) user-data: entry)))
        (when (not lvl?)
          (setup-action mode date entry)
          (ensure-expanded~ tree row))
        (for-each (lambda (child)
                    (display-entry mode date tree row child (+ level 1)))
                  (get-children~ entry))))
    
    (receive (left-tree right-tree) (get-trees)
      (set! left-root  left-tree)
      (set! right-root right-tree)
      (set! left-base  (tree-base left-tree))
      (set! right-base (tree-base right-tree))
      (let ((comparer (new-comparer)))
        (receive (entry has-differences? scanned-values)
            (compare-nodes~ comparer #f left-tree right-tree
              left-content: left-content
              right-content: right-content
              ignored-nodes: ignored-nodes
              ignored-leaves: ignored-leaves
              ignored-extensions: ignored-extensions
              check-algorithm: (get-check-algorithm)
              feedback: (lambda (left right) (scan-feedback left right)))
          (display-results entry)
          (user-feedback "Done")
          (and has-differences? scanned-values)))))
  
  
  (method protected virtual (scan-feedback left right)
    (user-feedback "Scanning {a}..." left))
  
  
  ;;;
  ;;;; Results
  ;;;
  
  
  (method (setup-action mode base-date entry)
    (define (setup-backup base-date entry)
      (let ((left        (get-left~ entry))
            (left-value  (get-left-value~ entry))
            (right       (get-right~ entry))
            (right-value (get-right-value~ entry)))
        (set-action~ entry (backup-action base-date left left-value right right-value))))
    
    (define (setup-merge base-date entry)
      (when (eq? (get-kind~ entry) 'leaf)
        (let ((left        (get-left~ entry))
              (left-value  (get-left-value~ entry))
              (right       (get-right~ entry))
              (right-value (get-right-value~ entry)))
          (set-action~ entry (merge-action base-date left left-value right right-value)))))
    
    (case mode
      ((backup) (setup-backup base-date entry))
      ((merge) (setup-merge base-date entry))))
  
  
  (method (reset-actions)
    (let* ((mode (get-mode))
           (base-date (get-merge-date mode))
           (tree (locate 'results))
           (comparer self)
           (proc (lambda (row)
                   (let ((entry (get-user-data~ row)))
                     (when (is? entry Compare-Entry)
                       (setup-action~ comparer mode base-date entry))))))
      (for-each (lambda (n)
                  (let ((row (get-row~ tree n)))
                    (proc row)))
                (get-selection~ tree))
      (invalidate-view~ tree)))
  
  
  (method protected virtual (backup-action base-date left left-value right right-value)
    'copy-right)
  
  
  (method protected virtual (merge-action base-date left left-value right right-value)
    #f)
  
  
  (method protected virtual (get-merge-date mode)
    (when (eq? mode 'merge)
      (let ((date (get-base-date)))
        (when date
          (get-time~ date)))))

  
  ;;;
  ;;;; Actions
  ;;;
  
  
  (method protected virtual (add-item src dst)
    (copy-item src dst))
  
  
  (method protected virtual (copy-item src dst)
    (error "Unable to copy {t} to {t}" src dst))

  
  (method protected virtual (delete-item item)
    (error "Unable to delete {t}" item))
  
  
  (method protected virtual (compare-leaves left right user-data)
    )
  
  
  (method public (merge-done user-data)
    (let* ((tree (locate 'results))
           (n (user-data-index~ tree user-data)))
      (when n
        (let ((row (get-row~ tree n)))
          (remove-row~ tree row))
        (goto-next-comparable n))))
  
  
  (method protected (goto-next-comparable (rank #f))
    (goto-next comparable? rank))
  
  
  (method protected (goto-previous-comparable (rank #f))
    (goto-previous comparable? rank))
  
  
  (method protected (goto-next-uncomparable (rank #f))
    (goto-next uncomparable? rank))
  
  
  (method protected (goto-previous-uncomparable (rank #f))
    (goto-previous uncomparable? rank))
  
  
  (method protected (goto-next predicate (rank #f))
    (define (find-next tree rank predicate)
      (continuation-capture
        (lambda (return)
          (loop (for n from rank below (get-visible-count~ tree))
                (do (let* ((row (get-row~ tree n))
                           (entry (get-user-data~ row)))
                      (when (predicate entry)
                        (continuation-return return n)))))
          #f)))
    
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree))
           (rank (or rank (if (null? selection) 0 (+ (apply max selection) 1))))
           (next (find-next tree rank predicate)))
      (when next
        (set-selection~ tree (list next))
        next)))
  
  
  (method protected (goto-previous predicate (rank #f))
    (define (find-previous tree rank predicate)
      (continuation-capture
        (lambda (return)
          (loop (for n from rank downto 0)
                (do (let* ((row (get-row~ tree n))
                           (entry (get-user-data~ row)))
                      (when (predicate entry)
                        (continuation-return return n)))))
          #f)))
    
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree))
           (rank (or rank (if (null? selection) (- (get-visible-count~ tree) 1) (- (apply min selection) 1))))
           (previous (find-previous tree rank predicate)))
      (when previous
        (set-selection~ tree (list previous))
        previous)))
  
  
  (definition (comparable? entry)
    (and (eq? (get-kind~ entry) 'leaf) (get-left~ entry) (get-right~ entry)))
  
  
  (definition (uncomparable? entry)
    (xor (get-left~ entry) (get-right~ entry)))
  
  
  ;;;
  ;;;; Utilities
  ;;;
  
  
  (method protected virtual (make-item item root father)
    item)
  
  
  (method protected virtual (tree-base tree)
    #f)
  
  
  (method protected virtual (trim-left item)
    item)
  
  
  (method protected virtual (trim-right item)
    item)
  
  
  (method protected virtual (present-root left right)
    (present-item (or left right)))
  
  
  (method protected virtual (present-item item)
    )
  
  
  (method protected virtual (present-value kind value)
    )
  
  
  (method protected virtual (entry-image entry ref)
    (case (get-kind~ entry)
      ((node) {Image-Resource "TypeFolder"})
      ((leaf) {Image-Resource "TypeFile"})))
  
  
  (method protected virtual (action-image action left right)
    (case action
      ((copy-right)
       (cond ((not left) {Image-Resource "DeleteRight"})
             ((not right) {Image-Resource "AddRight"})
             (else {Image-Resource "CopyRight"})))
      ((merge)
       {Image-Resource "Merge"})
      ((copy-left)
       (cond ((not left) {Image-Resource "AddLeft"})
             ((not right) {Image-Resource "DeleteLeft"})
             (else {Image-Resource "CopyLeft"})))))
  
  
  (method protected virtual (get-compare-menu)
    (new Compare-Menu)))


;;;
;;;; Item-Column
;;;


(class Item-Column extends Tree-Column
  
  
  (method override (draw-data surface context row data width height)
    (draw-background surface context row data width height)
    (let ((lvl? (and (eq? (get-kind~ data) 'node) (get-left~ data) (get-right~ data))))
      (when (and (not lvl?) (present? data))
        (let ((image (present-image data)))
          (draw-image~ (get-images-portfolio~ (get-application)) surface context image (quotient (- width 16) 2) 0)))))
  
  
  (method abstract protected virtual (present? data)
    )
  
  
  (method abstract protected virtual (present-image data)
    ))


;;;
;;;; Left-Column
;;;


(class Left-Column extends Item-Column
  
  
  (method override (present? data)
    (get-left~ data))
  
  
  (method override (present-image data)
    {Image-Resource "Green"}))


;;;
;;;; Action-Column
;;;


(class Action-Column extends Tree-Column
  
  
  (method override (draw-data surface context row data width height)
    (draw-background surface context row data width height)
    (let ((action (get-action~ data)))
      (when action
        (let ((appl (get-application))
              (image (action-image~ creator action (get-left~ data) (get-right~ data))))
          (draw-image~ (get-images-portfolio~ appl) surface context image (+ (quotient (- width 16) 2) 1) 0))))))


;;;
;;;; Right-Column
;;;


(class Right-Column extends Item-Column
  
  
  (method override (present? data)
    (get-right~ data))
  
  
  (method override (present-image data)
    {Image-Resource "Blue"}))


;;;
;;;; Compare-Menu
;;;


(class Compare-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item>                                title: "Configure" icon: {Image-Resource "All"}
        (<Configure-Menu>))
      (<Separator-Item>   name: action-group)
      @wait
      (<Label-Item>       name: register-base-date title: "Register Base Date" action-handler: {Event-Handler :document on-register-base-date})
      (<Label-Item>       name: reset-actions      title: "Reset Actions" action-handler: {Event-Handler :document on-reset-actions})
      (<Separator-Item>   name: open-group)
      (<Label-Item>       name: open-green         title: "Open Green" icon: {Image-Resource "Green"} action-handler: {Event-Handler :document on-open-green})
      (<Label-Item>       name: open-blue          title: "Open Blue" icon: {Image-Resource "Blue"} action-handler: {Event-Handler :document on-open-blue}))))


;;;
;;;; Configure-Menu
;;;


(class Configure-Menu extends Context-Menu
  
  
  (method override (finish rest)
    (nextmethod rest)
    (update-menu))
  
  
  (method public (update-menu)
    (for-each (lambda (config)
                (let ((title (get-presentation-property~ config)))
                  (new Label-Item parent: self title: title action-handler: (new Event-Handler target-name: :document method-name: 'on-configure properties: (list configuration: config)))))
              (get-children~ (get-preferences '(tools compare-directories)))))))
