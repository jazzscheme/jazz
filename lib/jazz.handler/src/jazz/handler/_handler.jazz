;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Handlers
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.handler jazz


(import (jazz.action)
        (jazz.component)
        (jazz.event)
        (jazz.reference))


;;;
;;;; Notify
;;;


(definition public (notify-handler component handler sender properties)
  (typecase handler
    ((Procedure)
     (handler (apply make-event Event :action sender (or properties '()))))
    ((Handler)
     (let ((properties (or properties (get-properties handler))))
       (let ((event (apply make-event Event :action sender properties)))
         (invoke handler component event))))
    ((Action)
     (call-action handler sender properties))
    (else
     (error "Unknown handler type: {s}" handler))))


(definition public (invoke-handler component handler event)
  (typecase handler
    ((Procedure)
     (handler event))
    ((Handler)
     (invoke handler component event))
    ((Action)
     (call-action handler component #f event: event))
    (else
     (error "Unknown handler type: {s}" handler))))


;;;
;;;; Handler
;;;


(class Handler extends Object
  
  
  (slot execute     getter generate)
  (slot target      getter generate)
  (slot target-name getter generate)
  (slot method-name getter generate)
  (slot form        getter generate)
  
  
  (method override (initialize self (execute: execute #f) (target: target #f) (target-name: target-name #f) (method-name: method-name #f))
    (nextmethod self)
    (set! self.execute execute)
    (set! self.target target)
    (set! self.target-name target-name)
    (set! self.method-name method-name))
  
  
  (method override (print self output readably)
    (format output "猃簖簖ㄣ狒彗矧钺礤ㄣ灬篌镦箦戽┅翎蜱弭钺礤礤翳镤钺礤┅换换换蔑铘屮换礤翳镤秭弪蜷溴麽祀蝈驽蝈钽弩箦戽痱镢麒孱ㄥ窨翎蜱弭钺礤候彐弪孱沐痱镢礤翳镤钺礤┅礤翳镤秭弪蜷溴箦趱瓠泔铘屮箦戽泔铘屮舂箦簟骘蝽泔铘屮舂换换换深鲲脲换礤翳镤瘐忪殂鲩螋踽ㄩ铞镫箦戽箦钿弪蝈篝ㄩ屮邈豸ㄥ邈豸濠戾舄è翎蜱弭矧翎蜱弭ㄦ轭洵翎蜱弭箦戽箦钿弪┅痱镢ㄤ轶疳翥ㄣ灬篌镦翎蜱弭礤翳镤钺礤┅ㄡ痧禊痱镢翎蜱弭蝈篝┅┅礤翳镤瘐忪殂ㄦ轭洵翎蜱弭箦戽箦钿弪ㄤ彐轭ㄦ轭洵骘蝽泔眇镱孱舂ㄣ镱è铒泔眇镱孱舂ｆè礤眈骘蝽ㄧ弭沆狍蟓骘蝽ㄣ灬篌镦泔眇镱孱舂┅泔眇镱孱舂ㄥ祗ㄦ轭洵骘蝽ㄧ弭疳蝈铘泔眇镱孱舂┅┅ㄩㄩ蟓铒艨箦钿弪蔑眇镱孱舂蝈箫祧瀛铋汶钺礤翎蜱弭钺礤ㄣ镱è羼翎蜱弭钺礤烘矧愆ㄦ轭洵骘蝽箦钿弪┅è犷疳轵翎蜱弭钺礤ㄥ窨ㄣ狎翎蜱弭钺礤烘矧愆ㄣ栝熹犰獒ㄦ轭洵骘蝽箦钿弪ㄣ潋翎蜱弭钺礤┅ㄥ祗ㄣ栝熹犰獒箦钿弪翎蜱弭钺礤┅┅┅换换换碰孱舡柔钿戾换ㄤ彐轭轸轱疳汶徵麽祀弼孱舡栳钿戾狎珲礤铘痱镢麒孱ㄥ窨箫躜沐泔溴ㄣ狎狎珲礤铘螬候彐弪孱沐痱镢箫躜沐泔溴ㄣ徜狎珲礤铘螬┅┅ㄤ彐轭轸轱疳汶徵ㄣ镱篝蝓泗弼孱舡栳钿戾翎蜱弭钺礤礤翳镤钺礤痱镳弪糸弩铄碰孱舡柔钿戾翎蜱弭钺礤翎蜱弭钺礤礤翳镤钺礤礤翳镤钺礤痱镳弪糸弩痱镳弪糸弩┅ㄣ灬篌碰孱舡柔钿戾屮翦钿柔钿戾礤翳镤礤翎秭弪蜷溴磲蝮栳祆镡赍泗箦戽镡戛箦蜷犰辁瀛扉翦蜥镡戛礤翳镤礤翎秭弪蜷溴躅磲蝮栳祆镡赍泗箦戽泔铘孱舂ㄤ弩弪獒扉瀛扉翦蜥泔铘孱舂箪雉痱镳弪糸弩珏趑弪珏铄蜥翦礤翳镤秭弪蜷溴ㄩ铋糸犰辁箦戽蝈篝ㄢ轭洵脲黠蜾è痱镳弪糸弩Ж┅雉桢蝮蝈篝ㄡ痧禊铄繇弭栾箦戽雉桢蝮箦簟箦戽痱镳弪糸弩痱镳弪糸弩┅礤翳镤秭弪蜷溴痱轭箦戽秕麴豸蝈徜徕禊ㄦ矧磲秕麴豸猃簖簖猃ㄣ狒彗矧钺礤ㄣ灬篌镦箦戽┅矧翎蜱弭翎蜱弭钺礤礤翳镤钺礤ㄩ铛祆痱镳弪糸弩ㄦ矧磲溴翎殪候遽溴螨痱镳弪糸弩┅┅换换换零沐篌换礤翳镤瘐忪殂ㄧ弭痱镳弪豉箦戽脲黠蜾ㄧ弭痱镳弪糸弩脲黠蜾┅换换换深鲲脲换换义沐轹轭犷镳糸镱犰徙糸镱轶翦眇矧狎箫祯糸镱躅糸换忾沆遽铛忮赭邋弼孱栳钿戾蝮犷徙糸镱螽礤翳镤秭弪蜷溴ㄩ铞镫箦戽箦钿弪弼孱ㄡ泗轱詈徙糸镱ｆ┅ㄣ镱ㄥ邈豸ㄥ邈豸弼孱舂è羼翎蜱弭钺礤候彐弪孱沐戾è栳钿戾祜汜翦蝈驽蝈钽骘蝽礤翳镤钺礤┅ㄨ犷潇弪弼孱舂┅ㄥ祗戾è翎蜱弭矧翎蜱弭ㄦ轭洵翎蜱弭箦戽箦钿弪┅┅ㄩ翎蜱弭ㄩ铞镫瀛翎蜱弭箦戽翎蜱弭弼孱徙糸镱徙糸镱ㄥ蝌矧⒄钺忪麸骈钿弼孱栳钿戾翎蜱弭酏翎蜱弭钺礤┅┅┅礤翳镤瘐忪殂ㄩ铞镫瀛翎蜱弭箦戽翎蜱弭弼孱ㄡ泗轱詈徙糸镱ｆ┅戾è痱镢ㄤ轶疳翥ㄣ灬篌镦翎蜱弭礤翳镤钺礤┅ㄣ镱è铒痱镢ㄥ蝌矧⒄钺忪麸骈钿弼孱栳钿戾礤翳镤酏礤翳镤钺礤┅è轶翎蜱弭碰孱舡蔑铙蹴弪ㄨ镲氕徙糸镱箦戽弼孱灬礅溽ī戾è泔铙蹴弪ㄧ弭弼孱舡泔铙蹴弪翎蜱弭┅ㄤ屐轹弪弼孱泔铙蹴弪箦戽翎蜱弭痱镢弼孱舂┅┅ㄥ祗ㄨ镲氕徙糸镱箦戽弼孱灬礅溽ī痱镢翎蜱弭弼孱舂┅┅┅换换换柔钿戾颦义驽蝈钽换ㄤ彐轭轸轱疳汶徵麽祀栳钿戾颦蝈驽蝈钽狎珲礤铘痱镢痱镢箫躜沐泔溴ㄣ狎狎珲礤铘螬┅ㄤ彐轭轸轱疳汶徵ㄣ镱篝蝓泗栳钿戾颦蝈驽蝈钽簌礅镬疳蜥礤翦蝮铄柔钿戾颦义驽蝈钽簌礅镬疳蜥礤翦蝮┅ㄣ灬篌柔钿戾颦义驽蝈钽屮翦钿义驽蝈钽箪雉疳蜥礤翦蝮珏趑弪珏铄蜥翦礤翳镤秭弪蜷溴ㄩ铋糸犰辁箦戽簌礅镬疳蜥礤翦蝮铄繇弭栾箦戽簌礅镬箦簟箦戽疳蜥礤翦蝮疳蜥礤翦蝮┅礤翳镤秭弪蜷溴痱轭箦戽秕麴豸蝈徜徕禊ㄦ矧磲秕麴豸猃猃亢忑}"
            (category-name (class-of self))
            symbol
            (not-null? parameters)
            parameters))
  
  
  (method override (parameters-resolve self)
    @cant-find
    (generate (reference-resolve self) parameters)))


;;;
;;;; Listener
;;;


(definition public (add-listener listener lst)
  (cons listener lst))


(definition public (remove-listener listener lst)
  (if (is? listener Handler)
      (remove! listener lst)
    (remove! listener lst test: listener-target?)))


(definition public (listener? object lst)
  (member? object lst test: listener-target?))


(definition (listener-target? object listener)
  (eq? object (get-target listener))))
