;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Activity Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ide.activity.Activity-Manager jazz


(import (jazz.application)
        (jazz.graphic)
        (jazz.ide)
        (jazz.ide.activity)
        (jazz.markup)
        (jazz.view)
        (jazz.window))


(class undocumented Activity-Manager extends Palette
  
  
  (form
    (<install>                            title: "Activity Manager" position: {Point 787 917} size: {Dimension 247 57}
      (<!>                   name: root   layout-type: fill
        (<Activities-Border> name: border))))


  @windows-specific
  (method (window-style rest)
    (bitwise-ior WS_POPUP WS_CLIPCHILDREN))


  (method override (show self)
    (set-visible-flag self #t)
    (when application-active?
      (show-noactivate self)))
  
  
  (method override (mouse-activate? self)
    #f)


  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method override (close-window self)
    (hide self))
  
  
  (method override (destroy self)
    (destroy-manager)
    (nextmethod self))


  ;;;
  ;;;; Events
  ;;;
  
  
  (method public (close-manager self)
    (unless (not-null? (get-reporters self))
      (close self)))


  ;;;
  ;;;; Visibility
  ;;;
  
  
  (method override (toggle-visible? self)
    (let ((visible? (not (get-visible? self))))
      @wait
      (when visible?
        (let* ((reporters (get-reporters))
               (delay (/ 0.005 (if (null? reporters) 1 (length reporters)))))
          (set-effect root (new Wipe-Effect direction: 'up delay: delay))))
      (set-visible? self visible?)))


  ;;;
  ;;;; Registration
  ;;;
  
  
  (method public (register-activity self)
    (let* ((size (get-size self))
           (reporters (get-reporters self))
           (rank (if (null? reporters) 0 (+ (max-reporter-rank reporters) 1)))
           (border (locate self 'border))
           (reporter (new Activity-Reporter parent: border rank: rank))
           (height (get-height (get-size reporter))))
      (set-size reporter (new Dimension (- (get-width size) 14) height))
      (update-title self)
      (update-rectangle self)
      reporter))
  
  
  (method public (unregister-activity self reporter)
    (close reporter)
    (update-title self)
    (update-rectangle self))


  ;;;
  ;;;; Update
  ;;;
  
  
  (method package (update-title self)
    (let* ((n (count-if (lambda (reporter) (not (get-done reporter))) (get-reporters self)))
           (text (if (= n 0)
                     "No registered activities"
                   (format
                           "{a} activit{a} registered"
                           (capitalize (format-cardinality n))
                           (if (= n 1) "y" "ies")))))
      (set-title self (format "Activities Manager : {a}" text))
      (let ((status (find-status-bar (current-application))))
        (when status
          (set-title (child status 'activities) text)))))
  
  
  (method package (update-rectangle self)
    (define (compact-reporters)
      (let ((rank 0))
        (for-each (lambda (reporter)
                    (set-rank reporter rank)
                    (increase! rank))
                  (sort < (get-reporters self) key: reporter-rank))))
    
    (compact-reporters)
    (let ((width (get-width self))
          (reporters (get-reporters self))
          (base (manager-position)))
      (if (null? reporters)
          (begin
            (hide self)
            (set-position self base)
            (set-size self {Dimension 247 57}))
        (let* ((n (+ 1 (max-reporter-rank reporters)))
               (v (get-height (get-size (car reporters))))
               (height (+ 10 (* n (+ v 8)) 2)))
          (set-position self (new Point (get-h base) (- (+ (get-v base) 57) height)))
          (set-size self (new Dimension width height))))))
  
  
  (method public (clear-done self)
    (for-each (lambda (reporter)
                (when (get-done reporter)
                  (close reporter)))
              (get-reporters self))
    (update-rectangle self))


  ;;;
  ;;;; Utilities
  ;;;
  
  
  (method package (get-reporters self)
    (get-children (locate self 'border)))
  
  
  (definition (max-reporter-rank reporters)
    (apply max (map reporter-rank reporters)))
  
  
  (definition (reporter-rank reporter)
    (get-rank reporter)))


;;;
;;;; Activities-Border
;;;


(class Activities-Border extends View
  
  
  (method override (draw self surface context)
    (let ((bounds (get-bounds self)))
      (draw-edge surface bounds inner: 'raised outer: 'raised edges: '(left top right))
      (draw-edge surface bounds outer: 'sunken edges: '(bottom))))))
