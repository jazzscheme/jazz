;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; IDE Status
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ide.application.IDE-Status jazz


(import (jazz.application)
        (jazz.command)
        (jazz.component)
        (jazz.debugger)
        (jazz.descriptor)
        (jazz.event)
        (jazz.handler)
        (jazz.ide)
        (jazz.ide.activity)
        (jazz.layout)
        (jazz.markup)
        (jazz.menu)
        (jazz.preferences)
        (jazz.resource)
        (jazz.skin)
        (jazz.ui)
        (jazz.view)
        (jazz.window)
        (jazz.workspace))


(class IDE-Status extends Status-Toolbar


  (form
    (<install>
      (<Status-Label> name: message       size: {Dimension 500 20} location: center)
      (<Border-View>                      size: {Dimension 350 20} location: tail border-type: edge style: status
        (<Command-Shell> name: command-shell))
      @wait
      (<Status-Label> name: search-result title: "" size: {Dimension 200 20} location: tail mouse-down-handler: {Event-Handler :form on-mouse-down} context-menu-handler: {Event-Handler :form on-context-menu})
      (<Status-Label> name: process       size: {Dimension 150 20} location: tail mouse-down-handler: {Event-Handler :form on-mouse-down} context-menu-handler: {Event-Handler :form on-context-menu})
      (<Status-Label> name: workspace     title: "Default" size: {Dimension 150 20} location: tail icon: {Image-Resource "Workspace"} mouse-down-handler: {Event-Handler :form on-mouse-down} context-menu-handler: {Event-Handler :form on-context-menu})
      @wait
      (<Status-Label> name: activities    visible?: #f title: "No registered activities" size: {Dimension 250 20} location: tail icon: {Image-Resource "Task"} mouse-down-handler: {Event-Handler :form on-mouse-down} context-menu-handler: {Event-Handler :form on-context-menu})
      @wait
      (<Status-Label> name: locales       title: "English" size: {Dimension 100 20} location: tail icon: {Image-Resource "Trait"} mouse-down-handler: {Event-Handler :form on-mouse-down} context-menu-handler: {Event-Handler :form on-context-menu})
      (<Status-Label> name: look          title: "Look" size: {Dimension 100 20} location: tail icon: {Image-Resource "Grab"} mouse-down-handler: {Event-Handler :form on-mouse-down} context-menu-handler: {Event-Handler :form on-context-menu})))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method protected virtual (on-mouse-down self evt)
    (let* ((sender (get-sender evt))
           (name (get-name sender))
           (pos (acquire self sender (get-position evt))))
      (case name
        ((process) (popup-processes-menu self pos))
        ((workspace) (popup-workspaces-menu self pos))
        @wait
        ((activities) (toggle-visible? (get-activity-manager)))
        @wait
        ((locales) (track-popup-menu (new Locales-Menu) pos))
        ((look) (track-popup-menu self (new Look-Menu) pos)))))


  (method protected virtual (on-context-menu self evt)
    (let* ((sender (get-sender evt))
           (name (get-name sender))
           (pos (acquire self sender (get-position evt))))
      (case name
        ((process) (popup-processes-menu self pos))
        ((workspace) (popup-workspaces-menu self pos))
        @wait
        ((activities) (track-popup-menu (new Activities-Menu) pos))
        @wait
        ((locales) (track-popup-menu (new Locales-Menu) pos))
        ((look) (track-popup-menu self (new Look-Menu) pos)))))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Document"})

  
  ;;;
  ;;;; Component
  ;;;


  (method override (install self rest)
    (nextmethod self rest)
    (update-focused-process self))


  ;;;
  ;;;; Focused
  ;;;


  (method public (update-focused-process self)
    (let ((label (locate self 'process))
          (manager (get-debugger-manager)))
      (let ((focused-mode? (get-focused-mode? manager))
            (focused (get-focused-process manager)))
        (cond (focused-mode?
               (set-icon label (if focused {Image-Resource "Script"} #f))
               (set-title label (if focused (present focused) "")))
              (else
               (set-icon label #f)
               (set-title label "")))))))


;;;
;;;; Processes
;;;


(definition (popup-processes-menu view pos)
  (track-popup-menu view (new Processes-Menu) pos))


(class Processes-Menu extends Context-Menu
  
  
  (method override (install self rest)
    (nextmethod self rest)
    (insert-processes self))
  
  
  (method (insert-processes self)
    (define (process-image manager process live?) ;; dup from processes update
      (cond ((not live?)
             {Image-Resource "ProcessDead"})
            ((not (process-received-filenames? manager process))
             {Image-Resource "Link"})
            (else
             {Image-Resource "Script"})))
    
    (let ((manager (get-debugger-manager)))
      (for-each-process manager
        (lambda (debugger process live? focused?)
          (let ((title (present process))
                (icon (process-image manager process live?)))
            (let ((item (new Radio-Item parent: self title: title icon: icon action-handler: (new Event-Handler target: self method-name: 'on-action properties: (list process: process)))))
              (set-enabled? item live?)
              (set-selected? item focused?)))))))
  
  
  (method package (on-action self evt)
    (let ((manager (get-debugger-manager))
          (process (get-property evt process:)))
      (focus-process manager process))))


;;;
;;;; Workspaces
;;;


(definition (popup-workspaces-menu view pos)
  (let ((preferences (get-workspaces-preferences error?: #f)))
    (when preferences
      (let ((workspaces (get-visible-children preferences)))
        (when (>= (length workspaces) 1)
          (track-popup-menu view (new Workspaces-Menu) pos))))))


(class Workspaces-Menu extends Context-Menu
  
  
  (method override (install self rest)
    (define (insert-workspaces)
      (let* ((selected (selected-workspace))
             (preferences (get-workspaces-preferences))
             (workspaces (get-visible-children preferences))
             (default (find-component preferences 'default)))
        (when default
          (insert-workspace default (eq? default selected))
          (new Separator-Item parent: self))
        (for-each (lambda (workspace)
                    (unless (eq? workspace default)
                      (if (is? workspace Separator)
                          (new Separator-Item parent: self)
                        (insert-workspace workspace (eq? workspace selected)))))
                  workspaces)))
    
    (define (insert-workspace workspace selected?)
      (let* ((title (if workspace (get-presentation-property workspace) "None"))
             (icon (if workspace (get-icon workspace) {Image-Resource "Empty"}))
             (item (new Radio-Item parent: self title: title icon: icon action-handler: (new Event-Handler target: self method-name: 'on-action properties: (list workspace: workspace)))))
        (when selected?
          (set-selected? item #t))))
    
    (nextmethod self rest)
    (insert-workspaces))
  
  
  (method package (on-action self evt)
    (let ((workspace (get-property evt workspace:)))
      (apply-preferences workspace))))


;;;
;;;; Activities-Menu
;;;


@wait
(class Activities-Menu extends Context-Menu
  
  
  (form
    (<install>                           title: "Activities"
      (<Label-Item>     name: open       title: "Open" action-handler: {Event-Handler :form on-open})
      (<Label-Item>     name: close      title: "Close" action-handler: {Event-Handler :form on-close})
      (<Separator-Item>)
      (<Label-Item>     name: clear-done title: "Clear Done" icon: {Image-Resource "Done"} action-handler: {Event-Handler :form on-clear-done})))
  
  
  (method package (on-open self evt)
    (show (get-activity-manager)))
  
  
  (method package (on-close self evt)
    (hide (get-activity-manager)))
  
  
  (method (on-clear-done self evt)
    (clear-done (get-activity-manager))))


;;;
;;;; Look-Menu
;;;


(class Look-Menu extends Context-Menu
  
  
  (method override (install self rest)
    (define (insert-looks)
      (let ((current (get-look))
            (looks (registered-looks)))
        (for-each (lambda (look)
                    (insert-look look (eq? look current)))
                  looks)))
    
    (define (insert-look look checked?)
      (let* ((title (present look))
             (item (new Check-Item parent: self title: title action-handler: (new Event-Handler target: self method-name: 'on-action properties: (list look: look)))))
        (when checked?
          (set-checked? item #t))))
    
    (nextmethod self rest)
    (insert-looks))
  
  
  (method package (on-action self evt)
    (set-look (get-property evt look:))
    (redraw-window (current-toplevel)))))
