;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Spine Browser
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ide.code.Spine-Browser jazz


(import (jazz.catalog)
        (jazz.catalog.entry)
        (jazz.editor.lisp)
        (jazz.graphic)
        (jazz.ide)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.library.component)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view))


(class Spine-Browser extends Layout-View


  (property client initialize #f accessors generate)
  
  
  (slot client-entry     initialize #f)
  (slot client-selection initialize #f)
  (slot client-position  initialize #f)
  
  
  (slot preserved-states initialize (make-table test: eq? weak-keys: #t))
  

  (form
    (<install>                                        layout-type: fill
      (<Layout-View>                name: tools
        (<Push-Tool>                name: update      size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Refresh" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :guest on-update}))
      (<Scroller-View>              name: scroller    vscroll?: #t hscroll?: #t style: tool
        (<!>                        name: content     layout-type: fill
          (<Tree-Header>            name: header      shown?: #f
            (<!>                    name: content
              (<Spine-Tree>         name: definitions portfolio: :images show-header?: #f always-select?: #t selection-handler: {Event-Handler :form on-selection-change} double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
                (<Tree-Node-Column>                   title: "Spine" width: 329))))))))
  
  
  (method meta override (external-name . rest)
    'Spine)


  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon)
    {Image-Resource "AnalysisManager"})
  
  
  (method meta override (host-title)
    "Summary")
  
  
  (method override (client-activate)
    (define (client-entry guest)
      (when (is? guest Code-Text-View)
        (let ((moniker (get-moniker~ (get-document-controller~ guest))))
          (when moniker
            (let ((ext (get-extension~ moniker)))
              (if (extension=? ext "jml")
                  (safe-read-form moniker)
                (let ((cataloguer (get-cataloguer~ (get-application))))
                  (get-file-entry~ cataloguer moniker context: (get-context~ guest)))))))))
    
    (let ((guest (current-document)))
      (let ((entry (client-entry guest)))
        (if (not entry)
            (set-client/entry #f #f)
          (set-client/entry guest entry)))))
  
  
  (method override (client-deactivate)
    (set-client/entry #f #f))
  
  
  (method (tree-focus-gained)
    (when client
      (set! client-selection (get-selection~ client))
      (set! client-position (get-scrolled-position~ client))))
 

  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (get-tools-view)
    (locate 'tools))
  
  
  (method override (focus-default)
    (let ((tree (locate 'definitions)))
      (acquire-focus~ tree)))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions)
    (cons (find-actions 'spine-browser)
          (nextmethod)))
 

  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-update evt)
    (highlight-selection))


  (method (on-selection-change evt)
    (let ((sender (get-sender~ evt)))
      (case (get-name~ sender)
        ((definitions)
         (selection-change sender #f)))))


  (method (on-double-click evt)
    (let ((sender (get-sender~ evt)))
      (case (get-name~ sender)
        ((definitions)
         (selection-change sender #t)))))
  
  
  (method override (on-return-press key)
    (finish-focus))
  
  
  (method override (on-escape-press key)
    (cancel-focus))
  
  
  (method (on-cancel evt)
    (cancel-focus))
  
  
  (method (finish-focus)
    (when client
      (acquire-focus~ client)))
  
  
  (method (cancel-focus)
    (when client
      (when client-selection
        (set-selection-safe~ client client-selection ensure-displayed?: #f)
        (scroll-to~ client client-position))
      (acquire-focus~ client)))
  
  
  ;;;
  ;;;; Delegate
  ;;;
  
  
  (method (edit-definitions definition)
    (define (edit-jml-definition definition)
      (let* ((appl (get-application))
             (moniker (get-origin~ client-entry))
             (frame (edit-document~ appl moniker visible?: #f))
             (text (get-guest~ frame))
             (location (append (get-location~ definition) (list 0)))
             (range (location-range~ text location)))
        (set-selection-safe~ text range ensure-displayed?: 'center-caret)
        (focus-host~ frame)))
    
    (if (is? definition JML-Node)
        (edit-jml-definition definition)
      (edit~ definition activate-focus?: #f)))
  
  
  ;;;
  ;;;; Client
  ;;;

 
  (method public (set-client/entry clt entry)
    (when (neq? entry client-entry)
      (when client
        (preserve-state client)
        (remove-text-listener~ client self))
      (set! client clt)
      (set! client-entry entry)
      (when client
        (restore-state client)
        (when (get-highlight-hierarchy?~ (get-preferences 'documents))
          (add-text-listener~ client (new Event-Handler execute: (lambda (evt) (highlight-selection))))))))

  
  ;;;
  ;;;; Highlight
  ;;;
  
  
  (method (highlight-selection)
    (define (insert-entry tree entry child)
      (let ((parent (get-parent~ entry)))
        (when (and parent (is-not? parent File-Entry))
          (insert-entry tree parent entry)
          (add-row~ tree children: (list (new Tree-Node title: ""))))
        (typecase entry
          ((Lisp-Entry) (insert-lisp-entry tree entry child))
          (else (insert-default-entry tree entry child)))))
    
    (define (insert-lisp-entry tree entry child)
      (case (get-kind~ entry)
        ((module) (insert-module-entry tree entry child))
        ((class) (insert-class-entry tree entry child))
        ((method) (insert-method-entry tree entry child))
        (else (insert-default-entry tree entry child))))
    
    (define (insert-module-entry tree entry child)
      (insert-default-entry tree entry child image: {Image-Resource "Class"}))
    
    (define (insert-class-entry tree entry child (father: father #f))
      (let ((father (let ((ascendant (class-entry-ascendant entry)))
                      (when ascendant
                        (insert-class-entry tree ascendant child)))))
        (let ((method (and child (eq? (get-kind~ child) 'method) (find-definition~ entry (->string (get-name~ child))))))
          (let ((font (if method {Font Label-Bold} #f))
                (color (if method
                           (cond ((eq? entry (get-parent~ child)) {Color Dark-Green})
                                 ((memq? 'virtual (get-modifiers~ method)) {Color Dark-Red})
                                 (else {Color Dark-Blue}))
                         #f)))
            (insert-default-entry tree entry child font: font color: color image: {Image-Resource "BlueRed"} father: father)))))
    
    (define (insert-method-entry tree entry child)
      (insert-default-entry tree entry child image: {Image-Resource "Link"}))
    
    (define (insert-default-entry tree entry child (title: title #f) (font: font #f) (color: color #f) (image: image #f) (father: father #f))
      (let* ((title (or title (get-name~ entry)))
             (image (or image {Image-Resource "Slot"})))
        (add-row~ tree father: father state: 'expanded children: (list (new Tree-Node title: title font: font color: color image: image)) user-data: entry)))
    
    (let ((file (get-file~ client-entry))
          (start (get-start~ client)))
      (let ((entry (location->entry (list file (get-line~ start) (get-col~ start)))))
        (when entry
          (let ((tree (locate 'definitions)))
            (remove-every-row~ tree)
            (with-update-locked~ tree
              (lambda ()
                (insert-entry tree entry #f))))))))
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method (preserve-state client)
    (table-set! preserved-states client (get-view-state~ (locate 'definitions) use: 'text)))
  
  
  (method (restore-state client)
    (let ((state (table-ref preserved-states client #f)))
      (when state
        (set-view-state~ (locate 'definitions) state))))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method (selection-change tree double-click?)
    (define (same-file? definition moniker)
      (and moniker (pathname=? (get-file~ (get-file-definition~ definition)) moniker)))
    
    (let ((guest (current-document)))
      (when guest
        (let ((moniker (get-moniker~ (get-document-controller~ guest)))
              (selection (get-single-selection~ tree)))
          (when selection
            (let ((definition (get-user-data~ (get-row~ tree selection))))
              (unless (is? definition Exception)
                (when (and definition (or double-click? (is-not? definition Indexed-Entry) (same-file? definition moniker)))
                  (edit-definitions definition))))))))))


;;;
;;;; Spine-Tree
;;;


(class Spine-Tree extends Code-Tree
  
  
  (method override (focus-gained)
    (nextmethod)
    (tree-focus-gained~ (get-guest)))))
