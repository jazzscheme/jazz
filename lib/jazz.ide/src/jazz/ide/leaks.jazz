;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; GStreamer Leaks
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.ide.leaks jazz


(import (jazz.application)
        (jazz.catalog)
        (jazz.catalog.edit)
        (jazz.editor)
        (jazz.editor.jazz)
        (jazz.event)
        (jazz.graphic)
        (jazz.handler)
        (jazz.io)
        (jazz.layout)
        (jazz.markup)
        (jazz.outline)
        (jazz.platform)
        (jazz.project)
        (jazz.resource)
        (jazz.stream)
        (jazz.text)
        (jazz.tree)
        (jazz.ui)
        (jazz.view))


(class Leaks-Explorer extends Layout-View implements Filterable
  
  
  (property file initialize #f accessors generate)
  
  
  (slot kind)
  (slot port)
  (slot entries)
  (slot ignored)
  (slot active initialize #f)
  
  
  (form
    (<install>                                             layout-type: border
      (<Layout-View>                  name: toolbar        size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        (<Separator>                                       size: {Dimension 2 0})
        (<Label-View>                 name: file-label     justification: (head -3) title: "File:" size: {Dimension 37 20} font: {Font Label})
        (<Border-View>                                     size: {Dimension 300 20} border-type: fill border-insets: {Rect 0 1 0 0}
          (<Border-View>                                   border-type: edge style: entry
            (<Moniker-Widget>         name: file           protocol: file display-protocol?: #f display-logical?: #f moniker-update-handler: {Event-Handler :form on-moniker-update})))
        (<Separator>                                       size: {Dimension 24 0})
        (<Label-View>                 name: filter-label   justification: (head -3) title: "Filter:" size: {Dimension 37 20} font: {Font Label})
        (<Border-View>                                     size: {Dimension 250 20} border-type: fill border-insets: {Rect 0 1 0 0}
          (<Border-View>                                   border-type: edge style: entry
            (<Scroller-View>                               hscroll?: #f vscroll?: #f
              (<!>                    name: content        layout-type: fill
                (<Filter-Text-View>   name: filter-text)))))
        (<Separator>                                       size: {Dimension 4 0})
        (<Push-Tool>                  name: filter         size: {Dimension 22 22} tooltip?: #t tooltip-text: "Filter" resource: {Image-Resource "Spy"} portfolio: :images action-handler: {Event-Handler :form on-filter})
        (<Label-View>                 name: ignored        location: tail size: {Dimension 200 20} justification: tail font: {Font Label})
        (<Separator>                                       location: tail size: {Dimension 40 0})
        (<Push-Tool>                  name: update         location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Update" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :form on-update-leaks}))
      (<Splitter-View>                                     location: center orientation: vert mode: relative division: 50 style: document
        (<Splitter-View>                                   location: first orientation: horz mode: relative division: 40 style: document
          (<Border-View>                                   location: first border-type: edge style: entry
            (<Scroller-View>                               style: document hscroll?: #t vscroll?: #t
              (<!>                    name: content        layout-type: fill
                (<Tree-Header>                             style: document
                  (<!>                name: content
                    (<Life-Tree>      name: life-tree      selection-handler: {Event-Handler :form on-selection-change}))))))
          (<Border-View>                                   location: second border-type: edge style: entry
            (<Scroller-View>                               style: document hscroll?: #t vscroll?: #t
              (<!>                    name: content        layout-type: fill
                (<Tree-Header>                             style: document
                  (<!>                name: content
                    (<Objects-Tree>   name: objects-tree   selection-handler: {Event-Handler :form on-selection-change})))))))
        (<Splitter-View>                                   location: second orientation: horz mode: relative division: 35 style: document
          (<Border-View>                                   location: first border-type: edge style: entry
            (<Scroller-View>                               style: document hscroll?: #t vscroll?: #t
              (<!>                    name: content        layout-type: fill
                (<Tree-Header>                             style: document
                  (<!>                name: content
                    (<Stack-Tree>     name: stack-tree     selection-handler: {Event-Handler :form on-selection-change} double-click-handler: {Event-Handler :form on-double-click}))))))
          (<Border-View>                                   location: second border-type: edge style: entry
            (<Scroller-View>                               hscroll?: #t vscroll?: #t
              (<!>                    name: content
                (<Jazz-Text-View>     name: text           left-padding: 12 editable?: #f))))))))
  
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Configure"})
  
  
  (method meta override (host-title self)
    "Leaks Explorer")
  
  
  (method meta override (host-position self)
    {Point 28 14})
  
  
  (method meta override (host-size self)
    {Dimension 900 500})
  
  
  (method override (host-workspaces self)
    '(groupware))
  
  
  (method override (focus-default self)
    (acquire-focus (locate self 'filter-text)))
  
  
  ;;;
  ;;;; Filter
  ;;;
  
  
  (method override (filter self filter-search)
    (filter-rows (locate self 'objects-tree)
      (lambda (title)
        (search title filter-search))))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-moniker-update self evt)
    (update-file self))
  
  
  (method (on-selection-change self evt)
    (let ((sender (get-sender evt)))
      (case (get-name sender)
        ((objects-tree)
         (update-life self sender)
         (select-first-row (locate self 'life-tree)))
        ((life-tree)
         (update-stack self sender)
         (update-first-frame self sender))
        ((stack-tree)
         (update-frame self sender)))))
  
  
  (method (on-double-click self evt)
    (let ((sender (get-sender evt)))
      (edit-frame self sender)))
  
  
  (method (on-filter self evt)
    (filter-target (locate self 'filter-text)))
  
  
  (method (on-update-leaks self evt)
    (if (not file)
        (bell)
      (when port
        (close-port port))
      (set! port (open-input-file (path-settings file)))
      (update-leaks self)))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update-file self)
    (set! file (get-moniker (locate self 'file)))
    (set! kind (determine-kind self file))
    (set! port (open-input-file (path-settings file)))
    (update-leaks self))
  
  
  (method (determine-kind self file)
    (call-with-input-file (path-settings file)
      (lambda (port)
        (if (equal? (read-line port) "jazz")
            'jazz
          'gstreamer))))
  
  
  (method (update-leaks self)
    (case kind
      ((jazz)
       (set! entries (read-leaks port))
       (set! ignored '()))
      (else
       (receive (e i) (parse-leaks port)
         (set! entries e)
         (set! ignored i))))
    (update-ignored self)
    (update self))
  
  
  (method (update-ignored self)
    (let ((ignored (if (null? ignored) "" (format " ({a} audio/x-raw caps ignored)" (length ignored)))))
      (set-title (locate self 'ignored) ignored)))
  
  
  (method package (update self)
    (with-cursor :wait
      (lambda ()
        (let ((tree (locate self 'objects-tree)))
          (remove-every-row tree)
          (with-update-locked tree
            (lambda ()
              (for-each (lambda (entry)
                          (bind (what type address description refcount) entry
                            (when (memq? what '(alive invalid))
                              (add-row tree children: (list (new Tree-Node title: (->string what))
                                                            (new Tree-Label title: type)
                                                            (new Tree-Label title: address)
                                                            (new Tree-Label title: description)
                                                            (new Tree-Label title: refcount))
                                user-data: entry))))
                        entries))))
        (remove-every-row (locate self 'life-tree))
        (remove-every-row (locate self 'stack-tree))
        (clear-frame self))))
  
  
  (method (update-life self sender)
    (with-cursor :wait
      (lambda ()
        (let ((tree (locate self 'life-tree)))
          (remove-every-row tree)
          (let ((sender-entry (get-single-selected-data sender)))
            (when sender-entry
              (bind (sender-what sender-type sender-address sender-description sender-refcount) sender-entry
                (with-update-locked tree
                  (lambda ()
                    (for-each (lambda (entry)
                                (bind (what type address description refcount stack) entry
                                  (define (present-description)
                                    (define (present-frame frame)
                                      (bind (name line col) frame
                                        (if (not line)
                                            (->string name)
                                          (format "{a} + {a}" name line))))
                                    
                                    (if (eq? kind 'jazz)
                                        (let ((frame (find-first-frame self stack)))
                                          (if (not frame)
                                               description
                                            (present-frame frame)))
                                      description))
                                  
                                  (when (equal? address sender-address)
                                    (add-row tree children: (list (new Tree-Node title: (->string what))
                                                                  (new Tree-Label title: (present-description))
                                                                  (new Tree-Label title: refcount))
                                      user-data: entry))))
                              entries))))))))))
  
  
  (method (update-stack self sender)
    (with-cursor :wait
      (lambda ()
        (let ((tree (locate self 'stack-tree)))
          (remove-every-row tree)
          (let ((entry (get-single-selected-data sender)))
            (when entry
              (bind (what type address description refcount stack) entry
                (with-update-locked tree
                  (lambda ()
                    (if (eq? kind 'jazz)
                        (for-each (lambda (frame)
                                    (bind (name line col) frame
                                      (define (present-frame)
                                        (if (not line)
                                            (->string name)
                                          (format "{a} + {a}" name line)))
                                      
                                      (add-row tree children: (list (new Tree-Node title: (present-frame))
                                                                    (new Tree-Label title: ""))
                                        user-data: frame)))
                                  stack)
                      (for-each (lambda (frame)
                                  (bind (library function offset) frame
                                    (add-row tree children: (list (new Tree-Node title: (string-append function " + " offset))
                                                                  (new Tree-Label title: library))
                                      user-data: frame)))
                                stack)))))))))))
  
  
  (method (find-first-frame self stack)
    (cond ((null? stack)
           #f)
          ((>= (length stack) 3)
           (third stack))
          (else
           (last stack))))
  
  
  (method (update-first-frame self sender)
    (if (eq? kind 'jazz)
        (let ((entry (get-single-selected-data sender)))
          (if entry
              (bind (what type address description refcount stack) entry
                (let ((frame (find-first-frame self stack)))
                  (if frame
                      (show-frame self frame)
                    (clear-frame self))))
            (clear-frame self)))
      (clear-frame self)))
  
  
  (method (update-frame self sender)
    (if (eq? kind 'jazz)
        (let ((frame (get-single-selected-data sender)))
          (if frame
              (show-frame self frame)
            (clear-frame self)))
      (clear-frame self)))
  
  
  (method (show-frame self frame)
    (bind (name line col) frame
      (if (and (symbol? name)
               (composite-reference? name)
               line)
          (let ((entry (locator->entry name)))
            (if entry
                (let ((moniker (get-moniker entry)))
                  (if (exists? moniker)
                      (install-frame self moniker line col)
                    (clear-frame self)))
              (clear-frame self)))
        (clear-frame self))))
  
  
  (method (install-frame self moniker line col)
    (let ((text (locate self 'text)))
      (define (install-indicator cell)
        (fill (get-model text) {Color Frame} cell)
        (ensure-cell-displayed text cell)
        (set! active cell))
      
      (define (uninstall-indicator)
        (unfill (get-model text) active)
        (set! active #f))
      
      (when active
        (uninstall-indicator))
      (set-moniker text moniker)
      (install-indicator (new Cell line col))))
  
  
  (method (clear-frame self)
    (let ((text (locate self 'text)))
      (set-moniker text #f))
    (set! active #f))
  
  
  (method (edit-frame self sender)
    (let ((frame (get-single-selected-data sender)))
      (if (not frame)
          (bell)
        (if (eq? kind 'jazz)
            (bind (name line col) frame
              (if (and (symbol? name)
                       (composite-reference? name))
                  (let ((entry (locator->entry name)))
                    (if (not entry)
                        (bell)
                      (edit-entry entry)))
                (bell)))
          (bind (library function offset) frame
            (edit-definitions (current-application) function)))))))


;;;
;;;; Objects-Tree
;;;


(class Objects-Tree extends Tree-View
  
  
  (form
    (<install>                               filled-column: description multiple-selection?: #t
      (<Tree-Node-Column>  name: what        title: "What" width: 100 toplevel-controls?: #f display-images?: #f)
      (<Tree-Label-Column> name: type        title: "Type" width: 200)
      (<Tree-Label-Column> name: address     title: "Address" width: 150)
      (<Tree-Label-Column> name: description title: "Description" width: 175)
      (<Tree-Label-Column> name: refcount    title: "Refcount" width: 80))))


;;;
;;;; Life-Tree
;;;


(class Life-Tree extends Tree-View
  
  
  (form
    (<install>                               filled-column: description multiple-selection?: #t
      (<Tree-Node-Column>  name: what        title: "What" width: 100 toplevel-controls?: #f display-images?: #f)
      (<Tree-Label-Column> name: description title: "Description" width: 175)
      (<Tree-Label-Column> name: refcount    title: "Refcount" width: 80))))


;;;
;;;; Stack-Tree
;;;


(class Stack-Tree extends Tree-View
  
  
  (form
    (<install>                               filled-column: library multiple-selection?: #t
      (<Tree-Node-Column>  name: name        title: "Name" width: 400 toplevel-controls?: #f display-images?: #f)
      (<Tree-Label-Column> name: library     title: "Library" width: 150)))))
