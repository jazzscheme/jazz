;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Preferences Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ide.preference.Preferences-Manager jazz


(import (jazz.application)
        (jazz.designer)
        (jazz.designer.ui)
        (jazz.handler)
        (jazz.io)
        (jazz.markup)
        (jazz.outline)
        (jazz.preferences)
        (jazz.process)
        (jazz.resource)
        (jazz.tree)
        (jazz.ui)
        (jazz.view))


(class undocumented Preferences-Manager extends Canvas-Manager
  
  
  (form
    (<install>
      (<!>           name: toolbar
        (<Push-Tool> name: reload-preferences size: {Dimension 22 22} tooltip?: #t tooltip-text: "Reload Preferences" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :form on-reload-preferences}))))
  
  
  (method override (form-model self form)
    (case (get-name form)
      ((descendants) Preferences-Editor)
      (else (nextmethod self form))))
  
  
  (method override (install-in-host self host)
    (nextmethod self host)
    (let ((preferences (tool-preferences self)))
      (when preferences
        (apply-preferences (current-process) preferences host))))
  
  
  (method meta override (external-name self . rest)
    'preferences-manager)
  
  
  ;;;
  ;;;; Host
  ;;;
    
  
  (method meta override (host-icon self)
    {Image-Resource "Preferences"})
  
  
  (method meta override (host-title self)
    #f)
  
  
  (method meta override (host-position self)
    #f)
  
  
  (method meta override (host-size self)
    {Dimension 800 450})
  
  
  (method override (host-workspaces self)
    '(home))
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (focus-default self)
    (let ((tree (locate self '(descendants tree))))
      (acquire-focus tree)))
  
  
  ;;;
  ;;;; Component
  ;;;

  
  (method override (install self rest)
    (nextmethod self rest)
    (set-rights self (get-preferences-rights (get-application)))
    (set-client self (current-preferences-root)))
  
  
  ;;;
  ;;;; Controller
  ;;;


  (method override (new-document-controller self)
    (new Preferences-Manager-Controller self))
  
  
  ;;;
  ;;;; Document
  ;;;


  (method override (get-modified? self)
    #f)
  
  
  ;;;
  ;;;; Preferences
  ;;;
  
  
  (method override (tool-preferences self)
    (current-preferences '(tools preferences-manager)))
  
  
  ;;;
  ;;;; Session
  ;;;
  
  
  (method override (confirm-close self . rest)
    (save-session self))
  
  
  (method override (save-session self)
    (let ((pref (tool-preferences self))
          (designer (get-preferences-designer (get-application))))
      (save-preferences pref designer (get-host self))))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method package (on-save-preferences self evt)
    (with-cursor :wait
      (lambda ()
        (save self))))
  
  
  (method package (on-edit self evt)
    (let ((appl (get-application)))
      (edit-document appl {File Profile "settings" "Preferences.jml"})))
  
  
  (method package (on-reload-preferences self evt)
    (set-selection (locate self '(descendants tree)) '())
    (setup-preferences (current-process))
    (set-client self (current-preferences-root)))

  
  ;;;
  ;;;; Properties
  ;;;
  
  
  (method override (show-properties self)
    (let ((card (get-card self)))
      ;; quick patch
      (if (not card)
          (bell)
        (let ((tree (locate card 'tree)))
          (acquire-focus tree)
          (when (and (null? (get-selection tree)) (> (get-visible-count tree) 0))
            (set-single-selection tree 0))))))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method (set-application-selection self selection force?)
    )
  
  
  (method (close-application-selection self)
    ))


;;;
;;;; Controller
;;;


(class Preferences-Manager-Controller extends Component-Editor-Controller
  
  
  (method override (document-status self)
    "Preferences"))


;;;
;;;; Editor
;;;


(class Preferences-Editor extends Descendants-Editor
  
  
  (method override (form-model self form)
    (case (get-name form)
      ((tree) Preferences-Tree)
      (else (nextmethod self form)))))


;;;
;;;; Tree
;;;


(class Preferences-Tree extends Descendants-Tree
  
  
  (form
    (<install> portfolio: :images draggable?: #t reorderable?: #t))
  
  
  (method override (update-selection self expand-state selected-components)
    )
  
  
  (method override (row-state self component update? level)
    (if update?
        (if (< level 1) 'expanded 'collapsed)
      'collapsed))))
