;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; CLike Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.language.clike.text.CLike-Text-View jazz


(import (jazz.language.c)
        (jazz.library)
        (jazz.ui)
        (jazz.utilities))


(class CLike-Text-View extends Code-Text-View
  
  
  (method (install rest)
    (nextmethod rest)
    (let ((pref (preferences)))
      (add-style (new Text-Style name: 'Title            base: 'Default color: (get-title-color~ pref)))
      (add-style (new Text-Style name: 'Definition       base: 'Default color: (get-definition-color~ pref)))
      (add-style (new Text-Style name: 'Line-Comment     base: 'Default color: (get-line-comment-color~ pref)))
      (add-style (new Text-Style name: 'Balanced-Comment base: 'Default color: (get-balanced-comment-color~ pref)))
      (add-style (new Text-Style name: 'Keyword          base: 'Default color: (get-keyword-color~ pref)))
      (add-style (new Text-Style name: 'Profile-Macro    base: 'Default color: (get-profile-macro-color~ pref)))
      (add-style (new Text-Style name: 'Profile-Keyword  base: 'Default color: (get-profile-keyword-color~ pref)))
      (add-style (new Text-Style name: 'Profile-Type     base: 'Default color: (get-profile-type-color~ pref)))))
  
  
  (method (new-syntax)
    (new C-Syntax))


  ;;;
  ;;;; Match
  ;;;


  ;; temporarely...
  (method (flash-character? char)
    #f)

  
  ;;;
  ;;;; Tabulation
  ;;;


  (method (return-press key)
    (nextmethod key)
    (tabulate))

  
  ;;;
  ;;;; Completion
  ;;;
  
  
  (method (commented? pos)
    (let ((style (get-style-at pos)))
      (and style
           (or (inherits?~ style 'Title)
               (inherits?~ style 'Line-Comment)
               (inherits?~ style 'Balanced-Comment)))))

  
  ;;;
  ;;;; Keywords
  ;;;
  
  
  (method protected virtual (get-keywords)
    )
      
  
  ;;;
  ;;;; Profile
  ;;;

  
  (method protected virtual (get-profile-macros profile)
    )
  
  
  (method protected virtual (get-profile-keywords profile)
    )
  
  
  (method protected virtual (get-profile-types profile)
    )

  
  ;;;
  ;;;; Mouse Copy
  ;;;

  
  (method (mouse-insert copy)
    (let ((start (get-start))
          (end (get-end)))
      @implement-with-c-logic-ie-capitalized-variables
      (when (/= start (text-beginning))
        (let ((sc (get-char (previous-cell start))))
          (when (and (text-constituent?~ syntax (element copy 0)) (text-constituent?~ syntax sc) (not (memq? sc '(#\minus #\colon))))
            (set! copy (concatenate " " copy)))))
      @implement-with-c-logic-ie-capitalized-variables
      (when (and (/= end (text-ending)) (text-constituent?~ syntax (last copy)) (text-constituent?~ syntax (get-char end)))
        (set! copy (concatenate copy " ")))
      (with-update-locked
        (function ()
          (insert-string copy)))))

  
  ;;;
  ;;;; Coloring
  ;;;
  
  
  (method (compute-syntax-targets)
    (let ((profile (get-profile~ (get-application))))
      (append (list (list "/*" 'balanced-comment #f))
              (list (list "*/" 'balanced-comment-backward #f))
              (list (list "//" 'line-comment #f))
              (map (function (str) (list str 'keyword)) (get-keywords))
              (map (function (str) (list str 'profile-macro)) (get-profile-macros profile))
              (map (function (str) (list str 'profile-keyword)) (get-profile-keywords profile))
              (map (function (str) (list str 'profile-type)) (get-profile-types profile)))))
  
  
  (method (compute-syntax-automata)
    (build-search-automata (syntax-targets) #t #f self c-constituent?))

  
  (method (colorize-type found type start end colorizer recolorize?)
    (case type
      ((balanced-comment)          (colorize-balanced-comment          found start end colorizer 'Balanced-Comment))
      ((balanced-comment-backward) (colorize-balanced-comment-backward found start end colorizer 'Balanced-Comment recolorize?))
      ((line-comment)              (colorize-line-comment              found start end colorizer 'Line-Comment 'Title #\slash))
      ((keyword)                   (colorize-keyword                   found start end colorizer 'Keyword))
      ((profile-macro)             (colorize-keyword                   found start end colorizer 'Profile-Macro))
      ((profile-keyword)           (colorize-keyword                   found start end colorizer 'Profile-Keyword))
      ((profile-type)              (colorize-keyword                   found start end colorizer 'Profile-Type))))


  ;;;
  ;;;; Explorer
  ;;;


  (method (explorer-class)
    C-Explorer)
 
  
  ;;;
  ;;;; Expr
  ;;;
  
  
  (method (get-context)
    '(c))


  (method (current-name)
    (name-at (current-expr-range)))
  
  
  (method (name-at range)
    (when range
      (let ((name (get-string range)))
        (if (eqv? (element name 0) #\open-brace)
            (read-string-element name)
          name))))


  (method (current-expr)
    (current-name))
  
  
  (method (current-expr-range)
    (if (and (not completion-active?) (has-selection?))
        (get-selection)
      (expr-range (get-end))))
  
  
  (method (expr-range pos)
    (let ((expl (new C-Explorer self start: pos end: pos)))
      (outer-expr~ expl)
      (get-range~ expl)))
  
  
  (method (expr-at pos)
    (name-at (expr-range pos)))

  
  ;;;
  ;;;; Chapters
  ;;;
  
  
  (constant headers
    (list
      (concatenate "///" (string #\tab))
      (concatenate "///" (string #\space))))
  
  
  (method (get-headers)
    headers)))
