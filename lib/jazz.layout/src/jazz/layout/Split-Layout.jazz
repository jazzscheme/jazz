;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Split Layout
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.layout.Split-Layout jazz


(import (jazz.figure)
        (jazz.layout))


(class Split-Layout extends Layout

  
  (method override (layout self view figures width height (separator: separator #f) (state: state #f) (target: target #f) (orientation: orientation #f) (mode: mode #f) (division: division #f) (insets: insets #f))
    (define (layout-empty)
      (for-each (lambda (figure)
                  (set-visible? figure #f))
                figures)
      #f)
    
    (define (layout-zoom first second)
      (for-each (lambda (figure)
                  (if (if (is? target Figure)
                          (neq? figure target)
                        (neq? figure (case target
                                       ((first) first)
                                       ((second) second))))
                      (set-visible? figure #f)
                    (set-visible? figure #t)
                    (set-position figure (new Point (get-left insets) (get-top insets)))
                    (set-size figure (new Dimension (- width (get-left insets) (get-right insets)) (- height (get-top insets) (get-bottom insets))))))
                figures)
      #f)
    
    (define (layout-split first second)
      (let ((size (case orientation ((horz) width) ((vert) height)))
            (splitter-size (case separator ((resize) 4) ((edge) 2) ((line) 1) ((none) 0))))
        (case mode
          ((absolute)
           (when (negative? division)
             (set! division (+ size division)))
           (if (eq? orientation 'horz)
               (when (or (< division (+ (get-left insets) 1))
                         (> division (- width (get-right insets) splitter-size 1)))
                 (set! division (quotient width 2)))
             (when (or (< division (+ (get-top insets) 1))
                       (> division (- height (get-bottom insets) splitter-size 1)))
               (set! division (quotient height 2)))))
          ((relative)
           (let ((pct (fxround (percent (abs division) (cast <fl> size)))))
             (if (positive? division)
                 (set! division pct)
               (set! division (- size pct)))))))
      (let ((splitter-size (case separator ((resize) 4) ((edge) 2) ((line) 1) ((none) 0))))
        (case orientation
          ((horz)
           (let ((first-width (- division (get-left insets)))
                 (first-height (- height (get-top insets) (get-bottom insets)))
                 (second-width (- width division splitter-size (get-right insets)))
                 (second-height (- height (get-top insets) (get-bottom insets))))
             (when first
               (set-position first (new Point (get-left insets) (get-top insets)))
               (set-size first (new Dimension first-width first-height))
               (set-visible? first #t))
             (when second
               (set-position second (new Point (+ division splitter-size (if (eq? separator 'none) -1 0)) (get-top insets)))
               (set-size second (new Dimension (+ second-width (if (eq? separator 'none) 1 0)) second-height))
               (set-visible? second #t))
             (new Rect division (get-top insets) (+ division splitter-size) (- height (get-bottom insets)))))
          ((vert)
           (let ((first-width (- width (get-left insets) (get-right insets)))
                 (first-height (- division (get-top insets)))
                 (second-width (- width (get-left insets) (get-right insets)))
                 (second-height (- height division splitter-size (get-top insets))))
             (when first
               (set-position first (new Point (get-left insets) (get-top insets)))
               (set-size first (new Dimension first-width first-height))
               (set-visible? first #t))
             (when second
               (set-position second (new Point (get-left insets) (+ division splitter-size (if (eq? separator 'none) -1 0))))
               (set-size second (new Dimension second-width (+ second-height (if (eq? separator 'none) 1 0))))
               (set-visible? second #t))
             (new Rect (get-left insets) division (- width (get-right insets)) (+ division splitter-size)))))))
    
    (if (not state)
        (layout-empty)
      (let ((first (or (find-located view 'first) (first-child view)))
            (second (or (find-located view 'second) (second-child view))))
        (case state
          ((zoom) (layout-zoom first second))
          ((split) (layout-split first second))))))))
