;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Components
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.library.component jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui.dialog)
        (jazz.version))


;;;
;;;; Parameter
;;;


(definition public current-tag-reference-chain
  (make-parameter '()))


(definition public tag-reference-not-found-handler
  (make-parameter #f))


;;;
;;;; Form
;;;


(definition public (form->eval-expr form)
  (parameterize ((walk-for 'eval))
    (with-jazz-readtable
      (lambda ()
        (read-string-element
          (format "{s}" (->jml~ (form->node form))))))))


(definition public (open-form)
  (read-form (choose-file multiple-selection?: #f)))


(definition public (read-form file)
  (parameterize ((walk-for #f))
    (call-with-input-file (path-settings file char-encoding: 'UTF eol-encoding: 'cr-lf readtable: jazz-readtable)
      (lambda (input)
        (let ((expr (read input)))
          (if (eof-object? expr)
              (error "No expression found in form file: {a}" (parse~ file))
            (let ((form (parse-form expr file)))
              (set-origin~ form file)
              form)))))))


(definition public (parse-form expr (file #f))
  (define (parse expr proc)
    (if (and (pair? expr)
             (eq? (car expr) 'data))
        (parse-data expr proc)
      (error "Ill-formed data{a}" (if file (format " in: {a}" (parse~ file)) ""))))
  
  (define (parse-data expr proc)
    (bind (data dialect . info) expr
      (let ((version (assq 'version info))
            (import (assq 'import info))
            (form (assq 'form info)))
        (assert form)
        (proc (new Data
                (if version (apply new Version (cdr version)) #f)
                dialect
                (if import (cdr import) #f)
                (if file (new Alias (get-list~ (get-parent~ file))) #f))
              (cadr form)))))
  
  (parse expr
         (lambda (data form)
           (construct-form form (imports-locator (effective-imports~ data)) data))))


(definition public (safe-read-form file)
  ;; quick solution we should only catch reading and parsing errors
  (call-with-catch (lambda (obj)
                     (or (datum-parsing-exception? obj)
                         (error? obj)))
      (lambda (exc)
        (wrap-exception exc))
    (lambda ()
      (read-form file))))


(definition public (save-form form file)
  (with-jazz-readtable
    (lambda ()
      (call-with-output-file (path-settings file)
        (lambda (output)
          (print-form-data form output))))))


(definition public (print-form-data form output)
  (let ((data (get-data~ form)))
    (output-header (get-model~ form) output)
    (format output "{%}{%}")
    (format output "(data {a}{%}" (get-dialect~ data))
    (format output "{%}{%}")
    (let ((imports (get-imports~ data)))
      (when imports
        (format output "(import ")
        (let ((first? #t))
          (for-each (lambda (import)
                      (if first?
                          (set! first? #f)
                        (format output "{%}        "))
                      (format output "{s}" import))
                    imports)
          (format output "){%}"))
        (format output "{%}{%}")))
    (format output "(form{%}")
    (pretty-print~ (form->node form) output base: 1 space-name?: #t)
    (format output ")")
    (format output "){%}")))


(definition *file-forms*
  (make-table test: pathname=? hash: pathname-hash))


(definition public (register-file-form file form)
  (table-set! *file-forms* file form))

(definition public (registered-file-form file)
  (table-ref *file-forms* file #f))


(definition public (instantiate-file file . rest)
  (let ((form (let ((form (read-form file)))
                (register-file-form file form)
                form)))
    (apply instantiate~ form rest)))


(definition public (locator-add-reference locator reference-symbol reference)
  (lambda (symbol)
    (if (eq? symbol reference-symbol)
        reference
      (locator symbol))))
  

(definition public (locator-add-imports locator imports)
  (if (null? imports)
      locator
    (let ((new-locator (imports-locator imports)))
      (lambda (symbol)
        (or (new-locator symbol)
            (locator symbol))))))


(definition public (imports-locator imports)
  (let ((imports (map (lambda (import)
                        (if (pair? import) (car import) import))
                      imports)))
    (lambda (symbol)
      (let ((module-name (find-if (lambda (module-name)
                                    (boolean (module-get module-name symbol)))
                                  imports)))
        (and module-name
             (deserialize-runtime-reference (list 'module-public module-name symbol)))))))


;;;
;;;; Imports
;;;


(definition public (form-imports form)
  (define (collect-nodes form)
    (let ((factory (new List-Factory)))
      (iterate-nodes form (~ put factory))
      (get-output~ factory)))
  
  (define (collect-symbols form)
    (let* ((nodes (collect-nodes form))
           (client-forms (collect (lambda (node)
                                    (get-property~ node client-form:))
                                  nodes))
           (nodes (append nodes (append-map collect-nodes client-forms)))
           (properties (append-map get-properties~ nodes))
           (new-nodes (collect-if (lambda (action) (eq? action 'new))
                                  nodes
                                  key: get-action~)))
      (append
        (collect (lambda (val)
                   (and (is? val Reference)
                        (get-symbol~ val)))
                 (map-property (lambda (_ value) value) properties))
        (map get-model~ new-nodes))))
  
  (define (symbol-import symbol)
    (let ((context (get-context~ form)))
      (first (parse-runtime-reference (get-reference~ context symbol)))))
  
  (let ((symbols (collect-symbols form)))
    (remove-duplicates (collect symbol-import symbols))))


;;;
;;;; Components
;;;


(definition public (common-ancestor components)
  (cond ((null? components) #f)
        ((= 1 (length components))
         (first components))
        (else
         (common-ancestor (remove-false (remove-duplicates (map get-parent~ components)))))))


(definition public (collect-oldest-ancestors components common-ancestor)
  (collect-if (lambda (component)
                (or (eq? component common-ancestor)
                    (let ((ancestry (component-ancestry (get-parent~ component) common-ancestor)))
                      (every? (lambda (ancestor)
                                (not (memq? ancestor components)))
                              ancestry))))
              components))


(definition public (component-owned? component form ancestor)
  (let ((form (component-owned-form component form ancestor)))
    (and form (eq? (get-action~ form) 'new))))


(definition public (component-ancestry component ancestor)
  (define (end? scan)
    (if scan
        (eq? scan ancestor)
      (error "{t} is not a descendant of the reference component {t}" component ancestor)))
  
  (unfold-right end? identity get-parent~ component))


(definition public (component-in-reference-descendants? component ancestor)
  (continuation-capture
    (lambda (exit)
      (define (end? scan)
        (if scan
            (eq? scan ancestor)
          (continuation-return exit #f)))
      
      (unfold-right end? identity get-parent-component~ component))))


(definition public (component-owned-form component form ancestor)
  (owned-form form (component-ancestry component ancestor)))


(definition public (owned-form form ancestry)
  (if (null? ancestry)
      form
    (let ((cpn (car ancestry)))
      (let ((subforms (get-children~ form))
            (cpn-form (get-form~ cpn))
            (name (get-name~ cpn)))
        (cond ((not cpn-form)
               #f)
              ((memq? cpn-form subforms)
               (owned-form cpn-form (cdr ancestry)))
              ((not name)
               #f)
              (else
               (let ((subform (find-in subforms name key: get-name~ test: equal?)))
                 (and subform (owned-form subform (cdr ancestry))))))))))


(definition public (ownership-level owned-form)
  (and owned-form
       (case (get-action~ owned-form)
          ((new install) 'create)
          (else (if (null? (get-properties~ owned-form))
                    'traverse
                  'modify)))))


(definition public (first-ancestry-owned-form component ascendant form)
  (let (loop (cur component))
    (if (eq? component ascendant)
        form
      (let ((owned-form (owned-form form (component-ancestry cur ascendant))))
        (or owned-form (loop (get-parent~ cur)))))))


(definition public (ancestry-traits-owned-forms component ascendant form)
  (let (loop (current-form (first-ancestry-owned-form (get-parent~ component) ascendant form))
             (traits-owned-forms '()))
    (if current-form
        (let ((traits (get-traits~ current-form component)))
          (loop (get-parent~ current-form)
                (append traits-owned-forms
                        (collect (lambda (trait)
                                   (let ((trait-form (get-client-form~ trait)))
                                     (owned-form trait-form (component-ancestry component ascendant))))
                                 traits))))
      traits-owned-forms)))


(definition public (component-ascendant-owned-forms component ascendant)
  (let* ((ancestry (component-ancestry component ascendant))
         (class-forms (reverse (get-class-forms~ ascendant))))
    (append-map (lambda (class-form)
                  (let ((owned-form (owned-form class-form ancestry)))
                    (append (ancestry-traits-owned-forms component ascendant class-form)
                            (if owned-form
                                (append (map get-client-form~ (get-traits~ owned-form component))
                                        (list owned-form))
                              '()))))
                class-forms)))


(definition public (component-forms component)
  (let (loop (ascendant (get-parent~ component))
             (forms '()))
    (if ascendant
        (loop (get-parent~ ascendant)
              (append forms (component-ascendant-owned-forms component ascendant)))
        forms)))



(definition public (common-root-component component-list)
  (let ((roots (remove-duplicates (map get-root-parent~ component-list))))
    (when (pair? roots)
      (car roots))))


(definition public (component-descendant-value-list component (filter-proc: filter-proc identity) (map-proc: map-proc get-name~)) 
  (remove-duplicates (map map-proc (collect-if filter-proc (collect-descendants~ component)))))


(definition public (root-creator component)
  (let ((creator (get-creator~ component)))
    (if (and creator (not (eq? creator component)))
        (root-creator creator)
      component)))


;;;
;;;; Clipboard
;;;


(definition public (determine-unique-paster components)
  (case (length components)
    ((0) #f)
    ((1) (determine-paster~ (first components)))
    (else (let ((parents (remove-duplicates (map get-parent~ components))))
            (when (= 1 (length parents))
              (let ((parent (car parents)))
                (when parent
                  (determine-paster~ parent #f)))))))))
