;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Integrity Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.library.integrity.Integrity-Manager jazz


(class undocumented Integrity-Manager extends Object
  
  
  (slot constraints initialize '())
  
  
  (method override (initialize)
    (define (setup-constraints)
      (set! constraints (new List-Factory))
      (register-constraints)
      (set! constraints (get-output~ constraints)))
    
    (nextmethod)
    (setup-constraints))
  
  
  (method override (print printer readably)
    (print-unreadable self printer
      (lambda (printer)
        (let ((entries (length constraints)))
          (format printer "with {a} constraint{a} registered"
                  (format-cardinality entries)
                  (format-plural entries))))))


  ;;;
  ;;;; Setup
  ;;;
  
  
  (method public virtual (register-constraints)
    )
  
  
  (method public (register-constraint constraint model (root?: root? #f))
    (put~ constraints (list constraint model root?)))
  
  
  ;;;
  ;;;; Constraints
  ;;;
  
  
  (method public (validate-integrity root (context: context #f) (reporter: reporter #f))
    (clear-violations root)
    (validate-all root context reporter))
  
  
  (method protected virtual (validate-all root context reporter)
    (validate-hierarchy root root root context reporter))
  
  
  (method protected virtual (validate-hierarchy component root client context reporter)
    (when reporter
      (report-progress component root reporter))
    (unless (eq? (validate component root client context) 'stop-descent)
      (for-each (lambda (child)
                  (validate-hierarchy child root client context reporter))
                (get-children~ component))))
  
  
  (method public (validate component root client context)
    (let ((new-context (validation-context component root context)))
      (if (eq? (validate-component~ component root client context self) 'stop-descent)
          'stop-descent
        (continuation-capture
          (lambda (return)
            (for-each (lambda (info)
                        (bind (constraint model root?) info
                          (when (and (or (null? model) (is? component model))
                                     (or root? (branch-installed?~ root)))
                            (when (eq? (validate~ constraint component root client context self) 'stop-descent)
                              (continuation-return return 'stop-descent)))))
                      constraints))))))
  
  
  (method protected virtual (validation-context component root context)
    context)
  
  
  (method protected virtual (report-progress component root reporter)
    (test-cancelled~ reporter)
    (user-feedback~ reporter "Validating {a}..." (get-name~ component)))
  
  
  ;;;
  ;;;; Violations
  ;;;
  
  
  (method public virtual (add-violation violation component root client)
    (let* ((branch (get-active-branch~ root))
           (branch-name (and branch (get-name~ branch)))
           (branch-presentation (and branch (get-presentation-property~ branch))))
      (receive (path presentation) (present-path~ component root)
        (set-branch~ violation branch-name)
        (set-branch-presentation~ violation branch-presentation)
        (set-path~ violation path)
        (set-path-presentation~ violation (if branch (cons branch-presentation presentation) presentation))
        (add-violation~ client violation))))
  
  
  (method public virtual (clear-violations root)
    (clear-violations~ root))))
