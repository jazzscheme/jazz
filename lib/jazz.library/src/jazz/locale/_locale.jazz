;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Localization
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.locale jazz


;;;
;;;; Locales
;;;


(class Locales extends Object
  
  
  (slot locales accessors generate)
    
    
  (method override (initialize . rest)
    (define (uniquify-locales remaining uniquified)
      (if (null? remaining)
          (reverse uniquified)
        (assert (not-null? (cdr remaining)))
        (bind (locale string . new-remaining) remaining
          (assert (keyword? locale))
          (uniquify-locales new-remaining (if (memq? locale uniquified)
                                              uniquified
                                            (cons string (cons locale uniquified)))))))
    
    (nextmethod)
    (set! locales (uniquify-locales rest '())))
  
  
  (method override (print output readably)
    (format output "猃猃忑ㄣ狒彗矧钺礤ㄣ灬篌镦箦戽┅ㄩ祜汜戾祜汜戾螬礤翳镤秭弪蜷溴ㄣ镯疳蝈镡赍泗翎蜱弭ㄤ彐轭蝈沲蝮轹瀛泔眇狎痨轶舯痨轶舨豉疱ㄣ镱è犷铛祆痨轶舯铛祆痨轶舨┅ｔè铒ㄥ窨铛祆痨轶舯铛祆痨轶舨┅ｆè羼豉疱щ妁黠蜾戾è桢徜ㄣ狎痨轶舯┅ㄨ遽洳ㄣ狎痨轶舨┅ㄡ钿脲黠蜾桢徜暴ㄥ窨桢徜桢徜博蝈沲蝮轹瀛泔眇狎ㄣ潋痨轶舯ㄣ潋痨轶舨篝蜷铉┅┅ㄥ祗戾è桢徜ㄣ狎痨轶舯┅ㄨ遽洳ㄣ狎痨轶舨┅ㄡ钿矧ㄡ钿铒桢徜暴铒桢徜博ㄡ钿篝蜷铉桢徜暴篝蜷铉桢徜博篝蜷铉娇桢徜桢徜博┅蝈沲蝮轹瀛泔眇狎ㄣ潋痨轶舯ㄣ潋痨轶舨щ妁黠蜾┅┅┅ㄣ镱è轶铒艨翎蜱弭田汜戾螬洪钽镯疳糸忪濠è蝈沲蝮轹瀛泔眇狎祜汜戾ㄧ弭祜汜戾簋翎蜱弭щ妁黠蜾哄聃犰ㄥ祗侯雉羼踽飑┅换换换零沐篌换礤翳镤瘐忪殂ㄧ弭祜汜戾祜汜戾ㄧ弭祜汜戾祜汜戾┅换换换田汜扉换礤翳镤瘐忪殂鲩螋踽祜汜扉祜汜戾ㄧ弭祜汜戾祜汜戾┅换换换蔑铘屮趱犰田汜戾换ㄣ灬篌蔑铘屮趱犰田汜戾屮翦钿田汜戾箪雉泔铘屮珏趑弪珏铄蜥翦礤翳镤秭弪蜷溴ㄩ铋糸犰辁祜汜戾泔铘屮舂箦簟祜汜戾簋箦戽祜汜戾螬箦簟泔铘屮酤箦戽泔铘屮舂礤翳镤秭弪蜷溴痱轭秕麴豸蝈徜徕禊痱轭舡躅蝈徜徕戾箦戽秕麴豸灬礅溽秕麴豸┅换换换田汜扉换礤翳镤秭弪蜷溴祜汜扉祜汜戾ㄦ矧磲Ⅺ藓" (localize locales locale) context)))


(definition public (contextual-locales locales context)
  (new Contextual-Locales locales context))


;;;
;;;; Locale
;;;


(class Locale extends Object
  
  
  (slot name        getter generate)
  (slot parent-name getter generate)
  (slot titles      getter generate)
  
  
  (method override (initialize name <Keyword> parent-name titles <Locales>)
    (nextmethod)
    (set! nameself name)
    (set! parent-nameself parent-name)
    (set! titlesself titles))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name))))
  
  
  (method public (present)
    (localize titles)))


;;;
;;;; Global
;;;


(definition *locale-table*
  (let ((table (make-table test: eq?)))
    (table-set! table root: (new Locale root: #f (new Locales english: "root" french: "racine")))
    table))

(definition *active-locale*
  english:)


(definition public (get-locales)
  (sort string<? (table-keys *locale-table*) key: keyword->string))


(definition public (registered-locale name)
  (or (table-ref *locale-table* name #f)
      (error "unregistered locale")))


(definition public (register-locale locale)
  (let ((name (get-name locale)))
    (cond ((table-ref *locale-table* name #f)
           (error "a locale has already been registered under this name"))
          ((not (table-ref *locale-table* (get-parent-name locale) #f))
           (error "locale parent name is not registered"))
          (else
           (table-set! *locale-table* name locale)))))


(definition public (unregister-locale locale)
  (let ((name (get-name locale)))
    (table-clear *locale-table* name)))


(definition public (get-locale)
  *active-locale*)


(definition public (standardize-locale loc)
  (cond ((keyword? loc) loc)
        ((enumerator? loc) (symbol->keyword (enumerator->symbol loc)))
        ((symbol? loc) (symbol->keyword loc))
        (else (error "Invalid locale: {s}" loc))))


(definition public (set-locale loc (update?: update? #t))
  (let ((loc (standardize-locale loc)))
    (assert (registered-locale loc))
    (unless (eq? loc *active-locale*)
      (set! *active-locale* loc)
      (for-each (lambda (proc)
                  (proc loc update?))
                locale-changes))))


(definition locale-changes
  '())


(definition public (register-locale-change proc)
  (set! locale-changes (cons proc locale-changes)))


(definition public (unregister-locale-change proc)
  (set! locale-changes (remove! proc locale-changes)))


(definition public (localize object (loc *active-locale*))
  (let ((locale (registered-locale loc)))
    (or (and (is? object Locales)
             (or (localize object loc)
                 (localize object (get-parent-name locale))))
        object)))


(definition public (present-locale loc)
  (present (registered-locale loc))))
