;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Menus
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.menu.Menu jazz


(import (jazz.action)
        (jazz.locale)
        (jazz.locator)
        (jazz.markup)
        (jazz.menu)
        (jazz.view)
        (jazz.window))


(class Menu extends View
  
  
  (property display-shortcuts? initialize #t accessors generate)
  
  
  (slot item        initialize #f accessors generate)
  (slot highlighted initialize #f)
  (slot popped      initialize #f)
  (slot locator     initialize (new Menu-Locator self))


  (form
    (<install> visible?: #f))


  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions self)
    (cons (menu-actions self)
          (nextmethod self)))
  
  
  (method protected virtual (menu-actions self)
    (find-actions 'menu))

  
  ;;;
  ;;;; Component
  ;;;
  
  
  (method override (conclude self rest)
    (nextmethod self rest)
    (layout-menu self))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method package (parent-menu self)
    (and item (get-parent item)))
  
  
  (method protected (parent-menubar self)
    (let (iter (menu self))
      (and menu
           (if (is? menu Menubar)
               menu
             (iter (parent-menu menu))))))
  
  
  (method public (get-popped self)
    popped)
  
  
  (method public (set-popped self menu)
    (when popped
      (let ((initiator (popup-initiator popped)))
        (when initiator
          (invalidate-view initiator))))
    (set! popped menu))
  
  
  ;;;
  ;;;; Highlighted
  ;;;
  
  
  (method public (get-highlighted self)
    highlighted)
  
  
  (method public (set-highlighted self new)
    (let ((old highlighted))
      (set! highlighted new)
      (when old
        (invalidate-view old))
      (when new
        (invalidate-view new))))
  
  
  (method package (clear-highlighted self)
    (set! highlighted #f))
  
  
  ;;;
  ;;;; Focus
  ;;;
  
  
  (method override (focus-lose self)
    (set-highlighted self #f))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method (on-navigate-previous self evt)
    (navigate-previous self))
  
  
  (method (on-navigate-next self evt)
    (navigate-next self))
  
  
  (method (on-navigate-open self evt)
    (navigate-open self))
  
  
  (method (on-navigate-close self evt)
    (navigate-close self))
  
  
  (method (on-navigate-home self evt)
    (navigate-home self))
  
  
  (method (on-navigate-end self evt)
    (navigate-end self))


  (method override (key-press self evt)
    (enter locator (get-key evt)))


  (method override (up-arrow-press self modifiers)
    (navigate-previous self))


  (method override (down-arrow-press self modifiers)
    (navigate-next self))
  
  
  (method (navigate-to self navigate)
    (let ((items (navigable-items self)))
      (when (not-null? items)
        (set-highlighted self (navigate items highlighted)))))
  
  
  (method package (navigable-items self)
    (collect-if item-navigable? (collect-type Item children)))
  
  
  (method override (right-arrow-press self modifiers)
    (or (navigate-open self)
        (let ((menubar (parent-menubar self)))
          (when menubar
            (close-all-popups)
            (navigate-next menubar)
            (navigate-open menubar)))))
  
  
  (method override (left-arrow-press self modifiers)
    (let ((parent (parent-menu self)))
      (if (is? parent Menubar)
          (begin
            (close-all-popups)
            (navigate-previous parent)
            (navigate-open parent))
        (navigate-close self))))
  
  
  (method override (return-press self evt)
    (navigate-select self))


  (method override (escape-press self evt)
    (navigate-close self))
  
  
  ;;;
  ;;;; Navigate
  ;;;


  (method package (navigate-previous self)
    (navigate-to self previous-element))


  (method package (navigate-next self)
    (navigate-to self next-element))
  
  
  (method protected (navigate-home self)
    (navigate-to self
      (lambda (items highlighted)
        (first items))))
  
  
  (method protected (navigate-end self)
    (navigate-to self
      (lambda (items highlighted)
        (last items))))
  
  
  (method protected (navigate-select self)
    (when highlighted
      (if (get-content highlighted)
          (navigate-open self)
        (let ((item highlighted))
          (close-popups player all?: #t)
          (process-action item item)))))
  
  
  (method package (navigate-open self (highlight-last? #f))
    (when highlighted
      (let ((content (get-content highlighted)))
        (when content
          (popup-content highlighted)
          (set-highlighted content
            (if highlight-last?
                (last-child content)
              (first-child content)))
          #t))))
  
  
  (method protected (navigate-close self)
    (close-popup (get-player self))
    (when item
      (let ((menu (get-parent item))
            (focus (active-focus)))
        (when (eq? focus menu)
          (set-highlighted menu item)))))
  
  
  ;;;
  ;;;; Widget
  ;;;
  
  
  (method override (inner? self view)
    (memq? view children))

  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method override (invalidate-update self)
    (layout-menu self))
  
  
  (method public virtual (update self)
    )
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method public virtual (layout-menu self . rest)
    (define (collect-maximums)
      (let ((prefix-max 0)
            (icon-max 0)
            (body-max 0)
            (shortcut-max 0)
            (arrow-max 0))
        (for-each-view self
          (lambda (view)
            (let ((prefix (preferred-prefix-width view))
                  (icon (preferred-icon-width view))
                  (body (preferred-body-width view))
                  (shortcut (if display-shortcuts? (preferred-shortcut-width view) 0))
                  (arrow (preferred-arrow-width view)))
              (when (> prefix prefix-max) (set! prefix-max prefix))
              (when (> icon icon-max) (set! icon-max icon))
              (when (> body body-max) (set! body-max body))
              (when (> shortcut shortcut-max) (set! shortcut-max shortcut))
              (when (> arrow arrow-max) (set! arrow-max arrow)))))
        (values prefix-max icon-max body-max shortcut-max arrow-max)))
    
    (bind-keywords ((cleanup-separators? #t) (width #f)) rest
      (when (get-finished? self)
        (when cleanup-separators?
          (cleanup-separators self))
        (receive (prefix icon body shortcut arrow) (collect-maximums)
          (let ((width (max (+ prefix icon body shortcut arrow) (or width 0)))
                (v 1)
                (empty? #t))
            (for-each-view self
              (lambda (view)
                (let ((height (preferred-height view)))
                  (set-position view (new Point 1 v))
                  (set-size view (new Dimension width height))
                  (set-prefix-width view prefix)
                  (set-icon-width view icon)
                  (set-body-width view body)
                  (set-shortcut-width view shortcut)
                  (set-arrow-width view arrow)
                  (increase! v height)
                  (set! empty? #f))))
            (if empty?
                (set-size self {Dimension 120 300})
              (set-size self (new Dimension (+ width 2) (+ v 1)))))))))
  
  
  ;;;
  ;;;; Separators
  ;;;
  
  
  (method public (cleanup-separators self)
    (let ((visibles (collect-if get-visible? children))
          (previous-separator? #t)
          (kept-separator #f))
      (for-each (lambda (item)
                  (let* ((separator? (is? item Separator-Item))
                         (consecutive? (and separator? previous-separator?)))
                    (if consecutive?
                        (set-visible? item #f)
                      (when separator?
                        (set! kept-separator item))
                      (set! previous-separator? separator?))))
                visibles)
      (when (and previous-separator? kept-separator)
        (set-visible? kept-separator #f)))))


;;;
;;;; Menu-Locator
;;;


(class Menu-Locator extends Locator
  
  
  (slot menu)
  
  
  (method override (initialize self menu . rest)
    (set! self.menu menu)
    (apply nextmethod self rest))
  
  
  (method override (iterate-entries self proc)
    (let ((items (navigable-items menu))
          (current (get-highlighted menu)))
      (when (not-null? items)
        (let ((start (if (not current) 0 (find-rank (if prefix current (next-element items current)) items)))
              (card (cardinality items)))
          (loop (for n from 0 below card)
                (let ((rank (modulo (+ start n) card)))
                  (let ((item (element items rank)))             
                    (proc (localize (get-title item)) rank))))))))
  
  
  (method override (select-entry self rank)
    (let ((items (navigable-items menu)))
      (set-highlighted menu (element items rank))))))
