;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; SMTP Utilities
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Jeremie Lasalle Ratelle.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2012
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.network.smtp jazz


(import time)


(class MIME-Message extends Object
  
  
  (slot from getter generate)
  (slot to getter generate)
  (slot subject getter generate)
  (slot message getter generate)
  (slot cc getter generate)
  (slot bcc getter generate)
  (slot content-type getter generate)
  (slot attachments getter generate)  
  
  
  (method override (initialize from to subject message (cc: cc '()) (bcc: bcc '()) (content-type: content-type #f) (attachments: attachments '()))
    (set! from~self from)
    (set! to~self to)
    (set! subject~self (remove-unicode-characters subject))
    (set! message~self (remove-unicode-characters message))
    (set! cc~self cc)
    (set! bcc~self bcc)
    (set! content-type~self content-type)
    (set! attachments~self attachments))
  
  
  (definition boundary "CF69BC42-C9CB-11E0-82A0-A5C34824019B")
  
  
  (definition line-size 72)
  
  
  (definition attachment-buffer-size (* 3 (/ line-size 4)))
  
  
  (definition separator (format "--{a}\n" boundary))
  
  
  (definition ending (format "--{a}--\n" boundary))
  
  
  (method (remove-unicode-characters string)
    (list->string
      (map (lambda (ch)
             (let ((n (char->integer ch)))
               (if (> n 255) #\space ch)))
           (string->list string))))
  
  
  (method (format-mime-date date)
    (date->string date "~d ~b ~Y ~H:~M:~S ~z"))
  
  
  (method (make-mime-header)
    (string-append (format "From: {a}{%}" from)
                   (format "To: {a}{%}" (join to ", "))
                   (format "{?:Cc: {a}{%}~}" (not-null? cc) (join cc ", "))
                   (format "{?:Bcc: {a}{%}~}" (not-null? bcc) (join bcc ", "))
                   (format "Date: {a}{%}" (format-mime-date (system-date)))
                   (format "Subject: {a}{%}" subject)
                   "MIME-Version: 1.0\n"))
  
  
  (method (send-message port)
    (let* ((length (string-length message))
           (last-index (* line-size (quotient length line-size))))
      (let (loop (i 0))
        (if (< i last-index)
            (let ((next-i (+ i line-size)))
              (write-substring message i next-i port)
              (newline port)
              (loop next-i))
          (write-substring message i length port)))))
  
  
  (method (send-file file port)
    (let ((buffer (make-u8vector attachment-buffer-size)))
      (let (loop)
        (let ((size-read (read-subu8vector buffer 0 attachment-buffer-size file)))
          (if (< size-read attachment-buffer-size)
              (let ((sub-buffer (subu8vector buffer 0 size-read)))
                (display (u8vector->base64-string sub-buffer) port))
            (display (u8vector->base64-string buffer) port)
            (newline port)
            (loop))))))
  
  
  (method (send-on port)
    (display (make-mime-header) port)
    (unless (null? attachments)
      (display (format "Content-Type: multipart/mixed; boundary={a}\n" boundary) port)
      (display separator port))
    (display (format "Content-Type: {a};\n\n" (or content-type 'text/plain)) port)
    (send-message port)
    (display "\n\n" port)
    (for-each (lambda (file)
                (display separator port)
                (display "Content-Type: application/octet-stream\n" port)
                (display (format "Content-Disposition: attachment; filename={s}\n" (get-name~ file)) port)
                (display "Content-Transfer-Encoding: base64\n\n" port)
                (call-with-input-file (parse~ file)
                  (lambda (file-port)
                    (send-file file-port port)))
                (display "\n\n" port))
              attachments)
    (display (if (null? attachments) "\n\n" ending) port)))


(class SMTP-Client extends Object
  
  
  (slot port)
  
  
  (method override (initialize server-address (port-number: port-number 25) (username: username "") (password: password ""))
    (set! port (open-tcp-client (list
                                  server-address: server-address
                                  port-number: port-number
                                  eol-encoding: 'cr-lf
                                  buffering: 'line
                                  keep-alive: #t)))
    (get-reply "220" #f)
    (display (format "EHLO {a}\n" server-address) port)
    (let* ((commands (cdr (get-reply "250" #f)))
           (login-methods (get-login-methods commands)))
      (cond ((member? "LOGIN" login-methods)
             (auth-login username password))
            ((member? "PLAIN" login-methods)
             (auth-plain username password))
            (else
             (error "No supported login methods")))))
  
  
  (method override (destroy)
    (display "QUIT\n" port)
    (close-port port)
    (nextmethod))
  
  
  (method (get-reply code (invalid #f))
    (let ((reply (read-line port)))
      (if (eof-object? reply)
          (error "Unexpected end of file")
        (unless (string-starts-with? reply code)
          (if invalid
              (invalid)
            (error "Expected {a}, received {a}" code reply)))
        (if (string-starts-with? reply (format "{a}-" code))
            (cons (substring reply 4 (string-length reply))
                  (get-reply code invalid))
          '()))))
  
  
  (method (get-login-methods commands)
    (cdr (split (find commands "AUTH" test: (lambda (target str) (string-starts-with? str target)) return: 'item) " ")))
  
  
  (method (auth-login username password)
    (display "AUTH LOGIN\n" port)
    (get-reply "334")
    (display (ascii-string->base64-string username) port)
    (newline port)
    (get-reply "334" (lambda () (error "Invalid username.")))
    (display (ascii-string->base64-string password) port)
    (newline port)
    (get-reply "235" (lambda () (error "Invalid credentials."))))
  
  
  (method (auth-plain username password)
    (display "AUTH PLAIN\n" port)
    (get-reply "334")
    (display (ascii-string->base64-string 
               (list->string
                 (append '(#\nul) (string->list username) '(#\nul) (string->list password)))) port)
    (newline port)
    (get-reply "235" (lambda ()
                       (throw "Invalid credentials."))))
  
  
  (method (send-mail mail <MIME-Message>)
    (display "RSET\n" port)
    (get-reply "250")
    
    (display (format "MAIL FROM:<{a}>\n" (get-from~ mail)) port)
    (get-reply "250" (lambda () (error (format "Error adding sender {a}." (get-from~ mail)))))
      
    (for-each (lambda (recipient)
                (display (format "RCPT TO: {a}\n" recipient) port)
                (get-reply "250" (lambda () (error (format "Error adding recipient {a}." recipient)))))
              (get-to~ mail))
    
    (display "data\n" port)
    (get-reply "354" (lambda () (error "Error sending the message. Server not accepting data.")))
    
    (send-on~ mail port)
    
    (display "\n.\n" port)
    (get-reply "250" (lambda () (error "Error sending the message.")))))


(definition public (send-message message server (port-number: port-number 25) (username: username "") (password: password ""))
  (let ((client (new SMTP-Client server port-number: port-number username: username password: password)))
    (send-mail~ client message)
    (close~ client)))


(definition public (base64-string->ascii-string str)
  (list->string
    (map
      integer->char
      (u8vector->list
        (base64-string->u8vector str)))))


(definition public (ascii-string->base64-string str)
  (u8vector->base64-string 
    (list->u8vector
      (map char->integer
           (string->list str))))))
