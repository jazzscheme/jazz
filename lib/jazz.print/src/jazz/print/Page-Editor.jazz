;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Page Editor
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.print.Page-Editor jazz


(import (jazz.application)
        (jazz.component)
        (jazz.designer)
        (jazz.graphic)
        (jazz.markup)
        (jazz.preferences)
        (jazz.print)
        (jazz.ui)
        (jazz.window)
        (jazz.workspace))


(class undocumented Page-Editor extends Layout-View
  

  (property client initialize #f accessors explicit)
  
  
  (slot client-designer)
  (slot designer)

  
  (form
    (<install>                    layout-type: center background: {Color Dark}
      (<Border-View> name: border position: {Point 0 0} size: {Dimension 514 664} border-type: shadow)))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method override (host-workspaces self)
    '(page))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (find-page-setup self)
    (first-child (locate self 'border)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-print self evt)
    (print-document client))
  
  
  (method (on-print-preview self evt)
    (print-preview client from-layout?: #t))
  
  
  (method (on-page-setup self evt)
    (page-setup self))
  
  
  (method (on-fit self evt)
    (let ((fit (get-property evt fit:)))
      (set-fit self fit)))
  
  
  (method (set-fit self fit)
    (let ((page-setup (find-page-setup self)))
      (set-property designer page-setup 'fit fit)))

  
  (method package (on-close self evt)
    (close-workspace self #t))
  
  
  (method override (escape-press self evt)
    (close-workspace self #f))

  
  ;;;
  ;;;; Activate
  ;;;
  
  
  (method package (activate-page self)
    (update-page self))
  
  
  (method (update-page self)
    (define (update-fit toolbar page-setup)
      (let ((fit (get-fit page-setup)))
        (set-selected? (locate toolbar fit) #t)))
    
    (define (update-border page-setup)
      (let ((border (locate self 'border)))
        (set-size border (nu+ (get-size page-setup) {Dimension 4 4}))
        (layout-view self)))
    
    (let ((toolbar (find-docked (current-toplevel) Page-Toolbar))
          (page-setup (find-page-setup self)))
      (update-fit toolbar page-setup)
      (update-border page-setup)
      (invalidate-view self)))

  
  ;;;
  ;;;; Close
  ;;;
  
  
  (method (close-workspace self save?)
    (define (find-page-setup-form)
      (let ((form (get-client-form (get-trait (get-active-branch self)))))
        (let ((border-form (find-child form 'border)))
          (when border-form
            (find-child border-form 'page-setup)))))
    
    (let ((frame (get-host self))
          (workspace (selected-workspace)))
      (when save?
        (save-header/footer self)
        (apply-page-setup client client-designer (find-page-setup-form) (find-page-setup self)))
      (close frame)
      (apply-preferences (get-previous workspace))
      (invalidate-window (current-toplevel))))

  
  ;;;
  ;;;; Client
  ;;;


  (method public (get-client self)
    client)


  (method public (set-client self value)
    (set! client value)
    (set! client-designer (get-page-setup-designer client))
    (let ((border (locate self 'border)))
      (set! designer (new Former form: (<form> (<Page-Editor>)) reference: self))
      (let ((parcel (get-page-setup-parcel client client-designer)))
        (let ((page-setup (instantiate-parcel designer parcel border)))
          (set-name page-setup 'page-setup)
          (set-visible? page-setup #t)
          (set-size border (nu+ (get-size page-setup) {Dimension 4 4}))))
      (let ((trait (new Trait parent: self)))
        (add-child designer trait self trait: #f)
        (set-property designer trait client-form: (new Form textual?: #f action: 'install context: (get-form designer)) trait: #f)
        (set-active-trait self trait))))

  
  ;;;
  ;;;; Page Setup
  ;;;
  
  
  (method (page-setup self)
    #f)

  
  ;;;
  ;;;; Header/Footer
  ;;;
  
  
  (method (save-header/footer self)
    (let ((page-setup (find-page-setup self)))
      (when designer
        (let ((header (get-header page-setup)))
          (when (get-modified? header)
            (set-property designer header text-content: (get-content (get-model header)))))
        (let ((footer (get-footer page-setup)))
          (when (get-modified? footer)
            (set-property designer footer text-content: (get-content (get-model footer))))))))))
