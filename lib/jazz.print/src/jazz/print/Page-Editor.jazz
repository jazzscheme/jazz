;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Page Editor
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.print.Page-Editor jazz


(import (jazz.application)
        (jazz.component)
        (jazz.designer)
        (jazz.geometry (cond windows))
        (jazz.graphic)
        (jazz.jml)
        (jazz.platform (cond windows))
        (jazz.print)
        (jazz.ui)
        (jazz.ui.dialog (cond windows))
        (jazz.workspace))


(class undocumented Page-Editor extends Layout-View
  

  (property client initialize #f accessors explicit)
  
  
  (slot client-designer)
  (slot designer)

  
  (form
    (<install>                    layout-type: center background: {Color Dark}
      (<Border-View> name: border position: {Point 0 0} size: {Dimension 514 664} border-type: shadow)))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method override (host-workspaces)
    '(page))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (find-page-setup)
    (first-child~ (locate 'border)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-print evt)
    (print-document~ client))
  
  
  (method (on-print-preview evt)
    (print-preview~ client from-layout?: #t))
  
  
  (method (on-page-setup evt)
    (page-setup))
  
  
  (method (on-fit evt)
    (let ((fit (get-property~ evt fit:)))
      (set-fit fit)))
  
  
  (method (set-fit fit)
    (let ((page-setup (find-page-setup)))
      (set-property~ designer page-setup 'fit fit)))

  
  (method (on-close evt)
    (close-workspace #t))
  
  
  (method override (escape-press evt)
    (close-workspace #f))

  
  ;;;
  ;;;; Activate
  ;;;
  
  
  (method protected (activate-page)
    (update-page))
  
  
  (method (update-page)
    (define (update-fit toolbar page-setup)
      (let ((fit (get-fit~ page-setup)))
        (set-selected?~ (locate~ toolbar fit) #t)))
    
    (define (update-border page-setup)
      (let ((border (locate 'border)))
        (set-size~ border (nu+ (get-size~ page-setup) {Dimension 4 4}))
        (layout-view)))
    
    (let ((toolbar (find-docked~ (get-toplevel) Page-Toolbar))
          (page-setup (find-page-setup)))
      (update-fit toolbar page-setup)
      (update-border page-setup)
      (invalidate-view)))

  
  ;;;
  ;;;; Close
  ;;;
  
  
  (method (close-workspace save?)
    (define (find-page-setup-form)
      (let ((form (get-client-form~ (get-trait~ (get-active-branch)))))
        (let ((border-form (find-child~ form 'border)))
          (when border-form
            (find-child~ border-form 'page-setup)))))
    
    (let ((frame (get-host))
          (workspace (selected-workspace)))
      (when save?
        (save-header/footer)
        (apply-page-setup~ client client-designer (find-page-setup-form) (find-page-setup)))
      (close~ frame)
      (apply-preferences~ (get-previous~ workspace))
      (invalidate-window~ (get-toplevel))))

  
  ;;;
  ;;;; Client
  ;;;


  (method public (get-client)
    client)


  (method public (set-client value)
    (set! client value)
    (set! client-designer (get-page-setup-designer~ client))
    (let ((border (locate 'border)))
      (set! designer (new Former form: (<form> (<Page-Editor>)) reference: self))
      (let ((parcel (get-page-setup-parcel~ client client-designer)))
        (let ((page-setup (instantiate-parcel~ designer parcel border)))
          (set-name~ page-setup 'page-setup)
          (set-visible?~ page-setup #t)
          (set-size~ border (nu+ (get-size~ page-setup) {Dimension 4 4}))))
      (let ((trait (new Trait parent: self)))
        (add-child~ designer trait self trait: #f)
        (set-property~ designer trait client-form: (new Form textual?: #f action: 'install context: (get-form~ designer)) trait: #f)
        (set-active-trait trait))))

  
  ;;;
  ;;;; Page Setup
  ;;;
  
  
  (cond-expand
    (windows
      (method (page-setup)
        (define page-setup-resolution
          1000)
        
        (let ((dialog-ratio (/ (cast <fl> page-setup-resolution) default-resolution))
              (setup-ratio (/ (cast <fl> default-resolution) page-setup-resolution))
              (page-setup (find-page-setup)))
          (let ((min-margins (scale (get-min-margins~ page-setup) dialog-ratio))
                (margins (scale (get-layout-insets~ page-setup) dialog-ratio))
                (orientation (numerize-orientation (get-orientation~ page-setup)))
                (paper-size (get-paper-size~ page-setup))
                (paper-source (get-paper-source~ page-setup)))
            (receive (size min-margins margins orientation paper-size paper-source) (page-setup-dialog margins: margins orientation: orientation paper-size: paper-size paper-source: paper-source)
              (set-property~ designer page-setup 'size (scale size setup-ratio))
              (set-property~ designer page-setup 'min-margins (scale min-margins setup-ratio))
              (set-property~ designer page-setup 'layout-insets (scale margins setup-ratio))
              (set-property~ designer page-setup 'orientation (symbolize-orientation orientation))
              (set-property~ designer page-setup 'paper-size paper-size)
              (set-property~ designer page-setup 'paper-source paper-source)
              (update-page))))))
    (else
     (method (page-setup)
       #f)))
  
  
  (cond-expand
    (windows
      (definition (numerize-orientation orientation)
        (case orientation
          ((portrait) DMORIENT_PORTRAIT)
          ((landscape) DMORIENT_LANDSCAPE))))
    (else
      (definition (numerize-orientation orientation)
        #f)))
  
  
  (cond-expand
    (windows
      (definition (symbolize-orientation orientation)
        (ecase orientation
          ((DMORIENT_PORTRAIT) 'portrait)
          ((DMORIENT_LANDSCAPE) 'landscape))))
    (else
      (definition (symbolize-orientation orientation)
        #f)))

  
  ;;;
  ;;;; Header/Footer
  ;;;
  
  
  (method (save-header/footer)
    (let ((page-setup (find-page-setup)))
      (when designer
        (let ((header (get-header~ page-setup)))
          (when (get-modified?~ header)
            (set-property~ designer header text-content: (get-content~ (get-model~ header)))))
        (let ((footer (get-footer~ page-setup)))
          (when (get-modified?~ footer)
            (set-property~ designer footer text-content: (get-content~ (get-model~ footer))))))))))
