;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Preview Page
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.print.Preview-Page jazz


(import (jazz.application)
        (jazz.print)
        (jazz.ui)
        (jazz.view)
        (jazz.window))


(class undocumented Preview-Page extends View


  (property client initialize #f accessors explicit)
  (property no     initialize #f accessors explicit)
  
  
  (slot dragging?      initialize #f)
  (slot start-position initialize #f)
  
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-client self)
    client)


  (method public (set-client self value)
    (set! client value)
    (invalidate-view self))


  (method public (get-no self)
    no)


  (method public (set-no self value)
    (set! no value)
    (invalidate-view self))
  
  
  (method (get-pane self)
    (get-guest self))
  
  
  ;;;
  ;;;; Cursor
  ;;;
  
  
  (method override (cursor-update self evt)
    (let* ((pane (get-pane self))
           (scrollee (locate pane 'scrollee)))
      (if (or (vscroll-needed? scrollee) (hscroll-needed? scrollee))
          (if (get-mouse-down?)
              (set-cursor :closed-hand)
            (set-cursor :opened-hand))
        (set-cursor :arrow))))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down self evt)
    (set! dragging? #t)
    (set! start-position (get-position evt))
    (acquire-capture self))
  
  
  (method override (drag-move self evt)
    (when dragging?
      (let* ((delta (nu- (get-position evt) start-position))
             (pane (get-pane self))
             (scrollee (locate pane 'scrollee)))
        (scroll scrollee delta))))
  
  
  (method override (drag-up self evt . rest)
    (when dragging?
      (release-capture self)
      (set! dragging? #f)))
  
  
  (method override (double-click self evt)
    (let ((pane (get-pane self))
          (toplevel (current-toplevel)))
      (set-zoom pane 1.)
      (set-value (locate (find-docked toplevel Preview-Toolbar) 'zoom) 1.)
      (set-start pane no)))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw self surface context)
    (let* ((pane (get-pane self))
           (job (get-job pane))
           (page (get-page job no)))
      (print-page job surface page no)))))
