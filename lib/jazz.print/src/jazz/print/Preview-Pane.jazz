;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Preview Pane
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.print.Preview-Pane jazz


(import (jazz.action)
        (jazz.application)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.markup)
        (jazz.preferences)
        (jazz.print)
        (jazz.ui)
        (jazz.view)
        (jazz.window)
        (jazz.workspace))


(class undocumented Preview-Pane extends Layout-View


  (property client initialize #f accessors explicit)
  
  
  (slot job   getter generate)
  (slot pages)
  (slot start initialize 0)
  (slot preserved)
    
  
  (form
    (<install>                          layout-type: fill background: {Color Dark}
      (<Scroller-View>   name: scroller hscroll?: #t vscroll?: #t
        (<!>             name: content
          (<Layout-View> name: scrollee size: {Dimension 1078 664} layout-type: center
            (<View>      name: centered size: {Dimension 1078 664}))))))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method override (host-workspaces self)
    '(preview))

  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions self)
    (cons (find-actions 'preview-pane)
          (nextmethod self)))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-print self evt)
    (print-document client))
  
  
  (method (on-print-layout self evt)
    (print-layout client from-preview?: #t))
  
  
  (method package (on-home self evt)
    (set-start self 0))
  
  
  (method package (on-end self evt)
    (let* ((count (length pages))
           (pages (get-pages job))
           (last (- (length pages) 1)))
      (set-start self (- last (modulo last count)))))
  
  
  (method (on-previous self evt)
    (previous self))
  
  
  (method (on-next self evt)
    (next self))
  
  
  (method package (on-close self evt)
    (close-workspace self #t))
  
  
  (method override (escape-press self evt)
    (close-workspace self #f))
  
  
  (method (close-workspace self save?)
    (let ((frame (get-host self))
          (workspace (selected-workspace)))
      (restore-client self)
      (close frame)
      (apply-preferences (get-previous workspace))
      (invalidate-window (current-toplevel))))

  
  ;;;
  ;;;; Client
  ;;;


  (method public (get-client self)
    client)


  (method public (set-client self value)
    (set! client value)
    (set! preserved (setup-printing client)))
  
  
  (method package (restore-client self)
    (wrap-header/footer (get-page-setup client) '())
    (restore-printing client preserved))
  
  
  ;;;
  ;;;; Zoom
  ;;;
  
  
  (definition Zoom
    0.5)


  (method public (get-zoom self)
    Zoom)


  (method public (set-zoom self value)
    (set! Zoom value)
    (scroll-to (locate self 'scrollee) {Point 0 0})
    (layout-view self))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout self)
    (nextmethod self)
    (let* ((scrollee (locate self 'scrollee))
           (centered (locate self 'centered))
           (print-area (get-print-area client '()))
           (page-setup (get-page-setup client))
           (scaling (scale-content page-setup client default-resolution))
           (zoom (/ (cast <fl> Zoom) scaling))
           (display-size (get-size self))
           (border-size (nu+ (scale-shape (get-size page-setup) zoom) {Dimension 4 4}))
           (separator (scale-shape 50 zoom)))
      (close-children centered)
      (let ((h 0)
             (v 0)
             (max-h 0)
             (max-v 0)
             (vert-done? #f)
             (horz-done? #f)
             (fact (new List-Factory)))
        (while (not vert-done?)
          (while (not horz-done?)
            (let* ((border (new Border-View parent: centered position: (new Point h v) size: border-size border-type: 'shadow))
                   (page (new Preview-Page parent: border)))
              (increase! h (get-width border-size))
              (set! max-h (max max-h h))
              (put fact page)
              (increase! h separator)
              (when (> (+ h (get-width border-size)) (get-width display-size))
                (set! horz-done? #t))))
          (set! h 0)
          (set! horz-done? #f)
          (increase! v (get-height border-size))
          (set! max-v (max max-v v))
          (increase! v separator)
          (when (> (+ v (get-height border-size)) (get-height display-size))
            (set! vert-done? #t)))
        (set! pages (get-output fact))
        (set-size centered (new Dimension max-h max-v))
        (set-size scrollee (new Dimension (max (- (get-width display-size) 13) max-h) (max (- (get-height display-size) 13) max-v)))
        (layout-view scrollee)
        (update-scrollbars scrollee)
        (set! job (new Preview-Job #f client page-setup default-resolution zoom: zoom))
        (for-each (lambda (page)
                    (set-client page client))
                  pages)
        (set-start self start))))
  
  
  ;;;
  ;;;; Page
  ;;;
  
  
  (method (get-page-height self)
    (get-height size))
  
  
  ;;;
  ;;;; Navigation
  ;;;
  
  
  (method public (previous self)
    (let ((count (length pages)))
      (set-start self (max 0 (- start count)))))
  
  
  (method public (next self)
    (let* ((count (length pages))
           (pages (get-pages job))
           (pg (+ start count)))
      (when (< pg (length pages))
        (set-start self pg))))
  
  
  (method public (set-start self value)
    (set! start value)
    (let ((count (length (get-pages job))))
      (for-each (lambda (page n)
                  (let ((border (get-parent page))
                        (pg (+ start n)))
                    (if (>= pg count)
                        (set-visible? border #f)
                      (set-no page pg)
                      (set-visible? border #t))))
                pages
                (naturals 0 (length pages)))))))
