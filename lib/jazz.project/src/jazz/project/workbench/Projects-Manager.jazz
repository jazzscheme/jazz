;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Projects Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.project.workbench.Projects-Manager jazz


(import (jazz.catalog)
        (jazz.jml)
        (jazz.library)
        (jazz.project)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window))


(class undocumented Projects-Manager extends Layout-View
  
  
  (slot hierarchical? initialize #t)


  (form
    (<install>                                               size: {Dimension 553 508} layout-type: border
      (<Layout-View>                   name: toolbar         size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        @wait (<Toggle-Tool>                                       size: {Dimension 22 22} tooltip?: #t tooltip-text: "Display Flat / Hierarchical" resource: {Image-Resource "Tree"} portfolio: :images action-handler: {Event-Handler :form on-flat/hierarchical})
        (<Push-Button>                 name: update          title: "Update" size: {Dimension 100 22} location: tail action-handler: {Event-Handler :form on-update} default?: #t))
      (<Border-View>                   name: projects-border location: center border-type: edge style: entry
        (<Scroller-View>                                     vscroll?: #t
          (<!>                         name: content         layout-type: fill
            (<Tree-Header>                                   style: document
              (<!>                     name: content
                (<Tree-View>           name: projects        filled-column: name portfolio: :images selection-handler: {Event-Handler :form on-selection-change} double-click-handler: {Event-Handler :form on-double-click} multiple-selection?: #t
                  (<Tree-Node-Column>  name: name            title: "Name" width: 150)
                  (<Tree-Label-Column> name: statistics      title: "Statistics" width: 250)))))))))
  
  
  (method meta override (external-name . rest)
    'projects-manager)
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'projects))
  
  
  ;;;
  ;;;; Component
  ;;;

  
  (method override (finish initargs)
    (nextmethod initargs)
    (set-hierarchical? hierarchical?)
    (view-projects)
    (update-buttons))
    
  
  ;;;
  ;;;; Host
  ;;;
    
  
  (method meta override (host-icon)
    {Image-Resource "Catalog"})
  
  
  (method meta override (host-title)
    "Projects Manager")
  
  
  (method meta override (host-position)
    {Point 90 70})
  
  
  (method meta override (host-size)
    {Dimension 573 408})
  
  
  (method override (host-workspaces)
    '(home))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  ;;;
  ;;;; Events
  ;;;


  (method override (resize dh dv)
    (nextmethod dh dv)
    (move-figure~ (locate 'update) dh 0)
    (size-figure~ (locate 'projects-border) dh dv))
  
  
  (method (on-flat/hierarchical evt)
    (set-hierarchical? (not hierarchical?))
    (view-projects))
  
  
  (method (set-hierarchical? flag)
    (set! hierarchical? flag)
    (let ((node-column (get-node-column~ (get-tree))))
      (set-toplevel-controls?~ (get-node-column~ (get-tree)) flag)
      (set-display-images?~ node-column flag)))
  
  
  (method (on-selection-change evt)
    (update-buttons))
  
  
  ;;;
  ;;;; Update
  ;;;


  (method public (view-projects)
    (define (insert-workbench tree)
      (insert-project tree #f (get-workbench) 0))
    
    (define (insert-project tree row project level)
      (let ((catalog (get-catalog~ project))
            (handler (new Event-Handler execute: (lambda (evt) (set-active? project (get-checked?~ (get-sender~ evt)))))))
        (let ((state (if (= level 0) 'expanded 'collapsed))
              (c0 (new Check-Box title: (get-presentation-property~ project) checked?: (get-active?~ project) content-change-handler: handler))
              (c1 (new Tree-Label title: (if catalog (get-statistics~ catalog) ""))))
          (let ((row (add-row~ tree father: row state: state children: (list c0 c1) user-data: project)))
            (insert-children tree row project (+ level 1))))))
    
    (define (insert-children tree row component level)
      (for-each (lambda (child)
                  (cond ((and (is? child Project)
                              (not (concrete?~ child)))
                         (insert-project tree row child level))
                        ((is? child Project-Group)
                         (insert-children tree row child level))))
                (get-children~ component)))
    
    (define (set-active? project flag)
      (let ((workbench-manager (get-workbench-manager~ (get-application))))
        (when workbench-manager
          (set-project-active?~ workbench-manager project flag))))
    
    (let ((tree (get-tree)))
      (remove-every-row~ tree)
      (with-update-locked~ tree
        (lambda ()
          (insert-workbench tree)))))


  (method package (on-update evt)
    (with-cursor :wait
      (lambda ()
        (update-selection))))
  
  
  (method (on-double-click evt)
    (let ((sender (get-sender~ evt))
          (alt? (get-alt?~ evt)))
      (case (get-name~ sender)
        ((projects)
         (with-cursor :wait
           (lambda ()
             (update-selection)))))))
  
  
  (method (update-selection)
    (let ((tree (get-tree)))
      (define (effective-rows)
        (let ((selected-rows (get-selected-rows~ tree)))
          (if (null? selected-rows)
              (collect-rows~ tree)
            selected-rows)))
      
      (post-catalog
        (lambda ()
          (define (update-project project)
            (when (get-active?~ project)
              (let ((catalog (get-catalog~ project)))
                (when catalog
                  (update~ catalog)
                  (execute-event
                    (lambda ()
                      (let ((row (user-data-row~ tree project visible?: #f)))
                        (let ((data (second (get-children~ row))))
                          (set-title~ data (get-statistics~ catalog)))
                        (let ((line (row->line~ tree row)))
                          (when line
                            (let ((cell (new Cell line 1)))
                              (invalidate-cell~ tree cell)))))))))))
          
          (for-each (lambda (row)
                      (let ((project (get-user-data~ row)))
                        (for-each-project~ project update-project
                          include-self?: #t)))
                    (effective-rows))))))
  
  
  (method (update-buttons)
    (let ((tree (get-tree)))
      (let ((count (get-row-count~ tree)))
        (set-enabled?~ (locate 'update) (> count 0)))))
  

  ;;;
  ;;;; Session
  ;;;
  
  
  (method override (confirm-close . rest)
    (save-session))
  
  
  (method override (save-session)
    @to-convert
    (let ((info
           (list (external-name)
                 position: (get-position)
                 size:     (get-size))))
      (push-property~ properties 'Opened-Windows info)))))
