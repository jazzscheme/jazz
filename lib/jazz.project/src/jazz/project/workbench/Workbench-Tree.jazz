;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Workbench Tree
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.project.workbench.Workbench-Tree jazz


(import (jazz.action)
        (jazz.component)
        (jazz.designer.ui)
        (jazz.graphic)
        (jazz.jml)
        (jazz.project)
        (jazz.resource))


(class Workbench-Tree extends Descendants-Tree
  
  
  (form
    (<install> portfolio: :images draggable?: #t editable?: #f reorderable?: #t))
  

  (method override (focus-actions)
    (cons (find-actions 'workbench-tree)
          (nextmethod)))
  
  
  (method (on-add-project evt)
    (add-project~ (get-manager)))
  
  
  (method (on-add-document evt)
    (add-document~ (get-manager)))
  
  
  (method (on-add-directory evt)
    (add-directory~ (get-manager)))
  
  
  (method (on-add-group evt)
    (add-group~ (get-manager)))
  
  
  (method (get-manager)
    (get-guest~ (get-host)))

  
  (method override (context-menu evt)
    (let ((pos (get-position~ evt))
          (manager (get-manager)))
      (with-context-click pos
        (lambda ()
          (let ((menu (new-menu~ manager)))
            (when menu
              (track-popup-menu menu pos)))))))
  
  
  ;; This is a big patch, to go around the fact that if a user copy pastes a Jazz-File, there is
  ;; not yet an advance test to check for name duplications so that the error is detected only partway
  ;; through the recursion and that leaves the workbench in an unstable state.
  (method override (can-copy?)
    #f)
  
  
  (method override (can-paste-properties?)
    #f)
  
  
  (method override (get-draggable?)
    ;; until bug fix in the tree selection update
    #f)
  
  
  (method override (delete-selection)
    (delete-selection~ editor message: "Remove selected objects?"))
  
  
  (method override (component-color component owned-form)
    (let ((color (nextmethod component owned?)))
      (if (is? component Project)
          (if (get-active?~ component)
              color
            {Color Medium-Gray})
        color)))
  
  
  (method override (component-image component)
    (let ((image (nextmethod component)))
      (if (is? component Project)
          (if (get-active?~ component)
              image
            (disabled-image-resource image))
        image)))
  
  
  (method override (update-selection expand-state selected-components)
    )
  
  
  (method override (row-state component update? level)
    (if (dynamic-component? component)
        'dynamic
      (if update?
          (if (< level 1) 'expanded 'collapsed)
        'collapsed)))
  
  
  (method (dynamic-component? component)
    (and (is? component Project-Directory)
         (not-null? (get-components~ component))
         (get-parent~ component)
         (is? (get-parent~ component) Project)
         (get-dynamic?~ (get-parent~ component))))
  
  
  (method override (update-row row)
    (let ((component (get-user-data~ row)))
      (when (dynamic-component? component)
        (let ((rights (get-rights))
              (update? #t)
              (level (get-level~ row)))
          (insert-component-list row component (get-components~ component) rights update? level)))))
  
  
  (method override (insert-components row component rights update? level)
    (unless (dynamic-component? component)
      (insert-component-list row component (get-components~ component) rights update? level)))
  
  
  (method override (double-click evt)
    (let ((manager (get-manager))
          (component (selected-component)))
      (if (and (is? component Project)
               (concrete?~ component))
          (activate-selection~ manager)
        (nextmethod evt)
        (unless (or (is? component Project) (is? component Group))
          (launch-selection~ manager)))))
  
  
  (method override (return-press evt)
    (let ((manager (get-manager))
          (component (selected-component)))
      (if (is? component Project)
          (activate-selection~ manager)
        (unless (is? component Group)
          (launch-selection~ manager)))))
  
  
  (method override (receive-files pos pathnames)
    (let* ((cell (view->tree pos))
           (data (if (not cell) (get-workbench) (get-user-data~ (get-row (get-line~ cell)))))
           (manager (get-manager)))
      (if (or (is? data Project)
              (is? data Project-Group))
          (import-pathnames~ manager (ascendant-project~ manager data) pathnames data)
        (bell))))))
