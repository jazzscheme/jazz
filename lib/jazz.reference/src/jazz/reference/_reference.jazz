;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; References
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.reference jazz


(export (jazz.reference.syntax (phase syntax)))


;;;
;;;; Environment
;;;


(interface Environment-Provider
  
  
  (method public virtual abstract (locate-reference self symbol))
  (method public virtual abstract (require-reference self symbol))
  (method public virtual abstract (get-reference self symbol)))


(class Environment extends Object implements Environment-Provider
  
  
  (slot data    getter generate)
  (slot locator getter generate)
    
  
  (method override (initialize self data locator)
    (nextmethod self)
    (set! self.data data)
    (set! self.locator locator))
  
  
  (method override (print self output readably)
    (format output "~{{a} {a}}"
            (category-name (class-of self))
            data))


  (method override (setup-context self context)
    (setup-context data context))
  
  
  (method override (get-reference self symbol)
    (locator symbol))
  
  
  (method override (locate-reference self symbol)
    (let ((reference (locator symbol)))
      (and reference (resolve-runtime-reference reference))))
  
  
  (method override (require-reference self symbol)
    (or (locate-reference self symbol)
        (error "Unable to find reference: {s}" symbol))))


(definition public (construct-environment data locator)
  (let ((environment (new Environment data locator)))
    (setup-context environment environment)
    environment))


;;;
;;;; Reference
;;;


(class Reference extends Object
  
  
  (slot context getter generate)
  (slot symbol  getter generate)
    
  
  (method override (initialize self symbol)
    (nextmethod self)
    (set! self.symbol symbol))
  
  
  (method override (print self output readably)
    (format output "~{{a} {a}}"
            (category-name (class-of self))
            symbol))
  
  
  (method override (walk-references self proc)
    (proc symbol))


  (method override (setup-context self ctx)
    (set! context ctx))
  
  
  (method public (get-runtime-reference self)
    (get-reference context symbol))
  
  
  (method public (reference-resolve self)
    (require-reference context symbol))
  
  
  (method public virtual (parameters-resolve self)
    (reference-resolve self)))


(class Class-Reference extends Reference)


(definition package (walk-reference arguments proc)
  (proc (source-code (car arguments))))


(definition package (construct-reference symbol)
  (new Reference symbol))


(definition public (get-reference-import reference)
  (second (serialize-runtime-reference
            (get-runtime-reference reference))))


;;;
;;;; Class Reference
;;;


(definition package (walk-class-reference arguments proc)
  (proc (source-code (car arguments))))


(definition package (construct-class-reference symbol)
  (new Class-Reference symbol)))
