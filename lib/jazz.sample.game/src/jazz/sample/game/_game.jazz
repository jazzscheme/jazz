;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Games
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Joel Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Guillaume Cartier
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.sample.game jazz


(import (jazz.history))


;;;
;;;; Player
;;;


(class Player extends Object
  
  
  (method protected virtual (player-kind self)
    )
  
  
  (method protected virtual (human? self)
    (eq? (player-kind self) 'human))
  
  
  (method package virtual (computer? self)
    (eq? (player-kind self) 'computer))
  
  
  (method package virtual (computer-move self)
    )
  
  
  ;; for incremental algorithms
  (method package virtual (move-played self move token)
    ))


;;;
;;;; Game
;;;


(class Game extends Object implements History-Provider


  ;; players
  (slot players          initialize #f accessors generate)
  (slot next-player-rank initialize #f accessors generate)
  
  ;; history
  (slot history-manager  initialize #f)
  
  ;; status
  (slot status           initialize #f accessors generate)
  (slot winner           initialize #f accessors generate)

  
  ;;;
  ;;;; Initialize
  ;;;
  
  
  (method override (initialize self)
    (nextmethod self)
    (set! next-player-rank 0)
    (set! history-manager (new History-Manager self))
    (set! status 'play)
    (set! winner #f))

  
  ;;;
  ;;;; History
  ;;;
  
  
  (method override (current-history-item self)
    #f)
  
  
  (method override (update-history-actions self)
    )

  
  ;;;
  ;;;; Players
  ;;;
  
  
  (method public (register-players self lst)
    (set! players (list->vector lst)))
  
  
  (method public (get-next-player self)
    (vector-ref players next-player-rank))
  
  
  (method public (get-second-player self)
    (vector-ref players (modulo-players self (+ next-player-rank 1))))
  
  
  (method protected (next-player! self)
    (prog1 (get-next-player self)
      (set! next-player-rank (modulo-players self (+ next-player-rank 1)))))
  
  
  (method (modulo-players self rank)
    (modulo rank (vector-length players)))
  
  
  ;;;
  ;;;; Moves
  ;;;
  
  
  (method protected (add-move self move)
    (add-history history-manager (new History-Move move)))
  
  
  (method package (append-forward-history self move)
    (append-forward-history history-manager (new History-Move move)))
  
  
  (method public (get-moves self)
    (append (get-backward-moves self)
            (get-forward-moves self)))
  
  
  (method public (get-backward-moves self)
    (map get-move (reverse (get-backward-history history-manager))))
  
  
  (method public (get-forward-moves self)
    (map get-move (get-forward-history history-manager)))
  
  
  ;;;
  ;;;; Move
  ;;;
  
  
  (method package virtual (play self move)
    )
  
  
  (method package virtual (unplay self move)
    )
  
  
  ;;;
  ;;;; Play
  ;;;
  
  
  (method public virtual (play-move self move)
    )
  
  
  (method (inform-players self move token)
    (loop (for n from 0 below (vector-length players))
          (move-played (vector-ref players n) move token)))
  
  
  (method protected virtual (check-status self player move token)
    (cond ((win? self move token)
           (set! status 'win)
           (set! winner player))
          ((draw? self move token)
           (set! status 'draw))))
  
  
  (method protected virtual (win? self move token)
    #f)
  
  
  (method protected virtual (draw? self move token)
    #f))


;;;
;;;; History
;;;


(class History-Move extends History-Item
  
  
  (slot move getter generate)
  
  
  (method override (initialize self move)
    (nextmethod self)
    (set! self.move move))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" move))))))
