;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Data Rows
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Marcel Cote
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.schema.data.Data-Row jazz


(import (jazz.schema))


(class Data-Row extends Data-Container
  
  
  (slot values)
  
  
  (method override (initialize self table state values)
    (nextmethod self table state)
    (set! self.values (or values (map (lambda (column) #f) (get-columns table)))))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" values))))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method override (get-values self)
    values)
  
  
  ;;;
  ;;;; Get/Set
  ;;;
  
  
  (method override (get self name/rank)
    (cond ((integer? name/rank)
           (element values name/rank))
          ((string? name/rank)
           (continuation-capture
             (lambda (return)
               (let ((columns (get-columns table)))
                 (for-each (lambda (column value)
                             (when (ci=? (get-name column) name/rank)
                               (continuation-return return value)))
                           columns
                           values))
               (error "Unknown column: {t}" name/rank))))
          (else
           (map (~ get self) name/rank))))
  
  
  (method public (set self name value)
    (let (iterate (columns (get-columns table))
                  (scan values))
      (if (or (null? columns) (null? scan))
          (error "Unknown column: {t}" name)
        (let ((column (car columns)))
          (if (ci=? (get-name column) name)
              (set-car! scan value)
            (iterate (cdr columns) (cdr scan)))))))))
