;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Datatypes
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.schema.data.Datatype jazz


(import (jazz.database)
        (jazz.schema))


(class Datatype extends Object
  
  
  (slot name      getter generate)
  (slot size      getter generate)
  (slot precision getter generate)
  (slot scale     getter generate)
  
  
  (method meta public (new-datatype self name size (precision #f) (scale #f))
    (new self name
         (if (get-sizeable? (find-systype self name)) size #f)
         (if (get-precisionable? (find-systype self name)) precision #f)
         (if (get-scalable? (find-systype self name)) scale #f)))
  
  
  (method override (initialize self name size (precision #f) (scale #f))
    (nextmethod self)
    (set! self.name name)
    (set! self.size size)
    (set! self.precision precision)
    (set! self.scale scale))
  
  
  (method override (compare-object self target)
    (cond ((is-not? target Datatype)
           :incompatible)
          ((and (= name (get-name target))
                (= size (get-size target))
                (= precision (get-precision target))
                (= scale (get-scale target)))
           :equal)
          (else
           :not-equal)))
  
  
  (method override (print self output readably)
    (format output "~{{a} {s} {s}{? {s}~}{? {s}~}}"
            (category-name (class-of self))
            name
            size
            precision
            scale))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (has-size? self)
    (boolean size))
  
  
  (method public (has-precision? self)
    (boolean precision))
  
  
  (method public (has-scale? self)
    (boolean scale))
  

  (method public virtual (get-native-with-value self value (indirect?: indirect? #f))
    (let* ((default-value (get-default-value (find-systype (class-of self) name)))
           (value (or (let ((len (length value)))
                        (if (and (>= len 2)
                                 (= (subseq value 0 1) "(")
                                 (= (subseq value (- len 1) len) ")"))
                            (subseq value 1 (- len 1))
                          value))
                      (cond ((string? default-value) (format "'{a}'" default-value))
                            (else default-value)))))
      (cond ((number? default-value) value)
            ((string? default-value) (if indirect? (format "{a}" (double-apostrophe value))
                                       (format "{a}" value)))
            ((eq? value 'read-only) value)
            (else "NULL"))))


  (definition (double-apostrophe value)
    (substitute-alist '((#\' . "''")) value))
  
  
  (method public virtual (get-native self)
    (cond (size
           (format "{a}({a})" name size))
          ((null? precision)
           (format "{a}" name))
          @always-display-scale
          ((null? scale)
           (format "{a}({a})" name precision))
          (else
           (format "{a}({a},{a})" name precision scale))))
  
  
  ;;;
  ;;;; Systypes
  ;;;
  
  
  (method meta public virtual (get-systypes self)
    '())
  
  
  (method meta public (find-systype self name)
    (find-if (lambda (systype)
               (= (get-name systype) name))
             (get-systypes self)))
  
  
  (method public (get-defaultable? self)
    (let ((systype (find-systype (class-of self) name)))
      (and systype (get-defaultable? systype))))
  
  
  (method public (get-default-value self)
    (let ((systype (find-systype (class-of self) name)))
      (and systype (get-default-value systype))))
  
  
  ;;;
  ;;;; Presentation
  ;;;
  
  
  (method public virtual (present self)
    (cond (size
           (format "{a} {a}" name size))
          ((null? precision)
           (format "{a}" name))
          @always-display-scale-for-physical
          ((null? scale)
           (format "{a} {a}" name precision))
          (else
           (format "{a} {a} {a}" name precision scale))))))
