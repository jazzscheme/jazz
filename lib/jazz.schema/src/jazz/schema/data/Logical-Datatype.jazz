;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Logical Datatypes
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.schema.data.Logical-Datatype jazz


(import (jazz.schema))


(class Logical-Datatype extends Object
  
  
  (slot name      getter generate)
  (slot size      getter generate)
  (slot precision)
  (slot scale)
  
  
  @convert-literal
  (method meta (construct-literal context name size (precision #f) (scale #f))
    (assert (or (integer? size) (null? size)))
    (assert (or (integer? precision) (null? precision)))
    (assert (or (integer? scale) (null? scale)))
    (new self name size precision scale))
    
    
  (method override (initialize self name size (precision #f) (scale #f))
    (nextmethod self)
    (set! self.name name)
    (set! self.size size)
    (set! self.precision precision)
    (set! self.scale scale))


  (method override (compare-object self target)
    (cond ((is-not? target Logical-Datatype)
           :incompatible)
          ((and (= name (get-name target))
                (= size (get-size target))
                (= precision (get-precision target))
                (= scale (get-scale target)))
           :equal)
          (else
           :not-equal)))
  
  
  (method override (print self output readably)
    (format output "~{{a} {s} {s}{? {s}~}{? {s}~}}"
            (category-name (class-of self))
            name
            size
            precision
            scale))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (has-size? self)
    (not-null? size))
  
  
  (method public (has-precision? self)
    (not-null? precision))
  
  
  (method public (has-scale? self)
    (not-null? scale))
 
  
  ;;;
  ;;;; Presentation
  ;;;
  
  
  (method public virtual (present self)
    (if (null? size)
        (if (null? precision)
            (format "{a}" name)
          (format "{a} {a} {a}" name precision scale))
      (format "{a} {a}" name size)))))
