;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Search Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.search.Search-Manager jazz


(import (jazz.action)
        (jazz.markup)
        (jazz.resource)
        (jazz.search)
        (jazz.ui)
        (jazz.view))


(class undocumented Search-Manager extends View
  

  (form
    (<install>              size: {Dimension 857 169}
      (<Tab-View> name: tab position: {Point 4 5} size: {Dimension 848 159})))
  
  
  (method meta override (external-name self . rest)
    'search-manager)
    
  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon self)
    {Image-Resource "Find"})
  
  
  (method meta override (host-size self)
    {Dimension 857 169})
  
  
  (method meta override (host-title self)
    "Search")
        

  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions self)
    (cons (find-actions 'search-manager)
          (nextmethod self)))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (resize self dh dv)
    (size-figure (locate self 'tab) dh dv))
  
  
  (method (on-whole-words self evt)
    (toggle-whole-words self))
  
  
  (method (on-ignore-case self evt)
    (toggle-ignore-case self))
  
  
  (method (on-regexp self evt)
    (toggle-regexp self))
  
  
  (method (on-preserve-case self evt)
    (toggle-preserve-case self))
  
  
  (method package (on-play-recording self evt)
    (toggle-play-recording self))
  
  
  (method (toggle-whole-words self)
    (let ((checkbox (locate (get-search self) 'whole-words?)))
      (set-checked? checkbox (not (get-checked? checkbox)))))
  
  
  (method (toggle-ignore-case self)
    (let ((checkbox (locate (get-search self) 'ignore-case?)))
      (set-checked? checkbox (not (get-checked? checkbox)))))
  
  
  (method (toggle-regexp self)
    (let ((checkbox (locate (get-search self) 'regexp?)))
      (set-checked? checkbox (not (get-checked? checkbox)))))
  
  
  (method (toggle-preserve-case self)
    (let ((checkbox (locate (get-search self) 'preserve-case?)))
      (set-checked? checkbox (not (get-checked? checkbox)))))
  
  
  (method package (toggle-play-recording self)
    (let ((checkbox (locate (get-search self) 'play-recording?)))
      (set-checked? checkbox (not (get-checked? checkbox)))))

  
  ;;;
  ;;;; Search
  ;;;
  
  
  (method public (get-search self)
    (let* ((tab (locate self 'tab))
           (sheet (selected-sheet tab)))
      (get-sheet-search sheet)))
  
  
  (method public (get-search-named self name)
    (let* ((tab (locate self 'tab))
           (sheet (child tab name)))
      (when sheet
        (get-sheet-search sheet))))
  
  
  (method public (get-searches self)
    (let ((tab (locate self 'tab)))
      (map get-sheet-search (get-children tab))))
  
  
  (definition (get-sheet-search sheet)
    (first-child (get-content sheet)))
  
  
  ;;;
  ;;;; History
  ;;;
  
  
  (method package (current-history-item self)
    (current-history-item (get-search self)))
  
  
  (method package (install-history-item self item)
    (install-history-item (get-search self) item))
  
  
  ;;;
  ;;;; Definitions
  ;;;
  
  
  (method public (edit-definitions self search definitions (sort?: sort? #t))
    (let ((results (get-search-results)))
      (select-results results (make-definition-search-resultset search definitions) sort?: sort?)))
  
  
  (method public (edit-references self search references)
    (let ((results (get-search-results)))
      (select-results results (make-reference-search-resultset search references))))))
