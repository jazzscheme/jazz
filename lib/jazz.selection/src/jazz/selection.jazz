;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Selection Management
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.selection jazz


(hub allow-start-drag?)
(hub change-selection?)
(hub delay-selection)
(hub double-click)
(hub end-delayed?)
(hub get-alt?)
(hub get-control?)
(hub get-modifiers)
(hub get-mouse-view)
(hub get-shift?)
(hub locked?)
(hub mouse-down)
(hub mouse-part-control?)
(hub mouse-down?)
(hub mouse-up)
(hub mouse-up?)
(hub reset)
(hub set-modifiers)
(hub with-locked-operation)


;;;
;;;; Selection-Provider
;;;


(interface undocumented Selection-Provider)


;;;
;;;; Selection-Manager
;;;


(class undocumented Selection-Manager extends Object
  
  
  (property modifiers           initialize 0  getter generate)
  (property shift?              initialize #f getter generate)
  (property control?            initialize #f getter generate)
  (property alt?                initialize #f getter generate)
  (property multiple-selection? initialize #f getter generate)
  (property mouse-part          initialize #f getter generate)
  (property mouse-view          initialize #f getter generate)
  
  
  (slot provider          initialize #f)
  (slot current-operation initialize #f)
  (slot delay-selection?  initialize #f)
  
  
  (method override (initialize self p)
    (nextmethod self)
    (set! provider p))
  
  
  ;;;
  ;;;; Accessors
  ;;;
  
  
  (method public (debug-current-operation self)
    current-operation)
  
  
  (method public (mouse-down? self)
    (eq? current-operation 'mouse-down))
  
  
  (method public (mouse-up? self)
    (eq? current-operation 'mouse-up))
  
  
  (method public (mouse-double-down? self)
    (eq? current-operation 'mouse-double-down))
  
  
  (method public (mouse-double-up? self)
    (eq? current-operation 'mouse-double-up))
  
  
  (method public (keyboard? self)
    (eq? current-operation 'keyboard))
  
  
  (method public (locked? self)
    (eq? current-operation 'locked))
  
  
  (method package (set-modifiers self s? c? a?)
    (set! shift? s?)
    (set! control? c?)
    (set! alt? a?))
  
  
  ;;;
  ;;;; Steps
  ;;;
  
  
  (method public (with-locked-operation self proc)
    (let ((old-operation current-operation))
      (unwind-protect
          (begin
            (set! current-operation 'locked)
            (proc))
        (set! current-operation old-operation))))
  
  
  (method public (collapse? self row)
    )
  
  
  (method public (expand? self row)
    )
  
  
  (method public (end-delayed? self)
    (if (and multiple-selection? (not shift?) (not control?) (eq? mouse-part 'content))
        (mouse-up? self)
      (not (mouse-up? self))))
  
  
  (method public (allow-start-drag? self)
    (and (eq? mouse-part 'content)
         (mouse-down? self)))
  
  
  (method public (mouse-part-control? self)
    (eq? mouse-part 'control))
  
  
  ;;;
  ;;;; Actions
  ;;;
  
  
  (method public (delay-selection self)
    (set! delay-selection? #t))
  
  
  (method public (change-selection? self)
    (case current-operation
      ((mouse-down) (not delay-selection?))
      ((mouse-up) delay-selection?)
      (else #t)))
  
  
  (method public (mouse-down self view part shift? control? alt? ms?)
    (set! current-operation 'mouse-down)
    (set! mouse-view view)
    (set! mouse-part part)
    (set-modifiers self shift? control? alt?)
    (set! multiple-selection? ms?))
  
  
  (method public (mouse-up self shift? control? alt?)
    (if (mouse-double-down? self)
        (set! current-operation 'mouse-double-up)
      (set! current-operation 'mouse-up))
    (set-modifiers self shift? control? alt?))
  
  
  (method public (double-click self shift? control? alt?)
    (set! current-operation 'mouse-double-down)
    (set-modifiers self shift? control? alt?))
  
  
  (method public (keyboard self shift? control? alt?)
    (set! current-operation 'keyboard)
    (set-modifiers self shift? control? alt?))
  
  
  (method public (reset self)
    (set! current-operation #f)
    (set! mouse-part #f)
    (set-modifiers self #f #f #f)
    (set! multiple-selection? #f)
    (set! delay-selection? #f))))
