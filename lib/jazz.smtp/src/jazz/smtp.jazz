;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; SMTP
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Jeremie Lasalle Ratelle.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Guillaume Cartier
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.smtp jazz


(import (base64)
        (jazz.io)
        (jazz.time))


;;;
;;;; Message
;;;


(class MIME-Message extends Object
  
  
  (slot from              getter generate)
  (slot to                getter generate)
  (slot subject           getter generate)
  (slot message           getter generate)
  (slot cc                getter generate)
  (slot bcc               getter generate)
  (slot content-type      getter generate)
  (slot attachments       getter generate)
  (slot quoted-printable? getter generate)
  
  
  (method override (initialize self from to subject message (cc: cc '()) (bcc: bcc '()) (content-type: content-type #f) (attachments: attachments '()) (quoted-printable?: quoted-printable? #t))
    (set! self.from from)
    (set! self.to to)
    (set! self.subject subject)
    (set! self.message message)
    (set! self.cc cc)
    (set! self.bcc bcc)
    (set! self.content-type content-type)
    (set! self.attachments attachments)
    (set! self.quoted-printable? quoted-printable?))
  
  
  (definition boundary
    "CF69BC42-C9CB-11E0-82A0-A5C34824019B")
  
  (definition line-size
    72)
  
  (definition encoded-subject-size
    60)
  
  (definition attachment-buffer-size
    (* 3 (/ line-size 4)))
  
  (definition subject-buffer-size
    (* 3 (/ encoded-subject-size 4)))
  
  (definition separator
    (format "--{a}\n" boundary))
  
  (definition ending
    (format "--{a}--\n" boundary))
  
  
  (method (remove-unicode-characters self string)
    (list->string
      (map (lambda (ch)
             (let ((n (char->integer ch)))
               (if (> n 255) #\space ch)))
           (string->list string))))
  
  
  (method (format-mime-date self date)
    (date->string date "~d ~b ~Y ~H:~M:~S ~z"))
  
  
  (method (make-mime-header self)
    (string-append (format "From: {a}{%}" from)
                   (format "To: {a}{%}" (join to ", "))
                   (format "{?:Cc: {a}{%}~}" (not-null? cc) (join cc ", "))
                   (format "{?:Bcc: {a}{%}~}" (not-null? bcc) (join bcc ", "))
                   (format "Date: {a}{%}" (format-mime-date self (system-date)))
                   (format "Subject: {a}{%}" (encode-subject self))
                   "MIME-Version: 1.0\n"))
  
  
  (method (send-message self port)
    (let ((length (string-length message)))
      (let ((last-index (* line-size (quotient length line-size))))
        (let (loop (i 0))
          (if (< i last-index)
              (let ((next-i (+ i line-size)))
                (write-substring message i next-i port)
                (newline port)
                (loop next-i))
            (write-substring message i length port))))))
  
  
  (method (encode-subject self)
    (let ((buffer (make-u8vector subject-buffer-size))
          (u8-port (open-u8vector '(char-encoding: UTF-8)))
          (string-port (open-output-string)))
      (display subject u8-port)
      (force-output u8-port)
      (close-output-port u8-port)
      (let (loop)
        (let ((size (read-subu8vector buffer 0 subject-buffer-size u8-port)))
          (if (> size 0)
              (let ((last? (< size subject-buffer-size)))
                (let ((buffer (if last? (subu8vector buffer 0 size) buffer)))
                  (display "=?utf-8?B?" string-port)
                  (display (u8vector->base64-string buffer) string-port)
                  (display "?=" string-port)
                  (unless last? (display "\n " string-port))
                  (loop)))
            (get-output-string string-port))))))
  
  
  (method (send-file self file port)
    (let ((buffer (make-u8vector attachment-buffer-size)))
      (let (loop)
        (let ((size-read (read-subu8vector buffer 0 attachment-buffer-size file)))
          (if (< size-read attachment-buffer-size)
              (let ((sub-buffer (subu8vector buffer 0 size-read)))
                (display (u8vector->base64-string sub-buffer) port))
            (display (u8vector->base64-string buffer) port)
            (newline port)
            (loop))))))
  
  
  (method package (send-on self port)
    (display (make-mime-header self) port)
    (unless (null? attachments)
      (display (format "Content-Type: multipart/mixed; boundary={a}\n" boundary) port)
      (display separator port))
    (display (format "Content-Type: {a}; charset=\"UTF-8\"\n" (or content-type 'text/plain)) port)
    (when quoted-printable?
      (display "Content-Transfer-Encoding: quoted-printable\n\n" port))
    (display (if quoted-printable? (string->quoted-printable-string message) message) port)
    (display "\n\n" port)
    (for-each (lambda (file)
                (display separator port)
                (display "Content-Type: application/octet-stream\n" port)
                (display (format "Content-Disposition: attachment; filename={s}\n" (get-name file)) port)
                (display "Content-Transfer-Encoding: base64\n\n" port)
                (call-with-input-file (parse file)
                  (lambda (file-port)
                    (send-file self file-port port)))
                (display "\n\n" port))
              attachments)
    (display (if (null? attachments) "\n\n" ending) port)))


;;;
;;;; Client
;;;


(class SMTP-Client extends Object
  
  
  (slot port)
  
  
  (method override (initialize self server-address (port-number: port-number 25) (username: username "") (password: password ""))
    (set! port (open-tcp-client (list
                                  server-address: server-address
                                  port-number: port-number
                                  eol-encoding: 'cr-lf
                                  buffering: 'line
                                  keep-alive: #t)))
    (get-reply self "220" #f)
    (display (format "EHLO {a}\n" server-address) port)
    (let ((commands (cdr (get-reply self "250" #f))))
      (let ((login-methods (get-login-methods self commands)))
        (cond ((member? "LOGIN" login-methods)
               (auth-login self username password))
              ((member? "PLAIN" login-methods)
               (auth-plain self username password))
              (else
               (error "No supported login methods"))))))
  
  
  (method override (destroy self)
    (display "QUIT\n" port)
    (close-port port)
    (nextmethod self))
  
  
  (method (get-reply self code (invalid #f))
    (let ((reply (read-line port)))
      (if (eof-object? reply)
          (error "Unexpected end of file")
        (unless (string-starts-with? reply code)
          (if invalid
              (invalid)
            (error "Expected {a}, received {a}" code reply)))
        (if (string-starts-with? reply (format "{a}-" code))
            (cons (substring reply 4 (string-length reply))
                  (get-reply self code invalid))
          '()))))
  
  
  (method (get-login-methods self commands)
    (cdr (split (find commands "AUTH" test: (lambda (target str) (string-starts-with? str target)) return: 'item) " ")))
  
  
  (method (auth-login self username password)
    (display "AUTH LOGIN\n" port)
    (get-reply self "334")
    (display (ascii-string->base64-string username) port)
    (newline port)
    (get-reply self "334" (lambda () (error "Invalid username.")))
    (display (ascii-string->base64-string password) port)
    (newline port)
    (get-reply self "235" (lambda () (error "Invalid credentials."))))
  
  
  (method (auth-plain self username password)
    (display "AUTH PLAIN\n" port)
    (get-reply self "334")
    (display (ascii-string->base64-string 
               (list->string
                 (append '(#\nul) (string->list username) '(#\nul) (string->list password)))) port)
    (newline port)
    (get-reply self "235" (lambda ()
                       (throw "Invalid credentials."))))
  
  
  (method package (send-email-message self mail <MIME-Message>)
    (display "RSET\n" port)
    (get-reply self "250")
    
    (display (format "MAIL FROM:<{a}>\n" (get-from mail)) port)
    (get-reply self "250" (lambda () (error (format "Error adding sender {a}." (get-from mail)))))
      
    (for-each (lambda (recipient)
                (display (format "RCPT TO: {a}\n" recipient) port)
                (get-reply self "250" (lambda () (error (format "Error adding recipient {a}." recipient)))))
              (get-to mail))
    
    (display "data\n" port)
    (get-reply self "354" (lambda () (error "Error sending the message. Server not accepting data.")))
    
    (send-on mail port)
    
    (display "\n.\n" port)
    (get-reply self "250" (lambda () (error "Error sending the message.")))))


(definition public (send-email message server (port-number: port-number 25) (username: username "") (password: password ""))
  (let ((client (new SMTP-Client server port-number: port-number username: username password: password)))
    (send-email-message client message)
    (close client)))


(definition public (string->quoted-printable-string string)
  (let ((u8-port (open-u8vector '(char-encoding: UTF-8)))
        (string-port (open-output-string)))
    (display string u8-port)
    (force-output u8-port)
    (close-output-port u8-port)
    (let (loop (col 0) (byte (read-u8 u8-port)))
      (if (= col 75)
          (begin
            (display "=\n" string-port)
            (loop 0 byte))
        (let ((next (read-u8 u8-port)))
          (cond ((eof-object? byte) (get-output-string string-port))
                ((or (= byte 10)
                     (= byte 13)
                     (and (<= 33 byte) (<= byte 60))
                     (and (<= 62 byte) (<= byte 126))
                     (and (or (= byte 32) (= byte 9))
                          (not (or (eof-object? next) (= next 10) (= next 13)))))
                 (display (integer->char byte) string-port)
                 (loop (+ col 1) next))
                ((>= col 73)
                 (display "=\n" string-port)
                 (display #\= string-port)
                 (display (number->string byte 16) string-port)
                 (loop 3 next))
                (else
                 (display #\= string-port)
                 (display (number->string byte 16) string-port)
                 (loop (+ col 3) next)))))))))
