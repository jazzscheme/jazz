;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Process Snapshots
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.snapshot jazz


(import (jazz.debuggee)
        (jazz.inspector)
        (jazz.io)
        (jazz.locale)
        (jazz.platform)
        (jazz.process)
        (jazz.syntax (phase syntax))
        (jazz.time))


(declare (proper-tail-calls))


;;;
;;;; Settings
;;;


(define-setting snapshots-directory
  {Directory Settings "crashes"})

(define-setting snapshot-max-depth
  2)

(define-setting snapshot-max-width
  1024)

(define-setting snapshot-max-content
  256)


;;;
;;;; Callbacks
;;;


(definition *snapshot-callbacks*
  '())


(definition public (register-snapshot-callbacks proc)
  (assert (procedure? proc))
  (set! *snapshot-callbacks* (cons proc *snapshot-callbacks*)))


(definition public (get-snapshot-callback-continuations)
  (apply append (map (lambda (proc) (proc)) *snapshot-callbacks*)))


;;;
;;;; Exception Filter
;;;


(definition public (with-exception-snapshot filter catcher thunk)
  (with-exception-filter
    (lambda (exc)
      (and (filter exc)
           (begin
             (if (using-debugger?)
                 (let ((reason (exception-reason exc))
                       (detail (exception-detail exc)))
                   (break-internal reason: reason detail: detail exception: exc))
               (let ((reason (exception-reason exc)))
                 (snapshot-process directory: {Directory Settings "problems"} reason: reason exception: exc)))
             #t)))
    catcher
    thunk))


(definition public (new-safe-thread catcher thunk . rest)
  (apply new-thread
         (lambda ()
           (with-exception-snapshot
             (lambda (exc)
               #t)
             catcher
             thunk))
         rest))


;;;
;;;; Snapshot
;;;


(definition snapshot-mutex
  (make-mutex 'snapshot))


(definition (create-timestamped-file directory prefix extension)
  (with-exception-catcher
    (lambda (exc)
      (console "Snapshot timestamped file creation failed" exc)
      #f)
    (lambda ()
      (let ((file (timestamped-file (or directory (snapshots-directory)) (or prefix (->string (current-process-present))) (or extension "snapshot"))))
        (create file)
        file))))


(definition (call-with-snapshot-output-file args proc)
  (bind-keywords ((directory #f) (prefix #f) (extension #f) . others) args
    (let ((file (create-timestamped-file directory prefix extension)))
      (call-with-output-file (path-settings file)
        (lambda (output)
          (apply proc output others)))
      file)))


(definition public (snapshot-process . rest)
  ;; disabling interrupts is not enough because if we block on some operation then
  ;; control could go to another thread that could end up calling snapshot-process again
  (mutex-lock! snapshot-mutex)
  (disable-interrupts!)
  ;; note that if we are not exiting these won't get restored
  (clear-gc-interrupt-jobs!)
  (unwind-protect (continuation-capture
                    (lambda (continuation)
                      (call-with-snapshot-output-file rest
                        (lambda (output . rest)
                          (apply print-process-snapshot continuation output rest)))))
    (enable-interrupts!)
    (mutex-unlock! snapshot-mutex)))


(definition public (snapshot-current-thread . rest)
  (continuation-capture
    (lambda (continuation)
      (call-with-snapshot-output-file rest
        (lambda (output . rest)
          (apply print-process-snapshot continuation output current-thread-only?: #t rest))))))


(definition (print-process-snapshot continuation output (reason: reason #f) (exception: exception #f) (max-depth: max-depth (snapshot-max-depth)) (max-width: max-width (snapshot-max-width)) (max-content: max-content (snapshot-max-content)) (current-thread-only?: current-thread-only? #f))
  (let ((cache (make-table test: eqv?)))
    (define package-info-serial first)
    (define package-info-class second)
    (define package-info-presentation third)
    (define package-info-more? fourth)
    
    (define (print-process)
      (define (present-process-version)
        (let ((process (current-process)))
          (and process
               (present (process-version process)))))
      
      (let ((name (current-process-present))
            (version (present-process-version))
            (date (present-date (system-date)))
            (id (current-process-id)))
        (format output "(<process> name: {s}{? version: {s}~} date: {s} id: {s}" name version date id))
      (when reason
        (format output " reason: {s}" reason))
      (when exception
        (format output " exception: {s}" (exception-reason exception)))
      (force-output output)
      (print-threads)
      (force-output output)
      (print-values)
      (format output "){%}"))
    
    (define (print-threads)
      (format output "{%}")
      (format output "  (<threads>")
      (print-thread (current-thread) (or (and exception (get-exception-context exception)) continuation))
      (unless current-thread-only?
        (for-each (lambda (thread)
                    (when (neq? thread (current-thread))
                      (let ((continuation (thread-continuation thread)))
                        (when continuation
                          (print-thread thread continuation)))))
                  (top-threads))
        (for-each print-continuation
                  (get-snapshot-callback-continuations)))
      (format output ")"))
    
    (define (print-thread thread continuation)
      (define (stop-kind thread)
        (let ((stops (thread-active-stops thread)))
          (and (not-null? stops)
               (get-kind (last stops)))))
      
      (format output "{%}")
      (let ((name (thread-name thread))
            (state (present-thread-state (thread-state thread)))
            (stop (stop-kind thread)))
        (format output "    (<thread>")
        (when (and name (specified? name))
          (format output " name: {s}" name))
        (when state
          (format output " state: {s}" state))
        (when stop
          (format output " stop: {s}" stop)))
      (force-output output)
      (when continuation
        (print-stack continuation))
      (format output ")"))
    
    (define (print-continuation continuation)
      (when continuation
        (format output "{%}")
        (let ((serial (object->serial continuation)))
          (unless (table-ref cache serial #f)
            (let ((info (package-info continuation mutable?: #t max-width: max-width)))
              (table-set! cache serial info)))
          (format output "    (<thread>")
          (format output " name: {s}" serial))
        (force-output output)
        (when (continuation? continuation)
          (print-stack continuation))
        (format output ")")))
    
    (define (print-stack continuation)
      (loop (for cont in (get-continuation-stack continuation #f))
            (do (let ((name (get-continuation-name cont))
                      (location (get-continuation-location cont))
                      (variables (get-continuation-lexical-environment cont)))
                  (format output "{%}")
                  (format output "      (<frame> name: {s}" name)
                  (when location
                    (format output " location: {s}" location))
                  (force-output output)
                  (loop (for variable in variables)
                        (do (format output "{%}")
                            (bind (name value mutable?) variable
                              (let ((serial (object->serial value)))
                                (let ((info (or (table-ref cache serial #f)
                                                (let ((info (package-info value mutable?: mutable? max-width: max-width)))
                                                  (table-set! cache serial info)
                                                  info))))
                                  (let ((presentation (package-info-presentation info)))
                                    (let ((atomic? (and (or (atom? value)
                                                            (unspecified? value))
                                                        ;; this test is necessary as object->string can return a truncated unreadable string
                                                        (< (string-length presentation) max-width))))
                                      (format output "        (<variable> name: {s} serial: {s}{?: presentation: {a}~})" name serial atomic? presentation)
                                      (force-output output))))))))
                  (format output ")")
                  (force-output output)))))
    
    (define (print-values)
      (let ((content-cache (make-table test: eqv?)))
        (let ((depth-values (map (lambda (pair) (serial->object (package-info-serial (cdr pair)))) (table->list cache))))
          (loop (repeat max-depth)
                (let ((missed-values '()))
                  (define (packager value (kind: kind :value))
                    (let ((serial (object->serial value)))
                      (if (table-ref cache serial #f)
                          serial
                        (let ((info (package-info value kind: kind max-width: max-width)))
                          (table-set! cache serial info)
                          (set! missed-values (cons value missed-values))
                          serial))))
                  
                  (for-each (lambda (value)
                              (let ((serial (object->serial value)))
                                (when (package-info-more? (table-ref cache serial))
                                  (let ((content (inspect-value value max-width: max-width max-content: max-content packager: packager)))
                                    (unless (null? content)
                                      (table-set! content-cache serial content))))))
                            depth-values)
                  (set! depth-values missed-values))))
        (format output "{%}")
        (format output "    (<values>")
        (force-output output)
        (for-each (lambda (pair)
                    (bind (key serial class presentation more? mutable? kind) pair
                      (format output "{%}")
                      (format output "      (<value> serial: {a} class: {s} presentation: {s}" serial class presentation)
                      (force-output output)
                      (let ((content (table-ref content-cache serial '())))
                        (when (not-null? content)
                          (format output " content: {s}" content)))
                      (format output ")")
                      (force-output output)))
                  (sort < (table->list cache) key: car))
        (format output ")")
        (force-output output)))
    
    (with-exception-handler
      (lambda (exc)
        (format output "{%}{%}")
        (format output "*** INTERNAL ERROR ***{%}")
        (force-output output)
        (format output "{a}{%}{%}" (exception-reason exc))
        (force-output output)
        (continuation-capture
          (lambda (cont)
            (display-continuation-backtrace cont output)))
        (force-output output)
        (exit -1))
      (lambda ()
        (print-process)))))


;;;
;;;; Exception
;;;


(definition public (snapshot-exception-debugger (feedback: feedback #f))
  (lambda (exception)
    (with-system-exception-debugger
      (lambda ()
        (let ((snapshot (snapshot-process exception: exception)))
          (when feedback
            (feedback exception snapshot)))
        (exit-no-jobs 1)))))


(definition public (application-exception-debugger)
  (snapshot-exception-debugger
    feedback: application-exception-feedback))


(definition public (application-exception-feedback exception snapshot)
  (let ((process (current-process)))
    (system-message (format "{a} {a}{%}{%}{a}.{%}{a}.{%}{%}{a}{%}    {a}{a}"
                            (localize {Locales english: "An unexpected problem occurred while executing" french: "Une erreur imprévue s'est produite lors de l'exécution de"})
                            (current-process-present)
                            (localize {Locales english: "This will terminate the application immediately" french: "Ceci entraine la terminaision immédiate de l'application"})
                            (localize {Locales english: "Unsaved work currently in progress will be lost" french: "Tout travaux en cours non sauvegardés seront perdus"})
                            (localize {Locales english: "A log file has been generated at" french: "Un fichier de log a été généré à"})
                            (parse snapshot)
                            (if process
                                (let ((email (process-email process)))
                                  (if email
                                      (format "{%}{%}{a}{%}{a}: {a}."
                                              (localize {Locales english: "Help us fix this problem by sending the log file and any comments" french: "Aidez nous à corriger ce problème en fesant parvenir le fichier de log"})
                                              (localize {Locales english: "about how the problem occurred to" french: "ainsi que tout commentaires explicatifs à"})
                                              email)
                                    ""))
                              ""))
                    title: (localize {Locales english: "Application Error" french: "Erreur d'application"})
                    type: 'problem))))
