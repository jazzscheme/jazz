;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Stream Syntax
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.stream.syntax jazz


;;;
;;;; External
;;;


(definition tracable?
  #t)


(macro public (gst-external signature . rest)
  (define (parse-rest)
    (let ((first (car rest)))
      (if (or (memq? first '(:alloc :free))
              (and (pair? first) (eq? (car first) :free)))
          (cons (list (car rest)) (cdr rest))
        (cons '() rest))))
  
  (bind (modifiers type . rest) (parse-rest)
    (let* ((s-name (car signature))
           (params (cdr signature))
           (c-name-or-code (if (null? rest) (symbol->string s-name) (car rest)))
           (c-name (string->symbol (string-append (symbol->string s-name) "$c")))
           (error? (eq? type :gboolean))
           (type (if (eq? type :gboolean) 'gboolean type)))
      (if (and (not tracable?)
               (not error?))
          `(c-external (,s-name ,@params) ,@modifiers ,type ,c-name-or-code)
        `(begin
           (c-external (,c-name ,@params) ,@modifiers ,type ,c-name-or-code)
           (definition public ,s-name
             (lambda rest
               ,@(if (not tracable?)
                     '()
                   `((when (trace-gst?)
                       ((trace-gst) ',s-name rest))))
               ,(if (not error?)
                    `(apply ,c-name rest)
                  (let ((result (generate-symbol "result")))
                    `(let ((,result (apply ,c-name rest)))
                       (if (= ,result 0)
                           (error "Call to {a} failed" ',s-name)
                         ,result)))))))))))


;;;
;;;; Pipeline
;;;


(syntax public gst-pipeline2
  (lambda (form-src usage-environment macro-environment)
    (let ((elements (cdr (source-code form-src))))
      (sourcify-deep-if
        `(let ()
           (define (element . rest)
             (apply gst-element rest))
           
           (define (adder . rest)
             (apply gst-element "adder" rest))
           
           (define (appsrc . rest)
             (apply gst-element "appsrc" rest))
           
           (define (appsink . rest)
             (apply gst-element "appsink" rest))
           
           (define (audioconvert . rest)
             (apply gst-element "audioconvert" rest))
           
           (define (audiosink . rest)
             (let ((name (cond-expand
                           (windows "directsoundsink")
                           (mac "osxaudiosink")
                           (else "pulsesink"))))
               (apply gst-element name rest)))
           
           (define (audiotestsrc . rest)
             (apply gst-element "audiotestsrc" rest))
           
           (define (queue . rest)
             (apply gst-element "queue" rest))
           
           (define (vorbisenc . rest)
             (apply gst-element "vorbisenc" rest))
           
           (define (webrtcaudioprobe . rest)
             (apply gst-element "webrtcaudioprobe" rest))
          
           (gst-pipeline ,@elements))
        form-src))))


;;;
;;;; Unref
;;;


(define (expand-with-unref bindings body)
  `(let ,bindings
     (prog1 (let ()
              ,@(if (null? body)
                    '((unspecified))
                  body))
       ,@(map (lambda (binding)
                (let ((variable (car (source-code binding))))
                  `(if (null/pair? ,variable)
                       (for-each gst_object_unref ,variable)
                     (gst_object_unref ,variable))))
              bindings))))


(syntax public with-unref
  (lambda (form-src usage-environment macro-environment)
    (let ((bindings (source-code (cadr (source-code form-src))))
          (body (cddr (source-code form-src))))
      (sourcify-deep-if
        (expand-with-unref bindings body)
        form-src)))))
