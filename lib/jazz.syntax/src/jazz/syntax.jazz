;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Syntax
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.syntax jazz


(import (jazz.core (phase syntax)))


;;;
;;;; Setting
;;;


(syntax public define-setting
  (lambda (form-src usage-environment macro-environment)
    (bind (name . rest) (cdr (source-code form-src))
      (let ((name (source-code name)))
        (let ((expr (if (null? rest) #f (car rest)))
              (global (compose-identifier name 'setting)))
          (sourcify-deep-if
            `(begin
               (define ,global ,expr)
               (definition public (,name . rest)
                 (if (null? rest)
                     ,global
                   (set! ,global (car rest)))))
            form-src))))))


;;;
;;;; Delayed
;;;


(syntax public export-delayed
  (lambda (form-src usage-environment macro-environment)
    (bind (module-hack module-name . exports) (cdr (source-code form-src))
      (let ((module-hack (source-code module-hack))
            (module-name (source-code module-name))
            (exports (source-code exports)))
        (sourcify-deep-if
          `(begin
             ,@(map (lambda (export)
                      (let ((export (source-code export)))
                        `(definition public ,export
                           (lambda rest
                             (load-unit ',module-name)
                             (inline (set! ,(string->symbol (string-append (symbol->string module-hack) ":" (symbol->string export)))
                                           ,(string->symbol (string-append (symbol->string module-name) ":" (symbol->string export)))))
                             (apply ,export rest)))))
                    exports))
          form-src)))))


;;;
;;;; Lexical
;;;


(class Lexical-Special extends Expression
  
  
  (slot form getter generate)
  
  
  (method override (initialize self form . rest)
    (apply nextmethod self rest)
    (set! self.form form))
  
  
  (method override (emit-expression self declaration walker resume environment backend)
    (new-code
      (list 'quote form)
      Any
      #f)))


(special-form (lexicalname walker resume declaration environment form-src)
  (new Lexical-Special
    (get-name declaration)))


;;;
;;;; Time
;;;


(class Time-Special extends Expression
  
  
  (slot expr getter generate)
  (slot port getter generate)
  
  
  (method override (initialize self expr port . rest)
    (apply nextmethod self rest)
    (set! self.expr expr)
    (set! self.port port))
  
  
  (method override (emit-expression self declaration walker resume environment backend)
    (let ((expr-emit (emit-one-expression expr declaration walker resume environment backend))
          (port-emit (emit-one-expression port declaration walker resume environment backend)))
      (new-code
        (emit backend 'time self declaration walker resume environment expr-emit port-emit)
        Any
        #f))))


(special-form (time walker resume declaration environment form-src)
  (let ((form (strip-syntactic-closures form-src)))
    (let ((expr (cadr form))
          (port (if (null? (cddr form)) '(current-output-port) (caddr form))))
      (new Time-Special
        (walk walker resume declaration environment expr)
        (walk walker resume declaration environment port)))))


;;;
;;;; Once
;;;


(syntax public once
  (lambda (form-src usage-environment macro-environment)
    (let ((body (cdr (source-code form-src))))
      (sourcify-deep-if
        (let ((once? (generate-symbol "once?")))
          `(let ((,once? (static (box #t))))
             (when (unbox ,once?)
               (begin ,@body)
               (set-box! ,once? #f))))
        form-src))))


;;;
;;;; Inline
;;;


(class Inline extends Expression
  
  
  (slot expression getter generate)
  
  
  (method override (initialize self expression . rest)
    (apply nextmethod self rest)
    (set! self.expression expression))
  
  
  (method override (emit-expression self declaration walker resume environment backend)
    (new-code
      expression
      Any
      #f)))


(special-form (inline walker resume declaration environment form-src)
  (let ((form (source-code (cdr (source-code form-src)))))
    (let ((code (car form)))
      (new Inline
        code))))


;;;
;;;; Site
;;;


(class Site-Special extends Expression
  
  
  (slot expression getter generate)
  
  
  (method override (initialize self expression)
    (nextmethod self)
    (set! self.expression expression))
  
  
  (method override (emit-expression self declaration walker resume environment backend)
    (new-code
      (sourcified-form (emit-expression expression declaration walker resume environment backend))
      Any
      #f))
  
  
  (method override (tree-fold self down up here seed environment)
    (up self
        seed
        (tree-fold expression down up here (down self seed environment) environment)
        environment)))


(special-form (site walker resume declaration environment form-src)
  (let ((form (cdr (source-code form-src))))
    (let ((header (strip-syntactic-closures (car form)))
          (body (cdr form)))
      (let ((name (if (pair? header) (car header) header))
            (properties (if (pair? header) (cdr header) '())))
        (new Site-Special
          (walk walker resume declaration environment
            (sourcify-deep-if
              (let ((site (generate-symbol "site")))
                `(let ((,site <Call-Site> (static (register-site ',name ',properties))))
                   ((get-procedure ,site)
                    ,site
                     (lambda ()
                       ,@body))))
              form-src)))))))


;;;
;;;; Tie
;;;


(macro public (tie . objects)
  (define (process-char c out)
    (when (memq? c '(#\~ #\{))
      (display "~" out))
    (format out "{c}" c))
  
  (define (process-string control out out-parameters)
    (bind (command . arguments) (read-delimited control "tie parameter" #\})
      (if (not (symbol? command))
          (error "Tie currently only accepts variables as parameters: {t}" command)
        (if (null? arguments)
            (display "{a}" out)
          (format out "~{{l}}" arguments))
        (put out-parameters command))))
  
  (call-with-input-string (apply string-append objects)
    (lambda (control)
      (let ((out (open-output-string))
            (out-parameters (new List-Factory)))
        (let (iterate)
          (let ((c (read-char control)))
            (unless (eof-object? c)
              (case c
                ((#\~) (process-char (read-char control) out))
                ((#\{) (process-string control out out-parameters))
                (else (process-char c out)))
              (iterate))))
        (cons 'format (cons :string (cons (get-output-string out) (get-output out-parameters)))))))))
