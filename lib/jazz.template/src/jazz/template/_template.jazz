;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Template Expansion
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.template jazz


(import (jazz.io))


(definition name-delimiter-start
  #\%)

(definition name-delimiter-end
  #\%)

(definition content-delimiter-start
  #\%)

(definition content-delimiter-end
  #\%)


(definition public (copy-template template destination associations (ignored-nodes: ignored-nodes '()) (non-expanded-nodes: non-expanded-nodes '()) (non-expanded-leaves: non-expanded-leaves '()))
  (let ((name-expander (new Template-Expander delimiter-start: name-delimiter-start delimiter-end: name-delimiter-end))
        (content-expander (new Template-Expander delimiter-start: content-delimiter-start delimiter-end: content-delimiter-end)))
    (define (expand-name name)
      (continuation-capture
        (lambda (return)
          (let ((output (open-output-string)))
            (expand name-expander name associations output
              printer: (lambda (name value output)
                         (if (list? value)
                             (continuation-return return value)
                           (format output "{a}" value))))
            (get-output-string output)))))
    
    (define (expand-pathname pathname)
      (new-brother pathname (expand-name (get-name pathname))))
    
    (define (copy-template-file template destination)
      (let ((destination (expand-pathname destination)))
        (unless (exists? destination)
          (if (member? (get-name destination) non-expanded-leaves test: ci=?)
              (duplicate template destination)
            (call-with-output-file (path-settings destination)
              (lambda (output)
                (expand content-expander template associations output)))))))
    
    (define (copy-template-directory template destination root?)
      (let ((destination (if root? destination (expand-pathname destination))))
        (let ((destination-name (get-name destination)))
          (unless (member? destination-name ignored-nodes test: ci=?)
            (if (member? destination-name non-expanded-nodes test: ci=?)
                (unless (exists? destination)
                  (duplicate template destination))
              (unless (exists? destination)
                (create-directories destination))
              (iterate-directory template
                (lambda (path kind)
                  (let ((name (car path)))
                    (unless (filename=? name ".DS_Store")
                      (case kind
                        ((file)
                         (copy-template-file (new-file template name) (new-file destination name)))
                        ((directory)
                         (copy-template-directory (new-directory template name) (new-directory destination name) #f))))))
                full?: #f
                files?: #t
                directories?: #t
                recursive?: #f))))))
    
    (define (copy-template-pathname template destination root?)
      (if (file? template)
          (copy-template-file template destination)
        (copy-template-directory template destination root?)))
    
    (copy-template-pathname template destination #t)))


;;;
;;;; Template-Expander
;;;


(definition default-delimiter-escape
  #\~)

(definition default-delimiter-start
  #\%)

(definition default-delimiter-end
  #\%)


(class Template-Expander extends Object
  
  
  (slot delimiter-escape)
  (slot delimiter-start)
  (slot delimiter-end)
  
  
  (method override (initialize self (delimiter-escape: delimiter-escape default-delimiter-escape) (delimiter-start: delimiter-start default-delimiter-start) (delimiter-end: delimiter-end default-delimiter-end))
    (nextmethod self)
    (set! self.delimiter-escape delimiter-escape)
    (set! self.delimiter-start delimiter-start)
    (set! self.delimiter-end delimiter-end))
  
  
  (method public (expand self template associations output (printer: printer #f))
    (declare (proper-tail-calls))
    (define (template-reader template)
      (if (sequence? template)
          (open-input-string template)
        (open-input-file (path-settings template))))
    
    (define (process-variable input output associations printer)
      (let ((name (read-variable input)))
        (let ((info (assv name associations)))
          (if info
              (let ((value (cdr info)))
                (if printer
                    (printer name value output)
                  (format output "{a}" value)))
            (error "Unable to find variable {t} in association list" name)))))
    
    (define (read-variable input)
      (let ((name-output (open-output-string)))
        (let (iterate)
          (let ((c (read-char input)))
            (cond ((eof-object? c)
                   (error "Unexpected end of stream reached while reading template parameter"))
                  ((eqv? c delimiter-end)
                   (string->symbol (get-output-string name-output)))
                  (else
                   (write-char c name-output)
                   (iterate)))))))
    
    (with ((input (template-reader template)))
      (let (iterate)
        (let ((c (read-char input)))
          (when (not (eof-object? c))
            (if (eqv? c delimiter-start)
                (if (eqv? (peek-char input) delimiter-start)
                    (format output "{c}" (read-char input))
                  (process-variable input output associations printer))
              (format output "{c}" c))
            (iterate))))))))
