;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Image Style
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.text.Image-Style jazz


(import (jazz.graphic)
        (jazz.text))


(class Image-Style extends Style
  
  
  (attributes (alignment)
    (slot moniker initialize #f)
    (slot size    initialize #f))
  
  
  (slot image initialize #f)
  
  
  ;;;
  ;;;; Setup
  ;;;
  
  
  (method package (setup self)
    (let ((moniker (get-moniker self))
          (size (get-size self)))
      (when (and moniker (not image))
        (set! image (new Image moniker: (get-moniker self)))
        (unless size
          (set-size self (image-size image))))))
  
  
  ;;;
  ;;;; Image
  ;;;
  
  
  (method public (get-image self)
    (setup self)
    image)
  
  
  ;;;
  ;;;; Size
  ;;;
  
  
  (method override (get-width self)
    (setup self)
    (get-width (get-size self)))
  
  
  (method override (get-height self)
    (setup self)
    (get-height (get-size self)))
  
  
  (method override (run-width self run)
    (get-width self))
  
  
  ;;;
  ;;;; Text
  ;;;
  
  
  (method override (propagate? self)
    #f)
  
  
  ;;;
  ;;;; Scan
  ;;;
  
  
  (method override (scan self h start end tabs run proc)
    (default-scan self h start end tabs run proc))
  
  
  ;;;
  ;;;; Wrap
  ;;;
  
  
  (method override (wrap self context margin h v width tabs place break run)
    (place run)
    (+ h (get-width self)))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-style self surface <Surface> context string <Sequence> h <fx> v <fx> line-height <fx> tabs left enabled?)
    (let* ((width (get-width self))
           (height (get-height self))
           (top (justify-element self height line-height)))
      (draw-at (get-image self) surface context h (+ v top))
      width))))
