;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Text Style
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.text.Text-Style jazz


(import (jazz.exemplar)
        (jazz.graphic)
        (jazz.text))


(class Text-Style extends Style
  
  
  (attributes (alignment)
    (slot font      initialize #f test exemplar+=?)
    (slot color     initialize #f test exemplar+=?)
    (slot highlight initialize #f test exemplar+=?)
    (slot frame     initialize #f test exemplar+=?))
  
  
  ;;;
  ;;;; Size
  ;;;
  
  
  (method override (get-height self)
    (get-height (get-metrics (get-font self))))
  
  
  (method override (run-width self run)
    (let ((string (get-string run)))
      ;; warning this coerce could become inefficient
      (text-width (get-font self) (coerce string String))))
  
  
  ;;;
  ;;;; Scan
  ;;;
  
  
  (method override (scan self h <fx> start <fx> end <fx> tabs run <Run> proc)
    (let ((string (get-string run)))
      (let ((left #f)
            (mid #f)
            (right (cast <fl> h))
            (len <fx> (cardinality string)))
        (loop (for n from start below (max start (min end len)))
              (let ((c (element string n)))
                (if (eqv? c #\tab)
                    (begin
                      (while (<= (car tabs) right)
                        (set! tabs (cdr tabs)))
                      (let ((width (- (car tabs) right)))
                        (set! left right)
                        (set! mid (+ right (/ width 2)))
                        (set! right (+ left width))))
                  (let ((width (get-char-width (get-font self) c)))
                    (set! left right)
                    (set! mid (+ right (/ width 2)))
                    (increase! right width)))
                ;; quick solution because if there are many style
                ;; runs we could still get some imprecision errors
                (proc c n (fxround left) (fxround mid) (fxround right)))))))
  
  
  ;;;
  ;;;; Wrap
  ;;;
  
  
  (definition Break-Characters
    '(#\space))
  
  
  (method override (wrap self context margin h <fx> v <fx> width <fx> tabs place break run <Run>)
    (let* ((start h)
           (string (get-string run))
           (len (cardinality string))
           (end (- len 1))
           (offset 0)
           (brk #f)
           (brk-right #f)
           (rightmost h))
      (scan self h 0 len tabs run
        (lambda (c <char> n <fx> left <fx> mid <fx> right <fx>)
          (set! rightmost right)
          (when (memq? c Break-Characters)
            (set! brk (+ n 1))
            (set! brk-right right))
          (when (and (> (+ h (- right start margin)) width)
                     (or (not brk)
                         (not (memq? c Break-Characters))))
            (let ((n <fx> (or brk n)))
              (place (new Run (subseq! string offset n) self))
              (break right)
              (set! h margin)
              (set! start (if brk brk-right left))
              (set! offset n)
              (set! brk #f)))
          (when (>= n end)
            (if (= offset 0)
                (place run)
              (place (new Run (subseq! string offset (cardinality string)) self))))))
      (+ h (- rightmost start))))
  
  
  (method override (height-update self height)
    )
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-style self surface <Surface> context string <Sequence> h <fx> v <fx> line-height <fx> tabs left <fx> enabled? <bool>) <fx>
    (let ((font (or (get-font self) (registered-font 'Label))))
      (set-font surface font)
      (let* ((font-height (get-height (get-metrics font)))
             (v (+ v (justify-element self font-height line-height)))
             (color (if (getf context print-black/white?:)
                        (registered-color 'Black)
                      (or (get-color self) (registered-color 'Black))))
             (highlight (get-highlight self))
             (frame (get-frame self)))
        (if (not enabled?)
            (begin
              (draw-text surface h v (coerce string String) {Color Dark} enabled?: #f)
              0)
          (let ((width (draw-tabbed-text surface h v string tabs left color highlight line-height)))
            (when frame
              (let ((rect (new Rect h v (+ h width) (+ v line-height))))
                (frame-rect surface rect frame)))
            width)))))))
