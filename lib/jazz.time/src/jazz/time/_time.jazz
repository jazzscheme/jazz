;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Time
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.time jazz


(require (time)
         (jazz.time.classes))


(hub cpu-duration)
(hub real-duration)
(hub real-duration-in-milliseconds)
(hub reset)


(export (time))
(import (time))


;;;
;;;; Time
;;;


(definition package (construct-time second nanosecond (type time-utc))
  (make-time type nanosecond second))

  
(class Time-Class extends Class)


(class primitive Time metaclass Time-Class extends Object
   
  
  (method override (print self output readably)
    (let ((name (category-name (class-of self)))
          (second (time-second self))
          (nanosecond (time-nanosecond self))
          (type (time-type self)))
      (if (eq? type time-utc)
          (format output "~{{a} {a} {a}}" name second nanosecond)
        (format output "~{{a} {a} {a} {a}}" name second nanosecond type))))
  
  
  (method override (compare-object self target)
    (if (time? target)
        (cond ((time=? self target) :equal)
              ((time<? self target) :less)
              (else :greater))
      :incompatible)))


;;;
;;;; Date
;;;


(definition package (construct-date year month day hour minute second nanosecond (zone-offset #f))
  ;; backward compatibility
  (let ((effective-zone-offset
          (cond ((not zone-offset) (local-timezone))
                ((number? zone-offset) zone-offset)
                ((eq? 'Z zone-offset) 0)
                ((string? zone-offset) (* 60 (+ (* 60 (string->number (substring zone-offset 0 3))) (string->number (substring zone-offset 3 5)))))
                (else (error "Badly formed zone offset")))))
    (make-date nanosecond second minute hour day month year effective-zone-offset)))


(class Date-Class extends Class)


(class primitive Date metaclass Date-Class extends Object
  
  
  (method override (print self output readably)
    (format output "~{{a} {a} {a} {a} {a} {a} {a} {a} {a}}"
      (category-name (class-of self))
      (date-year self)
      (date-month self)
      (date-day self)
      (date-hour self)
      (date-minute self)
      (date-second self)
      (date-nanosecond self)
      (date-zone-offset self)))
  
  
  (method override (compare-object self target)
    (if (date? target)
        (let ((time1 (date->time-utc self))
              (time2 (date->time-utc target)))
          (cond ((time=? time1 time2) :equal)
                ((time<? time1 time2) :less)
                (else :greater)))
      :incompatible)))


(definition public (system-date)
  (current-date (system-timezone)))


(definition public (time->seconds time)
  (+ (exact->inexact (time-second time)) (/ (time-nanosecond time) 1000000000.)))


(definition public (seconds->time seconds)
  (let ((int-seconds (inexact->exact (floor seconds))))
    (make-time time-utc (inexact->exact (floor (* 1000000 (- seconds int-seconds)))) int-seconds)))


;; convert
(definition public (date-universal->local date)
  date)


;; convert
(definition public (date-local->universal date)
  date)


(definition public (parse-date-day date)
  (string->date date "~Y/~m/~d"))


(definition public (parse-date-time date)
  (string->date date "~Y/~m/~d ~H:~M"))


(definition public (parse-date-day-french date)
  (string->date date "~d/~m/~Y"))


(definition public (present-date date)
  (date->string date "~Y/~m/~d ~H:~M:~S"))


(definition public (present-date-day date)
  (date->string date "~Y/~m/~d"))


(definition public (present-date-time date)
  (date->string date "~Y/~m/~d ~H:~M"))


(definition public (present-date-day-french date)
  (date->string date "~d/~m/~Y"))


(definition public (present-date-hour date)
  (date->string date "~H:~M"))


(definition public (count-down delay feedback action)
  (define slices
    '(60 15 5 1))
  
  (define (present delay)
    (let ((mn (quotient delay 60))
          (sec (remainder delay 60)))
      (cond ((= mn 0)
             (format "{a}s" sec))
            ((= sec 0)
             (format "{a}mn" mn))
            (else
             (format "{a}mn {a}s" mn sec)))))
  
  (let (loop-slices (slices slices) (delay delay))
    (if (null? slices)
        (begin
          (feedback (present delay))
          (sleep delay)
          (action))
      (let ((slice (car slices)))
        (let ((n (quotient delay slice))
              (r (remainder delay slice)))
          (if (or (= n 0)
                  (and (= n 1) (= r 0)))
              (loop-slices (cdr slices) delay)
            (feedback (present delay))
            (let ((period (if (/= r 0) r slice)))
              (sleep period)
              (loop-slices slices (- delay period)))))))))


;;;
;;;; Arithmetic
;;;


(definition public (date= d1 d2)
  (and (= (date-year d1) (date-year d2))
       (= (date-month d1) (date-month d2))
       (= (date-day d1) (date-day d2))
       (= (date-hour d1) (date-hour d2))
       (= (date-second d1) (date-second d2))
       (= (date-nanosecond d1) (date-nanosecond d2))
       (= (date-zone-offset d1) (date-zone-offset d2))))


(definition public (date-add date count type)
  (let ((nanosecond (date-nanosecond date))
        (second (date-second date))
        (minute (date-minute date))
        (hour (date-hour date))
        (day (date-day date))
        (month (date-month date))
        (year (date-year date))
        (zone-offset (date-zone-offset date)))
    (case type
      ((nanosecond) (increase! nanosecond count))
      ((second) (increase! second count))
      ((minute) (increase! minute count))
      ((hour) (increase! hour count))
      ((day) (increase! day count))
      ((month) (increase! month count))
      ((year) (increase! year count))
      ((zone-offset) (increase! zone-offset count))
      (else (unimplemented)))
    (time-utc->date (date->time-utc (make-date nanosecond second minute hour day month year zone-offset)))))


(definition public (date-truncate-nanosecond date)
  (make-date 0
             (date-second date)
             (date-minute date)
             (date-hour date)
             (date-day date)
             (date-month date)
             (date-year date)
             (date-zone-offset date)))


;;;
;;;; Period
;;;


(class undocumented Period extends Range
  
  
  @convert
  (method public (get-duration field)
    (let ((delta (- (get-elapse (get-time end)) (get-elapse (get-time start)))))
      (get-duration (new Time delta) field)))
  
  
  @convert
  (method public (present)
    (format :string "{a} {a} {a}"
            (present-date start)
            (localize-object {Locales english: "to" french: "au"})
            (present-date end)))
  
  
  @convert
  (method public (for-each-day proc)
    (let ((date start))
      (while (<= date end)
        (proc date)
        (set! date (add date 'day 1)))))
  
  
  @convert
  (method public (includes-day-of-week? day)
    (for-each-day
      (lambda (date)
        (when (= (symbolic-day-of-week Date (get-day-of-week date)) day)
          (return #t))))
    #f))


;;;
;;;; Timer
;;;


(definition user-message
  (request-service 'user-message))


(class Timer extends Object


  (slot real-start)
  (slot cpu-start)


  (method override (initialize self)
    (nextmethod self)
    (reset self))


  (method public (reset self)
    (set! real-start (real-time))
    (set! cpu-start (cpu-time)))


  (method public (real-duration self)
    (- (real-time) real-start))
  
  (method public (real-duration-in-milliseconds self)
    (seconds->milliseconds (real-duration self)))
  
  (method public (cpu-duration self)
    (- (cpu-time) cpu-start))

  (method public (cpu-duration-in-milliseconds self)
    (seconds->milliseconds (cpu-duration self)))


  (method public (report-duration self)
    (user-message "Duration: {s} ms real, {s} ms cpu"
                  (real-duration-in-milliseconds self)
                  (cpu-duration-in-milliseconds self)))


  (method public (report-fps self)
    (user-message "FPS: {s}"
                  (/ 1. (real-duration self))))))
