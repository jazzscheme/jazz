;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Tree Embedded Search Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.tree.Tree-Embedded-Search-Text-View jazz


(import (jazz.action)
        (jazz.event)
        (jazz.ide)
        (jazz.outline)
        (jazz.search)
        (jazz.tree))


(class undocumented Tree-Embedded-Search-Text-View extends IDE-Text-View
  
  
  (slot tree accessors generate)
  

  ;;;
  ;;;; Tree
  ;;;
  
  
  (method (get-cell self)
    (site->cell tree (get-edited-site tree)))
  
  
  (method (get-cell-values self)
    (let ((cell (get-cell self)))
      (values (get-line cell) (get-col cell))))
  

  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions self)
    (cons (find-actions 'tree-embedded-search-text)
          (nextmethod self)))
  
  
  ;; delegate the shortcut
  (method override (upward-compound self)
    (toggle-play-recording (get-guest self)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (focus-lose self)
    (nextmethod self)
    (delay-event
      (lambda ()
        (end-edition self))))
  
  
  (method override (return-press self evt)
    (let ((guest (get-guest self)))
      (end-edition self)
      (let ((search (get-search guest)))
        (if (null? (get-contexts (get-find-targets search)))
            (bell)
          (on-start-search search evt)))))
  
  
  (method (on-end-edition self key)
    (end-edition self))
  
  
  (method override (tab-press self evt)
    (let ((shift? (get-shift? evt)))
      (if (and (has-completion? self) (not shift?))
          (end-completion self)
        (let* ((tab-manager (find-tab-manager tree))
               (to-view (navigated-view tab-manager tree shift?))
               (to-navigable? (and to-view (neq? to-view tree)))
               (width (get-columns-count tree))
               (height (get-visible-count tree)))
          (receive (line col) (get-cell-values self)
            (if (and to-navigable? shift? (= line 0) (= col 0))
                (navigate-tab tab-manager tree shift?)
              (let* ((max-col (- width 1))
                     (row-direction (cond ((and shift? (= col 0)) -1)
                                          ((and (not shift?) (= col max-col)) 1)
                                          (else 0)))
                     (line (modulo (+ line height row-direction) height))
                     (col (modulo (+ col width (if shift? -1 1)) width))
                     (cell (new Cell line col)))
              (if (and to-navigable? (not shift?) (= line 0) (= col 0))
                  (navigate-tab tab-manager tree shift?)
                (when (select-cell tree cell)
                  (end-edition self))))))))))
  
  
  (method override (left-arrow-move self extend? word?)
    (if word?
        (select-left tree (get-cell self))
      (nextmethod self extend? word?)))
  
  
  (method override (right-arrow-move self extend? word?)
    (if word?
        (select-right tree (get-cell self))
      (nextmethod self extend? word?)))
  
  
  (method override (up-arrow-move self extend? scroll?)
    (select-up tree (get-cell self)))
  
  
  (method override (down-arrow-move self extend? scroll?)
    (select-down tree (get-cell self) #f))
  
  
  (method package (end-edition self)
    (when player
      (end-edition tree)))))
