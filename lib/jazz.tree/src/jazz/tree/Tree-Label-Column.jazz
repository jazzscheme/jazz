;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Tree Label Columns
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.tree.Tree-Label-Column jazz


(import (jazz.graphic)
        (jazz.locale)
        (jazz.tree)
        (jazz.ui)
        (jazz.view))


(class Tree-Label-Column extends Tree-Column
  
  
  (property protected padding initialize 0 accessors explicit)
  
  
  ;;;
  ;;;; Cells
  ;;;
  
  
  (method override (cell-class self)
    Tree-Label)
  
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-padding self)
    padding)


  (method public (set-padding self value)
    (set! padding value)
    (invalidate-view parent))
  

  ;;;
  ;;;; Tooltip
  ;;;
  
  
  (method override (get-data-tooltip self data)
    (and (is? data Tree-Label)
         (get-title data)))

  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (definition Default-Color
    {Color Black})
  
  
  (method override (draw-at self surface context row data h v width height)
    (nextmethod self surface context row data h v width height)
    (draw-title self surface context row data h v width height))
  
  
  (method protected virtual (draw-title self surface context row data h v width height)
    (let ((title (localize (or (get-title data) "")))
          (font  (or (get-font data) (effective-font tree)))
          (color (get-color data)))
      (set-font surface font)
      (let ((pos (title-position self surface width height title)))
        (draw-text surface (+ h (get-h pos) (case justification ((head) (+ 1 padding)) ((tail) (- -1 padding)) (else 0))) (get-v pos) title (cell-color self color context)))))
  
  
  (method protected virtual (title-position self surface width height title)
    (justify-title self surface width height title justification))
  
  
  (method protected virtual (cell-color self color context)
    (let ((enabled? (view-enabled? tree))
          (selected? (and (getf context selected?:)
                          (cell-selected? self context))))
      (or (and color (determine-color color enabled? (and selected? (focused? tree))))
          (cond ((not enabled?) {Color Dark})
                (selected? (or (get-text-color (get-skin tree)) (selected-text-color tree)))
                (else (or (get-text-color (get-skin tree)) Default-Color))))))
  
  
  (method override (text-h self level)
    1)
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor self (context: context #f))
    (new jazz.tree.descriptors:Tree-Label-Descriptor))))
