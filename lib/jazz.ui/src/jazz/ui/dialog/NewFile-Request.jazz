;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; NewFile Request
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.dialog.NewFile-Request jazz


(import (jazz.graphic)
        (jazz.handler)
        (jazz.io)
        (jazz.layout)
        (jazz.markup)
        (jazz.outline)
        (jazz.text)
        (jazz.ui)
        (jazz.view))


(class undocumented NewFile-Request extends Pathname-Request
  
  
  (form
    (<install>
      (<!>                         name: buttonbar
        (<Separator>                               size: {Dimension 4 0})
        (<Layout-View>                             layout-type: fill layout-insets: {Rect 0 3 0 0} size: {Dimension 288 22}
          (<Border-View>                           border-type: solid border-color: {Color Entry-Border}
            (<Scroller-View>                       hscroll?: #f vscroll?: #f
              (<!>                 name: content   layout-type: fill
                (<Name-Text-View>  name: filename  accepts-returns?: #f show-unfocused-selection?: #f focus-selects-all?: #t return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press} content-change-handler: {Event-Handler :form on-content-change})))))
        (<!>                       name: ok        title: "Save"))))
  
  
  (method meta override (host-title self)
    "Choose New File")
  
  
  (property initial-filename  initialize #f)
  (property default-extension initialize #f)
  
  
  (method override (finish self rest)
    (nextmethod self rest)
    (when initial-filename
      (let ((text (locate self 'filename)))
        (let ((content
                (if (and default-extension (ends-with? initial-filename (concatenate "." default-extension)))
                    (subseq initial-filename 0 (- (cardinality initial-filename) (cardinality default-extension) 1))
                  initial-filename)))
          (set-string-content text content)
          (select-all text)))
      (update-buttons self)))
  
  
  (method override (focus-default self)
    (acquire-focus (locate self 'filename)))
  
  
  ;;;
  ;;;; Tab
  ;;;
  
  
  (method override (get-tab-stops self)
    (list
      (get-tree self)
      (locate self 'filename)))
  

  ;;;
  ;;;; Update
  ;;;
  
  
  (method override (update-content self)
    (let ((directories (get-content directory nodes?: #t leaves?: #f))
          (files (get-content directory nodes?: #f leaves?: #t)))
      (let ((tree (get-tree self)))
        (define (add pathname)
          (add-pathname self tree pathname))
        
        (remove-every-row tree)
        (with-update-locked tree
          (lambda ()
            (for-each add directories)
            (for-each add files))))))
  
  
  (method override (update-buttons self)
    (nextmethod self)
    (let ((text (locate self 'filename)))
      (set-enabled? (locate self 'ok) (not (empty-string? (get-string-content text))))))
  

  ;;;
  ;;;; Modal
  ;;;
  
  
  (method override (validate-dialog self)
    (let ((newfile (get-newfile self)))
      (when (exists? newfile)
        (let ((code (message-box (format "{a} already exists. Do you want to replace it?" (parse newfile))
                                 type: 'confirmation)))
          (when (memq? code '(no cancel))
            (signal-cancel))))))
  
  
  (method override (get-modal-result self)
    (current-directory-set! directory)
    (get-newfile self))
  
  
  (method (get-newfile self)
    (let ((content (get-string-content (locate self 'filename))))
      (let ((filename
              (if (and default-extension (not (search content ".")))
                  (concatenate content "." default-extension)
                content)))
        (anchorize self (new-file directory filename)))))
  

  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (selection-change self)
    (let ((pathname (selected-pathname self)))
      (when (file? pathname)
        (set-string-content (locate self 'filename) (get-name pathname))
        (update-buttons self))))
  
  
  (method (on-content-change self evt)
    (update-buttons self))
  
  
  (method override (default-action self)
    (let ((pathname (selected-pathname self)))
      (cond ((directory? pathname)
             (down self pathname))
            ((ok-enabled? self)
             (end-modal (get-host self) (get-modal-result self))))))))
