;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Host View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.ui.hosting.Host-View jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.utilities))


(class Host-View extends View
  
  
  (form
    (<install> background: {Color name: Medium}))
    
  
  (property content initialize {} getter get-content setter set-content)
  (property guest   initialize {} getter get-guest   setter set-guest)
  
  
  (slot focused initialize {})
  
  
  (method (destroy-component)
    (close-guest)
    (nextmethod))
  
  
  (method (close-guest)
    (when guest
      (let ((view guest))
        (set-guest {})
        (close~ view))))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-content)
    content)
  
  
  (method public (set-content view)
    (when (neq? view content)
      (set! content view)
      (layout-view)))
  
  
  (method public (get-guest)
    guest)
  
  
  (method public (set-guest view)
    (when (neq? view guest)
      (when guest
        (uninstall-guest))
      (when (not content)
        (set! content view))
      (set! guest view)
      (when guest
        (install-guest))))

  
  ;;;
  ;;;; Activation
  ;;;
  
  
  (method (mouse-activate?)
    #f)
  
  
  (method public virtual (mouse-activate)
    (focus-host))
  

  ;;;
  ;;;; Layout
  ;;;
  
  
  (method (layout)
    (when content
      (set-position~ content (new Point 0 0))
      (set-size~ content (get-size))))

  
  ;;;
  ;;;; View
  ;;;


  (method (get-host)
    self)
  
  
  ;;;
  ;;;; Guest
  ;;;
  
  
  (method (install-guest)
    (set-parent~ guest self)
    (set-position~ guest {Point 0 0})
    (set-size~ guest (get-size))
    (set-visible?~ guest #t)
    (install-in-host~ guest self)
    (install-tools))
  
  
  (method (uninstall-guest)
    (uninstall-from-host~ guest self)
    (close~ guest))
  
  
  ;;;
  ;;;; Focus
  ;;;
  
  
  (method public (focus-host)
    (cond (focused
           (reactivate-focused))
          (guest
           (focus-default~ guest))
          (else
           (acquire-focus))))
    
  
  (method public virtual (activate-focus)
    (when guest
      (set-guest-actions~ (get-application) (get-guest-actions guest))
      (host-activate~ guest)))
    
  
  (method public virtual (deactivate-focus)
    (let ((focus (get-focus)))
      (when (and focus (eq? (get-host~ focus) self))
        (set! focused (focus-master~ focus))))
    (set-guest-actions~ (get-application) '())
    (when guest
      (host-deactivate~ guest)))
  
  
  (method (focus-default)
    )
  
  
  (method protected (reactivate-focused)
    (when focused
      (acquire-focus~ focused)))
  
  
  (method protected (view-destroyed view)
    (when (eq? focused view)
      (set! focused {})))

  
  ;;;
  ;;;; Tools
  ;;;
  
  
  (method protected virtual (install-tools)
    (layout-view))))
