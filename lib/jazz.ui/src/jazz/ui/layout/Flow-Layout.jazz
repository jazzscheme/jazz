;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Flow Layout
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.layout.Flow-Layout jazz


(import (jazz.geometry)
        (jazz.ui))


(class Flow-Layout extends Layout
  
  
  (property insets        <Rect> initialize {Rect 0 0 0 0} accessors generate)
  (property justification        initialize 'horizontal    accessors generate)
  (property padding       <fx>   initialize 0              accessors generate)
  (property fill?         <fx>   initialize #f             accessors generate)
  
  
  ;;;
  ;;;; Layout
  ;;;


  ;; todo: unify layout-vertical/horizontal
  (method override (layout figures width height . rest)
    (define (layout-vertical)
      (let ((head (rect-position insets))
            (tail (new Point (get-left~ insets) (- height (get-bottom~ insets)))))
        (for-each (lambda (figure <Figure>)
                    (receive (where offset) (standardize-location (get-location~ figure))
                      (when (and (get-visible?~ figure) (eq? where 'head))
                        (let ((size (get-size~ figure)))
                          (set-position~ figure (+ head (new Point 0 offset)))
                          (when fill?
                            (set-size~ figure (new Dimension width (get-height~ size))))
                          (when size
                            (increase! head (new Point 0 (+ (get-height~ size) padding))))))))
                  figures)
        (for-each-reversed (lambda (figure <Figure>)
                             (receive (where offset) (standardize-location (get-location~ figure))
                               (when (and (get-visible?~ figure) (eq? where 'tail))
                                 (let* ((size (get-size~ figure))
                                        (height (get-height~ size)))
                                   (when (> (get-v~ (- tail height)) (get-v~ head))
                                     (set-position~ figure (+ (- tail (new Point 0 height)) (new Point 0 offset)))
                                     (when fill?
                                       (set-size~ figure (new Dimension width (get-height~ size))))
                                     (when size
                                       (decrease! tail (new Point 0 (+ height padding)))))))))
                           figures)
        (for-each (lambda (figure <Figure>)
                    (receive (where offset) (standardize-location (get-location~ figure))
                      (when (and (get-visible?~ figure) (eq? where 'center))
                        (set-position~ figure (+ head (new Point 0 offset)))
                        (set-size~ figure (new Dimension (if fill? width (get-width~ figure)) (- (get-v~ tail) (get-v~ head)))))))
                  figures)))
    
    (define (layout-horizontal)
      (let ((head (rect-position insets))
            (tail (new Point (- width (get-right~ insets)) (get-top~ insets))))
        (for-each (lambda (figure <Figure>)
                    (receive (where offset) (standardize-location (get-location~ figure))
                      (when (and (get-visible?~ figure) (eq? where 'head))
                        (let ((size (get-size~ figure)))
                          (set-position~ figure (+ head (new Point 0 offset)))
                          (when fill?
                            (set-size~ figure (new Dimension (get-width~ size) height)))
                          (when size
                            (increase! head (new Point (+ (get-width~ size) padding) 0)))))))
                  figures)
        (for-each-reversed (lambda (figure <Figure>)
                             (receive (where offset) (standardize-location (get-location~ figure))
                               (when (and (get-visible?~ figure) (eq? where 'tail))
                                 (let* ((size (get-size~ figure))
                                        (width (get-width~ size)))
                                   (when (> (get-h~ (- tail width)) (get-h~ head))
                                     (set-position~ figure (+ (- tail width) (new Point 0 offset)))
                                     (when fill?
                                       (set-size~ figure (new Dimension (get-width~ size) height)))
                                     (when size
                                       (decrease! tail (new Point (+ width padding) 0))))))))
                           figures)
        (for-each (lambda (figure <Figure>)
                    (receive (where offset) (standardize-location (get-location~ figure))
                      (when (and (get-visible?~ figure) (eq? where 'center))
                        (set-position~ figure (+ head (new Point 0 offset)))
                        (set-size~ figure (new Dimension (- (get-h~ tail) (get-h~ head)) (if fill? height (get-height~ figure)))))))
                  figures)))
    
    (define (standardize-location location)
      (let ((where (cond ((or (not location) (integer? location)) 'head) ((symbol? location) location) (else (first location))))
            (offset (cond ((integer? location) location) ((pair? location) (second location)) (else 0))))
        (values where offset)))
    
    (case justification
      ((vertical)
       (layout-vertical))
      (else
       (layout-horizontal))))))
