;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Localized Domain
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2012
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.locale.Localized-Domain jazz


(import (jazz.designer)
        (jazz.designer.view)
        (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.locale)
        (jazz.locale.components)
        (jazz.ui))


;;;
;;;; Localized-Domain
;;;


(class undocumented Localized-Domain extends Domain

   
  (form
    (<install> nullable?: #t))
  
  
  ;;;
  ;;;; Validation
  ;;;
  
  
  (method override (validate-value property value component root editor context manager)
    (validate-locales value (class-of root) (make-add-property-violation-proc property component root editor manager)))
  
  
  ;;;
  ;;;; Presentation
  ;;;
  
  
  (method override (present value)
    (if (not value)
        "{}"
      (->string value :reader)))
  
  
  ;;;
  ;;;; Entry
  ;;;
  
  
  (method override (setup-entry view client)
    (new Localized-Entry
      parent: view
      domain: self
      locales: (let ((roots (remove-duplicates (map (lambda (component)
                                                         (class-of (or (get-creator~ component)
                                                                       component)))
                                                       client))))
                 (and (pair? roots)
                      (null? (cdr roots))
                      (let ((locale-tree (find-locale-tree (car roots))))
                        (and locale-tree
                             (provide-locales~ locale-tree))))))))


;;;
;;;; Localized-Entry
;;;


(class undocumented Localized-Entry extends Value-Entry
  
  
  (property locales initialize #f)
  
  
  ;;;
  ;;;; Value
  ;;;
  
  
  (method (localize value)
    (or (and (pair? locales)
             (string? value)
             (new Locales all: value))
        value))
  
 
  (method override (get-value)
    (localize~ self (nextmethod)))
  
  
  (method override (set-value value)
    (nextmethod (if value
                    (localize~ self value)
                  "{}")))
  
  
  ;;;
  ;;;; Descriptor
  ;;;

  
  (method meta override (get-class-descriptor)
    (new Localized-Entry-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer.entry.Property-Entry))


(class Localized-Entry-Descriptor extends Property-Entry-Descriptor
  
  
  (form
    (<install>
      (<properties~>
        (<Property-Descriptor> name: locales
          (<Value-Domain>      name: domain  nullable?: #t))))))))
