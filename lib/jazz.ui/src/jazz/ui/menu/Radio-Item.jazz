;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Radio Items
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.menu.Radio-Item jazz


(import (jazz.library)
        (jazz.system)
        (jazz.ui))


(class Radio-Item extends Label-Item implements (Groupable Selectable)
  
  
  (property group     initialize {} getter get-group     setter set-group)
  (property selected? initialize #f getter get-selected? setter set-selected?)
  
  
  ;;;
  ;;;; Groupable
  ;;;


  (method override (get-group)
    group)


  (method override (set-group value)
    (set! group value))


  (method (radio-group)
    (collect-if (lambda (component)
                  (and (is? component Radio-Item)
                       (eq? (get-group~ component) group)))
                (get-children~ parent)))


  (method (find-selected)
    (find-if (lambda (view)
               (get-selected?~ view))
             (radio-group)))
  
  
  ;;;
  ;;;; Selectable
  ;;;


  (method override (get-selected?)
    selected?)
  

  (method override (set-selected? flag)
    (when (neq? flag selected?)
      (let ((current (find-selected)))
        (set-selected-state flag)
        (when current
          (set-selected-state~ current (not flag))))))
  
  
  (method override (unselect)
    (when selected?
      (set-selected-state #f)))
  
  
  (method (set-selected-state flag)
    (set! selected? flag)
    (invalidate-view))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (preferred-prefix-width)
    (if selected? 12 0))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-prefix surface context)
    (when selected?
      (let ((appl (get-application))
            (left 5)
            (top 2))
        (draw-image~ (get-small-icons-cache~ appl) surface context {Bitmap-Resource "Bullet"} left top))))))
