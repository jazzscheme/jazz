;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Display Outlines
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.outline.Display-Outline jazz


(import (jazz.ui))


(class undocumented Display-Outline extends Outline
  
  
  (slot first-displayed               initialize #f getter generate)   ;; first row that is displayed even if only partially
  (slot first-displayed-line   <fx>   initialize 0  getter generate)   ;; first displayed row's line
  (slot first-displayed-offset <fx>   initialize 0  getter generate)   ;; amount of scroll that would be needed to uncover a partially displayed row
  
  
  ;;;
  ;;;; Outline
  ;;;
  
  
  (method override (get-row n)
    (if (and first-displayed (>= n first-displayed-line))
        (get-row-from first-displayed (- n first-displayed-line))
      (nextmethod n)))
  
  
  (method override (call-with-v->line v scrolled proc)
    (if first-displayed
        (let ((displayed? (>= v (- scrolled))))
          (if displayed?
              (receive (line v height) (position-in-row (+ v scrolled) first-displayed first-displayed-line (- first-displayed-offset))
                (proc line v height))
            (nextmethod v scrolled proc)))
      (proc #f #f #f)))
  
  
  ;;;
  ;;;; Displayed
  ;;;
  
  
  (method override (update-displayed display-v)
    (let ((row (first-visible-son~ root-row))
          (line <fx> 0)
          (v display-v))
      (when row
        (let ((bottom <fx> (- v (get-height~ row))))
          (while (> bottom 0)
            (let ((next (get-next~ row)))
              (if (not next)
                  (set! bottom 0)
                (set! row next)
                (increase! line)
                (set! v (- bottom row-spacing))
                (set! bottom (- v (get-height~ row))))))))
      (set! first-displayed row)
      (set! first-displayed-line line)
      (set! first-displayed-offset (if row v 0))))
  
  
  (method override (follow display-v v)
    (define (follow-forward v)
      (while (> v 0)
        (let* ((height (+ (get-height~ first-displayed) row-spacing))
               (remaining (- height first-displayed-offset)))
          (if (< v remaining)
              (begin
                (increase! first-displayed-offset v)
                (set! v 0))
            (set! first-displayed (get-next~ first-displayed))
            (increase! first-displayed-line)
            (decrease! v remaining)
            (set! first-displayed-offset 0))))
      #t)
    
    (define (follow-backward v)
      (continuation-capture
        (lambda (return)
          (while (> v 0)
            (if (<= v first-displayed-offset)
                (begin
                  (decrease! first-displayed-offset v)
                  (set! v 0))
              (set! first-displayed (get-previous~ first-displayed))
              (if (not first-displayed)
                  (continuation-return return #f)
                (decrease! first-displayed-line)
                (decrease! v (+ first-displayed-offset 1))
                (set! first-displayed-offset (- (+ (get-height~ first-displayed) row-spacing) 1)))))
          #t)))
    
    ;; This is a patch for a complex bug
    ;; See the Outline-View.follow Bug.jazz file to reproduce it...
    (let ((succeeded?
            (cond ((> v 0) (follow-forward v))
                  ((< v 0) (follow-backward (- v))))))
      (when (not succeeded?)
        (update-displayed display-v))))))
