;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Outline View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.outline.Outline-View-Old jazz


(import (jazz.graphic)
        (jazz.library)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view))


(class Outline-View-Old extends View
  
  
  ;;;
  ;;;; Lexicon
  ;;;

  
  ;; visible   : a row that has all its parents expanded
  ;; displayed : a row that is displayed on-screen even if only partially
  ;; line      : the rank of a visible row

  
  (property protected default-row-height <fx>   initialize 17 accessors generate)
  (property protected row-spacing        <fx>   initialize 0  accessors explicit)
  (property protected left-padding       <fx>   initialize 0  accessors explicit)
  (property protected top-padding        <fx>   initialize 0  accessors explicit)
  (property protected background-filler         initialize #f accessors explicit)
  (property protected fit-to-scroller?          initialize #t accessors explicit)
  
  
  (slot protected root-row                      initialize #f getter generate)
  (slot protected outline                       initialize #f getter generate)
  (slot protected visible-count          <fx>   initialize 0  getter generate)
  (slot protected visible-height         <fx>   initialize 0  getter generate)
  (slot protected visible-width          <fx>   initialize 0  getter generate)
  (slot protected selection                     initialize #f)
  (slot protected line-size              <fx>   initialize 16)
  (slot protected page-size              <fx>   initialize 320)
  (slot protected hscrollbar?            <bool> initialize #f)
  (slot protected vscrollbar?            <bool> initialize #f)
  

  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method override (install rest)
    (nextmethod rest)
    (setup-outline))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (layout-scrollee))
  
  
  (method override (set-player plr)
    (when (neq? player plr)
      (nextmethod plr)
      (when root-row
        (set-player~ root-row plr))))
  
  
  (method (setup-outline)
    (set! root-row (new (row-class)))
    (set! outline (new Display-Outline row-spacing root-row))
    (set-parent~ root-row self)
    (set-father~ root-row #f)
    (set-level~ root-row -1)
    (set-state~ root-row 'expanded))


  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (size-change size)
    (nextmethod size)
    (unless (or update-locked? (not (get-finished?)))
      (layout-scrollee)))
  

  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-row-spacing)
    row-spacing)


  (method public (set-row-spacing spacing)
    (set! row-spacing spacing)
    (when outline
      (set-row-spacing~ outline spacing))
    (invalidate-view))

  
  (method public (get-left-padding)
    left-padding)
  
  
  (method public (set-left-padding padding)
    (set! left-padding padding)
    (invalidate-view))
  
  
  (method public (get-top-padding)
    top-padding)
  
  
  (method public (set-top-padding padding)
    (set! top-padding padding)
    (invalidate-view))
  

  (method public (get-background-filler)
    background-filler)


  (method public (set-background-filler value)
    (set! background-filler value)
    (invalidate-view))
  

  (method public (get-fit-to-scroller?)
    fit-to-scroller?)


  (method public (set-fit-to-scroller? value)
    (set! fit-to-scroller? value)
    (layout-scrollee))


  ;;;
  ;;;; State
  ;;;
  
  
  (method public (get-expand-state (include-invisibles?: include-invisibles? #f))
    (define (get-state row)
      (let ((title (row-title row))
            (state (get-state~ row))
            (sons (get-sons~ row)))
        (when (or (and include-invisibles? (> (cardinality sons) 0)) (eq? state 'expanded))
          (cons title
                (cons state
                      (collect get-state sons))))))
      
    (when root-row
      (collect get-state (get-sons~ root-row) type: List)))
  
  
  (method public (set-expand-state state)
    (define (set-state row info)
      (bind (title state . children) info
        (let ((subrow (find-by-title~ row title)))
          (when subrow
            (let ((actual (get-state~ subrow)))
              (if (eq? state 'expanded)
                  (unless (eq? actual 'expanded)
                    (expand/collapse subrow))
                (when (and (eq? actual 'dynamic) children)
                  (update-row subrow))))
            (iterate children
                     (lambda (info)
                       (set-state subrow info)))))))
    
    (when root-row
      (collapse-all-deep)
      (for-each (lambda (info)
                  (set-state root-row info))
                state)))
  
  
  (method public (with-preserved-expand-state thunk)
    (let ((saved-state (get-expand-state)))
      (set-selection '())
      (thunk)
      (set-expand-state saved-state)))
  
  
  ;;;
  ;;;; Spine
  ;;;
  
  
  (method public (expand-spine spine)
    (when root-row
      (letrec ((proc
                (lambda (row spine)
                  (bind (title . subspine) spine
                    (let ((subrow (find-by-title~ row title)))
                      (when subrow
                        (let ((actual (get-state~ subrow)))
                          (unless (or (eq? actual 'expanded) (null? subspine))
                            (expand/collapse subrow)))
                        (if (null? subspine)
                            subrow
                          (proc subrow subspine))))))))
        (proc root-row spine))))
  
  
  ;;;
  ;;;; Hierarchy
  ;;;
  
  
  (method override (find-subdrawing surface <Surface> device-pos <Point> pos <Point> ignore)
    (let ((line (v->line (get-v~ pos))))
      (when line
        (let ((row (cast <Outline-Row> (get-row line))))
          (find-in-drawing~ row surface device-pos ignore)))))
  

  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout-scrollee-content)
    (define (layout-rows)
      (let ((width (- (get-width) left-padding))
            (row (get-next~ root-row))
            (rt top-padding))
        (while row
          (let* ((height (effective-row-height~ row))
                 (rb (+ rt height)))
            (set-position~ row (new Point left-padding rt))
            (set-size~ row (new Dimension width height))
            (set! rt rb)
            (set! row (get-next~ row))
            (increase! rt row-spacing)))))
    
    (layout-rows))
  

  ;;;
  ;;;; Scrollee
  ;;;
  
  
  (method override (scrollee-size)
    (new Dimension (scrollee-width) (scrollee-height)))
  
  
  (method override (scrollee-width)
    (let ((width (+ left-padding visible-width)))
      (if (and scroller fit-to-scroller?)
          (max (get-display-width) width)
        width)))
  
  
  (method override (scrollee-height)
    (let ((height (+ top-padding visible-height)))
      (if (and scroller fit-to-scroller?)
          (max (get-display-height) height)
        height)))
  
  
  (method (scrollee-content-size)
    (new Dimension (scrollee-content-width) (scrollee-content-height)))
  
  
  (method public virtual (scrollee-content-width)
    (+ left-padding visible-width))
  
  
  (method public virtual (scrollee-content-height)
    (+ top-padding visible-height))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method override (invalidate-update)
    (update-displayed)
    (update-visible)
    (layout-scrollee)
    (invalidate-view)
    (update-scrollbars))
  
  
  (method public virtual (update-visible)
    )


  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-background surface context)
    )
  
  
  (method override (draw-children surface nodes context)
    (let ((clipper (get-clipper~ surface)))
      (let ((background (cond ((debug-paint?)
                               (cycle-debug-color!))
                              ((not (view-enabled?))
                               {Color Medium})
                              (else
                               (get-drawing-background context)))))
        (when (and background (neq? background 'none))
          (fill-rect~ surface clipper background)))
      (draw-outline surface context clipper (get-scrolled-distance-v))))
  
  
  (method protected virtual (draw-outline surface context clipper scrolled-v)
    (define (draw-row row line)
      (let ((selected? (line-draw-selected? line)))
        (set-font~ surface (effective-font))
        (let ((new-context (cons selected?: (cons selected? (cons line: (cons line context))))))
          (paint-content~ row surface #t '() new-context))))
    
    (let ((clipper (get-clipper~ surface)))
      (let ((top (get-top~ clipper))
            (bottom (get-bottom~ clipper))
            (row (get-first-displayed~ outline))
            (line <fx> (get-first-displayed-line~ outline))
            (rt <fx> (+ scrolled-v (- top-padding (get-first-displayed-offset~ outline))))
            (width <fx> (get-width)))
        (while (and row (< rt bottom))
          (let* ((height <fx> (get-height~ row))
                 (rb <fx> (+ rt height)))
            (when (>= rb top)
              (draw-row row line))
            (increase! line)
            (set! rt rb)
            (set! row (get-next~ row))
            (increase! rt row-spacing)))
        (draw-empty surface top bottom rt width))))
  
  
  (method protected virtual (draw-empty surface top bottom rt width)
    )


  ;;;
  ;;;; Scroll
  ;;;
  
  
  (method package (on-prior evt)
    (prior-page (get-shift?~ evt)))
  
  
  (method protected virtual (prior-page evt)
    (scroll (new Point 0 (get-page-size 'vert))))
  
  
  (method package (on-next evt)
    (next-page (get-shift?~ evt)))
  
  
  (method protected virtual (next-page evt)
    (scroll (new Point 0 (- (get-page-size 'vert)))))
  
  
  (method override (get-line-size orientation)
    (case orientation
      ((vert) default-row-height)
      ((horz) 8)))
  
  
  (method override (scroll-invalidate delta)
    (follow (- (get-y~ delta))))

  
  ;;;
  ;;;; Rows
  ;;;
  
  
  (method protected virtual (row-class)
    Outline-Row)

  
  (method public (add-row . rest)
    (bind-keywords ((class (row-class)) (row #f) (father #f) (position #f) . others) rest
      (let* ((father (or father root-row))
             (level (+ (get-level~ father) 1))
             (row (or row (apply new class parent: self others))))
        (set-height-noevent~ row default-row-height)
        (set-descendants-level~ row level)
        (add-son~ father row position)
        (when (row-visible?~ row)
          (increase! visible-count (+ 1 (count-visible~ row)))
          (when (not update-locked?)
            (let ((height (node-height~ row)))
              (increase! visible-height height)
              (update-visible-width-incremental level (get-effective-width~ row))
              (update-displayed)
              (update-visible)
              (let* ((line (row->line row))
                     (top (row-top line))
                     (display (get-display))
                     (display-size (get-size~ display))
                     (display-width (get-width~ display-size))
                     (display-height (get-height~ display-size)))
                (added-update-selection line 1)
                (layout-scrollee)
                (when (< top (- display-height height))
                  (scroll-view~ display (new Distance 0 height) (new Rect 0 top display-width (- display-height height))))))))
        (update-father-control father)
        row)))
  
  
  ;; is this really necessary?
  (method protected virtual (update-visible-width-incremental level width)
    )

  
  (method public virtual (remove-row row)
    (when (attached?~ row)
      (let* ((father (get-father~ row))
             (visible? (row-visible?~ row))
             (height (node-height~ row))
             (line (row->line row))
             (top (and line (row-top line))))
        (when visible?
          (decrease! visible-count (+ 1 (count-visible~ row)))
          (decrease! visible-height height))
        (remove-son~ father row)
        @is-this-really-a-good-idea
        (when (= (cardinality (get-sons~ father)) 0)
          (set-state~ father 'collapsed))
        (when line
          (removed-update-selection line 1))
        (when (and (not update-locked?) visible?)
          (update-displayed)
          (update-visible)
          (let* ((display (get-display))
                 (display-size (get-size~ display))
                 (display-width (get-width~ display-size))
                 (display-height (get-height~ display-size)))
            (layout-scrollee)
            (when (and top (< (+ top height) display-height))
              (scroll-view~ display (new Distance 0 (- height)) (new Rect 0 (+ top height) display-width display-height)))))
        (detach-descendants~ row)
        (update-father-control father))))
  
  
  (method public (move-row row father (position: position #f))
    (let ((selected? (line-selected? (row->line row))))
      (remove-row row)
      (let ((position (if (procedure? position) (position) position)))
        (add-row row: row father: father position: position)
        (when selected?
          (set-selection (cons (row->line row) (get-selection)) generate-event?: #f)))))
  
  
  (method public (find-ordered-position title father)
    (if (not title)
        0
      (ordered-position title (get-sons~ father) key: row-title test: (sort-predicate 'smaller/equal))))
  
  
  (method public virtual (remove-every-row)
    (setup-outline)
    (set! visible-count 0)
    (set! visible-height 0)
    (set! visible-width 0)
    (set! selection '())
    (when scroller
      (set-position {Point 0 0})
      (set-size {Dimension 0 0})
      (set-scroll-pos {Point 0 0}))
    (unless update-locked?
      (update-displayed)
      (layout-scrollee)
      (invalidate-view)))
  
  
  (method protected virtual (update-father-control father)
    )
  
  
  (method override (update-displayed)
    (update-displayed~ outline (get-scrolled-distance-v)))
  
  
  (method public (first-row)
    (first (get-sons~ root-row)))
  
  
  (method public (get-root-rows)
    (get-sons~ root-row))
  
  
  (method public (first-visible-row)
    (get-next~ root-row))
  
  
  (method public (get-row line <fx>) <Outline-Row+>
    ;; this test is necessary because an update lock will stop update-displayed...
    (if update-locked?
        (get-row-from~ outline root-row (+ line 1))
      (get-row~ outline line)))
  
  
  (method public (get-visible-row l)
    (continuation-capture
      (lambda (return)
        (for-each-visible-row
          (lambda (row line)
            (when (= line l)
              (continuation-return return row))))
        #f)))
  
  
  ;; for debugging...
  (method (get-row-classic line)
    (let ((row root-row))
      (while (and row (>= line 0))
        (set! row (get-next~ row))
        (decrease! line))
      row))
  
  
  (method public (row->line target)
    (continuation-capture
      (lambda (return)
        (for-each-visible-row
          (lambda (row line)
            (when (eq? row target)
              (continuation-return return line))))
        #f)))
  
  
  (method public (rows->lines targets)
    (map row->line targets))
  
  
  (method public (row-title row)
    (let ((child (first-child~ row)))
      ;; virtual dispatch problem View vs Tree-Label
      (if (is? child View)
          (get-title~ child)
        (get-title~ child))))
  
  
  (method (forward row line)
    (while (> line 0)
      (set! row (get-next~ row))
      (decrease! line))
    row)
  
  
  (method (backward row line)
    (while (> line 0)
      (set! row (get-previous~ row))
      (decrease! line))
    row)
  
  
  (method public (ensure-row-exists row)
    (ensure-visible-count (+ row 1)))
  
  
  (method public (ensure-visible-count line)
    (while (< visible-count line)
      (add-empty-row)))
  
  
  (method public virtual (add-empty-row (father: father #f))
    (let ((father (or father root-row)))
      (add-row father: father)))
  
  
  (method protected virtual (line-selected? line)
    (= line selection))
  
  
  (method protected virtual (line-draw-selected? line)
    (line-selected? line))
  

  (method public (get-row-count)
    (if (not root-row)
        0
      (cardinality (get-sons~ root-row))))


  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down evt)
    (acquire-focus)
    (let ((line (v->line (get-v~ (get-position~ evt)))))
      (set-selection line)))
  
  
  (method override (double-click evt)
    (let ((line (v->line (get-v~ (get-position~ evt)))))
      (when line
        (expand/collapse (get-row line)))))

  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method protected virtual (empty-selection)
    '())
  
  
  (method public virtual (get-selection)
    selection)
  
  
  (method public virtual (set-selection sel
                                        (force?: force? #f)
                                        (ensure-displayed?: ensure-displayed? #t)
                                        (generate-event?: generate-event? #t)
                                        (user-origin: user-origin #f))
    (when (or force? #t @parameterized-fix-me (/= sel selection))
      (let ((old selection))
        (set! selection sel)
        (when ensure-displayed?
          (ensure-displayed))
        (when (not-null? old)
          (invalidate-selection old))
        (when (not-null? selection)
          (invalidate-selection selection))
        (when generate-event?
          (call-selection-handler old selection force? user-origin)))))

  
  (method public virtual (invalidate-selection sel)
    (when (view-visible?)
      (with-transformation-surface
        (lambda (surface)
          (with ((region (selection-region sel (lambda (rect) (logical->device~ surface rect)))))
            (invalidate-region~ player region))))))
  
  
  (method protected virtual (selection-region sel transformer)
    (selection-row-region sel transformer))
  
  
  (method protected (selection-row-region line transformer)
    (new Region rect: (transformer (selection-row-rect line))))
    
  
  (method protected virtual (selection-row-rect line)
    (get-rect~ (get-row line)))

  
  ;; update slots that are line based
  (method protected virtual (added-update-selection line count)
    (when (and selection (>= selection line))
      (increase! selection count)))
  
  
  ;; update slots that are line based
  (method protected virtual (removed-update-selection line count)
    (when (not-null? selection)
      (let ((selected? #f))
        (when (>= selection line)
          (if (>= selection (+ line count))
              (decrease! selection count)
            (set! selection '())
            (set! selected? #t)))
        selected?)))
  
  
  ;;;
  ;;;; Navigation
  ;;;
  
  
  (method public (for-each-row proc)
    (for-each-descendant-row root-row -1 proc include-self?: #f))
  
  
  (method public (for-each-descendant-row row line proc (include-self?: include-self? #t) (stop-test: stop-test #f))
    (define (recurse row visible?)
      (let* ((visible? (and visible? (get-visible-flag~ row)))
             (son-visible? (and visible? (expanded?~ row))))
        (proc row (and visible? line))
        (when visible?
          (increase! line))
        (recurse-sons row son-visible?)))
    
    (define (recurse-sons row son-visible?)
      (unless (and stop-test (stop-test row))
        (iterate (get-sons~ row)
                 (lambda (row)
                   (recurse row son-visible?)))))
    
    (when include-self?
      (proc row line))
    (increase! line)
    (recurse-sons row (expanded?~ row)))
  
  
  (method public (for-each-visible-row proc (start: start #f) (end: end #f) (wrap?: wrap? #f))
    (let ((row (or start (get-next~ root-row)))
          (line (if start (row->line start) 0)))
      (when line
        (while (and row (neq? row end))
          (proc row line)
          (set! row (get-next~ row))
          (increase! line))
        (when (and wrap? start (not row))
          (for-each-visible-row proc end: (or end start))))))
  
  
  (method public (for-each-visible-descendant parent-row proc)
    (let* ((first-son (first-visible-son~ parent-row))
           (last-son (last-visible-descendant~ parent-row))
           (end-row (and last-son (get-next~ last-son))))
      (when first-son
        (for-each-visible-row proc start: first-son end: end-row))))
  
  
  (method public (collect-rows)
    (let ((queue (new-queue)))
      (for-each-row
        (lambda (row line)
          (enqueue queue row)))
      (queue-list queue)))
  
  
  (method public (collect-visible-rows)
    (let ((queue (new-queue)))
      (for-each-visible-row
        (lambda (row line)
          (enqueue queue row)))
      (queue-list queue)))

  
  ;;;
  ;;;; Content
  ;;;
  
  
  (method public virtual (get-content)
    (get-sons~ root-row))
  
  
  (method public virtual (set-content content)
    (remove-every-row)
    (set-sons~ root-row content)
    (set-next~ root-row (car content))
    (invalidate-columns)
    (set! visible-count (count-visible~ root-row))
    (set! visible-height (sons-height~ root-row))
    (set-selection (empty-selection))
    (update-displayed)
    (update-visible)
    (layout-scrollee)
    (invalidate-view)
    (update-scrollbars))
  
  
  (method public (set-content-model seq proc)
    (remove-every-row)
    (set-subrows root-row seq proc)
    (invalidate-columns)
    (set! visible-count (count-visible~ root-row))
    (set! visible-height (sons-height~ root-row))
    (set-selection (empty-selection))
    (update-displayed)
    (update-visible)
    (layout-scrollee)
    (invalidate-view)
    (update-scrollbars))
  
  
  (method public (set-subrows row seq proc)
    (unless (empty? seq)
      (let* ((row (or row root-row))
             (count 0)
             (height 0)
             (level (+ (get-level~ row) 1))
             (previous #f)
             (rows (map (lambda (info)
                          (let ((subrow (proc info)))
                            (set-father~ subrow row)
                            (set-level~ subrow level)
                            (set-height-noevent~ subrow default-row-height)
                            (when previous
                              (set-previous~ subrow previous)
                              (set-next~ previous subrow))
                            (set! previous subrow)
                            (increase! count)
                            (increase! height (get-height~ subrow))
                            subrow))
                        seq)))
        (set-sons~ row (and rows (coerce rows Axis)))
        (when (eq? (get-state~ row) 'expanded)
          (let ((old-next (get-next~ row))
                (first-son (first rows)))
            (link-rows row first-son)
            (let ((last (last-proper-descendant~ row)))
              (link-rows last old-next)))
          (increase! visible-count count)
          (increase! visible-height height)))))
  
  
  (method public virtual (invalidate-columns)
    )


  ;;;
  ;;;; State
  ;;;
  
  
  (method public virtual (expand/collapse row)
    (case (get-state~ row)
      ((collapsed) (expand row))
      ((expanded) (collapse row))))
  
  
  (method protected virtual (update-row row)
    )
  
  
  (method public (expand row (dynamic? #f))
    (when (neq? (get-state~ row) 'expanded)
      (set-state~ row 'expanded)
      (let ((first (first-descendant~ row))
            (last (last-visible-descendant~ row))
            (previous row)
            (next (get-next~ row))
            (in-expand? #t))
        (when first
          (show-rows first last previous next (row-visible?~ row) in-expand? dynamic?)))))
  
  
  (method protected virtual (invalidate-expand row top dynamic?)
    )
  
  
  (method public (expand-all row)
    (with-update-locked
      (lambda ()
        (unless (eq? (get-state~ row) 'expanded)
          (expand/collapse row))
        (iterate (get-sons~ row) expand-all))))
  
  
  (method public (collapse row)
    (when (neq? (get-state~ row) 'collapsed)
      (let ((first (first-descendant~ row))
            (in-collapse? #t))
        (if (not first)
            (set-state~ row 'collapsed)
          (let ((line (row->line row))
                (top (get-v~ (get-position~ row))))
            (let ((selected? (line-selected? line)))
              (invalidate-collapse line row top selected?)))
          (hide-rows first (last-proper-descendant~ row) (row-visible?~ row) in-collapse?)))))
  
  
  (method protected virtual (collapse-selection line selected?)
    )
  
  
  (method protected virtual (invalidate-collapse line row top selected?)
    (when selected?
      (invalidate-rect (line-rect line))))
  
  
  (method public (collapse-all row)
    (with-update-locked
      (lambda ()
        (iterate (get-sons~ row) collapse-all)
        (when (eq? (get-state~ row) 'expanded)
          (expand/collapse row)))))
  
  
  (method public (collapse-all-deep)
    (with-update-locked
      (lambda ()
        (define (collapse-sons row)
          (iterate (get-sons~ row) collapse))
        
        (define (collapse row)
          (when (eq? (get-state~ row) 'expanded)
            (collapse-sons row)
            (expand/collapse row)))
        
        (collapse-sons root-row))))
  
  
  (definition (link-rows before after)
    (when before
      (set-next~ before after))
    (when after
      (set-previous~ after before)))
  
  
  (definition (unlink-rows first last)
    (let ((previous (get-previous~ first))
          (next (get-next~ last)))
      (link-rows previous next)
      (set-previous~ first #f)
      (set-next~ last #f)))
  
  
  (method public (ensure-expanded row)
    (let ((scan (get-father~ row)))
      (while scan
        (when (eq? (get-state~ scan) 'collapsed)
          (expand scan))
        (set! scan (get-father~ scan)))))
  
  
  (method public (ensure-expanded-rows rows)
    (for-each ensure-expanded rows))


  ;;;
  ;;;; Visibility
  ;;;
  
  
  ;; this will currently only work with flat outlines
  (method public (set-visibilities predicate)
    (let ((previous root-row))
      (iterate (get-sons~ root-row)
               (lambda (row)
                 (let ((visible? (predicate row)))
                   (when visible?
                     (link-rows previous row)
                     (set! previous row)))))))
  
  
  (method package (show-rows first last previous next visible? in-expand? dynamic?)
    (link-rows previous first)
    (link-rows last next)
    (when (and (not update-locked?) visible?)
      (receive (count height width) (count-visible-size first last #t)
        (increase! visible-count count)
        (increase! visible-height height)
        (update-visible-width-incremental (get-level~ previous) width)
        (update-displayed)
        (update-visible)
        (let* ((line (or (row->line previous) -1))
               (top (or (get-v~ (get-position~ previous)) 0))
               (opens (+ top (get-height~ previous)))
               (display (get-display))
               (display-size (get-size~ display))
               (display-width (get-width~ display-size))
               (display-height (get-height~ display-size)))
          (added-update-selection (+ line 1) count)
          (invalidate-view)
          @buggy
          (when (< (+ top (get-scrolled-v)) display-height)
            (when in-expand?
              (invalidate-expand previous top dynamic?))
            (scroll-view~ display (new Distance 0 height) (new Rect 0 opens display-width display-height)))
          (layout-scrollee)
          (when (> count 0)
            (ensure-row-range-displayed (+ line 1) (+ line count)))))))
  
  
  (method package (hide-rows first last visible? in-collapse?)
    (receive (count height width) (count-visible-size first last visible?)
      (when visible?
        (decrease! visible-count count)
        (decrease! visible-height height))
      (let ((previous (get-previous~ first)))
        (unlink-rows first last)
        (when (and previous in-collapse?)
          (set-state~ previous 'collapsed))
        (when (and (not update-locked?) visible?)
          (update-displayed)
          (update-visible)
          (let* ((line (or (row->line previous) -1))
                 (top (or (get-v~ (get-position~ previous)) 0))
                 (opens (+ top (get-height~ first)))
                 (removed (removed-update-selection (+ line 1) count))
                 (display (get-display))
                 (display-size (get-size~ display))
                 (display-width (get-width~ display-size))
                 (display-height (get-height~ display-size)))
            (when (and previous in-collapse?)
              (collapse-selection line removed))
            (invalidate-view)
            @buggy
            (when (< top display-height)
              (invalidate-collapse line first top removed)
              (scroll-view~ display (new Distance 0 (- height)) (new Rect 0 (+ opens height) display-width (+ display-height height)))))
          (layout-scrollee)))))
  
  
  (method public (count-visible-size first last visible?)
    (let ((scan (and visible? first))
          (stop (and last (get-next~ last)))
          (count 0)
          (height 0)
          (width 0))
      (while (and scan (neq? scan stop))
        (increase! count)
        (increase! height (+ (get-effective-height~ scan) row-spacing))
        (let ((w (get-effective-width~ scan)))
          (when (and w (> w width))
            (set! width w)))
        (set! scan (get-next~ scan)))
      (values count height width)))
  
  
  ;;;
  ;;;; Scroll
  ;;;
  
  
  (method (follow v)
    (follow~ outline (get-scrolled-distance-v) v))


  (method public virtual (ensure-displayed . rest)
    (when (not-null? selection)
      (ensure-row-displayed selection)))
  
  
  (method public (ensure-row-displayed line)
    (let* ((row (get-row line))
           (top (get-top~ row))
           (bottom (get-bottom~ row)))
      (scroll (scroll-needed (new Point 0 top) (new Point 0 bottom) (get-scroll-context)))))
  
  
  (method public (ensure-row-range-displayed from to)
    (let ((top (get-top~ (get-row from)))
          (bottom (get-bottom~ (get-row to))))
      (scroll (scroll-needed (new Point 0 top) (new Point 0 bottom) (get-scroll-context)))))
  
  
  (method public (ensure-at-end)
    (ensure-row-displayed (- visible-count 1)))
  
  
  (method protected virtual (get-scroll-context)
    (cons 0.15 0.20))
  

  ;;;
  ;;;; Coordinates
  ;;;
  
  
  ;; This method gets called before the row is laid out.
  ;; This is why it cannot simply get the position of the row...
  (method protected (row-top line)
    (let ((row (first-visible-row))
          (v 0))
      (while (> line 0)
        (decrease! line)
        (increase! v (get-height~ row))
        (increase! v row-spacing)
        (set! row (get-next~ row)))
      v))
  
  
  (method protected virtual (line-rect line)
    (let ((row   (get-row line))
          (left  0)
          (top   (row-top line))
          (right (get-width)))
      (new Rect left top right (+ top (get-height~ row)))))
  
  
  (method (row-region line)
    (new Region rect: (get-bounds~ (get-row line))))
  
  
  (method protected (line->v l)
    (get-top~ (get-row l)))


  (method protected (v->line v)
    (call-with-v->line v
      (lambda (line v height)
        line)))


  (method protected (call-with-v->line v proc)
    (call-with-v->line~ outline v (get-scrolled-v)
      proc))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor (context: context #f))
    (new Outline-View-Old-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer)
        (jazz.jml))


(class Outline-View-Old-Descriptor extends View-Descriptor
  
  
  (form
    (<install>
      (<properties~>
        (<Property-Descriptor> name: default-row-height standard?: #f
          (<Value-Domain>      name: domain             nullable?: #t))
        (<Property-Descriptor> name: row-spacing        standard?: #f
          (<Value-Domain>      name: domain             nullable?: #t))
        (<Property-Descriptor> name: left-padding       standard?: #f
          (<Value-Domain>      name: domain             nullable?: #t))
        (<Property-Descriptor> name: top-padding        standard?: #f
          (<Value-Domain>      name: domain             nullable?: #t))
        (<Property-Descriptor> name: background-filler  standard?: #f
          (<Portfolio-Domain>  name: domain             nullable?: #t))
        (<Property-Descriptor> name: fit-to-scroller?   standard?: #f
          (<Boolean-Domain>    name: domain))))))))
