;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Picker
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.picker jazz


(import (jazz.graphic)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.development)
        (jazz.ui.view)
        (jazz.ui.window)
        (jazz.utilities))


;; Lexicon
;; - overlay : view added to the root to override mouse events


(definition In-Pick?
  #f)

(definition All-Overlays
  '())

(definition *highlighted-overlay*
  {})


(definition public (pick-figure (include: include {})
                                (exclude: exclude {})
                                (click: click {})
                                (menu: menu {})
                                (highlight: highlight {})
                                (highlight-root-only?: highlight-root-only? #f)
                                (report-figure?: report-figure? #t)
                                (obscure-unhighlighted?: obscure-unhighlighted? #t)
                                (tooltips?: tooltips? #t))
  (unless In-Pick?
    (with-locked-update
      (function ()
        (let ((windows (process-windows)))
          (when (not-null? windows)
            (setup-pick)
            (set! All-Overlays '())
            (for-each (function (overlapped)
                        (when (and (or (not include) (memq? overlapped include))
                                   (or (not exclude) (not (memq? overlapped exclude))))
                          (let ((overlay (install-overlay overlapped click menu highlight highlight-root-only? report-figure? obscure-unhighlighted? tooltips?)))
                            (invalidate-view~ overlay)
                            (set! All-Overlays (cons overlay All-Overlays)))))
                      windows)
            (set! In-Pick? #t)))))))


(definition (install-overlay overlapped click menu highlight highlight-root-only? report-figure? obscure-unhighlighted? tooltips?)
  (let ((old-root (get-root~ overlapped))
        (new-root (new Picker-Root)))
    (set-parent~ old-root new-root)
    (set-root~ overlapped new-root)
    (new Picker-Overlay
      parent: new-root
      position: {Point 0 0}
      size: (get-size~ new-root)
      click: click
      menu: menu
      highlight: highlight
      highlight-root-only?: highlight-root-only?
      report-figure?: report-figure?
      obscure-unhighlighted?: obscure-unhighlighted?
      tooltips?: tooltips?)))


(definition (remove-overlay overlay)
  (let ((new-root (get-root~ overlay)))
    (when new-root
      (let ((old-root (first-child~ new-root)))
        (set-parent~ old-root (get-parent~ new-root))
        (set-root~ (get-overlapped~ overlay) old-root)))
    (close~ overlay)))


(definition (root-overlay root)
  (second (get-children~ root)))


(definition (shortcut-hook shortcut)
  (cond ((shortcut=? shortcut {Shortcut Escape})
         (with-locked-update
           (function ()
             (end-pick)
             (clear-user-message)))
         #f)
        ((not *highlighted-overlay*)
         #f)
        (else
         (let ((highlighted-view (get-highlighted-view~ *highlighted-overlay*))
               (highlighted-position (get-highlighted-position~ *highlighted-overlay*)))
           (define (setup-view view)
             (let ((pos (acquire~ view highlighted-view highlighted-position)))
               (default-highlight~ *highlighted-overlay* view pos)
               (update-tooltip~ view (display-description (view-describe view pos)))
               (user-message "{a}" view)))
           
           (cond ((shortcut=? shortcut {Shortcut Up})
                  (if (eq? (get-highlighted-rect~ *highlighted-overlay*)
                           (get-last-cell-rect~ *highlighted-overlay*))
                      (change-highlight-rect~ *highlighted-overlay* (get-last-view-rect~ *highlighted-overlay*))
                    (let ((parent (find-ascendant~ highlighted-view View)))
                      (if (or (not parent) (is? parent Picker-Root))
                          (bell)
                        (setup-view parent))))
                  #t)
                 ((shortcut=? shortcut {Shortcut Down})
                  (let ((first (first-view~ highlighted-view)))
                    (if (not first)
                        (bell)
                      (setup-view first)))
                  #t)
                 ((shortcut=? shortcut {Shortcut Right})
                  (let ((brother (next-brother~ highlighted-view type: View)))
                    (if (eq? brother highlighted-view)
                        (bell)
                      (setup-view brother)))
                  #t)
                 ((shortcut=? shortcut {Shortcut Left})
                  (let ((brother (previous-brother~ highlighted-view type: View)))
                    (if (eq? brother highlighted-view)
                        (bell)
                      (setup-view brother)))
                  #t)
                 ((shortcut=? shortcut {Shortcut Return})
                  (end-pick)
                  (let ((click (get-click~ *highlighted-overlay*)))
                    (click highlighted-view highlighted-position))
                  #f)
                 ((shortcut=? shortcut {Shortcut F8})
                  (end-pick)
                  ;; quick solution
                  (simulate-mouse-move~ (get-toplevel))
                  #t)
                 (else
                  #f))))))


(definition (display-description description)
  (cons "Properties"
        (map (function (info)
               (bind (name value) info
                 (list :bulleted (format "{a}: {s}" name value))))
             description)))


(definition (setup-pick)
  (set-shortcut-hook~ (get-application) shortcut-hook))


(definition (end-pick)
  (set! In-Pick? #f)
  (set-shortcut-hook~ (get-application) {})
  (close-overlays))


(definition (close-overlays)
  (for-each remove-overlay All-Overlays)
  (set! All-Overlays '()))


;;;
;;;; Root
;;;


(class Picker-Root extends View)


;;;
;;;; Overlay
;;;


(class Picker-Overlay extends View
  
  
  (property click                  accessors generate)
  (property menu                   accessors generate)
  (property highlight              accessors generate)
  (property highlight-root-only?   accessors generate)
  (property report-figure?         accessors generate)
  (property obscure-unhighlighted? accessors generate)
  (property tooltips?              accessors generate)
  
  
  (slot highlighted-view     initialize {} getter generate)
  (slot highlighted-rect     initialize {} getter generate)
  (slot highlighted-position initialize {} getter generate)
  (slot last-mouse-position  initialize {} getter generate)
  (slot last-cell-rect       initialize {} getter generate)
  (slot last-view-rect       initialize {} getter generate)
  
  
  ;;;
  ;;;; Cursor
  ;;;
  
  
  (method override (cursor-update pos)
    (set-cursor :spy))
  
  
  ;;;
  ;;;; Tooltip
  ;;;
  
  
  (method override (close-self-tooltip)
    (when *current-tooltip-context*
      (close-tooltip)))
  
  
  (method override (get-tooltip?)
    tooltips?)
  
  
  (method override (get-formatted-tooltip?)
    #t)
  
  
  (method (calculate-tooltip-bounds view pos)
    (if view
        (values (get-tooltip-bounds~ view pos)
                (let ((display (get-display~ view)))
                  (acquire~ view display (get-bounds~ display))))
      (values #f #f)))
  
  
  (method override (get-tooltip-bounds pos)
    (receive (view pos) (find-view-info pos)
      (receive (cell-rect view-rect) (calculate-tooltip-bounds view pos)
        (or cell-rect view-rect))))
  
  
  (method override (get-tooltip-context pos)
    (receive (view pos) (find-view-info pos)
      (when view
        (receive (cell-rect view-rect) (calculate-tooltip-bounds view pos)
          (new Tooltip-Context
            view: view
            text: (display-description (view-describe view pos))
            bounds: (or cell-rect view-rect)
            font: {Font name: Label})))))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-move pos)
    (nextmethod pos)
    (unless (and last-mouse-position (nu=? last-mouse-position pos))
      (set! last-mouse-position pos)
      (receive (view pos) (find-view-info pos)
        (case highlight
          ((#f))
          ((#t) (default-highlight view pos))
          (else (highlight view pos)))
        (when report-figure?
          (if (not view)
              (user-message "{a}" self)
            (user-message "{a}" view))))))
  
  
  (method override (mouse-up pos)
    (nextmethod pos)
    (receive (view pos) (find-view-info pos)
      (with-locked-update
        (function ()
          (end-pick)
          (click view pos)))))
  
  
  (method (find-view-info pos)
    (let* ((root (get-root))
           (root-pos (acquire~ root self pos))
           (view (find-drawing~ root root-pos ignore: (list self)))
           (view-pos (essay view (acquire~ view root root-pos))))
      (values view view-pos)))
  
  
  ;;;
  ;;;; Highlight
  ;;;
  
  
  (method (default-highlight view pos)
    (let ((root (get-root~ (get-player~ view))))
      (let ((overlay (root-overlay root)))
        (if highlight-root-only?
            (update-highlight overlay view pos cell-rect: #f view-rect: (get-bounds~ root))
          (receive (cell-rect view-rect) (calculate-tooltip-bounds view pos)
            (update-highlight overlay view pos
              cell-rect: (and cell-rect (acquire~ root view cell-rect))
              view-rect: (and view-rect (acquire~ root view view-rect))))))))
  
  
  (method (change-highlight-rect override-rect)
    (update-highlight self highlighted-view highlighted-position override-rect: override-rect))
  
  
  (method (update-highlight overlay view pos (cell-rect: cell-rect last-cell-rect) (view-rect: view-rect last-view-rect) (override-rect: override-rect #f))
    (define (redraw same-overlay? overlay view)
      (if same-overlay?
          (when view
            (let ((player (get-player~ view))
                  (rect (acquire~ (get-root~ view) view (inflate-rect (get-bounds~ view) 1 1))))
              (redraw-window~ player rect: rect)))
        (when overlay
          (redraw-view~ overlay))))
    
    (unless (and (eq? *highlighted-overlay* overlay)
                 (eq? highlighted-view view)
                 (eq? last-view-rect view-rect)
                 (eq? last-cell-rect cell-rect)
                 (not override-rect))
      (let ((same-overlay? (eq? overlay *highlighted-overlay*))
            (old-overlay *highlighted-overlay*)
            (old-view highlighted-view))
        (set! *highlighted-overlay* overlay)
        (set! highlighted-view view)
        (set! highlighted-rect (or override-rect cell-rect view-rect))
        (set! highlighted-position pos)
        (set! last-cell-rect cell-rect)
        (set! last-view-rect view-rect)
        (redraw same-overlay? old-overlay old-view)
        (redraw same-overlay? *highlighted-overlay* highlighted-view))))
  
  
  ;;;
  ;;;; Menu
  ;;;
  
  
  (method override (context-menu pos)
    (if (not menu)
        (nextmethod pos)
      (close-popups)
      (receive (view pos) (find-view-info pos)
        (let ((root (get-root))
              (menu (menu view pos)))
          (setup-actions menu view pos)
          (track-popup-menu~ root menu (acquire~ root view pos))
          processed))))
  
  
  (method (setup-actions menu view pos)
    (for-each (function (item)
                (let* ((action-handler (get-action-handler~ item))
                       (execute (function (event)
                                  (end-pick)
                                  (when action-handler
                                    (let* ((properties (augment view: view pos: pos (get-properties~ action-handler)))
                                           (event (apply new Action-Event :action self properties)))
                                      (invoke~ action-handler self event))))))
                  (set-action-handler~ item (new Event-Handler execute: execute))))
              (get-children~ menu)))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (definition Overlay-Color
    {Color name: Black alpha: 0.25})
  
  (definition Highlight-Color
    {Color red: 109 green: 109 blue: 177})
    
  
  (method override (draw surface context lh lv)
    (define (highlighter surface)
      (set-style~ surface 'solid)
      (set-line-width~ surface 2)
      (set-color~ surface Highlight-Color))
    
    (cond ((or (not *highlighted-overlay*) (neq? (get-root) (get-root~ *highlighted-overlay*)))
           (when obscure-unhighlighted?
             (fill-rect~ surface (get-bounds) Overlay-Color)))
          (highlighted-rect
           (rectangle~ surface highlighted-rect highlighter {}))))))
