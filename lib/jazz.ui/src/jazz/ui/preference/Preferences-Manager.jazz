;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Preferences Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.preference.Preferences-Manager jazz


(import (jazz.designer)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui.view))


(class undocumented Preferences-Manager extends Canvas-Manager
  
  
  (method override (form-model form)
    (case (get-name~ form)
      ((descendants) Preferences-Editor)
      (else (nextmethod form))))
  
  
  (method override (install-in-host host)
    (nextmethod host)
    (let ((preferences (tool-preferences)))
      (when preferences
        (apply-preferences~ (get-process) preferences host))))
  
  
  (method meta override (external-name . rest)
    'preferences-manager)
  
  
  ;;;
  ;;;; Host
  ;;;
    
  
  (method meta override (host-icon)
    {Image-Resource "Preferences"})
  
  
  (method meta override (host-title)
    #f)
  
  
  (method meta override (host-position)
    #f)
  
  
  (method meta override (host-size)
    #f)
  
  
  (method override (host-workspaces)
    '(home))
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (focus-default)
    (let ((tree (locate '(descendants tree))))
      (acquire-focus~ tree)))
  
  
  ;;;
  ;;;; Component
  ;;;

  
  (method override (install rest)
    (nextmethod rest)
    (set-rights (get-preferences-rights~ (get-application)))
    (set-client (get-preferences-root~ (get-application))))
  
  
  ;;;
  ;;;; Controller
  ;;;


  (method override (new-controller)
    (new Preferences-Manager-Controller self))
  
  
  ;;;
  ;;;; Document
  ;;;


  (method override (get-modified?)
    #f)
  
  
  ;;;
  ;;;; Preferences
  ;;;
  
  
  (method override (tool-preferences)
    (get-preferences '(tools preferences-manager)))
  
  
  ;;;
  ;;;; Session
  ;;;
  
  
  (method override (confirm-close . rest)
    (save-session))
  
  
  (method override (save-session)
    (let ((pref (tool-preferences))
          (designer (get-preferences-designer~ (get-application))))
      (save-preferences~ pref designer (get-host))))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method package (on-save-preferences evt)
    (with-cursor :wait
      (lambda ()
        (save))))
  
  
  (method package (on-edit evt)
    (let ((appl (get-application)))
      (edit-document~ appl {File Profile "settings" "Preferences.jml"})))
  
  
  (method package (on-update evt)
    (set-selection~ (locate '(descendants tree)) '())
    (let ((process (get-process)))
      (load-preferences~ process)
      (set-client (get-preferences-root~ process))))

  
  ;;;
  ;;;; Properties
  ;;;
  
  
  (method override (show-properties)
    (let ((card (get-card)))
      ;; quick patch
      (if (not card)
          (bell)
        (let ((tree (locate~ card 'tree)))
          (acquire-focus~ tree)
          (when (and (null? (get-selection~ tree)) (> (get-visible-count~ tree) 0))
            (set-single-selection~ tree 0))))))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method (set-application-selection selection force?)
    )
  
  
  (method (close-application-selection)
    ))


;;;
;;;; Controller
;;;


(class Preferences-Manager-Controller extends Editor-View-Controller
  
  
  (method override (document-status)
    "Preferences"))


;;;
;;;; Editor
;;;


(class Preferences-Editor extends Descendants-Editor
  
  
  (method override (form-model form)
    (case (get-name~ form)
      ((tree) Preferences-Tree)
      (else (nextmethod form)))))


;;;
;;;; Tree
;;;


(class Preferences-Tree extends Descendants-Tree
  
  
  (form
    (<install> portfolio: :images draggable?: #t reorderable?: #t))
  
  
  (method override (update-selection expand-state selected-components)
    )
  
  
  (method override (row-state component update? level)
    (if update?
        (if (< level 1) 'expanded 'collapsed)
      'collapsed))))
