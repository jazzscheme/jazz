;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Toolbar Preferences
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.preference.Toolbar-Preferences jazz


(import (jazz.jml)
        (jazz.library)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.workspace))


(class undocumented Toolbar-Preferences extends Figure-Preferences
  
  
  (property model-name initialize #f accessors generate)


  (form
    (<install> icon: {Image-Resource "Window"}))
  
  
  ;;;
  ;;;; Applying
  ;;;
  
  
  (method override (apply-root player)
    (set-root~ player (singleton-toolbar (get-class) model-name))
    (apply-to (get-root~ player)))
  
  
  (method override (apply-preferences (parent #f))
    (assert parent)
    (let ((host (singleton-toolbar (get-class) model-name)))
      (set-parent~ host parent)
      (when title
        (set-title~ host title))
      (set-location~ host location)
      (client-activate~ (get-guest~ host))
      (set-visible?~ host #t)))
  
  
  (method (apply-to toolbar)
    (when title
      (set-title~ toolbar title))
    (set-location~ toolbar location)
    (client-activate~ (get-guest~ toolbar))
    (set-visible?~ toolbar #t))
  
  
  (method (get-class)
    (define default-toolbar-classes
      '((:menubar . Menubar)
        (:toolbar . Toolbar)
        (:status . Status-Toolbar)
        (:command . Command-Toolbar)
        (:minibuffer . Minibuffer-Toolbar)))
    
    (let* ((pref (get-workspaces-preferences))
           (classes (get-toolbar-classes~ pref))
           (pair (or (assq model-name classes) (assq model-name default-toolbar-classes)))
           (name (and pair (cdr pair))))
      (autoload-class (or name model-name))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Toolbar-Preferences-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Toolbar-Preferences-Descriptor extends Figure-Preferences-Descriptor
  
  
  (form
    (<install>                 image: {Image-Resource "Docked"}
      (<properties~>           categorized: (location model-name)
        (<Property-Descriptor> name: model-name))))
  
  
  ;;;
  ;;;; Present
  ;;;
  
  
  (method override (component-presentation-property? component property)
    (memq? (field-name property) '(title)))
  
  
  (method override (component-get-presentation component)
    (or (get-title~ component) "Toolbar"))
  
  
  (method override (component-set-presentation component designer title)
    (set-property~ designer component title: title)))))
