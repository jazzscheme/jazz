;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Projects Search
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.search.Projects-Search jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view))


(class undocumented Projects-Search extends Search-View

  
  (form
    (<install>
      (<!>                     name: domain      title: "Search"
        (<content~>
          (<Radio-Button>      name: definitions title: "Definitions" position: {Point 13 20} size: {Dimension 100 18} selected?: #t)
          (<Radio-Button>      name: references  title: "References" position: {Point 13 44} size: {Dimension 100 18})
          (<Label-View>                          title: "Project:" position: {Point 115 21} size: {Dimension 46 16})
          (<Border-View>                         position: {Point 163 17} size: {Dimension 155 22} border-insets: {Rect 2 3 2 2} border-color: {Color name: Medium}
            (<Border-View>                       border-color: {Color name: Entry-Border}
              (<Catalog-Combo> name: catalog     condensed?: #t)))
          (<Label-View>                          title: "Entries:" position: {Point 115 45} size: {Dimension 46 16})
          (<Border-View>                         position: {Point 163 41} size: {Dimension 155 22} border-insets: {Rect 2 3 2 2} border-color: {Color name: Medium}
            (<Border-View>                       border-color: {Color name: Entry-Border}
              (<Entries-Combo> name: entries)))))))
  
  
  (method override (finish rest)
    (define (setup-catalogs)
      (define (collect-catalogs)
        (map (lambda (catalog)
               (list catalog (->string (get-name~ catalog))))
             (get-catalogs~ (get-cataloguer~ (get-application)))))
      
      (let ((combo (locate 'catalog)))
        (set-choices~ combo (cons (list #f "All") (cons #f (collect-catalogs))))
        (set-value~ combo #f))
      (let ((combo (locate 'entries)))
        (set-choices~ combo (list (list #f "All")))
        (set-value~ combo #f)))
    
    (nextmethod rest)
    (setup-catalogs))
  

  ;;;
  ;;;; Domains
  ;;;

  
  (method override (get-search-domains)
    '(definitions
      references))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (catalog-change catalog)
    )
    

  ;;;
  ;;;; Search
  ;;;

  
  (method override (search-targets search-set)
    (define (search-targets-definitions search-set)
      (let ((catalog (selected-catalog))
            (cataloguer (get-cataloguer~ (get-application))))
        (let ((definitions (if catalog
                               (search-definitions~ catalog search-set)
                             (search-definitions~ cataloguer search-set))))
          (if (null? definitions)
              (message-box "No search results found")
            (case (length definitions)
              ((1) (edit~ (car definitions)))
              (else (let ((results (get-results)))
                      (set-mode~ results 'definitions)
                      (add-results~ results definitions (get-search-targets))
                      (select-results~ results))))))))
    
    (define (search-targets-references search-set)
      (let ((catalog (selected-catalog))
            (cataloguer (get-cataloguer~ (get-application))))
        (let ((references (if catalog
                              (search-references~ catalog search-set)
                            (search-references~ cataloguer search-set))))
          (if (null? references)
              (message-box "No search results found")
            (case (length references)
              ((1) (edit-reference~ (car references) search-set))
              (else (let ((results (get-results)))
                      (set-mode~ results 'references)
                      (set-context~ results #f)
                      (add-results~ results references (get-search-targets) search-set)
                      (select-results~ results))))))))
    
    (case (get-selected-element~ (get-content~ (locate 'domain)))
      ((definitions)
       (search-targets-definitions search-set))
      ((references)
       (search-targets-references search-set))))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method (selected-catalog)
    (get-value~ (locate 'catalog))))


;;;
;;;; Catalog
;;;


(class Catalog-Combo extends Combo-Box
  
  
  (method override (content-change origin)
    (catalog-change~ (get-creator) (get-value))))


(register-autoclass Catalog-Combo)


;;;
;;;; Entries
;;;


(class Entries-Combo extends Combo-Box)


(register-autoclass Entries-Combo))
