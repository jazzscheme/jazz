;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Search Results
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.search.Search-Results jazz


(import (jazz.catalog)
        (jazz.editor.jazz)
        (jazz.graphic)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.clipboard)
        (jazz.ui.search)
        (jazz.ui.view)
        (jazz.ui.window))


(class undocumented Search-Results extends Navigable-Results


  (slot current-resultset initialize #f  getter generate)
  (slot resultset-stack   initialize '() getter generate)


  (form
    (<install>
      (<!>                           name: tools
        (<!>                         name: clear     tooltip-text: "Pop Resultset")
        (<Separator>                                 size: {Dimension 4 0})
        (<Layout-View>                               size: {Dimension 150 22} layout-type: fill layout-insets: {Rect 0 2 0 2} location: tail
          (<Border-View>                             border-color: {Color Dark}
            (<Resultset-Combo>       name: resultset))))
      (<!>                           name: scroller
        (<!>                         name: content
          (<!>                       name: header
            (<!>                     name: content
              (<!>                   name: locations filled-column: location multiple-selection?: #t context-menu-handler: {Event-Handler :form on-context-menu}
                (<Tree-Node-Column>  name: name      title: "Name" width: 200)
                (<Tree-Label-Column> name: location  title: "Location" width: 337)
                (<Tree-Label-Column> name: type      title: "Type" width: 100))))))))
  
  
  (method meta override (external-name . rest)
    'search-results)
  
  
  (method override (form-model form)
    (case (get-name~ form)
      ((locations) Search-Results-Tree)
      (else (nextmethod form))))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon)
    {Image-Resource "Find"})
  
  
  (method meta override (host-title)
    "Search Results")
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-context-menu evt)
    (let* ((sender (get-sender~ evt))
           (pos (get-position~ evt))
           (acquired (acquire sender pos))
           (menu (get-context-menu)))
      (when menu
        (with-context-click~ sender pos
          (lambda ()
            (track-popup-menu menu acquired))))))
  
  
  (method override (get-context-menu)
    (new Found-Menu))

  
  ;;;
  ;;;; Edit
  ;;;
  

  (method override (edit-selection forward?)
    (let ((selection (get-selection~ (get-tree))))
      (for-each (lambda (rank)
                    (edit-result rank forward? #t #t))
                  selection)))
  

  (method public (edit-result rank forward? focus? visible?)
    (with-cursor :wait
      (lambda ()
        (set-active-search (get-search~ current-resultset))
        (let ((result (get-user-data~ (get-row~ (get-tree) rank))))
          (edit~ result forward? focus? visible?)))))
  
  
  ;;;
  ;;;; Replace
  ;;;
  
  
  (method public (replace-selected)
    (let ((search (get-search~ current-resultset)))
      (if (not search)
          (bell)
        (if (not (every? (lambda (result)
                           (is? result File-Search-Result))
                         (get-results~ current-resultset)))
            (message-box "Replace selected currently only supports file searches")
          (let ((code (message-box "Automatically save & close except for currently edited files?" type: 'confirmation)))
            (if (eq? code 'cancel)
                (throw-cancel)
              (set-active-search search)
              (let ((auto-save/close? (eq? code 'yes))
                    (selection (get-selection~ (get-tree)))
                    (modified-count 0))
                (for-each (lambda (rank)
                            (receive (frame new?) (edit-result rank #t #f #f)
                              (let* ((guest (get-guest~ frame))
                                     (text (get-text-view~ guest)))
                                (user-feedback "Processing {a}..." (name-status~ (get-controller~ text)))
                                (replace-all~ search text)
                                (when (and auto-save/close? new?)
                                  (when (get-modified?~ text)
                                    (save~ guest)
                                    (increase! modified-count))
                                  (close~ frame)))))
                          selection)
                (when auto-save/close?
                  (message-box (format "{a} file{a} modified" modified-count (format-plural modified-count))))
                (user-feedback "Done"))))))))


  ;;;
  ;;;; Result
  ;;;


  (method public (select-results search results)
    (let ((resultset (new Search-Resultset search results)))
      (preserve-current-selection)
      (set! current-resultset resultset)
      (set! resultset-stack (cons current-resultset resultset-stack))
      (update-resultset)
      (update-resultset-combo))
    (set-active-search search)
    (select-resultset))
  
  
  (method (set-resultset resultset)
    (preserve-current-selection)
    (set! current-resultset resultset)
    (update-resultset)
    (update-resultset-combo))
  
  
  (method (preserve-current-selection)
    (when current-resultset
      (set-selection~ current-resultset (get-selection~ (get-tree)))))


  ;;;
  ;;;; Resultset
  ;;;
  
  
  (method (pop-resultset)
    (set! resultset-stack (cdr resultset-stack))
    (set! current-resultset (car resultset-stack))
    (update-resultset)
    (update-resultset-combo))
  
  
  (method override (clear-resultset)
    (if (<= (length resultset-stack) 1)
        (bell)
      (pop-resultset)))
  
  
  (method (update-resultset-combo)
    (let ((combo (locate 'resultset)))
      (set-choices~ combo (map (lambda (result)
                                 (let ((search (get-search~ result)))
                                   (list result (if (not search) "" (present~ search)))))
                               resultset-stack))
      (set-value~ combo current-resultset)))


  ;;;
  ;;;; Update
  ;;;


  (method (update-resultset)
    (let ((appl (get-application))
          (tree (get-tree))
          (search (get-search~ current-resultset))
          (results (get-results~ current-resultset))
          (selection (get-selection~ current-resultset)))
      (remove-every-row~ tree)
      (with-update-locked~ tree
        (lambda ()
          (set-toplevel-controls?~ (get-node-column~ tree) (supports-drill-down?~ search))
          (for-each add-result (sort string<? results key: present-name~))))
      (when (and results (> (get-visible-count~ tree) 0))
        (set-selection~ tree (or selection (list 0))))
      (with-locked-update
        (lambda ()
          (select-docked Search-Results)))
      (update-search-actions~ appl)))

  
  (method (update-tree-row row)
    (let ((result (get-user-data~ row)))
      (for-each (lambda (result)
                  (add-result result father: row))
                (drill-down~ result))))
  
  
  (method (add-result result (father: father #f))
    (let ((tree (get-tree))
          (state (if (supports-drill-down?~ (get-search~ result)) 'dynamic 'collapsed))
          (node (new Tree-Node title: (present-name~ result) image: (get-icon~ result)))
          (loc (new Tree-Label title: (present-result~ result)))
          (type (new Tree-Label title: (present-type~ result)))
          (user-data result))
      (add-row~ tree father: father state: state children: (list node loc type) user-data: user-data))))


;;;
;;;; Search-Results-Tree
;;;


(class Search-Results-Tree extends Tree-View
          

  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions)
    (cons (find-actions 'search-results-tree)
          (nextmethod)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-search)
    (find-ascendant Search-Results))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (return-press key)
    (edit-selection~ (get-search) #t))
  
  
  (method override (copy-selection)
    (set-clipboard-text
      (format "{l detail: :human}"
        (map (lambda (n)
               (let ((result (get-user-data~ (get-row n))))
                 (present-name~ result)))
             (get-selection)))))
  
  
  (method override (select-all . rest)
    )
  
  
  (method (on-edit-selection evt)
    (edit-selection~ (get-search) #t))
  
  
  (method override (update-row row)
    (update-tree-row~ (get-search) row)))


;;;
;;;; Search-Results-Tree-Actions
;;;


(class Search-Results-Tree-Actions extends Actions
  
  
  (form
    (<install>
      (<Action-Item> name: action         action-handler: {Event-Handler :focus on-action} shortcut: {Shortcut :control Return})
      (<Action-Item> name: edit-selection action-handler: {Event-Handler :focus on-edit-selection} shortcut: {Shortcut F12} alternate-shortcut: {Shortcut :alt #\.}))))


;;;
;;;; Resultset-Combo
;;;


(class Resultset-Combo extends Combo-Box
  
  
  (method override (content-change origin)
    (set-resultset~ (get-creator~ parent) (get-value))))


;;;
;;;; Found-Menu
;;;


(class Found-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item> name: copy title: "Copy" icon: {Image-Resource "Copy"} action-handler: {Event-Handler :focus on-copy})))))
