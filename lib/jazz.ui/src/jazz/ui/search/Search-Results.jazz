;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Search Results
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.search.Search-Results jazz


(import (jazz.catalog)
        (jazz.editor.jazz)
        (jazz.graphic)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window))


(class undocumented Search-Results extends Navigable-Results


  (slot mode             initialize #f accessors generate)
  (slot constituent-test initialize #f accessors generate)
  (slot context          initialize #f accessors generate)
  (slot current-results  initialize #f)
  (slot results-stack    initialize '() getter generate)


  (form
    (<install>
      (<!>                           name: tools
        (<!>                         name: clear                       tooltip-text: "Pop Results")
        (<Separator>                 size: {Dimension 4 0})
        (<Border-View>                                                 size: {Dimension 120 21} border-insets: {Rect 2 2 2 2} border-color: {Color Medium}
          (<Border-View>                                               border-color: {Color Dark}
            (<Stack-Combo>           name: stack))))
      (<!>                           name: scroller
        (<!>                         name: content
          (<!>                       name: header
            (<!>                     name: content
              (<!>                   name: results                     filled-column: location multiple-selection?: #t context-menu-handler: {Event-Handler :form on-context-menu}
                (<Tree-Node-Column>  name: name                        title: "Name" width: 200)
                (<Tree-Label-Column> name: location                    title: "Location" width: 337)
                (<Tree-Label-Column> name: type                        title: "Type" width: 100))))))))
  
  
  (method meta override (external-name . rest)
    'search-results)
  
  
  (method override (form-model form)
    (case (get-name~ form)
      ((results) Search-Results-Tree)
      (else (nextmethod form))))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta override (host-icon)
    {Image-Resource "Find"})
  
  
  (method meta override (host-title)
    "Search Results")
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-context-menu evt)
    (let* ((sender (get-sender~ evt))
           (pos (get-position~ evt))
           (acquired (acquire sender pos))
           (menu (get-context-menu)))
      (when menu
        (with-context-click~ sender pos
          (lambda ()
            (track-popup-menu menu acquired))))))
  
  
  (method override (get-context-menu)
    (new Found-Menu))
  
  
  (method override (clear-results)
    (if (<= (length results-stack) 1)
        (bell)
      (pop-results)))

  
  ;;;
  ;;;; Navigation
  ;;;
  

  (method override (edit-selection forward?)
    (let ((selection (get-selection~ (get-tree))))
      (define (edit)
        (for-each (lambda (item)
                    (edit-item item forward? #t #t))
                  selection))
      
      (case (length selection)
        ((0))
        ((1) (edit))
        (else (with-locked-update
                (lambda ()
                  (edit)))))))


  (method public (edit-item item forward? focus? visible?)
    (let ((definition (get-user-data~ (get-row~ (get-tree) item))))
      (edit-document definition forward? focus? visible?)))


  ;;;
  ;;;; Edition
  ;;;


  (method (edit-document info forward? focus? visible?)
    (define (edit-source source results targets)
      (let* ((appl (get-application)))
        (receive (frame new?) (edit-document~ appl source visible?: #f return-new?: #t)
          (let ((guest (get-guest~ frame)))
            (when (and targets (is? guest Text-View))
              (if (number? targets)
                  (set-caret~ guest (new Cell results targets))
                (if forward?
                    (select-expr~ guest targets)
                  (select-expr-backward~ guest targets))))
            (when visible?
              (focus-host~ frame))
            (values frame new?)))))
    
    (define (edit-reference entry target)
      (if (string? target)
          (edit-textual-reference~ entry target constituent-test activate-focus?: focus?)
        (edit-reference~ entry target activate-focus?: focus?)))
    
    (with-cursor :wait
      (lambda ()
        (cond ((is? info Catalog-Entry)
               (case mode
                 ((references)
                  (edit-reference info (get-references-target)))
                 (else
                  (edit~ info activate-focus?: focus?))))
              ((and (pair? info) (is? (car info) Catalog-Entry))
               (bind (entry . target) info
                 (case mode
                   ((references)
                    (edit-reference entry target))
                   (else
                    (edit~ entry activate-focus?: focus?)))))
              ((is? (car info) Moniker)
               (bind (source results targets) info
                 (edit-source source results targets)))
              (else
               (bind (class source results targets) info
                 (edit-source source results targets)))))))


  ;;;
  ;;;; Results
  ;;;
  
  
  (method protected virtual (item-icon info)
    (cond ((is? info Class-Entry)         {Image-Resource "Class"})
          ((is? info Interface-Entry)     {Image-Resource "TypeInterface"})
          ((is? info Static-Init-Entry)   {Image-Resource "StaticInit"})
          ((is? info Instance-Init-Entry) {Image-Resource "InstanceInit"})
          ((is? info Variable-Entry)      {Image-Resource "ClassVariable"})
          ((is? info Constructor-Entry)   {Image-Resource "ClassConstructor"})
          ((is? info Method-Entry)        {Image-Resource "ClassMethod"})
          (else                           {Image-Resource "TypeFile"})))
  
  
  (method protected virtual (item-moniker info)
    (cond ((is? info Catalog-Entry)
           (get-moniker~ info))
          ((is? (car info) Moniker)
           (car info))
          (else
           (cadr info))))
  
  
  (method protected virtual (item-location info)
    (let ((moniker (item-moniker info)))
      (if (not moniker)
          ""
        (present-location~ moniker))))
  
  
  (method protected virtual (item-type info)
    (cond ((is? info Class-Entry) "Class")
          ((is? info Interface-Entry) "Interface")
          ((is? info Static-Init-Entry) "Static Init")
          ((is? info Instance-Init-Entry) "Instance Init")
          ((is? info Variable-Entry) "Variable")
          ((is? info Constructor-Entry) "Constructor")
          ((is? info Method-Entry) "Method")
          (else "")))


  (method public (present-info info)
    (cond ((is? info Catalog-Entry)
           (present~ info))
          ((is? (car info) Moniker)
           (bind (moniker pos target) info
             (present-name~ moniker)))
          (else
           (->string (car info)))))


  (method public (add-results results targets (target #f))
    (preserve-current-selection)
    (set! current-results (list results targets target #f))
    (set! results-stack (cons current-results results-stack))
    (update-results)
    (update-stack-combo))
  
  
  (method (pop-results)
    (set! results-stack (cdr results-stack))
    (set! current-results (car results-stack))
    (update-search-targets)
    (update-results)
    (update-stack-combo))
  
  
  (method (set-results info)
    (preserve-current-selection)
    (set! current-results info)
    (update-search-targets)
    (update-results)
    (update-stack-combo))
  
  
  (method (get-references-target)
    (third current-results))
  
  
  (method (preserve-current-selection)
    (when current-results
      (set-fourth! current-results (get-selection~ (get-tree)))))
  
  
  (method (update-search-targets)
    (bind (results targets target) current-results
      (let ((appl (get-application)))
        (end-search-edition~ appl)
        (set-search-targets~ appl targets))))
  
  
  (method (update-stack-combo)
    (let ((combo (locate 'stack)))
      (set-choices~ combo (map (lambda (info)
                                 (bind (results targets target) info
                                   (list info (join (map first targets) ", "))))
                               results-stack))
      (set-value~ combo current-results)))


  (method (update-results)
    (bind (results targets target selection) current-results
      (let ((appl (get-application))
            (tree (get-tree)))
        (remove-every-row~ tree)
        (with-update-locked~ tree
          (lambda ()
            (let ((flag
                   (case mode
                     ((references) #t)
                     (else #f))))
              (set-toplevel-controls?~ (get-node-column~ tree) flag))
            (for-each (lambda (info)
                        (add-item info target: target))
                      (sort string<? results key: present-info))))
        (when (and results (> (get-visible-count~ tree) 0))
          (set-selection~ tree (or selection (list 0))))
        (with-locked-update
          (lambda ()
            (select-docked Search-Results)))
        (update-search-actions~ appl))))
  
  
  (method (add-item info (father: father #f) (target: target #f))
    (let ((tree (get-tree))
          (state (if (eq? mode 'references) 'dynamic 'collapsed))
          (node (new Tree-Node title: (present-info info) image: (item-icon info)))
          (location (new Tree-Label title: (item-location info)))
          (type (new Tree-Label title: (item-type info)))
          (user-data (if target (cons info target) info)))
      (add-row~ tree father: father state: state children: (list node location type) user-data: user-data)))

  
  (method protected (update-tree-row row)
    (case mode
      ((references)
        (bind (entry . target) (get-user-data~ row)
          (let* ((name (get-name~ entry))
                 (appl (get-application))
                 (cataloguer (get-cataloguer~ appl))
                 (definitions (get-references~ cataloguer name context: context)))
            (for-each (lambda (info)
                        (add-item info father: row target: (list (make-search-context (->string name) whole-words?: #t))))
                      (sort string<? definitions key: present-info)))))))
  
  
  (method (selected-files)
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree)))
      (collect (lambda (rank)
                 (let ((info (get-user-data~ (get-row~ tree rank))))
                   (when (and (is? info Pair) (is? (car info) File))
                     (car info))))
               selection))))


;;;
;;;; Stack-Combo
;;;


(class Stack-Combo extends Combo-Box
  
  
  (method override (content-change origin)
    (set-results~ (get-creator~ parent) (get-value))))


;;;
;;;; Found-Menu
;;;


(class Found-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item> name: copy title: "Copy" icon: {Image-Resource "Copy"} action-handler: {Event-Handler :focus on-copy})))))
