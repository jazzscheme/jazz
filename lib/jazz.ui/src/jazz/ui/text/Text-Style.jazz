;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Text Style
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.text.Text-Style jazz


(import (jazz.graphic)
        (jazz.library)
        (jazz.literals)
        (jazz.ui))


(class Text-Style extends Style
  
  
  (attributes (alignment)
    (font-family initialize {})
    (font-size   initialize {})
    (font-weight initialize {})
    (font-style  initialize {})
    (font        initialize {})
    (color       initialize {})
    (highlight   initialize {})
    (frame       initialize {}))
  
  
  (method (fold-literal)
    (fold-exemplar 'Text-Style))
  
  
  ;;;
  ;;;; Size
  ;;;
  
  
  (method (get-height)
    (get-height~ (get-metrics~ font)))
  
  
  (method (run-width run)
    (let ((string (get-string~ run)))
      ;; warning this coerce could become inneficient
      (text-width~ font (coerce string String))))
  
  
  ;;;
  ;;;; Scan
  ;;;
  
  
  (method (scan h <fx> start <fx> end <fx> tabs run <Run> proc)
    (let ((string (get-string~ run)))
      (let ((left {})
            (mid {})
            (right h)
            (len <fx> (cardinality string)))
        (loop (for n from start below (max start (min end len)))
              (let ((c (element string n)))
                (if (eqv? c #\tab)
                    (begin
                      (while (<= (car tabs) right)
                        (set! tabs (cdr tabs)))
                      (let ((width (- (car tabs) right)))
                        (set! left right)
                        (set! mid (+ right (fxround/ width 2)))
                        (set! right (+ left width))))
                  (let ((width (get-char-width~ font c)))
                    (set! left right)
                    (set! mid (+ right (fxround/ width 2)))
                    (increase! right width)))
                (proc c n left mid right))))))
  
  
  ;;;
  ;;;; Wrap
  ;;;
  
  
  (definition Break-Characters
    '(#\space))
  
  
  (method (wrap context margin h <fx> v <fx> width <fx> tabs place break run <Run>)
    (let* ((start h)
           (string (get-string~ run))
           (len (cardinality string))
           (end (- len 1))
           (offset 0)
           (brk {})
           (brk-right {})
           (rightmost h))
      (scan h 0 len tabs run
        (function (c <char> n <fx> left <fx> mid <fx> right <fx>)
          (set! rightmost right)
          (when (memq? c Break-Characters)
            (set! brk (+ n 1))
            (set! brk-right right))
          (when (and (> (+ h (- right start margin)) width)
                     (or (not brk)
                         (not (memq? c Break-Characters))))
            (let ((n <fx> (either brk n)))
              (place (new Run (subseq! string offset n) self))
              (break right)
              (set! h margin)
              (set! start (if brk brk-right left))
              (set! offset n)
              (set! brk {})))
          (when (>= n end)
            (if (= offset 0)
                (place run)
              (place (new Run (subseq! string offset (cardinality string)) self))))))
      (+ h (- rightmost start))))
  
  
  (method (height-update height)
    )
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method (draw-style surface <Surface> context string <Subseq$string$> h <fx> v <fx> line-height <fx> tabs left <fx> enabled? <bool>) <fx>
    (set-font~ surface font)
    (let* ((font-height (get-height~ (get-metrics~ font)))
           (v (+ v (justify-element font-height line-height)))
           (color (if (getf context print-black/white?:)
                      (registered-color 'Black)
                    (either color (registered-color 'Black)))))
      (if (not enabled?)
          (begin
            (draw-text~ surface h v (coerce string String) #f)
            0)
        (let ((width (draw-tabbed-text~ surface h v string tabs left color highlight)))
          (when frame
            (let ((rect (new Rect h v (+ h width) (+ v line-height))))
              (frame-rect~ surface rect frame)))
          width))))))
