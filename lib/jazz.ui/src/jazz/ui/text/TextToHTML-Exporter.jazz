;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Text -> HTML Export
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.text.TextToHTML-Exporter jazz


(import (jazz.locale)
        (jazz.ui))


(class undocumented TextToHTML-Exporter extends Object
    
  
  (slot text)
  (slot output)
  
  
  (method override (initialize text output)
    (nextmethod)
    (set! text~self text)
    (set! output~self output))
  
  
  (method public (export-html)
    (print-header)
    (to-html text #f)
    (print-footer))
  
  
  (method (to-html element in-table?)
    (typecase element
      ((Text-View) (text->html element in-table?))
      ((Paragraph) (paragraph->html element in-table?))
      ((Tree-View) (tree->html element #t))
      ((Tree-Row) (row->html element))
      ((Tree-Label) (tree-label->html element))
      ((Tree-Label-Column) (tree-column->html element))
      ((Table-Border) (tree->html (locate~ element 'tree)
                                (get-shown?~ (locate~ element 'header))))
      ((Table-Cell) (to-html (locate~ element 'text) in-table?))
      (else (error "Unsupported element: {t}" element))))
  
  
  (method (paragraph->html paragraph in-table?)
    (let* ((fmt (get-format~ paragraph))
           (bulleted? (get-bulleted?~ fmt)))
      (when bulleted?
        (format output "<UL type=disc><LI style='margin-left:{a}in;font-size:12.0pt;font-family:\"Times New Roman\"'>{%}"
                (/ (- (get-left-margin~ fmt) 16) 64)))
      (for-each (lambda (run)
                  (run->html run in-table?))
                (get-runs~ paragraph))
      (when bulleted?
        (format output "</LI></UL>{%}"))
      (unless bulleted?
        (format output "<BR>{%}"))))
  
  
  @future
  (method (run->html run in-table?)
    (let* ((style (get-style~ run))
           (default? (eqv? (get-name~ style) 'Base))
           (color (get-value~ style color:)))
      ;; Tags opening
      (when (not default?)
        (when (not-null? color) (format output "<COLOR={a}>" (get-name~ color))))
      ;; Content printing
      (format output "{a}" (get-string~ run))
      ;; Tags closing
      (when (not default?)
        (when (not-null? color) (format output "</COLOR>")))))
  
  
  (method (run->html run in-table?)
    (let ((style (get-style~ run)))
      (if (is? style View-Style)
          (to-html (get-view~ style) in-table?)
        (let ((name (get-name~ style))
              (string (get-string~ run)))
          (typecase style
            ((Hyperlink-Style)
             (format output "<A href=\"#{a}\">{a}</A>" string string))
            ((Text-Style)
             (case name
               ((Heading0) (format output "<H1><A name=\"{a}\">{a}</A></H1>" string string))
               ((Heading1) (format output "<H2><A name=\"{a}\">{a}</A></H2>" string string))
               ((Heading2) (format output "<H3><A name=\"{a}\">{a}</A></H3>" string string))
               ((Heading3) (format output "<H4><A name=\"{a}\">{a}</A></H4>" string string))
               (else (format output "{a}" string)))))))))
  
  
  (method (text->html element in-table?)
    (for-each (lambda (paragraph)
                (paragraph->html paragraph in-table?))
              (get-paragraphs~ element)))
  
  
  (method (tree->html tree header?)
    @w
    (format output "<TABLE BORDER=\"0\" style=\"border: 1px solid black\" CELLSPACING=\"0\" BGCOLOR=WHITE>{%}")
    (format output "<TABLE BORDER=\"1\" CELLSPACING=\"0\" BGCOLOR=WHITE>{%}")
    (when header?
      (format output "<TR>{%}")
      (for-each (lambda (column)
                  (format output "<TH>")
                  (to-html column #t)
                  (format output "</TH>{%}"))
                (get-columns~ tree))
      (format output "</TR>{%}"))
    (for-each-row~ tree row->html)
    (format output "</TABLE>{%}"))
  
  
  (method (row->html row)
    (format output "<TR>")
    (let ((in-table? #t)
          (first? #t))
      (for-each (lambda (child)
                  (format output "<TD VALIGN=\"TOP\">")
                  (when (and first? (/= (get-level~ row) 0))
                    (format output "-- "))
                  (to-html child in-table?)
                  (format output "</TD>{%}")
                  (set! first? #f))
                (get-children~ row)))
    (format output "</TR>{%}"))
  
  
  (method (tree-label->html data)
    (let ((string (localize (get-title~ data))))
      (if (empty-string? string)
          (format output "&nbsp;")
        (format output "{a}" string))))
  
  
  (method (tree-column->html column)
    (format output "{a}" (localize (get-title~ column))))
  
  
  (method public (print-header (name: name "") (header?: header? #f) (header-level: header-level 2) (style-sheet: style-sheet #f))
    (format output "<HTML>{%}")
    (format output "<HEAD>{%}")
    (when (/= name "")
      (format output "<TITLE>{a}</TITLE>{%}" (localize name)))
    (when style-sheet
      (format output "<LINK REL=STYLESHEET HREF=\"{a}\" TYPE=\"text/css\">{%}"
        style-sheet))
    (format output "</HEAD>{%}")
    (format output "{%}")
    (when header?
      @am
      (print-title name header-level centered?: #t))
    @w
    (format output "<BODY BGCOLOR=#b7d6d9>{%}"))
  
  
  (method public (print-footer)
    (format output "</BODY>{%}")
    (format output "</HTML>"))))
