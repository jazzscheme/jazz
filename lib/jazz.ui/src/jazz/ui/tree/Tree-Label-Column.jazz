;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Tree Label Columns
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.tree.Tree-Label-Column jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.locale)
        (jazz.ui))


(class Tree-Label-Column extends Tree-Column
  
  
  (property protected padding initialize 0 accessors explicit)
  
  
  ;;;
  ;;;; Cells
  ;;;
  
  
  (method override (cell-class)
    Tree-Label)
  
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-padding)
    padding)


  (method public (set-padding value)
    (set! padding value)
    (invalidate-view~ parent))
  

  ;;;
  ;;;; Tooltip
  ;;;
  
  
  (method override (get-data-tooltip data)
    (when (is? data Tree-Label)
      (get-title~ data)))

  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (definition Default-Color
    {Color Black})
  
  
  (method override (draw-at surface context row data h v width height)
    (nextmethod surface context row data h v width height)
    (draw-title surface context row data h v width height))
  
  
  (method protected virtual (draw-title surface context row data h v width height)
    (let ((title (localize (or (get-title~ data) "")))
          (font  (or (get-font~ data) (effective-font~ parent)))
          (color (get-color~ data)))
      (set-text-color~ surface (cell-color color context))
      (set-font~ surface font)
      (let ((pos (title-position surface width height title)))
        (draw-text~ surface (+ h (get-h~ pos) (case justification ((head) (+ 1 padding)) ((tail) (- -1 padding)) (else 0))) (get-v~ pos) title))))
  
  
  (method protected virtual (title-position surface width height title)
    (justify-title surface width height title justification))
  
  
  (method protected (cell-color color context)
    (let ((enabled? (view-enabled?~ parent))
          (selected? (and (getf context selected?:)
                          (cell-selected? context))))
      (or (and color (determine-color~ color enabled? (and selected? (focused?~ parent))))
          (cond ((not enabled?) {Color Dark})
                (selected? (selected-text-color~ parent))
                (else (or (get-text-color~ (get-look~ parent)) Default-Color))))))
  
  
  (method override (text-h level)
    1)
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Tree-Label-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Tree-Label-Descriptor extends Tree-Column-Descriptor
  
  
  (form
    (<install>
      (<properties~>
        (<Property-Descriptor> name: padding title: "Padding")))))))
