;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Tree View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Stephane Le Cornec
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.tree.Tree-View jazz


(import (jazz.designer)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.library.component)
        (jazz.locale)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.skin)
        (jazz.ui.view)
        (jazz.ui.window))


(class Tree-View extends Outline-View-Old implements Selection-Provider
  

  ;;;
  ;;;; Lexicon
  ;;;
  
  
  ;; visible   : a row that has all its parents expanded
  ;; displayed : a row that is displayed on-screen even if only partially
  ;; row       : a Row object
  ;; column    : a Column object
  ;; site      : a Site object (row + column)
  ;; line      : the rank of a visible row
  ;; col       : the rank of a visible column
  ;; cell      : a Cell object (line + col)
  
  
  ;;;
  ;;;; Notes
  ;;;
  
  
  ;; For now, cell selection-mode only supports single select mode

  
  (definition delayed-autorow
    #f)

  
  (property protected portfolio                 initialize #f   accessors explicit)
  (property protected show-header?              initialize #t   accessors generate)
  ;; row cell node
  (property protected selection-mode            initialize 'row accessors explicit)
  (property protected multiple-selection?       initialize #f   accessors explicit)
  (property protected can-select-empty?         initialize #t   accessors explicit)
  (property protected always-select?            initialize #f   accessors generate)
  (property protected show-unfocused-selection? initialize #t   accessors explicit)
  (property protected focused-on-descendants?   initialize #f   accessors explicit)
  (property protected auto-expand/collapse?     initialize #f   accessors generate)
  (property protected cell-background           initialize #f   accessors explicit)
  (property protected empty-background          initialize #f   accessors explicit)
  (property protected filled-column             initialize #t   accessors generate)
  (property protected column-spacing            initialize 0    accessors explicit)
  (property protected column-frequency          initialize 1    accessors explicit)
  (property protected reorderable?              initialize #f   accessors generate)
  (property protected content                   initialize #f   accessors explicit)
  
  
  (slot protected columns                       initialize '()  getter generate)
  (slot protected node-column                   initialize #f   getter generate)
  (slot protected sorted-columns                initialize '()  getter generate)
  (slot protected selection-pivot               initialize #f)
  (slot protected highlighted                   initialize '())
  (slot protected indentation                   initialize 16   getter generate)
  (slot protected columns-right            <fx> initialize 0    getter generate)
  (slot protected edited-site                   initialize #f   getter generate)
  (slot protected cell-editor                   initialize #f   getter generate)
  (slot protected auto-expanded-row             initialize #f   setter generate)
  (slot protected locator                       initialize (new Tree-Locator self))
  (slot protected selection-manager             initialize (new Selection-Manager self))
  (slot protected merge-cell-table              initialize (make-table test: site=? hash: site-hash) getter generate)
  
  
  (form
    (<install> background: {Color Tree-Background}))


  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set! selection '()))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (invalidate-columns)
    (fill-column)
    (invalidate-header))
  
  
  (method override (destroy-component)
    (end-edition)
    (nextmethod))
  

  ;;;
  ;;;; Component
  ;;;


  (method override (add-child child)
    (nextmethod child)
    (when (is? child Tree-Column)
      (set! columns (append! columns (list child)))
      (invalidate-columns)
      (fill-column)
      (invalidate-header)))
  
  
  (method override (remove-child child)
    (nextmethod child)
    (unless (destroying?)
      (when (is? child Tree-Column)
        (set! columns (remove! child columns))
        (when (eq? child node-column)
          (set! node-column #f))
        ;; should idealy be done incrementaly
        (invalidate-columns)
        (fill-column)
        (invalidate-header))))
  
  
  (method override (validate-component validation-context)
    (let ((values (filled-column-values (list self)))) 
      (unless (or (null? values) (memq? filled-column values))
        (add-violation~ 
          (get-manager~ validation-context) 
          (new Integrity-Violation message: (format "filled-column must be one of {a}: {t}" (join (map ->string values) ", ") filled-column) property: filled-column) 
          self 
          (get-root~ validation-context) 
          (get-editor~ validation-context)))))

  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-portfolio)
    (when portfolio
      (if (is? portfolio Portfolio)
          portfolio
        (set! portfolio (child portfolio))
        portfolio)))


  (method public (set-portfolio object)
    (set! portfolio object)
    (invalidate-view))


  (method public (get-multiple-selection?)
    multiple-selection?)


  (method public (set-multiple-selection? flag)
    (set! multiple-selection? flag)
    (invalidate-view))


  (method public (get-can-select-empty?)
    can-select-empty?)


  (method public (set-can-select-empty? flag)
    (set! can-select-empty? flag)
    (invalidate-view))


  (method public (get-show-unfocused-selection?)
    show-unfocused-selection?)


  (method public (set-show-unfocused-selection? flag)
    (set! show-unfocused-selection? flag)
    (invalidate-view))


  (method public (get-focused-on-descendants?)
    focused-on-descendants?)


  (method public (set-focused-on-descendants? flag)
    (set! focused-on-descendants? flag)
    (invalidate-view))


  (method public (get-selection-mode)
    selection-mode)


  (method public (set-selection-mode value)
    (set! selection-mode value)
    (invalidate-view))


  (method public (get-cell-background)
    cell-background)
  
  
  (method public (set-cell-background color)
    (set! cell-background color)
    (invalidate-view))
  
  
  (method public (get-empty-background)
    empty-background)
  
  
  (method public (set-empty-background color)
    (set! empty-background color)
    (invalidate-view))


  (method public (get-column-spacing)
    column-spacing)


  (method public (set-column-spacing spacing)
    (set! column-spacing spacing)
    (invalidate-view))


  (method public (get-column-frequency)
    column-frequency)


  (method public (set-column-frequency frequency)
    (set! column-frequency frequency)
    (invalidate-view))


  ;;;
  ;;;; Focus
  ;;;


  (method override (focus-gained)
    (nextmethod)
    (invalidate-selection selection))


  (method override (focus-lost)
    (nextmethod)
    (invalidate-selection selection))
  
  
  (method public (selection-border)
    (if (focused?)
        (focused-border)
      (unfocused-border)))
  
  
  (method protected virtual (focused-border)
    (let ((look (get-look)))
      (get-focused-border~ look)))
  
  
  (method protected virtual (unfocused-border)
    (let ((look (get-look)))
      (get-unfocused-border~ look)))
  
  
  (method public (selection-color)
    (if (focused?)
        (focused-color)
      (unfocused-color)))
  
  
  (method protected virtual (focused-color)
    (let ((look (get-look)))
      (or (get-focused-color~ look)
          {Color Focused-Selection})))
  
  
  (method protected virtual (unfocused-color)
    (let ((look (get-look)))
      (or (get-unfocused-color~ look)
          {Color Unfocused-Selection})))
  
  
  (method public (selected-text-color)
    (if (focused?)
        {Color Focused-Selected-Text}
      {Color Unfocused-Selected-Text}))
  
  
  (method public virtual (focused?)
    (or (is-focused?)
        (and focused-on-descendants?
             (contains-focus?))))
  
  
  (method public (focus-and-select)
    (acquire-focus)
    (when (> (get-visible-count) 0)
      (set-single-selection 0)))


  ;;;
  ;;;; Actions
  ;;;
  

  (method override (focus-actions)
    (cons (find-actions 'tree)
          (nextmethod)))


  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-expand evt)
    (let ((row (get-selected-row)))
      (if (not row)
          (bell)
        (expand row))))
  
  
  (method (on-expand-all evt)
    (let ((row (get-selected-row)))
      (if (not row)
          (bell)
        (expand-all row))))
  
  
  (method (on-collapse evt)
    (let ((row (get-selected-row)))
      (if (not row)
          (bell)
        (collapse row))))
  
  
  (method (on-collapse-all evt)
    (let ((row (get-selected-row)))
      (if (not row)
          (bell)
        (collapse-all row))))
  
  
  (method protected virtual (on-properties evt)
    )
  
  
  ;;;
  ;;;; Header
  ;;;
  
  
  (method public (get-header)
    (and parent (let ((header (get-parent~ parent)))
                  (and (is? header Tree-Header)
                       header))))
  
  
  (method public (invalidate-header)
    (let ((header (get-header)))
      (when header
        (invalidate-view~ header))))
  
  
  (method virtual (make-header-menu column)
    #f)


  (method public (header-menu column pos)
    (let ((menu (make-header-menu column)))
      (when menu
        (layout-menu~ menu)
        (let ((header (get-header)))
          (track-popup-menu menu (acquire header (point+ pos (header-position~ header column))))))))
  
  
  ;;;
  ;;;; Hierarchy
  ;;;
  
  
  (method override (find-subdrawing surface <Surface> device-pos <Point> pos <Point> ignore)
    (or (let ((cell (view->tree pos)))
          (and cell
               (let ((merge-cell (get-merge-cell (cell->site cell))))
                 (and merge-cell
                      (let ((data (get-site-data (get-first-site~ merge-cell))))
                        (and (is? data View)
                             (let ((row (get-parent~ data)))
                               (let ((position (get-position~ row))
                                     (size (get-size~ row)))
                                 ;; bypassing the row's find-in-drawing as the merge-cell can exceed the row's bounds
                                 (with-clipping-transformation~ surface (get-h~ position) (get-v~ position) (get-width~ size) (get-height~ size) (get-transformation~ row)
                                   (lambda ()
                                     (find-in-drawing~ data surface device-pos ignore)))))))))))
        (nextmethod surface device-pos pos ignore)))


  ;;;
  ;;;; Layout
  ;;;
  
  
  (method protected (layout-column column)
    (let ((row (get-next~ root-row))
          (rt 0))
      (while row
        (let* ((height (get-effective-height~ row))
               (rb (+ rt height)))
          (set-size~ row (new Dimension columns-right height))
          (set! rt rb)
          (set! row (get-next~ row))
          (increase! rt row-spacing)))))
  

  ;;;
  ;;;; Scrollee
  ;;;
  
  
  (method override (layout-scrollee)
    ;; column width change must be done before calling nextmethod
    (unless (or update-locked? (inhibit-scrollee-layouts?))
      (when filled-column
        (fill-column)))
    (nextmethod))
  
  
  (method override (scrollee-width)
    (let ((width (if show-header? columns-right (+ visible-width column-spacing))))
      (if (and scroller fit-to-scroller?)
          (max (get-display-width) width)
        width)))
  

  ;;;
  ;;;; Update
  ;;;
  
  
  ;; todo: do both of these in a single pass
  (method override (update-visible)
    (define (update-visible-height)
      (let ((count 0)
            (height 0)
            (first? #t))
        (for-each-visible-row
          (lambda (row line)
            (increase! count)
            (increase! height (get-effective-height~ row))
            (if first?
                (set! first? #f)
              (increase! height row-spacing))))
        (set! visible-count count)
        (set! visible-height height)))
    
    ;; todo: can very easily optimize the text-h call away by calculating at the start the
    ;; base (text-h for some row - level * indentation) and then only adding level * indentation
    (define (update-visible-width)
      (when (not show-header?)
        (let ((width 0))
          ;; a quick not too clean solution for now...
          (when (is? node-column Tree-Node-Column)
            (for-each-visible-row
              (lambda (row line)
                (let* ((highlight-border 3)
                       (nw (+ (or (calculate-node-width~ row) 0) highlight-border))
                       (w (+ (text-h~ node-column (get-level~ row)) nw)))
                  (when (> w width)
                    (set! width w)))))
            (set! visible-width width)
            (set! columns-right width)
            (set-borders~ node-column (cons 0 width))
            (set-width~ node-column width)))))
    
    (update-visible-height)
    (update-visible-width))
  
  
  (method override (scroll-invalidate delta)
    (nextmethod delta)
    (when (/= (get-x~ delta) 0)
      (invalidate-header)))


  ;;;
  ;;;; Drawing
  ;;;
  
  
  (definition Highlighted-Outer-Color
    (new Color red: 0 green: 0 blue: 128))
  
  (definition Highlighted-Inner-Color
    (new Color red: 218 green: 218 blue: 255))
    
  (definition Highlighted-Insert-Color
    {Color Dark-Red})
  
  
  (method override (get-drawing-background context)
    (let ((look (get-look)))
      (or (get-background~ look)
          (nextmethod context))))

  
  (method override (draw-outline surface context clipper scrolled-v)
    (nextmethod surface context clipper scrolled-v)
    (let ((right (+ (get-right~ clipper) (get-h~ (get-position))))
          (height (get-height)))
      (when (and (view-enabled?) empty-background (<= columns-right right))
        (let ((rect (new Rect columns-right 0 right height)))
          (fill-rect~ surface rect (if (debug-paint?) (cycle-debug-color!) empty-background)))))
    (draw-highlighted surface))
  
  
  (method override (draw-empty surface top bottom rt width)
    (when (and (view-enabled?) empty-background (<= rt bottom))
      (let ((rect (new Rect 0 rt width bottom)))
        (fill-rect~ surface rect (if (debug-paint?) (cycle-debug-color!) empty-background)))))
  
  
  (method (draw-highlighted surface)
    (for-each-reversed (lambda (highlight)
                         (let ((row (get-row~ highlight))
                               (col (get-col~ highlight))
                               (insert (get-insert~ highlight)))
                           (if (not insert)
                               (draw-highlighted-cell surface row col highlight)
                             (let ((rect (highlighted-insert-rect row insert)))
                               (fill-rect~ surface rect Highlighted-Insert-Color)))))
                       highlighted))
  
  
  (method protected virtual (draw-highlighted-cell surface row col highlight)
    (let* ((inner-rect (highlighted-target-rect row col))
           (outer-rect (inflate-rect inner-rect 1 1)))
      (let ((outer-color (or (get-outer-color~ highlight) (highlighted-outer-color)))
            (inner-color (or (get-inner-color~ highlight) (highlighted-inner-color))))
        (frame-rect~ surface outer-rect outer-color)
        (when (highlight-inner?)
          (frame-rect~ surface inner-rect inner-color)))))
  
  
  (method protected virtual (highlighted-outer-color)
    Highlighted-Outer-Color)
  
  
  (method protected virtual (highlighted-inner-color)
    Highlighted-Inner-Color)
  
  
  (method protected virtual (highlight-inner?)
    #t)
  
  
  (method public virtual (header-color)
    {Color Medium})
  
  
  (method override (draw-children surface <Surface> nodes context)
    (define (paint-data context column row h v width height data)
      (let ((view? (is? data View)))
        (with-clipping-transformation~ surface h v width height (and view? (get-transformation~ data))
          (lambda ()
            (if (not view?)
                (draw-data~ column surface context row data width height)
              (render-drawing~ data surface context))))))
    
    (define (paint-merge-cell merge-cell)
      (let ((rect (merge-cell-rect merge-cell)))
        (receive (top-left bottom-right) (merge-corner-site merge-cell)
          (let ((left-column (get-column~ top-left))
                (top-row (get-row~ top-left))
                (left (get-left~ rect))
                (top (get-top~ rect))
                (right (get-right~ rect))
                (bottom (get-bottom~ rect))
                (data (get-site-data (get-first-site~ merge-cell)))
                (selected? (merge-cell-selected? merge-cell)))
            (let ((new-context `(selected?: ,selected? line: ,(row->line top-row) ,@context)))
              (paint-data new-context left-column top-row left top (- right left) (- bottom top) data))))))
    
    (parameterize ((redrawn-merge-cells (make-table test: eq?)))
      (nextmethod surface nodes context)
      (iterate-table (redrawn-merge-cells)
        (lambda (merge-cell value)
          (paint-merge-cell merge-cell)))))
  
  
  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-look)
    (find-look 'tree))
  
  
  ;;;
  ;;;; Highlight
  ;;;
  
  
  (method public (get-highlighted)
    highlighted)
  
  
  (method public (set-highlighted value)
    (define (invalidate-highlighted highlighted)
      (for-each (lambda (highlight)
                  (let ((row (get-row~ highlight))
                        (col (get-col~ highlight))
                        (insert (get-insert~ highlight)))
                    (if (not insert)
                        (invalidate-highlighted-cell row col)
                      (invalidate-rect (highlighted-insert-rect row insert)))))
                highlighted))
    
    (assert (every? (lambda (object) (is? object Tree-Highlight)) value))
    (when (not (equal? value highlighted))
      (let ((old highlighted))
        (set! highlighted value)
        (invalidate-highlighted old)
        (invalidate-highlighted highlighted))))
  
  
  (method protected virtual (invalidate-highlighted-cell row col)
    (with ((inner (highlighted-target-rect row col))
           (outer (inflate-rect inner 1 1))
           (region (frame-region outer insets: {Rect 2 2 2 2})))
      (invalidate-region region)))

  
  (method (highlighted-target-rect row col)
    (if (not col)
        (cell-text-rect (new Cell (row->line row) 0))
      (cell-rect (new Cell (row->line row) col))))
  
  
  (method (highlighted-insert-rect row insert)
    (let* ((height (get-height))
           (line (row->line row))
           (rect (line-rect line) @old-l&f-probably-better (cell-content-rect (new Cell line 0)))
           (before-first? (and (= line 0) (eq? insert 'before)))
           (after-last? (and (= line (- visible-count 1)) (eq? insert 'after)))
           (half-spacing (- (fxround/ row-spacing 2) 1))
           (offset (cond (before-first? 0)
                         (after-last? -2)
                         ((eq? insert 'after) half-spacing)
                         (else (- half-spacing row-spacing))))
           (proc
            (lambda (v)
              (let* ((top (+ v offset))
                     (bottom (+ top 2)))
                (new Rect (get-left~ rect) top (get-right~ rect) bottom)))))
      (case insert
        ((before) (proc (get-top~ rect)))
        ((after) (proc (get-bottom~ rect))))))

  
  ;;;
  ;;;; Rows
  ;;;
  
  
  (method override (update-visible-width-incremental level width)
    @wait-optimized
    (let ((width (+ (text-h~ node-column level) width)))
      (when (> width visible-width)
        (set! visible-width width))))

  
  (method override (remove-row row)
    (when (and (attached?~ row) (eq? row auto-expanded-row))
      (close-auto-expanded-row))
    (nextmethod row))
  
  
  (method override (remove-every-row)
    (close-auto-expanded-row)
    (set-sorted-column #f #f)
    (set! highlighted '())
    (set! selection-pivot #f)
    (unmerge-all-cells)
    (nextmethod))
  
  
  (method public (unmerge-all-cells)
    (empty-table merge-cell-table))
  
  
  ;;;
  ;;;; Cursor
  ;;;
  
  
  (method override (cursor-update evt)
    (if (mouse-copy-down? evt)
        (set-cursor :finger)
      (nextmethod evt)))
  

  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (allow-start-drag? pos)
    (and (nextmethod pos)
         (allow-start-drag?~ selection-manager)))
  
  
  (method override (mouse-down evt)
    (let ((view (get-position~ evt)))
      (let* ((cell (view->tree view))
             (pos (and cell (cell-coordinates cell view)))
             (part (and cell (find-part~ (get-column (get-col~ cell)) cell pos))))
        (mouse-down~ selection-manager view part (get-shift?~ evt) (get-control?~ evt) (get-alt?~ evt) (get-multiple-selection?)))
      (acquire-focus)
      (acquire-capture)
      (let ((cell (view->tree view))
            (shift? (get-shift?~ evt))
            (control? (get-control?~ evt)))
        (if (not cell)
            (when can-select-empty?
              (set-selection '() user-origin: 'mouse))
          (let ((pos (cell-coordinates cell view))
                (column (get-column (get-col~ cell))))
            (mouse-down~ column cell pos shift? control?))))))
  
  
  (method override (drag-move evt)
    (let ((view (get-position~ evt)))
      (let ((cell (view->tree view)))
        (when cell
          (let ((pos (cell-coordinates cell view))
                (column (get-column (get-col~ cell))))
            (drag-move~ column cell pos))))))
  
  
  (method override (drag-up evt (dragging? #f))
    (let ((shift? (get-shift?~ evt))
          (control? (get-control?~ evt))
          (alt? (get-alt?~ evt)))
      (mouse-up~ selection-manager shift? control? alt?)
      (let ((pos (get-mouse-down-view~ selection-manager)))
        (when (and pos (not (mouse-down-part-control?~ selection-manager)))
          (let ((cell (view->tree pos)))
            (when cell
              (let ((row (get-row (get-line~ cell)))
                    (column (get-column (get-col~ cell))))
                (when auto-expand/collapse?
                  (auto-expand/collapse row))
                (focus-delegate)
                (drag-up~ column cell pos))))
          (process-action self))))
    (release-capture)
    (reset~ selection-manager))
  
  
  (method override (call-double-click evt)
    (let ((view (get-position~ evt)))
      (let ((cell (view->tree view)))
        (when cell
          (let* ((pos (cell-coordinates cell view))
                 (column (get-column (get-col~ cell)))
                 (part (find-part~ column cell pos)))
            (case part
              ((control) (mouse-down evt))
              ((content) (nextmethod evt))
              (else (double-click~ column cell pos))))))))
  
  
  (method override (double-click evt)
    (double-click~ selection-manager (get-shift?~ evt) (get-control?~ evt) (get-alt?~ evt))
    (let ((view (get-position~ evt)))
      (let ((cell (view->tree view)))
        (when cell
          (let* ((pos (cell-coordinates cell view))
                 (column (get-column (get-col~ cell)))
                 (part (find-part~ column cell pos)))
            (double-click~ column cell pos))))))
  
  
  (method override (context-menu evt)
    (let ((view (get-position~ evt)))
      (let ((cell (view->tree view)))
        (when cell
          (let ((row (get-row (get-line~ cell))))
            (context-menu~ row (acquire~ row self view)))))))
  
  
  (method (cell-coordinates cell <Cell> pos <Point>) <Point>
    (- pos (cell-position cell)))


  ;;;
  ;;;; Mouse Copy
  ;;;
  
  
  (method override (mouse-data pos)
    (let ((cell (view->tree pos)))
      (when cell
        (get-cell-title cell))))


  ;;;
  ;;;; Keyboard
  ;;;


  (method override (key-press evt)
    (let ((key (get-key~ evt)))
      (when (and (use-locator?) (alphanumeric? key) (> visible-count 0))
        (enter~ locator key))))


  ;;;
  ;;;; Locator
  ;;;
  
  
  (method protected virtual (use-locator?)
    #t)
  
  
  (method public (locate-prefix prefix)
    (locate~ locator prefix))
  

  ;;;
  ;;;; Sort
  ;;;

  
  (method public virtual (header-click column)
    (let* ((current-direction (column-sorting column))
           (new-direction (case current-direction
                            ((ascending) 'descending)
                            ((descending) 'ascending)
                            (else 'ascending))))
      (unless (eq? current-direction new-direction)
        (set-sorted-column column new-direction)
        (sort-rows column new-direction)))
    (ensure-displayed))
    
  
  (method protected (set-sorted-column column direction)
    (if (not column)
        (set! sorted-columns '())
      (set! sorted-columns (list (cons column direction))))
    (invalidate-header))

  
  (method public (column-sorting column)
    (let ((pair (assq column sorted-columns)))
      (and pair
           (cdr pair))))
  
  
  (method protected (sort-rows column direction)
    (define (call-sort rows equal predicate)
      (let ((col (column->col column)))
        (let ((dual? (/= col 0)))
          (define (get-node row children n)
            (let ((data (element children n)))
              (or (sort-value~ (get-column n) row data) "")))
          
          (sort (lambda (rx ry)
                  (let* ((cx (get-children~ rx))
                         (cy (get-children~ ry))
                         (nx (localize (get-node rx cx col)))
                         (ny (localize (get-node ry cy col))))
                    (or (predicate nx ny)
                        (and dual?
                             (equal nx ny)
                             (predicate (localize (get-node rx cx 0)) (localize (get-node ry cy 0)))))))
                rows))))
    
    (receive (equal smaller greater) (sort-predicates)
      (let ((predicate (case (column-sorting column)
                         ((ascending) smaller)
                         ((descending) greater))))
        (define (iter row)
          (let ((sorted (call-sort (get-sons~ row) equal predicate)))
            (reorder-sons~ row sorted)
            (iterate (get-sons~ row) iter)))
        
        (with-preserved-selection
          (lambda ()
            (with-update-locked
              (lambda ()
                (iter root-row))))
          ensure-displayed?: #f))))
  
  
  ;;;
  ;;;; Selector
  ;;;
  

  (method public (selector-click row)
    (acquire-focus)
    (case selection-mode
      ((cell)
       (let ((col (if selection-pivot (get-col~ selection-pivot) 0)))
         (let ((select-list
                 (loop (for n from 0 below (get-columns-count))
                       (collect (new Cell (row->line row) n)))))
           (set-selection select-list))))
      (else
       (set-selection (list (row->line row))))))
  
  
  (method public (selector-multi-select row initial-line)
    (let ((new-line (row->line row)))
      (let ((from-line (min initial-line new-line))
            (to-line (max initial-line new-line)))
        (case selection-mode
          ((cell)
           (let ((select-list (new-queue)))
             (loop (for line from from-line to to-line)
                   (loop (for col from 0 below (get-columns-count))
                         (enqueue select-list (new Cell line col))))
             (set-selection (queue-list select-list))))
          (else
           (let ((select-list
                   (loop (for line from from-line to to-line)
                         (collect line))))
             (set-selection select-list)))))))
  
  
  ;;;
  ;;;; Content
  ;;;
  
  
  (method override (get-content)
    content)
  
  
  (method override (set-content value)
    (loop (for row in value)
          (for i from 0)
          (loop (for info in row)
                (for j from 0)
                (do (let* ((cell (new Cell i j))
                           (data (get-cell-data cell)))
                      (set-title~ data info)
                      (invalidate-cell cell))))))


  ;;;
  ;;;; Context
  ;;;

  
  (method override (with-context-click pos proc)
    (acquire-focus)
    (let ((cell (view->tree pos)))
      (if (not cell)
          (when can-select-empty?
            (set-selection '()))
        (let ((row (get-row (get-line~ cell))))
          (case selection-mode
            ((cell)
             (unless (member? cell (get-selection) test: cell=?)
               (set-selection (list cell))))
            (else
             (unless (memq? (get-line~ cell) (get-selection))
               (set-selection (list (get-line~ cell)))))))
        (update-view))
      (proc)))

  
  ;;;
  ;;;; Flatten
  ;;;
  
  
  (method public (flattened?)
    (let ((path-column (get-path-column)))
      (and path-column (get-visible?~ path-column))))
  
  
  (method public (flatten/unflatten)
    (let* ((path-column (get-path-column))
           (flattened? (flattened?)))
      (if (not path-column)
          (error "Unable to find path column")
        (set-visible?~ path-column (not flattened?))
        (fill-column)
        (with-preserved-selection
          (lambda ()
            (with-update-locked
              (lambda ()
                (if flattened?
                    (unflatten)
                  (flatten)))))))))
  
  
  (method public (get-path-column)
    (find-if (lambda (column)
               (is? column Tree-Path-Column))
             columns))
  
  
  (method public virtual (flatten-row? row)
    #t)
  
  
  (method public (flatten)
    (let ((sons (get-sons~ root-row)))
      (iterate sons
               (lambda (son)
                 (if (flatten-row? son)
                     (disconnect~ son)
                   (flatten~ son root-row))))
      (iterate sons
               (lambda (son)
                 (unless (disconnected?~ son)
                   (set-visible?~ son #f))))))
  
  
  (method public (unflatten)
    (iterate (get-sons~ root-row)
             (lambda (son)
               (when (disconnected?~ son)
                 (reconnect~ son))))
    (iterate (get-sons~ root-row)
             (lambda (son)
               (unless (get-visible?~ son)
                 (set-visible?~ son #t)))))
  
  
  ;;;
  ;;;; Filter
  ;;;
  
  
  (method public (filter-rows cell-title-filter (row-filter: row-filter #f))
    (define (determine-visible? row)
      (and (or (not row-filter) (row-filter row))
           (or (not cell-title-filter)
               (some? (lambda (cell)
                        (let ((title (get-title~ cell)))
                          (and title
                               (cell-title-filter title))))
                      (get-children~ row)))))
  
    (define (make-row-visible row visible?)
      (set-visible?~ row visible?)
      (when visible?
        (let loop ((node (get-father~ row)))
             (when node
               (set-visible?~ node #t)
               (loop (get-father~ node))))))
    
    (scroll-to {Point 0 0})   ; quicky
    (set-selection '())       ; quicky
    (with-update-locked
      (lambda ()
        (for-each-row
          (lambda (row rank)
            (make-row-visible row (determine-visible? row)))))))
  

  ;;;
  ;;;; Tooltip
  ;;;


  (method override (get-tooltip?)
    #t)
  
  
  (method (calculate-tooltip-bounds cell)
    (cell-rect cell))
  
  
  (method override (get-tooltip-bounds pos)
    (let ((cell (view->tree pos)))
      (when cell
        (calculate-tooltip-bounds cell))))
  
  
  (method override (get-tooltip-context pos)
    (if (get-tooltip-text)
        (nextmethod pos)
      (let ((cell (view->tree pos)))
        (when cell
          (let ((row (get-row (get-line~ cell)))
                (column (get-column (get-col~ cell)))
                (text (localize (get-cell-tooltip cell))))
            (when text
              (let* ((width (get-width~ column))
                     (height (get-height~ row))
                     (content-rect (content-rect~ column row cell width height))
                     (formatted? (get-formatted-tooltip?))
                     (text-width (if formatted? 0 (+ (text-width~ (effective-font) text) 2))))
                (when (or (cell-tooltips? cell) (> text-width (rect-width content-rect)))
                  (let ((rect (calculate-tooltip-bounds cell))
                        (h (+ (content-h~ column (get-level~ row)) (if (= (get-col~ cell) 0) 2 -2))))
                    (new Tooltip-Context
                      view: self
                      text: (if formatted? text (split-lines text))
                      bounds: rect
                      position: (get-tooltip-position text rect h 0)
                      padding: (new Dimension 0 0)
                      font: (effective-font)))))))))))
  
  
  (method protected virtual (cell-tooltips? cell)
    #f)
  
  
  (method protected virtual (get-cell-tooltip cell)
    (let ((column (get-column (get-col~ cell)))
          (data (get-cell-data cell)))
      (get-data-tooltip~ column data)))
  
  
  (method protected virtual (get-tooltip-position text rect h v)
    (+ (rect-position rect) (new Point (- h 10) (+ v (rect-height rect) 5))))
  
  
  (method protected virtual (get-column-tooltip column)
    #f)


  ;;;
  ;;;; Dnd
  ;;;
  
  
  (method override (get-drag-drop)
    (when (view->tree mouse-down-point)
      (nextmethod)))


  ;;;
  ;;;; Drag
  ;;;
  
  
  (method override (get-drag-source)
    (let* ((pos (get-drag-position))
           (cell (view->tree pos))
           (row (get-row (get-line~ cell)))
           (col (get-col~ cell)))
      (get-drag-cell-source pos row col)))
  
  
  (method protected virtual (get-drag-cell-source pos row col)
    (new Tree-Drag-Source view: self position: pos @old image: @old (get-drag-image pos) row: row col: col))
  
  
  (method override (get-drag-image pos)
    (let* ((cell (view->tree pos))
           (line (get-line~ cell)))
      (get-node-image line)))

  
  (method public (get-node-image line)
    @old
    (let* ((cdc (new Client-Surface player))
           (font (effective-font))
           (row (get-row line))
           (data (get-cell-data (new Cell line 0)))
           (image (get-image~ data))
           (title (get-title~ data))
           (width (get-width~ node-column))
           (height (get-height~ row))
           (bitmap (unspecified))
           (mask (unspecified)))
      (with ((memdc (new Memory-Surface cdc (new Dimension width height) keep-bitmap?: #t))
             (maskdc (new Memory-Surface cdc (new Dimension width height) keep-bitmap?: #t)))
        (fill-rect~ memdc (new Rect 0 0 width height) {Color Tree-Background})
        (fill-rect~ maskdc (new Rect 0 0 width height) {Color Tree-Background})
        (draw-image~ portfolio memdc #f image 0 0)
        (draw-mask~ portfolio maskdc image 0 0)
        (set-font~ memdc font)
        (draw-text~ memdc 19 2 title {Color Black})
        (set-font~ maskdc font)
        (draw-text~ maskdc 19 2 title {Color Black})
        (set! bitmap (get-bitmap~ memdc))
        (set! mask (get-bitmap~ maskdc)))
      (let* ((portfolio (new Portfolio))
             (no (add-masked-bitmap~ portfolio bitmap mask)))
        (new Image portfolio: portfolio resource: no))))
    

  ;;;
  ;;;; Drop
  ;;;
  
  
  (method override (current-drop drag-drop pos)
    (define (get-reorder-target drag-drop pos)
      (call-with-view->tree pos
        (lambda (cell v height)
          (when cell
            (let* ((line (get-line~ cell))
                   (quarter (get-row-quarter v height))
                   (current (get-target~ drag-drop))
                   (row (get-row line)))
              (if (and current (eq? self (get-view~ current)) (eq? row (get-row~ current)) (eqv? quarter (get-quarter~ current)))
                  current
                (get-row-reorder-target drag-drop row quarter)))))))
    
    (define (get-row-quarter v height)
      (let ((quarter (/ (cast <fl> height) 4))
            (middle (/ (cast <fl> height) 2)))
        (cond ((< v quarter) 1)
              ((< v middle) 2)
              ((< v (- height quarter)) 3)
              (else 4))))
    
    (if (get-surrogate)
        (nextmethod drag-drop pos)
      (if (and reorderable? (eq? self (get-view~ (get-source~ drag-drop))))
          (get-reorder-target drag-drop pos)
        (get-tree-target drag-drop pos))))
  
  
  (method protected virtual (get-row-reorder-target drag-drop row quarter)
    (let* ((same-row? (memq? (row->line row) selection))
           (src-selection (get-selection~ (get-view~ (get-source~ drag-drop))))
           (src-range (contiguous-range src-selection))
           (dst (get-user-data~ row))
           (dst-line (row->line row))
           (current-rank (if (memq? quarter '(1 2)) dst-line (+ dst-line 1)))
           (accept? (or (not src-range) (and (not (memq? current-rank src-selection)) (/= current-rank (+ (cdr src-range) 1))))))
      (when accept?
        (let ((part (if (= current-rank dst-line) 'before 'after)))
          (new Tree-Drop-Target view: self row: row part: part)))))
  
  
  (method protected virtual (get-tree-target drag-drop pos)
    (call-with-view->tree pos
      (lambda (cell v height)
        (when cell
          (let ((row (get-row (get-line~ cell)))
                (current (get-target~ drag-drop)))
            (if (and current (eq? self (get-view~ current)) (eq? row (get-row~ current)))
                current
              (get-row-target drag-drop row)))))))
  
    
  (method public (get-tree-data-target drag-drop pos)
    (call-with-view->tree pos
      (lambda (cell v height)
        (when cell
          (let ((row (get-row (get-line~ cell)))
                (col (get-col~ cell))
                (source (get-source~ drag-drop))
                (current (get-target~ drag-drop)))
            (when (or (neq? (get-view~ source) self) (neq? (get-row~ source) row) (/= (get-col~ source) col))
              (if (and current (eq? self (get-view~ current)) (eq? row (get-row~ current)) (= col (get-col~ current)))
                  current
                (get-cell-target drag-drop row col))))))))
  
  
  (method protected virtual (get-row-target drag-drop row)
    #f)
  
  
  (method protected virtual (get-cell-target drag-drop row col)
    #f)
    
  
  (method override (highlight-drop drag-drop pos)
    (let ((target (get-target~ drag-drop)))
      ;; temp patch... this should always be a t-d-t
      (when (is? target Tree-Drop-Target)
        (let* ((row (get-row~ target))
               (col (get-col~ target))
               (part (get-part~ target))
               (insert
                (case part
                  ((row) #f)
                  ((before) 'before)
                  ((after) 'after)))
               (highlight (new Tree-Highlight row col insert: insert outer-color: Highlighted-Outer-Color inner-color: Highlighted-Inner-Color)))
          (set-highlighted (cons highlight (get-highlighted)))))))
  
  
  (method override (unhighlight-drop drag-drop)
    (set-highlighted (if (null? highlighted)
                         '()
                       (cdr highlighted))))


  ;;;
  ;;;; Tab
  ;;;
  
  
  (method override (tab-stop-enter)
    )

  
  ;;;
  ;;;; Edition
  ;;;
    
  
  (method (on-move evt)
    (if arrow-press-handler
        (invoke-handler self arrow-press-handler evt)
      (let ((direction (get-property~ evt direction:))
            (modifiers (get-modifiers~ evt)))
        (case direction
          ((up) (up-arrow-action modifiers #t))
          ((down) (down-arrow-action modifiers #t))
          ((left) (left-arrow-action modifiers #t))
          ((right) (right-arrow-action modifiers #t))))))


  (method virtual package (on-home evt)
    (home-action))
  
  
  (method public (home-action)
    (let ((count (get-visible-count)))
      (when (> count 0)
        (case selection-mode
          ((cell)
           (let ((col (if selection-pivot (get-col~ selection-pivot) 0)))
             (set-selection (list (new Cell 0 col)))))
          (else
           (set-selection (list 0)))))))


  (method virtual package (on-end evt)
    (end-action))
  
  
  (method public (end-action)
    (let ((count (get-visible-count)))
      (when (> count 0)
        (case selection-mode
          ((cell)
           (let ((col (if selection-pivot (get-col~ selection-pivot) 0)))
             (set-selection (list (new Cell (- count 1) col)))))
          (else
           (set-selection (list (- count 1))))))))


  (method override (left-arrow-press modifiers)
    (left-arrow-action modifiers #t))
  
  
  (method public (left-arrow-action modifiers user-origin)
    (define (logical-col cell)
      (let ((site (cell->site cell)))
        (when site
          (let ((merge-cell (get-merge-cell site)))
            (if merge-cell
                (get-first-col~ merge-cell)
              (get-col~ cell))))))
    
    (case selection-mode
      ((cell)
       (when (not-null? selection)
         (let ((cell selection-pivot))
           (let ((col (logical-col cell)))
             (when (> col 0)
               (move-cell (new Cell (get-line~ cell) (- col 1)) 'left user-origin))))))
      ((row)
       (let ((row (get-selected-row)))
         (if (not row)
             (bell)
           (if (expanded?~ row)
               (expand/collapse row)
             (when (> (get-level~ row) 0)
               (set-selection (list (row->line (get-father~ row)))))))))))


  (method override (up-arrow-press modifiers)
    (up-arrow-action modifiers #t))
  
  
  (method public (up-arrow-action modifiers user-origin)
    (define (logical-row cell)
      (let ((site (cell->site cell)))
        (when site
          (let ((merge-cell (get-merge-cell site)))
            (if merge-cell
                (get-first-line~ merge-cell)
              (get-line~ cell))))))
    
    (case selection-mode
      ((cell)
       (when (not-null? selection)
         (let ((cell selection-pivot))
           (let ((line (logical-row cell)))
             (when (> line 0)
               (move-cell (new Cell (- line 1) (get-col~ cell)) 'up user-origin))))))
      (else
       (let ((count (get-visible-count)))
         (if (null? selection)
             (when (> count 0)
               (move-selection (- count 1) 'backward user-origin))
           (let ((line (car selection)))
             (when (> line 0)
               (move-selection (- line 1) 'backward user-origin))))))))


  (method override (right-arrow-press modifiers)
    (right-arrow-action modifiers #t))
  
  
  (method public (right-arrow-action modifiers user-origin)
    (define (logical-col cell)
      (let ((site (cell->site cell)))
        (when site
          (let ((merge-cell (get-merge-cell site)))
            (if merge-cell
                (get-last-col~ merge-cell)
              (get-col~ cell))))))
    
    (case selection-mode
      ((cell)
       (when (not-null? selection)
         (let ((cell selection-pivot))
           (let ((col (logical-col cell)))
             (when (< col (- (get-columns-count) 1))
               (move-cell (new Cell (get-line~ cell) (+ col 1)) 'right user-origin))))))
      (else
       (let ((row (get-selected-row)))
         (if (not row)
             (bell)
           (unless (expanded?~ row)
             (expand/collapse row)))))))


  (method override (down-arrow-press modifiers)
    (down-arrow-action modifiers #t))
  
  
  (method public (down-arrow-action modifiers user-origin)
    (define (logical-row cell)
      (let ((site (cell->site cell)))
        (when site
          (let ((merge-cell (get-merge-cell site)))
            (if merge-cell
                (get-last-line~ merge-cell)
              (get-line~ cell))))))

    (let* ((count (get-visible-count))
           (last (- count 1)))
      (case selection-mode
        ((cell)
         (when (not-null? selection)
           (let ((cell selection-pivot))
             (let ((line (logical-row cell)))
               (when (< line last)
                 (move-cell (new Cell (+ line 1) (get-col~ cell)) 'down user-origin))))))
        (else
         (if (null? selection)
             (when (> count 0)
               (move-selection 0 'forward user-origin))
           (let ((line (car selection)))
             (when (< line last)
               (move-selection (+ line 1) 'forward user-origin))))))))
    
  
  (method (move-selection line direction user-origin)
    (let ((row (get-row line)))
      (set-selection (list line) user-origin: user-origin)
      (when user-origin
        (update-view))))
  
  
  (method (move-cell cell direction user-origin)
    (set-selection (list cell) ensure-displayed?: #f user-origin: user-origin)
    (let ((rect (cell-rect cell)))
      (case direction
        ((left)  (scroll (new Point (horizontal-scroll-needed (get-left~ rect) 'backward) 0)))
        ((up)    (scroll (new Point 0 (vertical-scroll-needed (get-top~ rect) 'backward))))
        ((right) (scroll (new Point (horizontal-scroll-needed (get-right~ rect) 'forward) 0)))
        ((down)  (scroll (new Point 0 (vertical-scroll-needed (get-bottom~ rect) 'forward))))))
    (when user-origin
      (update-view)))
  
  
  (method (line-displayed? line)
    (let* ((row (get-row line))
           (top (get-top~ row))
           (bottom (get-bottom~ row))
           (needed (scroll-needed (new Point 0 top) (new Point 0 bottom) #f)))
      (= (get-v~ needed) 0)))
  
  
  (method (cell-displayed? cell)
    (let* ((rect (cell-rect cell))
           (needed (scroll-needed (new Point (get-left~ rect) (get-top~ rect)) (new Point (get-right~ rect) (get-bottom~ rect)) #f)))
      (and (= (get-h~ needed) 0)
           (= (get-v~ needed) 0))))
  
  
  (method override (select-all (user-origin: user-origin #f))
    (when multiple-selection?
      (set-selection (naturals 0 visible-count) user-origin: user-origin)))

  
  ;;;
  ;;;; Columns
  ;;;
  
  
  (method public (for-each-column proc)
    (for-each proc (get-columns) (iota (get-columns-count))))
  
  
  (method public (fill-column)
    (define (find-filled-column name)
      (cond ((integer? name) (element columns name))
            ((eq? name #t) (and (not-null? columns) (last columns)))
            (else (find-if (lambda (column)
                             (eq? (get-name~ column) name))
                           columns))))
    
    (when (and filled-column (not-null? columns) (get-finished?))
      (let ((column (find-filled-column filled-column)))
        (when column
          (let ((col (column->col column)))
            (when (< col (length columns))
              (let* ((filled-borders (get-borders~ column))
                     (space-after (- columns-right (borders-right filled-borders)))
                     (size (get-display-size))
                     (delta (max (- (borders-width filled-borders)) (- (get-width~ size) space-after (borders-right filled-borders))))
                     (old-right (cdr filled-borders))
                     (new-right (+ old-right delta)))
                (for-each (lambda (column)
                            (bind (left . right) (get-borders~ column)
                              (set-borders~ column (cons (+ left delta) (+ right delta)))))
                          (tail columns (+ col 1)))
                (set-cdr! filled-borders new-right)
                (increase! columns-right delta)
                (set-width~ column (max (+ (get-width~ column) delta) min-column-width))
                (let ((left (max (- (min old-right new-right) 2) 0))
                      (top 0)
                      (right (get-width~ size))
                      (bottom (get-height~ size)))
                  (when (and (< left right) (< top bottom))
                    (invalidate-rect (new Rect left top right bottom))))
                (invalidate-header))))))))

  
  (method public (add-column . rest)
    (bind-keywords ((class Tree-Column) . others) rest
      (apply new class parent: self others)))
  
  
  (method public (remove-column column)
    (remove-child column))
  
  
  (method public (remove-columns (preserve-node-column?: preserve-node-column? #f) (preserve: preserve '()))
    (define (remove-columns-preserving)
      (remove-every-row)
      (for-each (lambda (column)
                  (unless (or (and preserve-node-column? (is? column Tree-Node-Column))
                              (memq? (get-name~ column) preserve))
                    (remove-column column)))
                (get-columns))
      (invalidate-header))
    
    (define (remove-all-columns)
      (remove-every-row)
      (set-children '())
      (set! columns '())
      (set! columns-right 0)
      (set! node-column #f)
      (invalidate-header))
    
    (if (or preserve-node-column? preserve)
        (remove-columns-preserving)
      (remove-all-columns)))

  
  (method override (invalidate-columns)
    (define (update-node-column)
      (set! node-column (find-if (lambda (column)
                                   (is? column Tree-Node-Column))
                                 columns)))
    
    (define (update-columns)
      (let ((left 0)
            (last-right #f))
        (for-each (lambda (column rank)
                    (set-rank~ column rank)
                    (let* ((width (get-width~ column))
                           (right (+ left width)))
                      (set-borders~ column (cons left right))
                      (when (get-visible?~ column)
                        (set! left (+ right (if (space-column? column rank) column-spacing 0))))
                      (set! last-right right)))
                  columns
                  (naturals 0 (length columns)))
        (set! columns-right last-right)))
    
    (when (not-null? columns)
      (update-node-column)
      (update-columns)))
  
  
  (method protected virtual (space-column? column rank)
    (= (modulo (+ rank 1) column-frequency) 0))
    
  
  (method public (get-columns-count)
    (length columns))
    
  
  (method public (get-column col)
    (element columns col))
  
  
  (method public (find-column name)
    (find-component name))
  
  
  (method public (get-node-column-index)
    (column->col node-column))
  
  
  (method public (column->col column)
    (get-rank~ column))

  
  ;;;
  ;;;; Rows
  ;;;
  
  
  (method override (row-class)
    Tree-Row)
  
  
  (method override (update-father-control father)
    (when (and (not update-locked?) (neq? father root-row) (row-visible?~ father))
      (let* ((line (row->line father))
             (top (row-top line)))
        (when (< top (get-height))
          (invalidate-rect (control-rect~ node-column father top))
          (invalidate-rect (image-rect~ node-column father top))))))
  
  
  (method override (add-empty-row (father: father #f))
    (let ((father (or father root-row)))
      (add-row father: father children: (map (lambda (column)
                                               (new-empty-cell~ column))
                                             columns))))
  
  
  (method override (line-selected? line)
    (case selection-mode
      ((cell) (some? (lambda (cell) (= (get-line~ cell) line)) selection))
      (else (memq? line selection))))
  
  
  (method override (line-draw-selected? line)
    (and (nextmethod line)
         (or (has-focus?) show-unfocused-selection?)))

  
  ;;;
  ;;;; Merge
  ;;;
  
  
  (method public (merge-cells cell nbline nbcol)
    (if (and (> nbline 0) (> nbcol 0))
        (let ((cell-data (get-cell-data cell)))
          (if cell-data
              (let ((title (get-cell-title cell))
                    (site (cell->site cell)))
                (when site
                  (when (get-merge-cell site)
                    (unmerge-cells cell)))
                (when (or (> nbline 1) (> nbcol 1))
                  (let ((cell-line (get-line~ cell))
                        (cell-col (get-col~ cell)))
                    (if (and (< (+ cell-line nbline -1) (get-row-count))
                             (< (+ cell-col nbcol -1) (get-columns-count)))
                        (let ((merged-rows (let ((queue (new-queue)))
                                             (loop (for r from cell-line below (+ cell-line nbline))
                                                   (enqueue queue (get-row r)))
                                             (queue-list queue)))
                              (merged-columns (let ((queue (new-queue)))
                                                (loop (for c from cell-col below (+ cell-col nbcol))
                                                      (enqueue queue (get-column c)))
                                                (queue-list queue))))
                          (merge-sites merged-rows merged-columns))
                      (error "Merge cell out of tree bounds")))))
            (error "First cell is empty")))
      (error "Invalid merge-cells")))
  
  
  (method protected (merge-sites rows columns (inhibit-invalidate?: inhibit-invalidate? #f))
    (let ((merge-cell (new Merge-Cell rows columns self)))
      (for-each (lambda (column)
                  (for-each (lambda (row)
                              (set-merge-cell! (new Site row column) merge-cell))
                            rows))
                columns)
      (unless inhibit-invalidate?
        (let ((rect (merge-cell-rect merge-cell)))
          (when rect
            (let ((site-data (get-site-data (new Site (first rows) (first columns)))))
              (when (is? site-data View)
                (set-rect~ site-data (acquire~ (get-parent~ site-data) self rect))))
            (invalidate-rect rect))))))
  
  
  (method public (unmerge-cells cell)
    (let ((merge-cell-to-remove (get-merge-cell (cell->site cell))))
      (when merge-cell-to-remove
        (let ((rect (merge-cell-rect merge-cell-to-remove)))
          (for-each-cell~ merge-cell-to-remove
            (lambda (row column)
              (let ((site (row/column->site row column)))
                (clear-merge-cell site))))
          (when rect
            (invalidate-rect rect))))))
  
  
  (method public (add-merged-row row merge-cell)
    (let ((merged-title (get-cell-title (get-master-cell~ merge-cell)))
          (is-valid? #t)
          (site-list '()))
      (for-each (lambda (column)
                  (let ((new-title (get-data-title (get-child~ row (column->col column)))))
                    (if (equal? merged-title new-title)
                        (let ((site (new Site row column)))
                          (set-merge-cell! site merge-cell)
                          (set! site-list (cons site site-list)))
                      (set! is-valid? #f))))
                (get-merged-columns~ merge-cell))
      (if is-valid?
          (add-merged-row~ merge-cell row)
        (for-each clear-merge-cell site-list))
      @creates-problems-with-edited-cells
      (when (not is-valid?)
        (error "Merged cells title must be identical"))))

  
  (method public (get-merge-cell site)
    (table-ref merge-cell-table site #f))
  
  
  (method public (safe-get-merge-cell site)
    (case selection-mode
      ((cell) (get-merge-cell site))
      (else #f)))
  
  
  (method public (set-merge-cell! site merge-cell)
    (table-set! merge-cell-table site merge-cell))
  
  
  (method public (clear-merge-cell site)
    (table-clear merge-cell-table site))
  
  
  (method (merge-cell-selected? merge-cell)
    (and (or (has-focus?) show-unfocused-selection?)
         (case selection-mode
           ((cell)
            (some? (lambda (cell)
                     (let ((site (cell->site cell)))
                       (when site
                         (eq? merge-cell (get-merge-cell site)))))
                   (get-selection)))
           (else
            #f))))
    

  (method (merge-corner-site merge-cell)
    (values (get-first-site~ merge-cell) (get-last-site~ merge-cell)))
    

  (method (merge-corner-cell merge-cell)
    (values (get-first-cell~ merge-cell) (get-last-cell~ merge-cell)))
  
  
  (method public (merge-cell-rect merge-cell)
    (receive (top-left bottom-right) (merge-corner-site merge-cell)
      (if (and top-left bottom-right)
          (let ((top-left-rect (unmerged-site-rect top-left))
                (bottom-right-rect (unmerged-site-rect bottom-right)))
            (new Rect (get-left~ top-left-rect) (get-top~ top-left-rect) (get-right~ bottom-right-rect) (get-bottom~ bottom-right-rect)))
        #f)))
  
  
  (method public (invalidate-merge-cell merge-cell)
    (when (view-visible?)
      (with-transformation-surface
        (lambda (surface)
          (with ((region (new Region rect: (logical->device~ surface (merge-cell-rect merge-cell)))))
            (invalidate-region~ player region))))))
  
  
  (method public (get-master-cell cell)
    (let ((merge-cell (safe-get-merge-cell (cell->site cell))))
      (if merge-cell
          (get-master-cell~ merge-cell)
        cell)))
  
  
  (method public (get-master-site site)
    (let ((merge-cell (safe-get-merge-cell site)))
      (if merge-cell
          (cell->site (get-master-cell~ merge-cell))
        site)))
  
  
  (method public (get-top-site site)
    (let ((merge-cell (safe-get-merge-cell site)))
      (if merge-cell
          (cell->site (get-first-cell~ merge-cell))
        site)))
  

  ;;;
  ;;;; User
  ;;;
  
  
  (method public (row-index target)
    (continuation-capture
      (lambda (return)
        (for-each-visible-row
          (lambda (row line)
            (when (eq? row target)
              (continuation-return return line))))
        #f)))
  
  
  (method public (user-data-row data (test: test eqv?) (include-invisibles?: include-invisibles? #f))
    (let ((rows (if include-invisibles? (collect-rows) (collect-visible-rows))))
      (find rows data key: get-user-data~ test: test return: 'item)))


  (method public (user-data-rows data-list (test: test eqv?) (include-invisibles?: include-invisibles? #f))
    (let ((fact (new List-Factory)))
      ((if include-invisibles? for-each-row for-each-visible-row)
       (lambda (row line)
         (let ((user-data (get-user-data~ row)))
           (when (member? user-data data-list test: test)
             (put~ fact row)))))
      (get-output~ fact)))


  ;; Because it returns the line, this method only makes sense on visible rows
  (method public (user-data-index data (test: test eqv?))
    (continuation-capture
      (lambda (return)
        (for-each-visible-row
          (lambda (row line)
            (when (test (get-user-data~ row) data)
              (continuation-return return line))))
        #f)))


  ;; Because it returns the line, this method only makes sense on visible rows
  (method public (user-data-indexes data-list (test: test eqv?))
    (let ((fact (new List-Factory)))
      (for-each-visible-row
        (lambda (row line)
          (let ((user-data (get-user-data~ row)))
            (when (member? user-data data-list test: test)
              (put~ fact line)))))
      (get-output~ fact)))
  
  
  (method public (select-user-data-row data (test: test eqv?) (include-invisibles?: include-invisibles? #f) (error?: error? #t))
    (let ((row (user-data-row data test: test include-invisibles?: include-invisibles?)))
      (if (not row)
          (when error?
            (error "Unable to select user-data: {t}" data))
        (set-single-selection (row->line row)))))

  
  ;;;
  ;;;; Cells
  ;;;
  
  
  (method public (cell-values cell)
    (values (get-row (get-line~ cell))
            (get-column (get-col~ cell))))
  
  
  (method public (get-site-data site)
    (let ((row (get-row~ site))
          (col (column->col (get-column~ site))))
      (get-child~ row col)))
  
  
  (method public (get-cell-data cell)
    (let ((row (get-row (get-line~ cell))))
      (when row
        (when (>= (get-col~ cell) (length (get-children~ row)))
          (set! selection '())
          (error "Sequence overrun"))
        (get-child~ row (get-col~ cell)))))
  
  
  (method public (set-cell-data cell data)
    (let ((row (get-row (get-line~ cell))))
      (set-element! (get-children~ row) (get-col~ cell) data)))
  
  
  (method public (get-data-title data)
    (cond ((is? data Tree-Label)
           (get-title~ data))
          ((is? data Tree-Data-View)
           (get-title~ (get-preserved-data~ data)))))
  
  
  (method public (set-data-title data title)
    (cond ((is? data Tree-Label)
           (set-title~ data title))
          ((is? data Tree-Data-View)
           (set-title~ (get-preserved-data~ data) title))))
  
  
  (method public (get-cell-title cell)
    (let ((data (get-cell-data cell)))
      (when data
        (get-data-title data))))
  
  
  (method public (set-cell-title cell title)
    (let ((data (get-cell-data cell)))
      (when data
        (set-data-title data title))))

  
  (method public (invalidate-site site)
    (invalidate-rect (site-rect site)))
  
  
  (method public (invalidate-cell cell)
    (invalidate-rect (cell-rect cell)))
  
  
  (method public (redraw-cell cell)
    (invalidate-cell cell)
    (update-view))


  ;;;
  ;;;; State
  ;;;
  
  
  (method override (expand/collapse row)
    (let ((top (get-v~ (get-position~ row))))
      (invalidate-rect (control-rect~ node-column row top)))
    (case (get-state~ row)
      ((dynamic) (dynamic-expand row))
      (else (nextmethod row))))
  
  
  (method public (dynamic-expand row)
    (update-row row)
    (expand row #t))
  
  
  (method override (invalidate-expand row top dynamic?)
    (when node-column
      (invalidate-rect (image-rect~ node-column row top))
      (if (and dynamic? (= (cardinality (get-sons~ row)) 0))
          (invalidate-rect (control-rect~ node-column row top))
        (invalidate-rect (plus/minus-rect~ node-column row top)))))
  
  
  (method override (invalidate-collapse line row top selected?)
    (when node-column
      (invalidate-rect (image-rect~ node-column row top))
      (if selected?
          (invalidate-rect (line-rect line))
        (invalidate-rect (plus/minus-rect~ node-column row top)))))
  
  
  (method override (collapse-selection line removed)
    (case selection-mode
      ((cell)
       (when removed
         (let ((cell (new Cell line (get-col~ removed))))
           (unless (member? cell selection)
             (set-selection (cons cell selection))))))
      (else
       (when (and removed (not (line-selected? line)))
         (set-selection (cons line selection)))))
    (unless selection-pivot
      (set! selection-pivot (and (not-null? selection) (car selection)))))
  
  
  (method public (auto-expand/collapse row (close-auto-expanded?: close-auto-expanded? #t) (auto-toggle?: auto-toggle? #f))
    (define (auto-toggle row auto-toggle?)
      (if (expanded?~ row)
          (when auto-toggle?
            (when (collapsable?~ row)
              (collapse row))
            (set! auto-expanded-row #f))
        (when (expandable?~ row)
          (expand/collapse row))
        (set! auto-expanded-row row)))
    
    (let ((selected-rows (selection->rows selection)))
      (cond ((mouse-down?~ selection-manager)
             (when (and close-auto-expanded? auto-expanded-row (neq? auto-expanded-row row))
               (close-auto-expanded new-spine: (and row (get-spine~ row)) force?: #f)
               (set! delayed-autorow auto-expanded-row)
               (set! auto-expanded-row #f)))
            ((mouse-up?~ selection-manager)
             (when delayed-autorow
               (if row
                   (close-delayed-row new-spine: (get-spine~ row))
                 (let ((row (if (expanded?~ delayed-autorow)
                                delayed-autorow
                              (get-father~ delayed-autorow))))
                   (when (collapsable?~ row)
                     (collapse row)))
                 (set! delayed-autorow #f)))
             (when row
               (auto-toggle row auto-toggle?)))
            (else
             (when close-auto-expanded?
               (close-auto-expanded new-spine: (and row (get-spine~ row))))
             (when row
               (auto-toggle row auto-toggle?))))
      (set! selection (rows->selection selected-rows))))
  
  
  (method public (close-delayed-row (new-spine: new-spine #f))
    (when delayed-autorow
      (let ((old-spine (get-spine~ delayed-autorow))
            (done? #f))
        (set! delayed-autorow #f)
        (for-each-reversed (lambda (row)
                             (unless done?
                               (if (and new-spine (memq? row new-spine))
                                   (set! done? #t)
                                 (when (collapsable?~ row)
                                   (collapse row)))))
                           old-spine))))
  
  
  (method public (close-auto-expanded (new-spine: new-spine #f) (force?: force? #t))
    (when auto-expanded-row
      (let ((old-spine (get-spine~ auto-expanded-row))
            (done? #f))
        (set! auto-expanded-row #f)
        (for-each-reversed (lambda (row)
                             (unless done?
                               (if (and new-spine (memq? row new-spine))
                                   (set! done? #t)
                                 (if force?
                                     (when (collapsable?~ row)
                                       (collapse row))
                                   (set! auto-expanded-row row)
                                   (set! done? #t)))))
                           old-spine))))
  
  
  (method (close-auto-expanded-row)
    (when auto-expanded-row
      (when (collapsable?~ auto-expanded-row)
        (collapse auto-expanded-row))
      (set! auto-expanded-row #f)))
  
  
  (method public (set-dynamic row)
    (when (neq? (get-state~ row) 'dynamic)
      (set-state~ row 'dynamic)
      (let* ((line (row->line row))
             (top (row-top line)))
        (invalidate-rect (control-rect~ node-column row top)))))

  
  ;;;
  ;;;; Scroll
  ;;;


  (method override (ensure-displayed . rest)
    (when (not-null? selection)
      (if (eq? selection-mode 'cell)
          (ensure-cell-displayed (car selection))
        (ensure-row-displayed (selection-row (car selection))))))
  
  
  (method (ensure-cell-displayed cell)
    (let ((rect (cell-rect cell)))
      ;; the second pos should take in the cell width and height
      (scroll (scroll-needed (rect-position rect) (rect-end-position rect) (get-scroll-context)))))

  
  ;;;
  ;;;; Selection
  ;;;


  (method (cell->site cell)
    (if (is? cell Cell)
        (let ((column (get-column (get-col~ cell)))
              (row (get-row (get-line~ cell))))
          (new Site row column))
      cell))


  (method (row/column->site row column)
    (new Site row column))


  (method (site->cell site)
    (if (is? site Site)
        (let ((col (column->col (get-column~ site)))
              (line (row->line (get-row~ site))))
          (new Cell line col))
      site))


  (method (site->line/col site)
    (let ((col (column->col (get-column~ site)))
          (line (row->line (get-row~ site))))
      (values line col)))

  
  (method (process-multi-selection-row element shift? control? user-origin)
    (cond ((and shift? control?)
           (set! selection-pivot element)
           (unless (member? element selection)
             (set-selection (list element) set-pivot?: #f user-origin: user-origin)))
          (shift?
            (let ((pivot (or selection-pivot 0)))
              (cond ((= element pivot) (set-selection (list element) set-pivot?: #f user-origin: user-origin))
                    ((> element pivot) (set-selection (naturals pivot (+ element 1)) set-pivot?: #f user-origin: user-origin))
                    ((< element pivot) (set-selection (naturals element (+ pivot 1)) set-pivot?: #f user-origin: user-origin)))))
          (control?
            (if (member? element selection test: nu=?)
                (set-selection (remove element selection test: equal?) set-pivot?: #f user-origin: user-origin)
              (set-selection (cons element selection) set-pivot?: #f user-origin: user-origin))
            (when (and selection-pivot (= element selection-pivot))
              (cond ((null? selection) (set! selection-pivot #f))
                    ((< selection-pivot (car selection)) (set! selection-pivot (car selection)))
                    ((> selection-pivot (last selection)) (set! selection-pivot (last selection))))))))
  

  (method (process-multi-selection-cell element shift? control? user-origin)
    (cond ((and shift? control?)
           (set! selection-pivot element)
           (unless (member? element selection test: cell=?)
             (set-selection (list element) set-pivot?: #f user-origin: user-origin)))
          (shift?
            (unless (or (and selection-pivot (/= (get-col~ element) (get-col~ selection-pivot)))
                        (not selection-pivot))
              (let ((col (get-col~ selection-pivot))
                    (element (get-line~ element))
                    (pivot (get-line~ selection-pivot)))
                (define (make-cell line)
                  (new Cell line col))
                (cond ((= element pivot) (set-selection (map make-cell (list element)) set-pivot?: #f user-origin: user-origin))
                      ((> element pivot) (set-selection (map make-cell (naturals pivot (+ element 1))) set-pivot?: #f user-origin: user-origin))
                      ((< element pivot) (set-selection (map make-cell (naturals element (+ pivot 1))) set-pivot?: #f user-origin: user-origin))))))
          (control?
            (if (member? element selection test: cell=?)
                (set-selection (remove element selection test: cell=?) set-pivot?: #f user-origin: user-origin)
              (set-selection (cons element selection) set-pivot?: #f user-origin: user-origin))
            (when selection-pivot
              (let ((element-col (get-col~ element))
                    (pivot-col (get-col~ selection-pivot)))
                (if (= element-col pivot-col)
                    (when (cell=? element selection-pivot)
                      (let ((pivot-line (get-line~ selection-pivot)))
                        (cond ((null? selection) (set! selection-pivot #f))
                              ((< pivot-line (get-line~ (car selection))) (set! selection-pivot (car selection)))
                              ((> pivot-line (get-line~ (last selection))) (set! selection-pivot (last selection))))))
                  (set! selection-pivot #f)))))))
  
  
  (method public (process-selection cell shift? control? user-origin)
    (let ((element (case selection-mode ((cell) cell) (else (get-line~ cell)))))
      (if (and multiple-selection? (or shift? control?))
          ((case selection-mode
             ((cell) process-multi-selection-cell)
             ((row)  process-multi-selection-row)) element shift? control? user-origin)
        (when (and (not always-select?) (member? element selection test: (if (eq? selection-mode 'cell) cell=? equal?)) (allow-start-drag?~ selection-manager))
          (delay-selection~ selection-manager))
        (set! selection-pivot element)
        (set-selection (list element) set-pivot?: #f user-origin: user-origin))))
  
  
  (method override (get-selection)
    selection)
  
  
  (method public (get-ordered-selection)
    (sort < selection))
  
  
  (method override (set-selection sel
                                  (force?: force? #f)
                                  (set-pivot?: set-pivot? #t)
                                  (ensure-displayed?: ensure-displayed? #t)
                                  (auto-expand?: auto-expand? #t)
                                  (generate-event?: generate-event? #t)
                                  (user-origin: user-origin #f))
    (define (inval-selection selection)
      (when (not-null? selection)
        (for-each (lambda (cell)
                    (let ((merge-cell (and (is? cell Cell) (get-merge-cell (cell->site cell)))))
                      (if merge-cell
                          (invalidate-merge-cell merge-cell)
                        (invalidate-selection (list cell)))))
                  selection)))
    
    (unless (locked?~ selection-manager)
      (reset~ locator)
      (when (change-selection?~ selection-manager)
        (let ((old-selection selection))
          (set! selection sel)
          (when set-pivot?
            (set! selection-pivot (if (null? selection) #f (car selection))))
          (inval-selection old-selection)
          (inval-selection selection)
          (when generate-event?
            (selection-change old-selection force? user-origin))))
      (when (and auto-expand/collapse? auto-expand? (eq? user-origin 'mouse))
        (unless multiple-selection?
          (let ((auto-row (and (not-null? sel) (get-row (selection-row (car sel))))))
            (when (or (not auto-row) (collapsed?~ auto-row))
              (auto-expand/collapse auto-row auto-toggle?: #t)))))
      (let ((selection-cell (selection-cell)))
        (when selection-cell
          (let ((data (get-cell-data selection-cell)))
            (when (is? data View)
              (let ((outline-view (get-outline-view~ data)))
                (when (and (eq? outline-view self)
                           (is-focused?))
                  (acquire-focus~ data)))))))
      (when (and ensure-displayed? (end-delayed?~ selection-manager))
        (ensure-displayed))))
  
  
  (method protected virtual (selection-change old-selection force? user-origin)
    (with-locked-operation~ selection-manager
      (lambda ()
        (call-selection-handler old-selection selection force? user-origin))))
  
  
  (method public (get-selection-state (use: use 'row))
    (define (save-selection selection)
      (case selection-mode
        ((cell) (map save-cell selection))
        (else (map save-row selection))))
    
    (define (save-cell cell)
      (cons (save-row (get-line~ cell)) (get-col~ cell)))
    
    (define (save-row line)
      (case use
        ((row) (get-row line))
        ((rank) line)
        ((text) (line->text line))
        ((data) (get-user-data~ (get-row line)))))
    
    (define (line->text line)
      (map row-title (get-spine~ (get-row line))))
    
    (let ((saved-selection (save-selection (get-selection)))
          (saved-position (nu+ (get-scrolled-position) (selection-position (get-selection))))
          (saved-pivot (and selection-pivot (save-selection (list selection-pivot))))
          (saved-edited (and edited-site (save-cell (site->cell edited-site)))))
      (list use
            saved-selection
            saved-position
            saved-pivot
            saved-edited)))
  
  
  (method public (set-selection-state state (test: test eqv?) (ensure-expanded?: ensure-expanded? #f) (ensure-displayed?: ensure-displayed? #t) (generate-event?: generate-event? #f))
    (bind (use saved-selection saved-position saved-pivot saved-edited) state
      (let ((row->line (if ensure-expanded?
                           (lambda (row)
                             (ensure-expanded row)
                             (row->line row))
                         row->line)))
        (define (restore-selection saved-selection)
          (remove-false
            (case selection-mode
              ((cell) (map restore-cell saved-selection))
              (else (map restore-row saved-selection)))))
        
        (define (restore-cell saved)
          (bind (row . col) saved
            (let ((line (restore-row row)))
              (when line
                (new Cell line col)))))
        
        (define (restore-row saved)
          (case use
            ((row) (row->line saved))
            ((rank) saved)
            ((text) (text->line saved))
            ((data) (user-data-index saved test: test))))
        
        (define (text->line text)
          (let (iter (row root-row) (text text))
            (if (null? text)
                (row->line row)
              (let ((subrow (find-by-title~ row (car text))))
                (when subrow
                  (iter subrow (cdr text)))))))
        
        (when saved-pivot
          (let ((pivot (restore-selection saved-pivot)))
            (set! selection-pivot (cond ((not-null? pivot) (car pivot))
                                        ((null? selection) #f)
                                        (else (car selection))))))
        (when saved-edited
          (let ((edited (restore-cell saved-edited)))
            (if (not edited)
                (kill-edition)
              (set! edited-site (cell->site (get-master-cell edited)))
              (replace-site edited-site cell-editor)
              (let* ((rect (site-rect edited-site))
                     (size (new Dimension (- (get-right~ rect) (get-left~ rect)) (- (get-bottom~ rect) (get-top~ rect)))))
                (set-size~ cell-editor size)))))
        (let ((selection (restore-selection saved-selection)))
          (scroll-to (nu- saved-position (selection-position selection)))
          (set-selection selection ensure-displayed?: ensure-displayed? generate-event?: generate-event? auto-expand?: #f)))))
  
  
  (method (selection-position selection)
    (with ((region (selection-region selection identity no-clipping?: #t)))
      (rect-position (get-box~ region))))
  
  
  (method public (with-preserved-selection thunk (use: use 'row) (test: test eqv?) (ensure-expanded?: ensure-expanded? #f) (ensure-displayed?: ensure-displayed? #t) (generate-event?: generate-event? #f))
    (let ((saved-state (get-selection-state use: use)))
      (thunk)
      (set-selection-state saved-state test: test ensure-expanded?: ensure-expanded? ensure-displayed?: ensure-displayed? generate-event?: generate-event?)))
  
  
  (method public (get-single-selection)
    (if (null? selection)
        #f
      (car selection)))
  
  
  (method public (set-single-selection sel . rest)
    (assert (or (integer? sel) (is? sel Cell)))
    (apply set-selection (list sel) rest))
  
  
  (method public (auto-select-first)
    (when (and (null? selection) (> (get-visible-count) 0))
      (set-selection (list 0))))
  
  
  (method (selected-row-proc)
    (case selection-mode
      ((cell)
       (lambda (cell)
         (get-row (get-line~ cell))))
      (else
       get-row)))
  
  
  (method public (get-selected-row)
    (let ((selection (get-single-selection)))
      (when selection
        ((selected-row-proc) selection))))
  
  
  (method public (get-selected-rows)
    (map (selected-row-proc) (sort < selection)))
  
  
  (method public (get-selected-data)
    (map get-user-data~ (get-selected-rows)))
  
  
  (method public (get-single-selected-data)
    (let ((row (get-selected-row)))
      (when row
        (get-user-data~ row))))
  
  
  (method public (get-selection-data selection)
    (map (lambda (line)
           (let ((row (get-row line)))
             (get-user-data~ row)))
         selection))
  
  
  (method public (with-deleted-selection selection proc (can-survive?: can-survive? #f))
    (let* ((selected-lines (if (eq? selection-mode 'cell) (map get-line~ selection) selection))
           (data (and can-survive? (get-selection-data selected-lines)))
           (smallest (apply min selected-lines)))
      (proc)
      (let ((sel (and can-survive? (remove-false (map user-data-index data)))))
        (if (and can-survive? (= (length selection) (length sel)))
            (set-selection sel)
          (when (and smallest (> smallest 0))
            (let ((previous (- smallest 1)))
              (set-single-selection (if (eq? selection-mode 'cell) (new Cell previous 0) previous))))))))
  
  
  (method (selection->rows selection)
    (case selection-mode
      ((cell)
       (map (lambda (cell)
              (cons (get-visible-row (get-line~ cell)) (get-col~ cell)))
            selection))
      (else
       (map get-visible-row selection))))
  
  
  (method (rows->selection rows)
    (case selection-mode
      ((cell)
       (remove-false (map (lambda (pair)
                            (bind (row . col) pair
                              (new Cell (row-index row) col)))
                          rows)))
      (else
       (remove-false (map row-index rows)))))
  
  
  ;; update slots that are line based
  (method override (added-update-selection line count)
    (let ((proc (case selection-mode
                  ((cell) (lambda (cell)
                            (let ((l (get-line~ cell)))
                              (if (>= l line)
                                  (new Cell (+ l count) (get-col~ cell))
                                cell))))
                  (else (lambda (l)
                          (if (>= l line)
                              (+ l count)
                            l))))))
      (set! selection (map proc selection))
      (when selection-pivot
        (let ((new-pivot (proc selection-pivot)))
          (when new-pivot
            (set! selection-pivot new-pivot))))))
  
  
  ;; update slots that are line based
  (method override (removed-update-selection line count)
    (let ((end-line (+ line count))
          (removed #f))
      (let ((proc (case selection-mode
                    ((cell) (lambda (cell)
                              (let ((l (get-line~ cell)))
                                (cond ((< l line)
                                       cell)
                                      ((>= l end-line)
                                       (new Cell (- l count) (get-col~ cell)))
                                      (else
                                       #f)))))
                    (else (lambda (l)
                            (cond ((< l line)
                                   l)
                                  ((>= l end-line)
                                   (- l count))
                                  (else
                                   #f)))))))
        (set! selection (collect (lambda (old)
                                   (let ((new (proc old)))
                                     (unless (or new removed)
                                       (set! removed old))
                                     new))
                                 selection))
        (when selection-pivot
          (let ((pivot (proc selection-pivot)))
            (set! selection-pivot (or pivot (and (not-null? selection) (car selection))))))
        removed)))

  
  (method override (selection-region sel transformer (no-clipping?: no-clipping? #f))
    (define (element-region element)
      (case selection-mode
        ((cell) (new Region rect: (transformer (cell-rect element))))
        (else (selection-row-region element transformer))))
    
    (let ((region (new Region)))
      (loop (for element in sel)
            (let ((merge-cell (safe-get-merge-cell (cell->site element))))
              (if merge-cell
                  (with ((row-region (new Region rect: (transformer (merge-cell-rect merge-cell)))))
                    (combine-or!~ region row-region))
                (with ((row-region (element-region element)))
                  (combine-or!~ region row-region)))))
      (unless no-clipping?
        (with ((clipper (new Region rect: (transformer (get-view-clipper)))))
          (combine-and!~ region clipper)))
      region))
  
  
  (method override (selection-row-rect line)
    (define (left-with-node row)
      (let* ((node-left (row-text-left~ node-column row))
             (columns-left (if (> (length columns) 1)
                               (borders-left (get-borders~ (second columns)))
                             node-left)))
        (min node-left columns-left)))
    
    (define (left-without-node)
      (borders-left (get-borders~ (first columns))))
    
    (let ((row (get-row line)))
      (let ((left   (if node-column (left-with-node row) (left-without-node)))
            (top    (get-v~ (get-position~ row))))
        (new Rect (- left 3) top columns-right (+ top (get-height~ row))))))
  
  
  (method (selection-row element)
    (case selection-mode
      ((cell) (get-line~ element))
      (else element)))
  
  
  (method (selection-cell)
    (and (not multiple-selection?)
         (let ((selection (get-single-selection)))
           (and selection
                (case selection-mode
                  ((cell) selection)
                  ((row) (and (= 1 (get-columns-count))
                              (new Cell selection 0)))
                  ((node) (new Cell selection 0)))))))
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method override (get-view-state (use: use 'row))
    (list (get-expand-state) (get-selection-state use: use)))
  
  
  (method override (set-view-state state)
    (bind (expand-state selection-state) state
      (set-expand-state expand-state)
      (set-selection-state selection-state)))
  
  
  ;;;
  ;;;; Visibility
  ;;;
  
  
  (method public (cell-visible? cell)
    (and (< (get-line~ cell) visible-count)
         (< (get-col~ cell) (get-columns-count))))


  ;;;
  ;;;; Borders
  ;;;
  
  
  (definition (borders-left borders)
    (car borders))
  
  
  (definition (borders-right borders)
    (cdr borders))
  
  
  (definition (borders-width borders)
    (- (borders-right borders) (borders-left borders)))
  

  ;;;
  ;;;; Coordinates
  ;;;
  
  
  ;; temp patch to be virtual...
  (method override (line-rect line <fx>) <Rect>
    (let* ((row   (get-row line))
           (left  (borders-left (get-borders~ (car columns))))
           (top   (get-v~ (get-position~ row)))
           (right columns-right))
      (new Rect left top right (+ top (get-height~ row)))))
  
  
  (method public (cell-position cell <Cell>) <Point>
    (let ((rect (cell-rect cell)))
      (new Point (get-left~ rect) (get-top~ rect))))
  
  
  (method public (site-rect site <Site>) <Rect>
    (let ((merge-cell (safe-get-merge-cell site)))
      (if merge-cell
          (merge-cell-rect merge-cell)
        (unmerged-site-rect site))))
  
  
  (method public (cell-rect cell <Cell>) <Rect>
    (site-rect (cell->site cell)))

  
  (method public (unmerged-site-rect site <Site>) <Rect>
    (let* ((borders (get-borders~ (get-column~ site)))
           (row (get-row~ site))
           (top (get-v~ (get-position~ row))))
      (new Rect (borders-left borders) top (borders-right borders) (+ top (get-height~ row)))))
  
  
  (method public (cell-region cell <Cell>) <Region>
    (new Region rect: (cell-rect cell)))
  
  
  (method (cell-content-rect cell <Cell>) <Rect>
    (let* ((line    (get-line~ cell))
           (col     (get-col~ cell))
           (row     (get-row line))
           (column  (get-column col))
           (top     (get-v~ (get-position~ row)))
           (data    (get-cell-data cell))
           (width   (get-width~ column))
           (borders (get-borders~ column))
           (left    (borders-left borders)))
      (offset-rect (content-rect~ column row data width (get-height~ row)) left top)))
    
  
  (method (cell-text-rect cell <Cell>) <Rect>
    (let* ((line  (get-line~ cell))
           (col   (get-col~ cell))
           (row   (get-row line))
           (top   (get-v~ (get-position~ row)))
           (data  (get-cell-data cell))
           (width (get-width~ node-column)))
      (offset-rect (text-rect~ (get-column col) row data width (get-height~ row) #f) 0 top)))
  
  
  (method public (view->tree pos <Point> (any?: any? #f) (uniform-height?: uniform-height? #f))
    (define (visible-cell cell)
      (if any?
          cell
        (when (cell-visible? cell)
          cell)))
    
    (let ((line (if uniform-height? (quotient (get-v~ pos) default-row-height) (v->line (get-v~ pos))))
          (col (h->col (get-h~ pos))))
      (when (and line col (>= line 0))
        (let ((cell (new Cell line col)))
          (let ((merge-cell (get-merge-cell (cell->site cell))))
            (visible-cell (if merge-cell (get-first-cell~ merge-cell) cell)))))))


  (method public (call-with-view->tree pos <Point> proc)
    (call-with-v->line (get-v~ pos)
      (lambda (line v height)
        (if line
            (let ((col (h->col (get-h~ pos))))
              (if (and line col (>= line 0))
                  (let ((cell (new Cell line col)))
                    (if (cell-visible? cell)
                        (proc cell v height)
                      (proc #f #f #f)))
                (proc #f #f #f)))
          (proc #f #f #f)))))
  

  (method public (h->col h)
    (continuation-capture
      (lambda (return)
        (loop (for col from 0 below (get-columns-count))
              (for column in columns)
              (bind (left . right) (get-borders~ column)
                (when (and (<= h right) (/= left right))
                  (continuation-return return col))))
        #f)))
  
  
  ;;;
  ;;;; Embedded Edition
  ;;;
  
  
  (method public (in-edition?)
    edited-site)
  
  
  (method public (edit-cell cell (editor: editor #f) (after-end: after-end #f))
    (assert (not update-locked?))
    (assert (< (get-col~ cell) (get-columns-count)))
    (end-edition)
    (let ((rect (cell-editor-rect cell))
          (view (new Tree-Data-View
                  preserved-data: (get-cell-data cell)
                  after-end: after-end
                  visible?: #f
                  children: (list editor)))
          (site (cell->site cell)))
      (replace-site (get-top-site site) view)
      (set-rect~ view (acquire~ (get-parent~ view) self rect))
      (set! edited-site site)
      (set! cell-editor view)
      (set-visible?~ view #t)))
  
  
  (method public (end-edition)
    (when edited-site
      (let ((site edited-site)
            (editor cell-editor))
        (set! edited-site #f)
        (set! cell-editor #f)
        (receive (line col) (site->line/col site)
          (when (and line col)
            (let ((bounds (cell-editor-rect (new Cell line col)))
                  (after-end (get-after-end~ editor)))
              (replace-site (get-master-site site) (get-preserved-data~ editor))
              (close~ editor)
              (when after-end
                (after-end site))
              (invalidate-rect bounds)))))))
  
  
  (method public (kill-edition)
    (when edited-site
      (let ((site edited-site)
            (editor cell-editor))
        (set! edited-site #f)
        (set! cell-editor #f)
        (replace-site (get-master-site site) (get-preserved-data~ editor))
        (close~ editor))))
  
  
  (method public (replace-site site child)
    (replace-child~ (get-row~ site) (column->col (get-column~ site)) child))
  
  
  (method public (replace-cell cell child)
    (replace-child~ (get-row (get-line~ cell)) (get-col~ cell) child))
  
  
  (method public (get-edited-cell)
    (and edited-site (site->cell edited-site)))
  
  
  (method (cell-editor-rect cell)
    ;; quicky
    (let ((merge-cell (safe-get-merge-cell (cell->site cell))))
      (if merge-cell
          (merge-cell-rect merge-cell)
        (let* ((line    (get-line~ cell))
               (col     (get-col~ cell))
               (row     (get-row line))
               (column  (get-column col))
               (top     (get-v~ (get-position~ row)))
               (cell    (get-cell-data cell))
               (width   (if show-header? (get-width~ column) (get-width)))
               (borders (get-borders~ column))
               (left    (borders-left borders))
               (h       (text-h~ column (get-level~ row)))
               (rect    (content-rect~ column row cell width (get-height~ row))))
          (offset-rect rect left top)))))
  
  
  (method public (cell-up cell)
    (let ((merge-cell (safe-get-merge-cell (cell->site cell))))
      (let ((line (- (if merge-cell
                         (get-first-line~ merge-cell)
                       (get-line~ cell)) 1))
            (col (get-col~ cell)))
        (when (>= line 0)
          (new Cell line col)))))
  
  
  (method public (cell-down cell (validate? #t))
    (let ((merge-cell (safe-get-merge-cell (cell->site cell))))
      (let ((line (+ (if merge-cell
                         (get-last-line~ merge-cell)
                       (get-line~ cell)) 1))
            (col (get-col~ cell)))
        (when (or (not validate?) (< line (get-visible-count)))
          (new Cell line col)))))
  
  
  (method public (cell-left cell)
    (let* ((merge-cell (safe-get-merge-cell (cell->site cell)))
           (col (- (or (and merge-cell (get-first-col~ merge-cell))
                       (get-col~ cell))
                   1))
           (cell (new Cell (get-line~ cell) col)))
      (cond ((< col 0) #f)
            ((get-visible?~ (get-column col))
             cell)
            (else
             (cell-left cell)))))
  
  
  (method public (cell-right cell)
    (let* ((merge-cell (safe-get-merge-cell (cell->site cell)))
           (col (+ (or (and merge-cell (get-first-col~ merge-cell))
                       (get-col~ cell))
                   1))
           (cell (new Cell (get-line~ cell) col)))
      (cond ((>= col (get-columns-count)) #f)
            ((get-visible?~ (get-column col))
             cell)
            (else
             (cell-right cell)))))
  
  
  (method public (select-up cell)
    (let ((sel (cell-up cell)))
      (when sel
        (end-edition)
        (select-cell sel))))
  
  
  (method public (select-down cell (validate? #t))
    (let ((sel (cell-down cell validate?)))
      (when sel
        (end-edition)
        (select-cell sel))))
    
  
  (method public (select-left cell)
    (let ((sel (cell-left cell)))
      (when sel
        (end-edition)
        (select-cell sel))))

  
  (method public (select-right cell)
    (let ((sel (cell-right cell)))
      (when sel
        (end-edition)
        (select-cell sel))))
  
  
  (method public virtual (select-cell cell . rest)
    (set-single-selection 
      (if (eq? selection-mode 'cell)
          cell
        (get-line~ cell))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor (context: context #f))
    (new Tree-View-Descriptor)))



;;;
;;;; Tree-View definitions
;;;


(definition (filled-column-values client)
  (if (and (pair? client) (null? (cdr client)))
      (component-descendant-value-list (car client) filter-proc: (lambda (descendant) (is? descendant Tree-Column))) 
    '()))



;;;
;;;; Locator
;;;


(class Tree-Locator extends Locator
  
  
  (slot tree)
  (slot column initialize #f)
  
  
  (method override (initialize tree . rest)
    (set! tree~self tree)
    (apply nextmethod rest))
  
  
  (method override (iterate proc)
    (define (prepare-iterate)
      (let ((sel (get-single-selection~ tree)))
        (case (get-selection-mode~ tree)
          ((cell)
           (set! column (if sel (get-col~ sel) 0))
           (cons (and sel (get-row~ tree (get-line~ sel)))
                 (lambda (row) (element (get-children~ row) column))))
          (else
           (cons (and sel (get-row~ tree sel))
                 (lambda (row) (car (get-children~ row))))))))
    
    (bind (row . child-get) (prepare-iterate)
      (for-each-visible-row~ tree
        (lambda (row line)
          (let ((child (child-get row)))
            (let ((title (typecase child
                           ((Tree-Label) (get-title~ child))
                           (else (get-title~ child)))))
              (proc title line))))
        start: (when row (if prefix row (get-next~ row)))
        wrap?: #t)))
  
  
  (method override (select line)
    (case (get-selection-mode~ tree)
      ((cell) (set-single-selection~ tree (new Cell line column)))
      (else (set-single-selection~ tree line)))))


;;;
;;;; Merge-Cell
;;;


(class Merge-Cell extends Object
  
  
  (slot merged-rows    initialize '() accessors generate)
  (slot merged-columns initialize '() accessors generate)
  (slot tree           initialize #f  accessors generate)
  
  
  (method override (initialize rows-list columns-list cells-tree)
    (nextmethod)
    (set! merged-rows rows-list)
    (set! merged-columns columns-list)
    (set! tree cells-tree))
  
  
  (method public (get-master-cell)
    (let ((master-row (first merged-rows))
          (master-column (first merged-columns)))
      (site->cell~ tree (new Site master-row master-column))))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (let ((r (length merged-rows))
              (c (length merged-columns)))
          (format output "{a} row{a} {a} column{a}" r (format-plural r) c (format-plural c))))))
  
  
  (method public (add-merged-row row)
    (set! merged-rows (cons row merged-rows)))
  
  
  (method public (for-each-cell proc)
    (for-each (lambda (row)
                (for-each (lambda (column)
                            (proc row column))
                          merged-columns))
              merged-rows))
  
  
  (method public (get-first-row)
    (first merged-rows))
  
  
  (method public (get-last-row)
    (last merged-rows))
  
  
  (method public (get-first-column)
    (first merged-columns))
  
  
  (method public (get-last-column)
    (last merged-columns))
  
  
  (method public (get-first-site)
    (new Site (get-first-row) (get-first-column)))
  
  
  (method public (get-last-site)
    (new Site (get-last-row) (get-last-column)))
  
  
  (method public (get-first-line)
    (let ((first-line #f))
      (for-each (lambda (row)
                  (when (get-visible?~ row)
                      (let ((line (row->line~ tree row)))
                        (when (or (not first-line)
                                  (< line first-line))
                          (set! first-line line)))))
                merged-rows)
      first-line))
  
  
  (method public (get-last-line)
    (let ((last-line 0))
      (for-each (lambda (row)
                  (when (get-visible?~ row)
                      (let ((line (row->line~ tree row)))
                        (when (> line last-line)
                          (set! last-line line)))))
                merged-rows)
      last-line))
  
  
  (method public (get-first-col)
    (let ((first-col #f))
      (for-each (lambda (column)
                  (when (get-visible?~ column)
                    (let ((col (column->col~ tree column)))
                      (when (or (not first-col)
                                (< col first-col))
                        (set! first-col col)))))
                merged-columns)
      first-col))
  
  
  (method public (get-last-col)
    (let ((last-col 0))
      (for-each (lambda (column)
                  (when (get-visible?~ column)
                      (let ((col (column->col~ tree column)))
                        (when (> col last-col)
                          (set! last-col col)))))
                merged-columns)
      last-col))


  (method public (get-first-cell)
    (let ((first-line (get-first-line))
          (first-col (get-first-col)))
      (if (and first-line first-col)
          (new Cell first-line first-col)
        #f)))


  (method public (get-last-cell)
    (let ((last-line (get-last-line))
          (last-col (get-last-col)))
      (new Cell last-line last-col)))
)


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))




(class Tree-View-Descriptor extends Outline-View-Old-Descriptor
  
  
  (form
    (<install>                                                  image: {Image-Resource "AnalysisManager"}
      (<properties~>
        (<Property-Descriptor>  name: portfolio                 standard?: #f
          (<Portfolio-Domain>   name: domain                    nullable?: #t))
        (<Property-Descriptor>  name: show-header?              standard?: #f
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: selection-mode
          (<Enumeration-Domain> name: domain                    nullable?: #f choices: ((cell "cell") (row "row") (node "node"))))
        (<Property-Descriptor>  name: multiple-selection?
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: can-select-empty?         standard?: #f
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: always-select?            standard?: #f
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: show-unfocused-selection?
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: focused-on-descendants?   standard?: #f
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: auto-expand/collapse?     standard?: #f
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: cell-background           standard?: #f
          (<Value-Domain>       name: domain                    nullable?: #t))
        (<Property-Descriptor>  name: empty-background          standard?: #f
          (<Value-Domain>       name: domain                    nullable?: #t))
        (<Property-Descriptor>  name: filled-column
          (<Enumeration-Domain> name: domain                    nullable?: #t choices-handler: {Reference filled-column-values}))
        (<Property-Descriptor>  name: column-spacing            standard?: #f
          (<Integer-Domain>     name: domain))
        (<Property-Descriptor>  name: column-frequency          standard?: #f
          (<Integer-Domain>     name: domain))
        (<Property-Descriptor>  name: reorderable?              standard?: #f
          (<Boolean-Domain>     name: domain))
        (<Property-Descriptor>  name: content                   standard?: #f
          (<Value-Domain>       name: domain                    nullable?: #t)))))
  
  
  (method override (component-get-categorized-addable-classes component)
    `((,(reference Tree-Label-Column)))))))
