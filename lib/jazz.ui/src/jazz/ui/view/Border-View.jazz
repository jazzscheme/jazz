;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Border Views
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Border-View jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.jml)
        (jazz.ui)
        (jazz.ui.skin))


(class Border-View extends Layout-View

  
  ;; none edge raised palette shadow solid
  (property border-type   initialize 'solid         accessors explicit)
  (property border-color  initialize {Color Black}  accessors explicit)
  (property border-insets initialize {Rect 1 1 1 1} accessors explicit)
  (property border-sides  initialize 'all           accessors explicit)


  (form
    (<install> layout-type: fill))
  
  
  (method override (mouse-activate?)
    #f)

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-border-type)
    border-type)
  
  
  (method public (set-border-type type)
    (set! border-type type)
    (recalculate-border-insets)
    (invalidate-borders))
  
  
  (method public (get-border-color)
    border-color)
  
  
  (method public (set-border-color color)
    (set! border-color color)
    (invalidate-borders))
  
  
  (method public (get-border-insets)
    border-insets)
  
  
  (method public (set-border-insets insets)
    (set! border-insets insets)
    (layout-view))
  
  
  (method protected virtual (recalculate-border-insets)
    (case border-type
      ((none) (set-border-insets {Rect 0 0 0 0}))
      ((solid) (set-border-insets {Rect 1 1 1 1}))
      ((edge raised palette) (set-border-insets (case style ((entry) {Rect 1 1 1 1}) (else {Rect 2 2 2 2}))))
      ((shadow) (set-border-insets {Rect 1 1 3 3}))))
  
  
  (method public (get-border-sides)
    border-sides)
  
  
  (method public (set-border-sides sides)
    (set! border-sides sides)
    (invalidate-borders))
  
  
  ;;;
  ;;;; Style
  ;;;

  
  (method override (style-update)
    (recalculate-border-insets))

  
  ;;;
  ;;;; Client
  ;;;


  (method override (add-child child)
    (nextmethod child)
    (when (is? child View)
      (layout-view)))
  

  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (fill-position)
    (+ (nextmethod)
       (new Point (get-left~ border-insets) (get-top~ border-insets))))
  
  
  (method override (fill-size)
    (- (nextmethod)
       (new Dimension
         (+ (get-left~ border-insets) (get-right~ border-insets))
         (+ (get-top~ border-insets) (get-bottom~ border-insets)))))
  
  
  ;;;
  ;;;; Widget
  ;;;
  
  
  (method override (inner? view)
    (memq? view children))
  
  
  (method override (inner-widget)
    (let ((view (first-view)))
      (if (and (eq? layout-type 'fill)
               view)
          (inner-widget~ view)
        self)))
  
  
  ;;;
  ;;;; Invalidate
  ;;;
  
  
  (method (invalidate-borders)
    (invalidate-border insets: border-insets))
  

  ;;;
  ;;;; Drawing
  ;;;

  
  (method override (draw surface context)
    (define (standardize-sides)
      (listify border-sides))
    
    (let ((bounds (get-bounds)))
      (if (not (view-enabled?))
          (frame-rect~ surface bounds {Color Dark})
        (case border-type
          ((none))
          ((solid)
           (when border-color
             (case border-sides
               ((none))
               ((all) (frame-rect~ surface bounds border-color))
               (else (frame-partial-rect~ surface bounds (standardize-sides) border-color)))))
          ((edge)
           (let ((look (get-look)))
             (cond ((and (eq? style 'entry) (get-edge-flat?~ look))
                    (frame-rect~ surface bounds {Color Entry-Border}))
                   ((and (eq? style 'outline) (get-edge-flat?~ look))
                    (let ((inner (inflate-rect bounds -1 -1)))
                      (frame-rect~ surface inner {Color Outline-Border})))
                   (else
                    (draw-edge~ surface bounds outer: 'sunken inner: (case (get-left~ border-insets) ((1) #f) ((2) 'sunken)) edges: 'rectangle)))))
          ((raised)
           (draw-edge~ surface bounds inner: 'raised outer: 'raised edges: 'rectangle))
          ((shadow)
           (let ((inner (expand-rect bounds 0 0 (- 1 (get-right~ border-insets)) (- 1 (get-bottom~ border-insets)))))
             (frame-rect~ surface inner border-color)
             (set-color~ surface {Color Black})
             (set-line-width~ surface 1)
             (line~ surface (get-right~ inner) 2 (get-right~ inner) (get-bottom~ bounds))
             (line~ surface (+ (get-right~ inner) 1) 2 (+ (get-right~ inner) 1) (get-bottom~ bounds))
             (line~ surface 2 (get-bottom~ inner) (get-right~ bounds) (get-bottom~ inner))
             (line~ surface 2 (+ (get-bottom~ inner) 1) (get-right~ bounds) (+ (get-bottom~ inner) 1))))))))
  
  
  (method override (draw-children surface nodes context)
    (nextmethod surface nodes context)
    (case border-type
      ((palette)
       (let ((bounds (get-bounds)))
         (draw-edge~ surface bounds outer: 'sunken inner: 'raised edges: '(top))))))
  
  
  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-look)
    (case style
      ((entry) (find-look 'border/entry))
      ((outline) (find-look 'border/outline))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor (context: context #f))
    (new jazz.ui.designer:Border-View-Descriptor))))
