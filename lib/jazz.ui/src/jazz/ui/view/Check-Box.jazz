;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Check Boxes
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Check-Box jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.locale)
        (jazz.system)
        (jazz.ui))


(class Check-Box extends View
  
  
  (definition box-size
    13)
  
  
  (property checked?   initialize #f                  getter get-checked?   setter set-checked?)
  (property editable?  initialize #t                  accessors generate)
  (property focusable? initialize #f                  accessors generate)
  (property color      initialize {Color name: Black} getter get-color      setter set-color)
  (property flat?      initialize #f                  accessors generate)
  
  
  (slot tracking? initialize #f)
  (slot inside?   initialize #f)
  
  
  (method override (default-size)
    {Dimension 100 16})
  
  
  (method override (get-sort-value)
    (localize title))
  
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-checked?)
    checked?)
  

  (method public (set-checked? flag)
    (set! checked? flag)
    (call-content-change {})
    (set-modified? #t)
    (invalidate-view))


  (method public (get-color)
    color)


  (method public (set-color value)
    (set! color value)
    (invalidate-view))
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method protected virtual (change-state reverse?)
    (set-checked? (not checked?)))
  
  
  (method (process-change-state)
    (change-state (shift-down?))
    (set-modified? #t)
    (if (not outline-view)
        (process-action self)
      (process-action~ outline-view self)))
  
  
  ;;;
  ;;;; Regions
  ;;;
  
  
  (method (text-extent)
    (let ((title (localize title)))
      (text-extent~ font title)))
  
  
  (method (box-rect)
    (let* ((extent (text-extent))
           (h (quotient (- (get-width) (+ box-size (if title (+ 5 (get-width~ extent)) 0))) 2))
           (v (quotient (- (get-height) (if title (get-height~ extent) box-size)) 2)))
      (new Rect h v (+ h box-size) (+ v box-size))))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw surface context lh lv)
    (let* ((title (localize title))
           (extent (if title (text-extent) #f))
           ;; quicky
           (center? (not extent))
           (box-h (if center? (quotient (- (get-width) box-size) 2) 0))
           (box-v (quotient (- (get-height) (if extent (get-height~ extent) box-size)) 2))
           (box-rect (new Rect box-h box-v (+ box-h box-size) (+ box-v box-size)))
           (grayed? (or (not enabled?) (and tracking? inside?))))
      (unless flat?
        (draw-edge~ surface box-rect inner: 'sunken outer: 'sunken edges: 'rectangle))
      (fill-rect~ surface (inflate-rect box-rect -2 -2) (if grayed? {Color name: Medium} {Color name: White}))
      (draw-check surface (+ 3 box-h) (+ 3 box-v) grayed?)
      (when title
        (let ((selected? (getf context selected?:))
              (text-h (+ 18 box-h))
              (text-v box-v))
          (when selected?
            (let ((rect
                   (if (and (is? outline-view Tree-View) (memq? (get-selection-mode~ outline-view) '(row cell)))
                       (new Rect (- text-h 3) (- text-v 2) (get-width) (+ text-v (get-height~ extent) 2))
                     (new Rect (- text-h 3) (- text-v 2) (+ text-h (get-width~ extent) 3) (+ text-v (get-height~ extent) 2)))))
              (fill-rect~ surface rect {Color name: Focused-Selection})))
          (set-font~ surface font)
          (set-text-color~ surface (either (determine-color~ color enabled? selected?)
                                           {Color name: White}))
          (draw-text~ surface text-h text-v title (or enabled? selected?))))))
  
  
  (method protected virtual (draw-check surface h v grayed?)
    (when checked?
      (set-color~ surface (if grayed? {Color name: Dark} {Color name: Black}))
      (draw-check~ surface h v)))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (key-press key)
    (case key
      ((#\space)
       (process-change-state))))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down pos)
    (if (and outline-view (not (in-rect? pos (box-rect))))
        (outline-select focus?: #t)
      (when (and enabled? editable?)
        (set! tracking? #t)
        (set! inside? #t)
        (acquire-capture)
        (invalidate-view))))
  
  
  (method override (drag-move pos)
    (when tracking?
      (let ((over? (over-view? pos)))
        (when (neq? over? inside?)
          (set! inside? over?)
          (invalidate-view)))))
  
  
  (method override (drag-up pos . rest)
    (when tracking?
      (release-capture)
      (set! tracking? #f)
      (when inside?
        (when focusable?
          (acquire-focus))
        (process-change-state))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Check-Box-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Check-Box-Descriptor extends View-Descriptor


  ;;;
  ;;;; Designer
  ;;;

  
  (method override (class-property-presentation class property)
    (case (field-name property)
      ((checked?) "Checked")
      ((color) "Color")
      (else (nextmethod class property)))))))
