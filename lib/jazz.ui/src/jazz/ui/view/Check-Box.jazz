;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Check Boxes
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Check-Box jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.locale)
        (jazz.platform)
        (jazz.tree)
        (jazz.view))


(class Check-Box extends View
  
  
  (definition box-size
    13)
  
  
  (property checked?    initialize #f            accessors explicit)
  (property selectable? initialize #f            accessors generate)
  (property editable?   initialize #t            accessors generate)
  (property color       initialize {Color Black} accessors explicit)
  (property flat?       initialize #f            accessors generate)
  
  
  (slot tracking? initialize #f)
  (slot inside?   initialize #f)
  
  
  (method override (default-size)
    {Dimension 100 16})
  
  
  (method override (get-sort-value)
    (localize title))
  
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-checked?)
    checked?)
  

  (method public (set-checked? flag)
    (set! checked? flag)
    (call-content-change #f)
    (invalidate-view))


  (method public (get-color)
    color)


  (method public (set-color value)
    (set! color value)
    (invalidate-view))
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method protected virtual (change-state reverse?)
    (set-checked? (not checked?)))
  
  
  (method (process-change-state)
    (change-state (shift-down?))
    (set-modified? #t)
    (if (not outline-view)
        (process-action self)
      (process-action~ outline-view self)))
  
  
  ;;;
  ;;;; Regions
  ;;;
  
  
  (method (text-extent)
    (let ((title (localize title)))
      (text-extent~ (effective-font) title)))
  
  
  (method (box-rect)
    (let* ((title (localize title))
           (extent (if title (text-extent) #f))
           ;; quicky
           (center? (not extent))
           (h (if center? (quotient (- (get-width) box-size) 2) 0))
           (v (quotient (- (get-height) (if extent (get-height~ extent) box-size)) 2)))
      (new Rect h v (+ h box-size) (+ v box-size))))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw surface context)
    (define (effective-selected?)
      (if (not outline-view)
          (getf context selected?:)
        (cell-selected?~ (outline-column) context)))
    
    (let* ((title (localize title))
           (extent (if title (text-extent) #f))
           ;; quicky
           (center? (not extent))
           (box-h (if center? (quotient (- (get-width) box-size) 2) 0))
           (box-v (quotient (- (get-height) (if extent (get-height~ extent) box-size)) 2))
           (box-rect (new Rect box-h box-v (+ box-h box-size) (+ box-v box-size)))
           (enabled? (view-enabled?))
           (grayed? (or (not enabled?) (and tracking? inside?))))
      (unless flat?
        (draw-edge~ surface box-rect inner: 'sunken outer: 'sunken edges: 'rectangle))
      (fill-rect~ surface (inflate-rect box-rect -2 -2) (if grayed? {Color Medium} {Color White}))
      (draw-check surface (+ 3 box-h) (+ 3 box-v) grayed?)
      (when title
        (let ((selected? (effective-selected?))
              (text-h (+ 18 box-h))
              (text-v box-v))
          (when selected?
            (let ((color (if (is? outline-view Tree-View) (selection-color~ outline-view) {Color Focused-Selection}))
                  (rect
                   (if (and (is? outline-view Tree-View) (eq? (get-selection-mode~ outline-view) 'cell))
                       (new Rect (- text-h 3) (- text-v 2) (get-width) (+ text-v (get-height~ extent) 2))
                     (new Rect (- text-h 3) (- text-v 2) (+ text-h (get-width~ extent) 3) (+ text-v (get-height~ extent) 2)))))
              (fill-rect~ surface rect color)))
          (set-font~ surface (effective-font))
          (draw-text~ surface text-h text-v title (or (determine-color~ color enabled? selected?)
                                                      {Color White})
            enabled?: (or enabled? selected?))))))
  
  
  (method protected virtual (draw-check surface h v grayed?)
    (when checked?
      (set-color~ surface (if grayed? {Color Dark} {Color Black}))
      (draw-check~ surface h v)))


  ;;;
  ;;;; Focus
  ;;;


  (method override (focus-gained)
    (nextmethod)
    (when outline-view
      (focus-gained~ outline-view)))


  (method override (focus-lost)
    (nextmethod)
    (when outline-view
      (focus-lost~ outline-view)))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (key-press evt)
    (case (get-key~ evt)
      ((#\space)
       (process-change-state))
      (else
       (when outline-view
         (call-key-press~ outline-view evt)))))
  
  
  (method override (arrow-press evt)
    (when outline-view
      (call-arrow-press~ outline-view evt)))
  
  
  (method override (return-press evt)
    (when outline-view
      (call-return-press~ outline-view evt)))
  
  
  (method override (escape-press evt)
    (when outline-view
      (call-escape-press~ outline-view evt)))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down evt)
    (when outline-view
      (outline-select))
    (when (and (view-enabled?) editable? (or (not outline-view) (in-rect? (get-position~ evt) (box-rect))))
      (set! tracking? #t)
      (set! inside? #t)
      (acquire-capture)
      (invalidate-view)))
  
  
  (method override (drag-move evt)
    (when tracking?
      (let ((over? (over-view? (get-position~ evt))))
        (when (neq? over? inside?)
          (set! inside? over?)
          (invalidate-view)))))
  
  
  (method override (drag-up evt . rest)
    (when tracking?
      (release-capture)
      (set! tracking? #f)
      (when inside?
        (when selectable?
          (acquire-focus))
        (process-change-state))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor (context: context #f))
    (new jazz.ui.descriptors:Check-Box-Descriptor))))
