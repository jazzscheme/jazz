;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Color Combo
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Color-Combo jazz


(import (jazz.exemplar)
        (jazz.graphic)
        (jazz.handler)
        (jazz.markup)
        (jazz.text)
        (jazz.ui)
        (jazz.view))


(class undocumented Color-Combo extends Combo-Box
  
  
  (form
    (<install>                              background: {Color White}
      (<Layout-View>    name: color-contour size: {Dimension 14 15} background: {Color White} layout-type: fill layout-insets: {Rect 1 2 2 2}
        (<Border-View>  name: color-border  border-type: solid border-color: {Color Black}
          (<Color-View> name: color)))))
  
  
  (method override (form-model self form)
    (case (get-name form)
      ((body) Color-Text-View)
      (else (nextmethod self form))))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-color self)
    (get-background (locate self 'color)))
  
  
  (method public (set-color self color)
    (set-color-value self color)
    (let ((text (locate self 'body)))
      (set-string-content text (if (not color) "" (present color)))
      (select-all text)))
  
  
  (method (set-color-value self color)
    (set-background (locate self 'color) color))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (content-change self origin)
    (let ((content (get-content self)))
      (set-color-value self (if (empty-string? content) #f (parse-exemplar Color content)))
      (set-modified? self #t)))
  
  
  (method override (button-action self)
    (let ((pos (new Point (- (get-width self) 12) (get-height self))))
      (popup self (new Color-Picker colors: #f action-handler: (new Event-Handler target: self method-name: 'on-color-picked)) pos initiator: self)))
  
  
  (method (on-color-picked self evt)
    (let ((color (get-property evt color:)))
      (close-popups self)
      (set-color self color)
      (call-content-change self 'popup)))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout self)
    (let ((size (get-size self))
          (width (get-height self))
          (countour (locate self 'color-contour)))
      (set-position countour {Point 0 0})
      (set-position (locate self 'scroller) (new Point (get-right countour) 0))
      (set-size (locate self 'scroller) (new Dimension (- (get-width size) (get-width countour) 15) (get-height size)))
      (set-position (locate self 'pick) (new Point (- (get-width size) width) 0))
      (set-size (locate self 'pick) (new Dimension width width)))))


;;;
;;;; Color-View
;;;


(class Color-View extends View
  
  
  (method override (draw self surface context)
    (nextmethod self surface context)
    (when (not background)
      (let ((size (get-size self)))
        (set-color surface {Color Black})
        (set-line-width surface 1)
        (line surface (get-width size) 0 0 (get-height size)))))))
