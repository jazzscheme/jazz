;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Combo Buttons
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Combo-Button jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.skin)
        (jazz.ui))


(class undocumented Combo-Button extends Push-Tool

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-combo)
    parent)
  
  
  ;;;
  ;;;; Control
  ;;;
  
  
  (method override (get-state)
    (child-state~ parent self))
  
  
  (method override (mouse-enter)
    (unless surrogate
      (enter-child~ parent self)))
  
  
  (method override (mouse-leave)
    (unless surrogate
      (leave-child~ parent self)))


  (method override (mouse-down evt)
    (child-down~ parent self))


  (method override (drag-up evt . rest)
    (child-up~ parent self))
  
  
  ;;;
  ;;;; Mouse
  ;;;


  (method override (down)
    (when (view-enabled?)
      (when (eq? (get-kind~ parent) 'pick)
        (process-action self))))


  (method override (up)
    (when (eq? (get-kind~ parent) 'choose)
      (process-action self)))
  
  
  ;;;
  ;;;; Popup
  ;;;
  
  
  (method override (close-popups (event: event #f) (all?: all? #f) (exclude: exclude #f) (initiator: initiator #f))
    (unless (memq? event '(mouse-down double-click))
      (nextmethod event: event all?: all? exclude: exclude initiator: initiator)
      (invalidate-view)))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (light-color)
    {Color Light-Background})
  
  
  (method override (dark-color)
    {Color Dark})
    
    
  (method override (draw-inactive surface)
    (when (highlight-pick?~ parent)
      (let ((bounds (get-bounds))
            (skin (find-skin 'ui:combo)))
        (frame-rect~ surface bounds (or (get-button-border~ skin) (if (view-enabled?) {Color Light} {Color Medium}))))))
  
  
  (method override (draw-tool surface context)
    (case (get-kind~ parent)
      ((pick) (draw-arrow surface))
      ((choose) (draw-ellipsis surface))))
  
  
  (method (draw-arrow surface)
    (let ((skin (find-skin 'ui:combo)))
      (set-color~ surface (or (get-arrow-color~ skin) (if (view-enabled?) {Color Black} {Color Dark})))
      (let* ((size (get-size))
             (offset (get-offset))
             (h (+ offset (- (quotient (get-width~ size) 2) 2)))
             (v (+ offset (- (quotient (get-height~ size) 2) 1))))
        (draw-arrow-tip~ surface h v 3 'down))))
  
  
  (method (draw-ellipsis surface)
    (let ((title "..."))
      (let ((pos (justify-lazy (lambda () (get-text-extent~ surface title))
                               (lambda () size)
                               '(center center))))
        (draw-text~ surface (get-h~ pos) (get-v~ pos) title {Color Black} enabled?: (view-enabled?)))))
  
  
  (method (get-offset)
    (if (memq? (get-state) '(pushed choose))
        1
      0))
  
  
  ;;;
  ;;;; Recording
  ;;;
  
  
  (method override (record-actions?)
    (record-actions?~ (get-combo)))))
