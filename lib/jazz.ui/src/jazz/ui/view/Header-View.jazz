;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Header View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Header-View jazz


(import (jazz.skin)
        (jazz.ui))


(class Header-View extends Base-Container


  (property flat?       initialize #f accessors explicit)
  (property shown?      initialize #t accessors explicit)
  (property selector?   initialize #f accessors explicit)
  (property selectable? initialize #t accessors generate)
  
  
  (slot client initialize #f)
  
  
  (method override (default-name self)
    'header)


  ;;;
  ;;;; Access
  ;;;


  (method public (get-flat? self)
    flat?)


  (method public (set-flat? self value)
    (when (neq? value flat?)
      (set! flat? value)
      (layout-view self)))


  (method public (get-shown? self)
    shown?)
  

  (method public (set-shown? self value)
    (when (neq? value shown?)
      (set! shown? value)
      (layout-view self)))
  
  
  (method public (get-selector? self)
    selector?)
  
  
  (method public (set-selector? self value)
    (when (neq? value selector?)
      (set! selector? value)
      (layout-view self)))


  ;;;
  ;;;; Client
  ;;;
  
  
  (method public (get-client self)
    (or client (let ((clt (find-client self)))
                 (set! client clt)
                 clt)))
  
  
  (method public (set-client self clt)
    (set! client clt))
  
  
  (method protected virtual (find-client self)
    #f)
  
  
  ;;;
  ;;;; Widget
  ;;;
  
  
  (method override (content-inner? self view)
    #t)

  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout self)
    (let ((content (get-content self)))
      (when content
        (let ((size (get-size self)))
          (set-position content (content-position self size))
          (set-size content (content-size self size))))))
  
  
  (method (content-position self size)
    (new Point (get-selector-width self) (get-header-height self)))
  
  
  (method (content-size self size)
    (new Dimension
      (get-width size)
      (- (get-height size) (get-header-height self))))
  
  
  (method public virtual (get-header-height self)
    (if shown? (if flat? 15 16) 0))

  
  (method public virtual (get-selector-width self)
    (if selector? (if flat? 15 16) 0))


  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-skin self)
    (case style
      ((tool) (find-skin 'ui:header/tool))
      ((document) (find-skin 'ui:header/document))
      (else (find-skin 'ui:header))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor self (context: context #f))
    (new jazz.ui.descriptors:Header-View-Descriptor))))
