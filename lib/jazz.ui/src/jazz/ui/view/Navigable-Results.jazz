;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Navigable Results
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Navigable-Results jazz


(import (jazz.application)
        (jazz.event)
        (jazz.handler)
        (jazz.markup)
        (jazz.outline)
        (jazz.resource)
        (jazz.tree)
        (jazz.ui))


(class undocumented Navigable-Results extends Layout-View


  (form
    (<install>                            size: {Dimension 650 400} layout-type: border
      (<Layout-View>       name: tools
        (<Push-Tool>       name: edit     size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Edit Selection" resource: {Image-Resource "EditResult"} portfolio: :images action-handler: {Event-Handler :guest on-edit-selection}))
      (<Scroller-View>     name: scroller location: center vscroll?: #t hscroll?: #t style: tool
        (<!>               name: content  layout-type: fill
          (<Tree-Header>   name: header   style: tool
            (<!>           name: content
              (<Tree-View> name: results  selection-mode: row portfolio: :images selection-mode: row double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press})))))))
  
  
  (method override (finish self rest)
    (nextmethod self rest)
    (invalidate-columns (get-tree self)))
  
  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method override (get-tools-view self)
    (locate self 'tools))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-tree self)
    (locate self 'results))

  
  ;;;
  ;;;; Events
  ;;;


  (method (on-double-click self evt)
    (let ((sender (get-sender evt)))
      (case (get-name sender)
        ((results)
         (edit/next-selection self)))))
  
  
  (method override (on-return-press self evt)
    (edit/next-selection self))
  
  
  (method (on-edit-selection self evt)
    (edit/next-selection self))
  
  
  (method (edit/next-selection self)
    (if (editable-row? self (get-single-selected-data (get-tree self)))
        (call-edit-selection self 'forward)
      (edit-next self)))
  
  
  ;;;
  ;;;; Results
  ;;;
  
  
  (method public (clear-results self)
    (remove-every-row (get-tree self)))

  
  ;;;
  ;;;; Navigation
  ;;;
  
  
  (method public (edit-next self)
    (let* ((tree (get-tree self))
           (selection (get-selection tree))
           (current (and (not-null? selection) (last selection)))
           (rank (next-editable self current)))
      (if (not rank)
          (begin
            (set-selection tree '())
            #f)
        (set-selection tree (list rank))
        (update-view tree)
        (call-edit-selection self 'forward)
        #t)))
  
  
  (method public (edit-previous self)
    (let* ((tree (get-tree self))
           (selection (get-selection tree))
           (current (and (not-null? selection) (car selection)))
           (rank (previous-editable self current)))
      (if (not rank)
          (begin
            (set-selection tree '())
            #f)
        (set-selection tree (list rank))
        (call-edit-selection self 'backward)
        #t)))
  
  
  (method public (edit-current self)
    (call-edit-selection self 'forward))
  
  
  (method package (call-edit-selection self select)
    (let ((appl (current-application)))
      (edit-selection self select)
      (set-active-results appl self)
      (update-source-actions appl)))
  

  (method package virtual (edit-selection self select)
    )
  
  
  (method protected virtual (editable-row? self user-data)
    user-data)
  
  
  (method protected virtual (next-editable self current)
    (let* ((tree (get-tree self))
           (count (get-visible-count tree))
           (rank (if current (+ current 1) 0)))
      (continuation-capture
        (lambda (return)
          (while (< rank count)
            (let ((row (get-row tree rank)))
              (if (editable-row? self (get-user-data row))
                  (continuation-return return rank)
                (increase! rank))))
          #f))))
  
  
  (method protected virtual (previous-editable self current)
    (let* ((tree (get-tree self))
           (count (get-visible-count tree))
           (rank (if current (- current 1) (- count 1))))
      (continuation-capture
        (lambda (return)
          (while (>= rank 0)
            (let ((row (get-row tree rank)))
              (if (editable-row? self (get-user-data row))
                  (continuation-return return rank)
                (decrease! rank))))
          #f))))
  
  
  (method public (select-resultset self)
    (when application-active?
      (let ((tree (get-tree self)))
        (acquire-focus tree))))
  
  
  (method public (select-first self)
    (let ((tree (get-tree self))
          (rank (next-editable self #f)))
      (if (not rank)
          (bell)
        (set-selection tree (list rank)))))))
