;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Push Buttons
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Push-Button jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.locale)
        (jazz.markup)
        (jazz.skin)
        (jazz.view))


(class Push-Button extends View
  
  
  (property default? initialize #f accessors explicit)
  (property flat?    initialize #f accessors explicit)
  (property color    initialize #f accessors explicit)
  
  
  (slot state initialize 'inactive)
  
  
  (form
    (<install> background: {Color Medium}))
  
  
  (method override (mouse-activate? self)
    #f)
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-default? self)
    default?)
  
  
  (method public (set-default? self flag)
    (set! default? flag)
    (invalidate-view self))
  
  
  (method public (get-flat? self)
    flat?)
  
  
  (method public (set-flat? self flag)
    (set! flat? flag)
    (invalidate-view self))


  (method public (get-color self)
    color)


  (method public (set-color self value)
    (set! color value)
    (invalidate-view self))


  ;;;
  ;;;; State
  ;;;
  
  
  (method public (get-state self)
    state)
  
  
  (method public (set-state self value)
    (set! state value)
    (invalidate-view self))

  
  ;;;
  ;;;; Font
  ;;;
  
  
  (method override (effective-font self)
    (or font {Font Label-User}))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (default-size self)
    {Dimension 100 22})
  
  
  (method override (preferred-size self)
    (let ((title (localize title)))
      (if (not title)
          (default-size self)
        (new Dimension (+ (get-width (text-extent (effective-font self) title)) 50) 22))))
  
  
  ;;;
  ;;;; Mouse
  ;;;


  (method override (mouse-enter self)
    (when (and (view-enabled? self) (or (not (get-mouse-down?)) (eq? (get-mouse-down-view) self)))
      (case (get-state self)
        ((inactive) (set-state self 'highlighted))
        ((out-pushed) (set-state self 'pushed)))))


  (method override (mouse-leave self)
    (when (and (view-enabled? self) (or (not (get-mouse-down?)) (eq? (get-mouse-down-view) self)))
      (case (get-state self)
        ((highlighted) (set-state self 'inactive))
        ((pushed) (set-state self 'out-pushed)))))
  
  
  (method override (mouse-down self evt)
    (when (view-enabled? self)
      (set-state self 'pushed)))
  
  
  (method override (drag-up self evt . rest)
    (when (view-enabled? self)
      (unwind-protect
          (up self)
        (case (get-state self)
          ((pushed) (set-state self 'highlighted))
          ((out-pushed) (set-state self 'inactive))))))


  (method package (up self)
    (when (eq? (get-state self) 'pushed)
      (process-action self self)))

  
  ;;;
  ;;;; Opaqueness
  ;;;
  
  
  (method override (get-opaque? self)
    (let ((skin (get-skin self)))
      (if (neq? (get-background skin) 'none)
          (nextmethod self)
        #f)))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-background self surface context)
    (let ((skin (get-skin self)))
      (when (neq? (get-background skin) 'none)
        (nextmethod self surface context))))
  
  
  (method override (draw self surface context)
    (let ((title (or (localize title) ""))
          (offset (case state ((pushed) 1) (else 0)))
          (rect (get-bounds self))
          (skin (get-skin self))
          (enabled? (view-enabled? self)))
      (let ((background? (neq? (get-background skin) 'none)))
        (when default?
          (when background?
            (frame-rect surface rect (if enabled? {Color Black} {Color Dark}))
            (set! rect (inflate-rect rect -1 -1))))
        (if (not background?)
            (unless (eq? style 'text)
              (frame-rect surface rect {Color red: 100 green: 100 blue: 100})
              (let ((from (if enabled? {Color Dark-Red alpha: .5} {Color Dark alpha: .5}))
                    (to {Color red: 0 green: 0 blue: 0 alpha: .5}))
                (gradient-fill-rect surface (inflate-rect rect -1 -1) (list (list 0 from) (list 1 to)) direction: 'vertical)))
          (if flat?
              (draw-edge surface rect outer: 'raised edges: 'rectangle)
            (draw-push-button surface rect (if (eq? state 'pushed) #t #f))))
        (let* ((size (get-size self))
               (width (get-width size))
               (height (get-height size))
               (extent (get-text-extent surface title))
               (h (if (eq? style 'text) 0 (quotient (- width (get-width extent)) 2)))
               (v (- (quotient (- height (get-height extent)) 2) (if (eq? style 'text) 1 0)))
               (color (or color
                          (if background?
                              (if enabled? {Color Black} {Color Dark})
                            (if enabled? {Color red: .809 green: .701 blue: .263} {Color red: 130 green: 130 blue: 130})))))
          (when (not background?)
            (set-font surface (effective-font self)))
          (draw-text-at self surface (+ h offset) (+ v offset) title color enabled?)))))
  
  
  (method protected virtual (draw-text-at self surface h v title color enabled?)
    (draw-text surface h v title color enabled?: enabled?))
  
  
  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-skin self)
    (find-skin 'ui:button))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor self (context: context #f))
    (new jazz.ui.descriptors:Push-Button-Descriptor))))
