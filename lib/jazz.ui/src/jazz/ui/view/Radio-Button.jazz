;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Radio Buttons
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Radio-Button jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.locale)
        (jazz.skin)
        (jazz.tree)
        (jazz.ui)
        (jazz.ui.skin)
        (jazz.view))


(class Radio-Button extends View implements (Groupable Selectable)
  
  
  (definition box-size
    13)
  
  
  (property set              initialize #f accessors generate)
  (property group            initialize #f accessors explicit)
  (property selected?        initialize #f accessors explicit)
  (property radio-background initialize #f accessors generate)
  (property editable?        initialize #t accessors generate)
  
  
  (slot tracking? initialize #f)
  (slot inside?   initialize #f)
  
  
  (method override (default-size self)
    {Dimension 100 16})

  
  ;;;
  ;;;; Groupable
  ;;;


  (method override (get-group self)
    group)


  (method override (set-group self value)
    (set! group value))

  
  ;;;
  ;;;; Selectable
  ;;;


  (method override (get-selected? self)
    selected?)
  

  (method override (set-selected? self flag)
    (when (neq? flag selected?)
      (let ((current (get-current-selection self)))
        (set-selected-state self flag)
        (when current
          (set-selected-state current (not flag))))))
  
  
  (method override (unselect self)
    (when selected?
      (set-selected-state self #f)))
  
  
  (method (get-current-selection self)
    (continuation-capture
      (lambda (return)
        (for-each-button self
          (lambda (button)
            (when (get-selected? button)
              (continuation-return return button))))
        #f)))
  
  
  (method (for-each-button self proc)
    (for-each (lambda (child)
                (when (and (is? child Radio-Button)
                           (eqv? group (get-group child)))
                  (proc child)))
              (if set
                  (get-members set)
                (get-children parent))))
  
  
  (method package (set-selected-state self flag)
    (set! selected? flag)
    (invalidate-view self))
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method (process-change-state self)
    (set-selected? self #t)
    (set-modified? self #t)
    (if (not outline-view)
        (process-action self self)
      (process-action outline-view self)))

  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (preferred-size self)
    (let ((title (localize title)))
      (if (not title)
          (default-size self)
        (let ((extent (text-extent (effective-font self) title)))
          (new Dimension
            (+ 20 (get-width extent))
            (max 18 (get-height extent)))))))
  
  
  (method (box-rect self)
    (let* ((title (or (localize title) ""))
           (height (if (or (not title) (empty-string? title)) 13 (get-height (text-extent (effective-font self) title))))
           (h 18)
           (v (quotient (- (get-height self) height) 2)))
      (new Rect 0 (+ v 1) box-size (+ v box-size 1))))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw self surface context)
    (define (effective-selected?)
      (if (not outline-view)
          (getf context selected?:)
        (cell-selected? (outline-column self) context)))
    
    (let* ((title (or (localize title) ""))
           (extent (get-text-extent surface title))
           (height (if (or (not title) (empty-string? title)) 13 (get-height extent)))
           (h 18)
           (v (quotient (- (get-height self) height) 2)))
      (let ((enabled? (view-enabled? self)))
        (draw-radio-button surface (new Rect 0 (+ v 1) box-size (+ v box-size 1)) selected?: selected? inactive?: (or (not enabled?) (and tracking? inside?)) radio-background: radio-background)
        (let* ((selected? (effective-selected?))
               (box-h 0)
               (box-v (quotient (- (get-height self) (if extent (get-height extent) box-size)) 2))
               (text-h (+ 18 box-h))
               (text-v box-v))
          (when selected?
            (let ((color (if (is? outline-view Tree-View) (selection-color outline-view) {Color Focused-Selection}))
                  (rect
                   (if (and (is? outline-view Tree-View) (eq? (get-selection-mode outline-view) 'cell))
                       (new Rect (- text-h 3) (- text-v 2) (get-width self) (+ text-v (get-height extent) 2))
                     (new Rect (- text-h 3) (- text-v 2) (+ text-h (get-width extent) 3) (+ text-v (get-height extent) 2)))))
              (fill-rect surface rect color)))
          (draw-text surface h v title (text-color self selected?) enabled?: enabled?)))))
  
  
  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-skin self)
    (find-skin 'ui:radio))
  
  
  (method (text-color self selected?)
    (let ((skin (get-skin self)))
      (or (get-text-color skin)
          (if enabled?
              (or (determine-color {Color Black} enabled? selected?)
                  {Color White})
            {Color Dark}))))


  ;;;
  ;;;; Focus
  ;;;


  (method override (focus-gained self)
    (nextmethod self)
    (when outline-view
      (focus-gained outline-view)))


  (method override (focus-lost self)
    (nextmethod self)
    (when outline-view
      (focus-lost outline-view)))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (key-press self evt)
    (case (get-key evt)
      ((#\space)
       (process-change-state self))
      (else
       (when outline-view
         (call-key-press outline-view evt)))))
  
  
  (method override (arrow-press self evt)
    (when outline-view
      (call-arrow-press outline-view evt)))
  
  
  (method override (return-press self evt)
    (when outline-view
      (call-return-press outline-view evt)))
  
  
  (method override (escape-press self evt)
    (when outline-view
      (call-escape-press outline-view evt)))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down self evt)
    (when outline-view
      (outline-select self))
    (when (and (view-enabled? self) editable? (or (not outline-view) (in-rect? (get-position evt) (box-rect self))))
      (set! tracking? #t)
      (set! inside? #t)
      (acquire-capture self)
      (invalidate-view self)))
  
  
  (method override (drag-move self evt)
    (when tracking?
      (let ((over? (over-view? self (get-position evt))))
        (when (neq? over? inside?)
          (set! inside? over?)
          (invalidate-view self)))))
  
  
  (method override (drag-up self evt . rest)
    (when tracking?
      (release-capture self)
      (set! tracking? #f)
      (when inside?
        (process-change-state self))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor self (context: context #f))
    (new jazz.ui.descriptors:Radio-Button-Descriptor))))
