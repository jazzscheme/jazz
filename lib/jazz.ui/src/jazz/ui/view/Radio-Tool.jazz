;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Radio Tools
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Radio-Tool jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.locale)
        (jazz.ui))


(class Radio-Tool extends Image-Tool implements (Groupable Selectable)


  (property group     initialize {} accessors explicit)
  (property selected? initialize #f accessors explicit)

  
  (method override (default-size)
    {Dimension 22 22})

  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (preferred-size)
    (if (not title)
        (default-size)
      (new Dimension (+ (text-width~ font (localize title)) 24) 21)))
  
  
  ;;;
  ;;;; Drawing
  ;;;


  (method override (draw-frame surface)
    (if (and selected? (memq? (get-state) '(inactive highlighted out-pushed)))
        (draw-pushed surface)
      (nextmethod surface)))


  (method override (draw-tool surface context)
    (let ((offset (if title 0 (if (or selected? (eq? (get-state) 'pushed)) 2 1))))
      (if (not title)
          (draw-image surface context offset offset)
        (let* ((title (localize title))
               (extent (get-text-extent~ surface title))
               (center (justify extent (get-size) '(center center))))
          (set-text-color~ surface {Color name: Black})
          (draw-text~ surface (+ offset (get-h~ center)) (+ offset (get-v~ center)) title enabled?)))))
  
  
  (method override (draw-background surface context)
    (if selected?
        (let ((bounds (get-bounds))
              (color (if title {Color name: White} {Color name: Light-Background})))
          (fill-rect~ surface bounds color))
      (nextmethod surface context)))
  
  
  ;;;
  ;;;; Mouse
  ;;;


  (method override (up)
    (when (and (eq? (get-state) 'pushed) (not selected?))
      (let ((pushed (find-pushed)))
        (when pushed
          (set-selected?~ pushed #f)))
      (set-selected? #t)
      (process-action self)))
  
  
  ;;;
  ;;;; Groupable
  ;;;


  (method override (get-group)
    group)


  (method override (set-group value)
    (set! group value))


  (method (radio-group)
    (collect-if (lambda (component)
                  (and (is? component Radio-Tool)
                       (eq? (get-group~ component) group)))
                (get-children~ parent)))


  (method (find-pushed)
    (find-if get-selected?~ (radio-group)))
  
  
  ;;;
  ;;;; Selectable
  ;;;


  (method override (get-selected?)
    selected?)
  

  (method override (set-selected? flag)
    (when (neq? flag selected?)
      (let ((current (find-pushed)))
        (set-selected-state flag)
        (when current
          (set-selected-state~ current (not flag))))))
  
  
  (method override (unselect)
    (when selected?
      (set-selected-state #f)))
  
  
  (method (set-selected-state flag)
    (set! selected? flag)
    (invalidate-view))))
