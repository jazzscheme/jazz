;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Scroller Views
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Scroller-View jazz


(import (jazz.graphic)
        (jazz.skin)
        (jazz.ui)
        (jazz.view))


(class Scroller-View extends Base-Container

  
  (property client   initialize #f accessors explicit)
  (property vscroll? initialize #f accessors generate)
  (property hscroll? initialize #f accessors generate)
  (property flat?    initialize #f accessors generate)
  
  
  (slot vscroll initialize #f getter generate)
  (slot hscroll initialize #f getter generate)
  
  
  (method override (default-name self)
    'scroller)
  
  
  (method override (finish self rest)
    (nextmethod self rest)
    (unless client
      (set-client self (get-scroll-client (get-content self)))))
  
  
  (method override (form-model self form)
    (case (get-name form)
      ((content) Scrollee-View)
      (else (nextmethod self form))))

  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method public (get-client self)
    client)
  
  
  (method public (set-client self clt)
    (when (neq? clt client)
      (set! client clt)
      (when client
        (invalidate-layout self)
        (set-scroller client self)
        (layout-scrollee client))))
  
  
  ;;;
  ;;;; Widget
  ;;;
  
  
  (method override (inner? self view)
    (or (nextmethod self view)
        (eq? view vscroll)
        (eq? view hscroll)))
  
  
  (method override (content-inner? self view)
    #t)

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (size-change self size)
    (nextmethod self size)
    ;; If the client is not the content as for tree headers, then let the
    ;; client container's own size-change take care of laying out the scrolle...
    (when (and client (eq? client (get-client (get-content self))))
      (layout-scrollee client)
      (layout-scrollee-patch client)))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout self)
    (let ((v? (vscroll-active? self))
          (h? (hscroll-active? self)))
      (define (content-position size)
        {Point 0 0})
      
      (define (content-size size)
        (new Dimension
          (- (get-width size) (if v? scrollbar-size 0))
          (- (get-height size) (if h? scrollbar-size 0))))
      
      (define (vscroll-position size)
        (new Point (- (get-width size) scrollbar-size) 0))
      
      (define (vscroll-size size)
        (new Dimension scrollbar-size (- (get-height size) (if h? scrollbar-size 0))))
      
      (define (hscroll-position size)
        (new Point 0 (- (get-height size) scrollbar-size)))
      
      (define (hscroll-size size)
        (new Dimension (- (get-width size) (if v? scrollbar-size 0)) scrollbar-size))
      
      (let ((size (get-size self)))
        (let ((content (find-child self 'content)))
          (when content
            (set-position content (content-position size))
            (set-size content (content-size size))
            (when client
              (layout-scrollee client))))
        (when (and v? vscroll)
          (set-position vscroll (vscroll-position size))
          (set-size vscroll (vscroll-size size)))
        (when (and h? hscroll)
          (set-position hscroll (hscroll-position size))
          (set-size hscroll (hscroll-size size))))))
  

  ;;;
  ;;;; Drawing
  ;;;

  
  (method override (draw self surface context)
    (when (and vscroll? hscroll?)
      (let ((skin (find-skin 'ui:scrollbar)))
        (unless (eq? (get-background skin) 'none)
          (let ((bounds (get-bounds self)))
            (let ((r (get-right bounds))
                  (b (get-bottom bounds)))
              (let ((rect (new Rect (- r scrollbar-size) (- b scrollbar-size) r b)))
                (fill-rect surface rect {Color Medium}))))))))

  
  ;;;
  ;;;; Scroll
  ;;;
  
  
  (method override (set-scroll-pos self pos (redraw?: redraw? #f))
    (when vscroll (set-pos vscroll (get-v pos) redraw?: redraw?))
    (when hscroll (set-pos hscroll (get-h pos) redraw?: redraw?)))
    
  
  (method override (set-scroll-info self part min max line page)
    (case part
      ((vert) (when vscroll (set-info vscroll min max line page)))
      ((horz) (when hscroll (set-info hscroll min max line page)))))


  (method override (scroll-action self action line page thumb-pos orientation)
    (define (scroll-by amount)
      (scroll client (case orientation
                        ((vert) (new Point 0 amount))
                        ((horz) (new Point amount 0)))
        redraw?: #t))
    
    (when client
      (case action
        ((line-up)     (scroll-by    line))
        ((line-down)   (scroll-by (- line)))
        ((page-up)     (scroll-by    page))
        ((page-down)   (scroll-by (- page)))
        ((thumb-track) (scroll-by (- (case orientation
                                       ((vert) (get-scrolled-distance-v client))
                                       ((horz) (get-scrolled-distance-h client)))
                                     thumb-pos))))))


  ;;;
  ;;;; Scrollbar
  ;;;
  
  
  (method (vscroll-active? self)
    (and vscroll (get-visible? vscroll)))
  
  
  (method public (set-vscroll-needed? self flag)
    (when (and vscroll? (neq? (boolean flag) (boolean (vscroll-active? self))))
      (if flag
          (if vscroll
              (show vscroll)
            (set! vscroll (new Scrollbar parent: self orientation: 'vert flat?: flat? style: style)))
        (when vscroll
          (hide vscroll)))
      (layout-view self)))
  
  
  (method (hscroll-active? self)
    (and hscroll (get-visible? hscroll)))
  
  
  (method public (set-hscroll-needed? self flag)
    (when (and hscroll? (neq? flag (boolean hscroll)))
      (if (not hscroll)
          (set! hscroll (new Scrollbar parent: self orientation: 'horz flat?: flat? style: style))
        (close hscroll)
        (set! hscroll #f))
      (layout-view self)))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor self (context: context #f))
    (new jazz.ui.descriptors:Scroller-View-Descriptor))))
