;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Scroller Views
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Scroller-View jazz


(import (jazz.designer)
        (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.ui)
        (jazz.ui.view))


(class Scroller-View extends Base-Container

  
  (property client   initialize #f accessors explicit)
  (property vscroll? initialize #f accessors generate)
  (property hscroll? initialize #f accessors generate)
  (property flat?    initialize #f accessors generate)
  (property style    initialize #f accessors generate)
  
  
  (slot vscroll initialize #f getter generate)
  (slot hscroll initialize #f getter generate)
  
  
  (method override (default-name)
    'scroller)
  
  
  (method override (finish rest)
    (nextmethod rest)
    (unless client
      (set-client (get-scroll-client~ (get-content)))))

  
  ;;;
  ;;;; Client
  ;;;
  
  
  (method public (get-client)
    client)
  
  
  (method public (set-client clt)
    (when (neq? clt client)
      (set! client clt)
      (when client
        (invalidate-layout)
        (set-scroller~ client self)
        (layout-scrollee~ client))))
  
  
  ;;;
  ;;;; Outer
  ;;;
  
  
  (method override (inner? view)
    (or (nextmethod view)
        (eq? view vscroll)
        (eq? view hscroll)))
  
  
  (method override (content-inner? view)
    #t)

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (size-change size)
    (nextmethod size)
    ;; If the client is not the content as for tree headers, then let the
    ;; client container's own size-change take care of laying out the scrolle...
    (when (and client (eq? client (get-client~ (get-content))))
      (layout-scrollee~ client)
      (layout-scrollee-patch~ client)))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout)
    (let ((v? (vscroll-active?))
          (h? (hscroll-active?)))
      (define (content-position size)
        {Point 0 0})
      
      (define (content-size size)
        (new Dimension
          (- (get-width~ size) (if v? scrollbar-size 0))
          (- (get-height~ size) (if h? scrollbar-size 0))))
      
      (define (vscroll-position size)
        (new Point (- (get-width~ size) scrollbar-size) 0))
      
      (define (vscroll-size size)
        (new Dimension scrollbar-size (- (get-height~ size) (if h? scrollbar-size 0))))
      
      (define (hscroll-position size)
        (new Point 0 (- (get-height~ size) scrollbar-size)))
      
      (define (hscroll-size size)
        (new Dimension (- (get-width~ size) (if v? scrollbar-size 0)) scrollbar-size))
      
      (let ((size (get-size)))
        (let ((content (find-child 'content)))
          (when content
            (set-position~ content (content-position size))
            (set-size~ content (content-size size))
            (when client
              (layout-scrollee~ client))))
        (when (and v? vscroll)
          (set-position~ vscroll (vscroll-position size))
          (set-size~ vscroll (vscroll-size size)))
        (when (and h? hscroll)
          (set-position~ hscroll (hscroll-position size))
          (set-size~ hscroll (hscroll-size size))))))
  

  ;;;
  ;;;; Drawing
  ;;;

  
  (method override (draw surface context lh lv)
    (when (and vscroll? hscroll?)
      (let ((bounds (get-bounds)))
        (let ((r (get-right~ bounds))
              (b (get-bottom~ bounds)))
          (let ((rect (new Rect (- r scrollbar-size) (- b scrollbar-size) r b)))
            (fill-rect~ surface rect {Color Medium}))))))

  
  ;;;
  ;;;; Scroll
  ;;;
  
  
  (method override (set-scroll-pos part pos (redraw?: redraw? #f))
    (case part
      ((vert) (when vscroll (set-pos~ vscroll pos redraw?: redraw?)))
      ((horz) (when hscroll (set-pos~ hscroll pos redraw?: redraw?)))))
    
  
  (method override (set-scroll-info part min max page)
    (case part
      ((vert) (when vscroll (set-info~ vscroll min max page)))
      ((horz) (when hscroll (set-info~ hscroll min max page)))))


  (method override (on-vscroll code thumb-pos orientation)
    (when client
      (case code
        ((line-up)     (scroll~ client (new Point 0    (get-line-size~ client orientation)) redraw?: #t))
        ((line-down)   (scroll~ client (new Point 0 (- (get-line-size~ client orientation))) redraw?: #t))
        ((page-up)     (scroll~ client (new Point 0    (get-page-size~ client orientation)) redraw?: #t))
        ((page-down)   (scroll~ client (new Point 0 (- (get-page-size~ client orientation))) redraw?: #t))
        ((thumb-track) (scroll~ client (new Point 0 (- (- (get-v~ (get-scrolled-position~ client))) thumb-pos)) redraw?: #t)))))


  (method override (on-hscroll code thumb-pos orientation)
    (when client
      (case code
        ((line-up)     (scroll~ client (new Point    (get-line-size~ client orientation)  0) redraw?: #t))
        ((line-down)   (scroll~ client (new Point (- (get-line-size~ client orientation)) 0) redraw?: #t))
        ((page-up)     (scroll~ client (new Point    (get-page-size~ client orientation)  0) redraw?: #t))
        ((page-down)   (scroll~ client (new Point (- (get-page-size~ client orientation)) 0) redraw?: #t))
        ((thumb-track) (scroll~ client (new Point (- (- (get-h~ (get-scrolled-position~ client))) thumb-pos) 0) redraw?: #t)))))


  ;;;
  ;;;; Scrollbar
  ;;;
  
  
  (method (vscroll-active?)
    (and vscroll (get-visible?~ vscroll)))
  
  
  (method public (set-vscroll-needed? flag)
    (when (and vscroll? (neq? (boolean flag) (boolean (vscroll-active?))))
      (if flag
          (if vscroll
              (show~ vscroll)
            (set! vscroll (new Scrollbar parent: self orientation: 'vert flat?: flat? style: style)))
        (when vscroll
          (hide~ vscroll)))
      (layout-view)))
  
  
  (method (hscroll-active?)
    (and hscroll (get-visible?~ hscroll)))
  
  
  (method public (set-hscroll-needed? flag)
    (when (and hscroll? (neq? flag (boolean hscroll)))
      (if (not hscroll)
          (set! hscroll (new Scrollbar parent: self orientation: 'horz flat?: flat? style: style))
        (close~ hscroll)
        (set! hscroll #f))
      (layout-view)))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Scroller-View-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Scroller-View-Descriptor extends Layout-View-Descriptor
  
  
  (form
    (<install>
      (<properties~>
        (<Property-Descriptor> name: client title: "Client")
        (<Property-Descriptor> name: vscroll? title: "Vscroll"
          (<Boolean-Domain>    name: domain))
        (<Property-Descriptor> name: hscroll? title: "Hscroll"
          (<Boolean-Domain>    name: domain))
        (<Property-Descriptor> name: flat? title: "Flat"
          (<Boolean-Domain>    name: domain))
        (<Property-Descriptor> name: style title: "Style")))))))
