;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Splitter View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.ui.view.Splitter-View jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.ui)
        (jazz.ui.window)
        (jazz.utilities))


(class Splitter-View extends View


  (property separator     initialize 'resize        accessors generate)
  (property state         initialize 'split         accessors generate)
  (property target        initialize {}             accessors generate)
  (property orientation   initialize 'horz          accessors generate)
  (property mode          initialize 'absolute      accessors generate)
  (property division      initialize 100            accessors generate)
  (property insets        initialize {Rect 0 0 0 0} accessors generate)
  (property full?         initialize #f             accessors generate)
  (property after-handler initialize {}             accessors generate)
  (property preferences   initialize {}             accessors generate)
  
  
  (slot split-layout     initialize (new Split-Layout))
  (slot bar-bounds       initialize {})
  (slot dragging?        initialize #f)
  (slot previous         initialize {})
  (slot preserved-insets initialize {} accessors generate)
    
  
  (form
    (<install> background: {Color name: Medium}))

  
  ;;;
  ;;;; Layout
  ;;;


  (method override (size-change size)
    (nextmethod size)
    (layout-view))

  
  (method override (layout)
    (unless (null? children)
      (let ((size (get-size))
            (mode (if dragging? 'absolute mode)))
        (set! bar-bounds (layout~ split-layout children (get-width~ size) (get-height~ size) separator: separator state: state target: target orientation: orientation mode: mode division: division insets: insets)))))

  
  ;;;
  ;;;; Children
  ;;;
  
  
  (method public (remove-children)
    (for-each (lambda (child)
                (when (is? child Splitter-View)
                  (remove-children~ child))
                (when (is? child Pad-View)
                  (remove-children~ child))
                (if (is? child Splitter-View)
                    (close~ child)
                  (set-visible?~ child #f)
                  (set-parent~ child {})
                  (when (is? child Host-View)
                    (host-uninstall~ (get-guest~ child)))))
              children))
  
  
  ;;;
  ;;;; Workspace
  ;;;
  
  
  (method public (workspace-zoomable? view)
    (define (get-brother view)
      (find-if (lambda (child)
                 (neq? child view))
               children))
    
    (let ((brother (get-brother view)))
      (or (is-not? brother Host-View)
          (let ((guest (get-guest~ brother)))
            (workspace-minimisable?~ guest)))))

  
  ;;;
  ;;;; Zoom
  ;;;
  
  
  (method public (zoom view)
    (set! state 'zoom)
    (set! target view)
    (layout-view))
    
  
  (method public (unzoom)
    (set! state 'split)
    (set! target {})
    (layout-view))
  
  
  ;;;
  ;;;; Cursor
  ;;;


  (method override (cursor-update pos)
    (if (and (eq? separator 'resize) bar-bounds (or dragging? (in-rect? pos bar-bounds)))
        (case orientation
          ((horz) (set-cursor :vertical-splitter))
          ((vert) (set-cursor :horizontal-splitter)))
      (nextmethod pos)))
  

  ;;;
  ;;;; Draw
  ;;;
  
  
  (method override (draw surface context lh lv)
    (when (and (eq? state 'split) bar-bounds)
      (case separator
        ((resize))
        ((edge)
         (case orientation
           ((horz) (draw-edge~ surface bar-bounds inner: 'raised outer: 'sunken edges: '(left)))
           ((vert) (draw-edge~ surface bar-bounds inner: 'raised outer: 'sunken edges: '(top)))))
        ((line)
         (case orientation
           ((horz) (draw-edge~ surface bar-bounds inner: 'raised edges: '(left)))
           ((vert) (draw-edge~ surface bar-bounds inner: 'raised edges: '(top))))))))
  
  
  ;;;
  ;;;; Convertion
  ;;;
  
  
  (method (absolute->relative division)
    (let ((size (case orientation
                  ((horz) (get-width))
                  ((vert) (get-height)))))
      (percentage division size)))
  
  
  (method (relative->absolute division)
    (let* ((size (case orientation
                   ((horz) (get-width))
                   ((vert) (get-height))))
           (pct (fxround (percent (abs division) size))))
      (if (positive? division)
          (set! division pct)
        (set! division (- size pct)))
      division))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down pos)
    (when (and (eq? separator 'resize) bar-bounds (in-rect? pos bar-bounds))
      (set! dragging? #t)
      (set! previous (case orientation ((horz) (get-h~ pos)) ((vert) (get-v~ pos))))
      (when (eq? mode 'relative)
        (set! division (relative->absolute division)))
      (acquire-capture)))
  
  
  (method override (drag-move pos)
    (when dragging?
      (let* ((n (case orientation ((horz) (get-h~ pos)) ((vert) (get-v~ pos))))
             (delta (- n previous)))
        (increase! division delta)
        (set! previous n)
        (layout-view)
        (update-view)
        (call-after-handler))))
  
  
  (method override (drag-up pos . rest)
    (when dragging?
      (release-capture)
      (when (eq? mode 'relative)
        (set! division (absolute->relative division)))
      (set! dragging? #f)))
 
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (call-after-handler)
    (when after-handler
      (invoke~ after-handler self (new Event after: self))))


  ;;;
  ;;;; Designer
  ;;;
  
  
  (method meta override (get-class-image)
    {Bitmap-Resource "TileVertically"})

  
  (method meta override (property-presentation property)
    (case (field-name property)
      ((separator) "Separator")
      ((state) "State")
      ((target) "Target")
      ((orientation) "Orientation")
      ((mode) "Mode")
      ((division) "Division")
      ((insets) "Insets")
      ((after-handler) "After Handler")
      (else (nextmethod property))))))
