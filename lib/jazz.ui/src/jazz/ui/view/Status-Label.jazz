;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Status Labels
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Status-Label jazz


(import (jazz.application)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.locale)
        (jazz.skin)
        (jazz.ui)
        (jazz.ui.skin)
        (jazz.view))


(class Status-Label extends View
    
  
  (property icon initialize #f accessors generate)
  
  
  (method override (mouse-activate? self)
    #f)
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method override (set-title self title)
    (unless (and (empty-string? title) (empty-string? self.title))
      (nextmethod self title)))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-up self evt)
    (process-action self self))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-background self surface context)
    (when background
      (let* ((bounds (get-bounds self))
             (rect (inflate-rect bounds -1 -1)))
        (fill-rect surface rect background))))
  
  
  (method override (draw self surface context)
    (draw-edge surface (get-bounds self) outer: 'sunken edges: 'rectangle)
    (draw-icon self surface context)
    (draw-body self surface))
  
  
  (method (draw-icon self surface context)
    (when icon
      (let ((appl (current-application))
            (left 2)
            (top 2))
        (draw-image (get-images-portfolio appl) surface context icon left top))))
  
  
  (method (draw-body self surface)
    (when title
      (let ((title (localize title))
            (left (if icon 20 3))
            (top 4)
            (color (or (get-text-color (get-skin self)) {Color Black})))
        (set-font surface {Font Caption})
        (draw-text surface left top title color))))
  
  
  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-skin self)
    (find-skin 'ui:statusbar))))
