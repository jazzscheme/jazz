;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Tab Sheet
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Tab-Sheet jazz


(import (jazz.graphic)
        (jazz.library)
        (jazz.ui))


(class Tab-Sheet extends Container


  (property title-color        initialize {} accessors explicit)
  (property header-color       initialize {} getter    generate   setter explicit)
  (property header-end-color   initialize {} accessors explicit)
  (property border-color       initialize {} getter    generate   setter explicit)
  (property closable?          initialize #f accessors generate)


  (slot protected borders                            accessors generate)
  (slot protected close-button-borders               accessors generate)
  (slot protected close-button-enter?  initialize #f accessors generate)
  (slot protected close-button-down?   initialize #f accessors generate)


  ;;;
  ;;;; Access
  ;;;
  
  
  (method override (set-title value)
    (nextmethod value)
    (update-borders~ parent))


  (method public (get-title-color)
    (if enabled?
        title-color
      {Color name: Dark}))


  (method public (set-title-color value)
    (set! title-color value)
    (invalidate-view~ parent))


  (method public (set-header-color value)
    (set! header-color value)
    (invalidate-view~ parent))


  (method public (get-header-end-color)
    (if header-end-color
        header-end-color
      (let ((backgound (get-background)))
        (if background
            background
          {Color name: Medium}))))


  (method public (set-header-end-color value)
    (set! header-end-color value)
    (invalidate-view~ parent))


  (method public (set-border-color value)
    (set! border-color value)
    (invalidate-view~ parent))


  ;;;
  ;;;; View
  ;;;

  
  (method override (get-transparent?)
    #t)
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout)
    (let ((content (find-child 'content)))
      (when content
        (let ((size (get-size)))
          (set-position~ content {Point 1 20})
          (set-size~ content (new Dimension (- (get-width~ size) 2) (- (get-height~ size) 21)))))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Tab-Sheet-Descriptor))


  ;;;
  ;;;; Close-button
  ;;;


  (method (is-close-button? pt)
    (let ((h (get-h~ pt))
          (v (get-v~ pt)))
      (bind (left right top buttom) (get-close-button-borders)
        (and (> h left) (< h right) (> v top) (< v buttom)))))


  (method (redraw-close-button)
    (bind (left right top bottom) (get-close-button-borders)
      (invalidate-rect (new Rect left top right bottom))))


  (method public (close-button-enter)
    (set-close-button-enter? #t)
    (redraw-close-button))


  (method public (close-button-leave)
    (set-close-button-enter? #f)
    (set-close-button-down? #f)
    (redraw-close-button))


  (method public (close-button-down)
    (set-close-button-down? #t)
    (redraw-close-button))


  (method public (close-button-up)
    (set-close-button-down? #f)
    (redraw-close-button))
  )


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Tab-Sheet-Descriptor extends Layout-View-Descriptor
  
  
  @too-many-icons!
  (method override (class-image class)
    {Image-Resource "TypeFolder"})
  
  
  (method override (class-property-presentation class property)
    (case (field-name property)
      ((title-color) "Title Color")
      (else (nextmethod class property))))
  
  
  (method override (component-get-default-property component)
    'title))))
