;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Tool Buttons
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Tool-Button jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.ui)
        (jazz.ui.skin)
        (jazz.ui.view))


(class Tool-Button extends Layout-View

  
  ;; inactive
  ;; highlighted
  ;; pushed
  ;; out-pushed
  (slot state initialize 'inactive)

  
  (method override (default-size)
    {Dimension 100 22})
  
  
  (method override (mouse-activate?)
    #f)
  
  
  ;;;
  ;;;; State
  ;;;
  
  
  (method public virtual (get-state)
    state)
  
  
  (method public virtual (set-state value)
    (set! state value)
    (invalidate-view))

  
  ;;;
  ;;;; Enabled
  ;;;
  
  
  (method override (set-enabled? flag)
    (when (not flag)
      (set! state 'inactive))
    (nextmethod flag))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-background surface context)
    (when (memq? (get-state) '(highlighted pushed))
      (let ((look (get-look)))
        (let ((highlight (get-highlight~ look)))
          (when highlight
            (fill-background~ surface (expand-rect (get-bounds) 1 1 -1 -1) highlight))))))


  (method override (draw surface context lh lv)
    (draw-frame surface)
    (draw-tool surface context))


  (method protected virtual (draw-frame surface)
    (case (get-state)
      ((inactive) (draw-inactive surface))
      ((highlighted) (draw-highlighted surface))
      ((pushed) (draw-pushed surface))
      ((out-pushed) (draw-out-pushed surface))))
  
  
  (method protected virtual (draw-inactive surface)
    )


  (method protected virtual (draw-highlighted surface)
    (draw-border surface (light-color) (dark-color)))


  (method protected virtual (draw-pushed surface)
    (draw-border surface (dark-color) (light-color)))
  
  
  (method protected virtual (light-color)
    {Color name: Light})
  
  
  (method protected virtual (dark-color)
    {Color name: Dark})


  (method protected virtual (draw-out-pushed surface)
    )


  (method (draw-border surface upper lower)
    (let ((rect (get-bounds)))
      (let ((left   0)
            (top    0)
            (right  (- (get-right~ rect) 1))
            (bottom (- (get-bottom~ rect) 1)))
        (set-color~ surface upper)
        (set-line-width~ surface 1)
        (move-to~ surface left bottom)
        (line-to~ surface left top)
        (line-to~ surface right top)
        (set-color~ surface lower)
        (move-to~ surface left bottom)
        (line-to-no-stroke~ surface right bottom)
        (line-to~ surface right top))))


  (method protected virtual (draw-tool surface context)
    )
  
  
  ;;;
  ;;;; Skin
  ;;;
  
  
  (method override (get-look)
    (find-look 'tool))
  
  
  ;;;
  ;;;; Mouse
  ;;;


  (method override (mouse-enter)
    (when (and (view-enabled?) (or (not mouse-down?) (eq? mouse-down-view self)))
      (case (get-state)
        ((inactive) (set-state 'highlighted))
        ((out-pushed) (set-state 'pushed)))))


  (method override (mouse-leave)
    (when (or (not mouse-down?) (eq? mouse-down-view self))
      (case (get-state)
        ((highlighted) (set-state 'inactive))
        ((pushed) (set-state 'out-pushed)))))


  (method override (mouse-down pos)
    (when (view-enabled?)
      (set! state 'pushed)
      (down)
      (invalidate-view)))


  (method public virtual (down)
    )


  (method override (drag-up pos . rest)
    (when (view-enabled?)
      (unwind-protect
          (up)
        (case (get-state)
          ((pushed) (set-state 'highlighted))
          ((out-pushed) (set-state 'inactive))))))


  (method public virtual (up)
    )))
