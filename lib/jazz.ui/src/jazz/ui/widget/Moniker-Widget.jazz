;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Moniker Widget
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.widget.Moniker-Widget jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.io)
        (jazz.io.protocol)
        (jazz.jml)
        (jazz.library)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view))


(class Moniker-Widget extends View
  

  (property mode                   initialize 'open getter get-mode       setter set-mode)
  (property protocol               initialize 'file accessors generate)
  (property parameters             initialize '()   accessors generate)
  (property display-logical?       initialize #f    accessors generate)
  (property display-protocol?      initialize #f    accessors generate)
  (property anchors                initialize {}    accessors generate)
  (property moniker-update-handler initialize {}    accessors generate)
  (property read-only?             initialize #f    getter get-read-only? setter set-read-only?)


  (form
    (<install>                                size: {Dimension 150 17}
      (<Scroller-View>         name: scroller hscroll?: #f vscroll?: #f
        (<!>                   name: content  layout-type: fill
          (<Moniker-Text-View> name: text     accepts-returns?: #f show-unfocused-selection?: #f focus-selects-all?: #t left-padding: 2 top-padding: 2 return-press-handler: {Event-Handler :form on-return-press} receive-files-handler: {Event-Handler :form on-receive-files})))
      (<Moniker-Button>        name: browse   background: {Color name: Medium} resource: {Image-Resource "Open"} portfolio: :images action-handler: {Event-Handler :form on-browse} receive-files-handler: {Event-Handler :form on-receive-files})))
  
  
  ;;;
  ;;;; Events
  ;;;
 
  
  (method (on-browse evt)
    (browse-moniker))
  
  
  (method override (on-return-press evt)
    (when return-press-handler
      (invoke~ return-press-handler self evt)))
  
  
  (method (on-receive-files evt)
    (let ((pos (get-property~ evt position:))
          (files (get-property~ evt files:)))
      (if receive-files-handler
          ;; rectify sender in event
          (invoke~ receive-files-handler self (new Event :receive-files self position: pos files: files))
        (let ((handler (get-protocol-handler protocol)))
          (if (and (= (length files) 1) (is? (car files) handler))
              (set-moniker (car files))
            (bell))))))
    
  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-mode)
    mode)


  (method public (set-mode value)
    (set! mode value)
    (let ((browse (locate 'browse)))
      (case mode
        ((open) (set-resource~ browse {Image-Resource "Open"}))
        ((save) (set-resource~ browse {Image-Resource "Save"})))))

  
  (method public (call-moniker-update)
    (if moniker-update-handler
        (invoke~ moniker-update-handler self (new Event :update self))
      (moniker-update)))

  
  (method public (get-text)
    (locate 'text))

  
  ;;;
  ;;;; Enabled
  ;;;
  
  
  (method override (set-enabled? flag)
    (set! enabled? flag)
    (set-enabled?~ (locate 'text) flag)
    (set-enabled?~ (locate 'browse) flag)
    (invalidate-view))

  
  ;;;
  ;;;; Outer
  ;;;
  
  
  (method override (inner? view)
    (memq? view children))
    
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (layout)
    (define (scroller-position size)
      {Point 0 0})
    
    (define (scroller-size size)
      (new Dimension (- (get-width~ size) 17) 19))
    
    (define (browse-position size)
      (new Point (- (get-width~ size) 17) 0))
    
    (define (browse-size size)
      {Dimension 17 17})
    
    (let ((size (get-size)))
      (set-position~ (locate 'scroller) (scroller-position size))
      (set-size~ (locate 'scroller) (scroller-size size))
      (set-position~ (locate 'browse) (browse-position size))
      (set-size~ (locate 'browse) (browse-size size))))
  
  
  ;;;
  ;;;; Browse
  ;;;
  
  
  (method public (browse-moniker)
    (define (get-moniker-safe)
      (catch (Parse-Error err
               (message-box (get-message~ err))
               (throw-cancel))
        (get-moniker)))
    
    (let* ((handler (get-protocol-handler protocol))
           (actual (get-moniker-safe))
           (arguments (augment mode: mode actual: actual parameters))
           (moniker (apply moniker-modal handler arguments)))
      (set-moniker moniker)
      (let ((text (get-text)))
        (acquire-focus~ text)
        (set-caret~ text (text-ending~ text))
        (ensure-displayed~ text force-horizontal: 'tail reposition?: #t))))
    
  
  ;;;
  ;;;; Content
  ;;;

  
  (method public (get-moniker)
    (define (parse-content content)
      (if (not (protocol-displayed?))
          (values (get-protocol-handler protocol) content)
        (bind (protocol . list) content
          (values (get-protocol-handler protocol) list))))
    
    (let ((string (get-string-content~ (get-text))))
      (unless (empty-string? string)
        (if (not (alias-displayed?))
            (let ((handler (get-protocol-handler protocol))
                  (parameters (tokenise-filename string)))
              (anchorize~ (moniker-new handler parameters) anchors: anchors))
          (let ((content (read-string-content string)))
            (receive (handler list) (parse-content content)
              (let ((moniker (moniker-new handler list)))
                (typecase moniker
                  ((Directory-Group) moniker)
                  ((Pathname) (anchorize~ moniker anchors: anchors))
                  (else moniker)))))))))
  
  
  (method public (set-moniker moniker (generate-event?: generate-event? #t))
    (set-string-content~ (get-text) (if (not moniker) "" (present-moniker moniker)))
    (when generate-event?
      (call-moniker-update)))
  
  
  (method protected virtual (moniker-update)
    )
  
  
  (method public (present-moniker moniker)
    (cond ((not moniker)
           "{}")
          ((not (alias-displayed?))
           (if (is? moniker Pathname)
               (parse~ moniker)
             (->string moniker)))
          ((not (protocol-displayed?))
           (present~ moniker))
          (else
           (format "{a}: {a}"
                   (get-protocol~ (class-of moniker))
                   (present~ moniker)))))
  
  
  (method (alias-displayed?)
    (if (not display-logical?)
        (display-aliases?~ (get-application))
      display-logical?))
  
  
  (method (protocol-displayed?)
    (if (not display-protocol?)
        (display-protocol?~ (get-application))
      display-protocol?))
  
  
  (method override (get-modified?)
    (get-modified?~ (get-controller~ (get-text))))
  
  
  (method override (set-modified? flag)
    (set-modified?~ (get-controller~ (get-text)) flag))
  
  
  ;;;
  ;;;; Read-Only
  ;;;


  (method public (get-read-only?)
    read-only?)


  (method public (set-read-only? value)
    (set! read-only? value)
    (set-editable?~ (get-text) (not read-only?))
    (set-enabled?~ (locate 'browse) (not read-only?)))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Moniker-Widget-Descriptor)))


;;;
;;;; Moniker-Button
;;;


(class Moniker-Button extends Push-Tool
  
  
  (method override (light-color)
    {Color name: Light-Background})
  
  
  (method override (dark-color)
    {Color name: Dark})
  
  
  (method override (draw-inactive surface)
    (let ((bounds (get-bounds)))
      (frame-rect~ surface bounds (if (get-enabled?) {Color name: Light} {Color name: Medium}))))
  
  
  (method override (draw-tool surface context)
    (draw-ellipsis surface))
  
  
  (method (draw-ellipsis surface)
    (let* ((size (get-size))
           (offset (get-offset))
           (title "...")
           (extent (get-text-extent~ surface title))
           (pos (justify extent size 'center)))
      (set-text-color~ surface {Color name: Black})
      (draw-text~ surface (+ (get-h~ pos) 1) (get-v~ pos) title enabled?)))
  
  
  (method (get-offset)
    (if (memq? (get-state) '(pushed choose))
        1
      0)))


(register-autoclass Moniker-Button)


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Moniker-Widget-Descriptor extends View-Descriptor
  
  
  (method override (class-property-presentation class property)
    (case (field-name property)
      ((mode) "Mode")
      ((protocol) "Protocol")
      ((parameters) "Parameters")
      ((display-logical?) "Display Logical")
      ((display-protocol?) "Display Protocol")
      ((anchors) "Anchors")
      ((moniker-update-handler) "Moniker Update Handler")
      (else (nextmethod class property)))))))
