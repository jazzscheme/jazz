;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; View Player
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.ui.window.View-Player jazz


(import (jazz.graphic)
        (jazz.platform)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window))
  

(class View-Player extends Window


  (property close-root? initialize #t getter get-close-root? setter set-close-root?)

  
  (slot root                    initialize {})
  (slot previously-focused-view initialize {} accessors generate)

  
  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (prepare-root)
    (destroy-mandatory))
  
  
  (method protected virtual (prepare-root)
    (set-root (new-root)))

  
  (method protected virtual (new-root)
    (new Host-View offscreen?: #t))
  
  
  (method override (find-to-modify name test)
    (if (eq? name 'root)
        (get-root)
      (nextmethod name test)))
  
  
  (method override (get-components)
    (cons root (get-children)))
  
  
  ;;;
  ;;;; Destruction
  ;;;
  
  
  (method override (destroy)
    (close-root)
    (nextmethod))
  
  
  (method protected virtual (close-root)
    (if close-root?
        (close~ root)
      (set-player~ root {}))
    (set! root {}))

  
  ;;;
  ;;;; Window
  ;;;


  (method override (erase-background surface)
    (if (and root (get-offscreen?~ root))
        processed
      (nextmethod surface)))

  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-close-root?)
    close-root?)


  (method public (set-close-root? value)
    (set! close-root? value))
  
  
  (method public (get-root)
    root)
  
  
  (method public (set-root value)
    (when (neq? value root)
      (set! root value)
      (set-name~ root 'root)
      (set-offscreen?~ root #t)
      (set-player~ root self)
      (layout-window)))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method override (size-change size)
    (nextmethod size)
    (layout-window))
  
  
  (method override (layout)
    (when root
      (set-position~ root {Point 0 0})
      (set-size~ root (get-size))))
  
  
  ;;;
  ;;;; Error
  ;;;
  
  
  (slot in-error? initialize #f)
  
  
  (method public (get-in-error?)
    in-error?)
  
  
  (method public (set-in-error? flag)
    (set! in-error? flag)
    (invalidate-window))
  
  
  (definition Error-Color
    {Color name: Dark-Red})
  
  
  (method (paint-in-error surface bounds)
    (fill-rect~ surface bounds Error-Color))
  
  
  ;;;
  ;;;; Paint
  ;;;
  
  
  (method override (paint region . platform-data)
    (with-platform-painting self platform-data
      (function (surface)
        (let ((clipper (if (is? region Rect) region (get-box~ region))))
          (setup-clipper~ surface clipper)
          (if in-error?
              (paint-in-error surface clipper)
            (set! in-error? #t)
            (paint~ root surface '() 0 0 0 0 1.0 #f)
            (set! in-error? #f))))))
  
  
  ;;;
  ;;;; Receive Files
  ;;;
  
  
  (method override (receive-files pos files)
    (unless in-error?
      (dispatch-receive-files~ root pos files))
    processed)
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down h v)
    (if in-error?
        (bell)
      (dispatch-mouse-down~ root h v))
    processed)
  
  
  (method override (mouse-move h v)
    (unless in-error?
      (dispatch-mouse-move~ root h v))
    processed)
  
  
  (method (simulate-mouse-move)
    (let ((pos (mouse-position)))
      (let ((view (find-view pos)))
        (when view
          (dispatch-mouse-move~ root (get-h~ pos) (get-v~ pos))))))
  
  
  (method override (mouse-up h v)
    (unless in-error?
      (dispatch-mouse-up~ root h v))
    processed)
  
  
  (method override (double-click h v)
    (unless in-error?
      (dispatch-double-click~ root h v))
    processed)
  
  
  (method override (context-menu h v)
    (if in-error?
        (set-in-error? #f)
      (dispatch-context-menu~ root h v))
    processed)
  
  
  (method override (middle-mouse-down h v)
    (unless in-error?
      (dispatch-middle-mouse-down~ root h v))
    processed)
  
  
  (method override (middle-mouse-up h v)
    (unless in-error?
      (dispatch-middle-mouse-up~ root h v))
    processed)
  
  
  (method override (right-mouse-down h v)
    (unless in-error?
      (dispatch-right-mouse-down~ root h v))
    processed)
  
  
  (method override (right-mouse-up h v)
    (unless in-error?
      (dispatch-right-mouse-up~ root h v))
    unprocessed)


  ;;;
  ;;;; Palette
  ;;;
  
  
  (method public (for-each-palette proc)
    (when root
      (for-each-palette~ root proc)))
  
  
  (method public (find-palette type)
    (when root
      (find-palette~ root type)))


  ;;;
  ;;;; Popup
  ;;;
  
  
  (method public (popup class . initargs)
    (let ((popup (new-popup class initargs)))
      (set-popups (cons popup (get-popups)))
      popup))


  (method (new-popup class initargs)
    (apply new class
      owner: (get-overlapped)
      visible?: #f
      initargs))
  
  
  (method public (close-popups (all?: all? #f) (exclude: exclude {}))
    (when (and (get-popups) (not (get-passing-through?)))
      (let* ((overlapped (get-overlapped))
             (owners (get-owners~ overlapped)))
        (for-each (function (popup)
                    (when (and (or all? (not (memq? popup owners))) (or (not exclude) (not (memq? popup exclude))))
                      (close-popup popup)))
                  (get-popups)))))
  

  ;;;
  ;;;; Focus
  ;;;
  
  
  (method override (focus-gain)
    (set-focused-window self)
    (when previously-focused-view
      (acquire-focus~ previously-focused-view))
    processed)
  
  
  (method override (focus-lose receiving)
    (let ((focus (get-focus)))
      (when (and focus (eq? (get-player~ focus) self))
        (set! previously-focused-view focus)
        (set-focus {})))
    (set-focused-window {})
    processed)
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (key-press char)
    (when (or (not Key-Hook)
              (not (Key-Hook char)))
      (if (null? (get-popups))
          (dispatch-key-press~ root char)
        (unless (popup-key-press~ (car (get-popups)) char)
          (close-popups)
          (dispatch-key-press~ root char))))
    processed)))
