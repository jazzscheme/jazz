;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Window
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Assem Hakmeh
;;;    Jeremie Lasalle Ratelle
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.ui.window.Window jazz


(import (jazz.graphic)
        (jazz.locale)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (jazz.utilities))


(class Window extends Figure
 

  (property title initialize {} getter get-title setter set-title)
  

  (slot handle initialize {})


  ;;;
  ;;;; Initialization
  ;;;


  (method override (initially-visible?)
    #f)
  
  
  (method (setup-handle handle)
    (set-window handle self)
    (set-handle handle))


  (method override (default-parent)
    (window-default-parent self))


  (method override (prepare rest)
    (nextmethod rest)
    (setup-handle (prepare-handle self rest)))
  
  
  (method override (conclude rest)
    (nextmethod rest)
    (size-change (get-size)))
  
  
  (method public (open?)
    (boolean handle))


  (method override (show)
    (nextmethod)
    (assert handle)
    (show-window handle))


  (method override (hide)
    (nextmethod)
    (assert handle)
    (hide-window handle))
  
  
  (method public (minimize)
    (assert handle)
    (minimize-window handle))
  
  
  (method public (set-state state)
    (assert handle)
    (case state
      ((minimized) (minimize-window handle))
      ((maximized) (maximize-window handle))
      ((restored) (restore-window handle))
      (else (error "Unknown window state: {s}" state))))


  (method override (close)
    (set-visible? #f)
    (destroy-component)
    (destroy-window handle)
    (set-window handle {})
    (set-handle {}))

  
  (method override (print printer readably)
    (print-unreadable self printer
      (function (printer)
        (if (not handle)
            (format printer "orphaned")
          (let ((position (get-position))
                (size (get-size)))
            (if (or (not title) (empty-string? title))
                (format printer "{a} {a}" position size)
              (format printer "{a} {a} {a}" title position size)))))))
  
  
  ;;;
  ;;;; Component
  ;;;


  (method override (add-child child)
    (set! children (cons child children)))
  
  
  (method override (add-form parent-form child-form)
    (add-child~ parent-form child-form))


  ;;;
  ;;;; Unprocessed
  ;;;
  
  
  (method protected virtual (unprocessed-message)
    unprocessed)


  ;;;
  ;;;; Paint
  ;;;


  (method protected virtual (erase-background handle)
    (assert handle)
    (platform-erase-background handle))
  

  (method protected virtual (paint region . platform-data)
    (paint-window platform-data))
  
  
  (method protected (paint-window platform-data)
    (with-platform-painting self platform-data
      (function (surface)
        (draw surface))))
  
  
  (definition *colors*
    '({Color name: Dark-Red}
      {Color name: Dark-Orange}
      {Color name: Dark-Yellow}
      {Color name: Dark-Green}
      {Color name: Dark-Blue}
      {Color name: Dark-Purple}))
  
  (definition *nb-colors*
    (length *colors*))
  
  (definition *current-color*
    0)
  

  (method (draw-placeholder surface)
    (set-color~ surface (cycle-debug-color!))
    (paint~ surface))
  

  (method protected virtual (draw surface)
    )


  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method protected virtual (mouse-wheel h v delta)
    (let ((view (get-mouse-in)))
      (if view
          (let ((acquired #f))
            (mouse-wheel~ view acquired delta)
            processed)
        unprocessed)))

  
  (method protected virtual (mouse-down h v)
    unprocessed)
  
  
  (method protected virtual (mouse-move h v)
    unprocessed)
  
  
  (method protected virtual (mouse-up h v)
    unprocessed)
  
  
  (method protected virtual (double-click h v)
    (mouse-down h v))
  
  
  (method protected virtual (middle-mouse-down h v)
    unprocessed)
  
  
  (method protected virtual (middle-mouse-up h v)
    unprocessed)
  
  
  (method protected virtual (right-mouse-down h v)
    unprocessed)
  
  
  (method protected virtual (right-mouse-up h v)
    unprocessed)
  
  
  ;; tofix : window-specific
  (method (call-context-menu h v)
    (let ((pos (acquire (get-desktop) (new Point h v))))
      (context-menu (get-h~ pos) (get-v~ pos))))
  
  
  (method protected virtual (context-menu h v)
    unprocessed)


  ;;;
  ;;;; Capture
  ;;;
  
  
  (method public (acquire-capture)
    (assert handle)
    (set-capture handle))
  
  
  (method public (release-capture)
    (unset-capture))


  ;;;
  ;;;; Cursor
  ;;;

  
  (method protected virtual (cursor-update hit)
    (if hit
        processed
      unprocessed))


  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method (system-key-down)
    (update-cursor)
    processed)
  
  
  (method (system-key-up)
    (update-cursor)
    processed)
    
  
  (method (key-down wparam)
    unprocessed)
  
  
  (method (key-up wparam)
    unprocessed)
  
  
  (method (call-key-press char)
    (key-press char))


  (method public virtual (key-press char)
    unprocessed)


  ;;;
  ;;;; Timer
  ;;;
  
  
  (method protected virtual (timer-pulse)
    (thread-yield!))


  ;;;
  ;;;; Handlers
  ;;;
  
  
  (method public (call-position-change h v)
    (position-change (new Point h v)))
  
  
  (method protected virtual (position-change position)
    unprocessed)


  (method public (call-size-change width height)
    (if (get-installed?)
        (size-change (new Dimension width height))
      unprocessed))


  (method protected virtual (size-change size)
    unprocessed)
  
  
  (method protected virtual (mouse-activate?)
    #f)


  (method protected virtual (on-mouse-activate)
    unprocessed)


  ;; when a toplevel window (including palettes) is activated or deactivated
  (method protected virtual (on-activate kind)
    (if (eq? kind 'inactive)
        (on-deactivate-palette)
      (on-activate-palette))
    unprocessed)

  
  (method protected virtual (on-activate-palette)
    )
  
  
  (method protected virtual (on-deactivate-palette)
    )


  ;; Called when a toplevel window (including palettes)
  ;; is activated or deactivated by a change of the active application
  (method protected virtual (activate-app status)
    unprocessed)


  (method protected virtual (on-close evt)
    (close-with-confirm))
  
  
  (method public virtual (close-with-confirm)
    (close))


  (method protected virtual (focus-gain)
    unprocessed)


  (method protected virtual (focus-lose receiving)
    unprocessed)
  
  
  ;;;
  ;;;; Drop Files
  ;;;
  
  
  (method (drop-files wparam)
    (platform-drop-files self wparam))
  
  
  (method protected virtual (receive-files pos files)
    )
  
  
  ;;;
  ;;;; Fade
  ;;;
  
  
  ;; tofix : this will need a complete rethinking for multi-platform...
  ;; should use cairo...
  ;; also the menubar should only fade-in the first menu so that wiggling
  ;; between all the menus is zero delay after the first one poped
  (method public (fade-in (fade-speed: fade-speed {}))
    @w
    (set-alpha-blending handle 0)
    (set-visible? #t)
    @w(
    (update-window)
    (let* ((max 255)
           (delay 0.15)
           (now (time->seconds (current-time)))
           (start now))
      (while (< now (+ start delay))
        (set-alpha-blending handle (fxceiling (* (- now start) (fl/ max delay))))
        (set! now (time->seconds (current-time))))
      (set-alpha-blending handle max))
    (set-layered? handle #f)))
  
  
  (method public (fade-out (to: to 0) (step: step 5))
    @need-to-get-layered-attributes
    (let ((alpha))
      (loop (for n from 0 to to by step)
            (set-alpha-blending handle n)
            (set! alpha n))
      (unless (= alpha to)
        (set-alpha-blending handle to))))


  ;;;
  ;;;; Management
  ;;;


  (method public (get-handle)
    handle)


  (method public (set-handle hw <any>)
    (set! handle hw))
  
  
  (method public (opened?)
    (boolean handle))


  (method override (set-parent parent)
    (nextmethod parent)
    (assert handle)
    (platform-set-parent handle parent))


  (method public (get-frame)
    (if (is? self Frame)
        self
      (when parent
        (get-frame~ parent))))
  
  
  (method public (get-overlapped)
    (if (is? self Overlapped)
        self
      (get-overlapped~ parent)))
  

  (method public virtual (get-title)
    title)
  

  (method public virtual (set-title value)
    (set! title value)
    (assert handle)
    (set-window-title handle (localize title)))
  

  (method public (get-window-title)
    (assert handle)
    (get-window-handle-title handle))
  

  (method public (acquire window <Window> shape)
    (assert handle)
    (typecase shape
      ((Point)
       (acquire-point handle (get-handle~ window) shape))
      ((Rect)
       (acquire-rect handle (get-handle~ window) shape))
      (else
        (error "Unable to translate {t}" shape))))
  
  
  (method public (get-position-in-root) <Point>
    (assert handle)
    (platform-translate-to-root handle (get-position)))
  
  
  (method override (get-position) <Point>
    (assert handle)
    (platform-get-position handle #f))
  
  
  (method override (set-position pos <Point>) <void>
    (assert handle)
    (unless (nu=? pos (get-position))
      (platform-set-position handle pos)))
  
  
  (method public (move-window dh <fx> dv <fx>)
    (let ((pos (get-position)))
      (set-position (new Point (+ (get-h~ pos) dh) (+ (get-v~ pos) dv)))))
  
  
  (method override (get-size) <Dimension>
    (assert handle)
    (platform-get-size handle))
  
  
  (method override (set-size new-size <Dimension>) <void>
    (assert handle)
    (let ((delta (nu- new-size (platform-get-size handle))))
      (unless (nu=? delta {Dimension 0 0})
        (set-window-size (nu+ (get-window-size) delta)))))
  
  
  (method public (size-window dwidth <fx> dheight <fx>)
    (let ((size (get-size)))
      (set-size (new Dimension (+ (get-width~ size) dwidth) (+ (get-height~ size) dheight)))))
  
  
  (method public (get-window-size) <Dimension>
    (assert handle)
    (platform-get-window-size handle))


  (method public (set-window-size size)
    (assert handle)
    (platform-set-window-size handle size))
  
  
  (method public (set-bounds rect)
    (assert handle)
    (platform-set-bounds handle rect))
  
  
  (method public (grow-bounds dh dv dwidth dheight)
    (let ((pos (+ (get-position) (new Point dh dv)))
          (size (+ (get-window-size) (new Dimension dwidth dheight))))
      (set-window-size size)
      (set-position pos)))


  (method public (get-width) <fx>
    (get-width~ (get-size)))

  
  (method public (get-height) <fx>
    (get-height~ (get-size)))
  

  (method public (get-rect) <Rect>
    (assert handle)
    (platform-get-rect handle (if (get-parent) (get-handle~ (get-parent)) #f)))
  

  (method public (get-bounds) <Rect>
    (assert handle)
    (platform-get-bounds handle))

  
  (method public (ensure-displayed)
    (let ((outer (if (not parent) (get-size~ (get-desktop)) (get-size~ parent)))
          (rect (get-rect)))
      (receive (new-position new-size) (fit-within outer rect padding: 8)
        (set-size new-size)
        (set-position new-position))))


  (method public (mouse-position) <Point>
    (acquire (get-desktop) (platform-mouse-position)))


  (method public (mouse-window)
    (find-window (mouse-position)))


  (method public (mouse-in-window?)
    (point-in-window? (mouse-position)))


  (method public (point-in-window? point)
    (let ((size (get-window-size)))
      (and (in-interval? (get-h~ point) 0 (get-width~ size))
           (in-interval? (get-v~ point) 0 (get-height~ size)))))


  (method public (find-window point)
    (get-window (platform-window-from-point point)))


  (method public (flash-window)
    (unimplemented 'flash-window)
    @FlashWindow
    (loop (repeat 3)
          (FlashWindow handle #t)
          (sleep .1)
          (FlashWindow handle #f)
          (sleep .1)))


  (method public (move x y cx cy)
    (assert handle)
    (platform-move-window handle x y cx cy))


  (method public (acquire-focus)
    (set-window-focus self))
  
  
  (method public (get-enabled?)
    (assert handle)
    (platform-window-enabled? handle))
  
  
  (method public (enable-window)
    (assert handle)
    (platform-enable-window handle))
  
  
  (method public (disable-window)
    (assert handle)
    (platform-disable-window handle))


  (method public (get-client-rect)
    (assert handle)
    (platform-get-client-rect handle))


  (method public (scroll-window dh dv rect (clip-rect: clip-rect {}))
    (assert handle)
    (platform-scroll-window handle dh dv rect clip-rect))

  
  (method public (invalidate-rect rect)
    (assert handle)
    (platform-invalidate-rect handle rect))

  
  (method public (invalidate-region region)
    (assert handle)
    (platform-invalidate-region handle region))

  
  (method public (invalidate-window)
    (assert handle)
    (platform-invalidate-window handle))

  
  (method public (redraw-window (rect: rect {}) (region: region {}))
    (assert handle)
    (if (or rect region)
        (platform-redraw-window handle rect region)
      (platform-redraw-window handle (get-client-rect) {})))


  (method public (invalidate-border (insets: insets {Rect 1 1 1 1}))
    (with-closed ((region (border-region insets: insets)))
      (invalidate-region region)))


  (method public (redraw-border (insets: insets {Rect 1 1 1 1}))
    (with-closed ((region (border-region insets: insets)))
      (redraw-window region: region)))


  (method public (border-region (insets: insets {Rect 1 1 1 1}))
    (frame-region (get-client-rect) insets: insets))


  (method public (update-window)
    (assert handle)
    (platform-update-window handle))
  
  
  (method public (show-noactivate)
    (assert handle)
    (platform-show-noactivate handle))
  
  
  (method public (hide-noactivate)
    (assert handle)
    (platform-hide-noactivate handle))


  ;;;
  ;;;; Children
  ;;;


  (method public (first-window)
    (let ((windows (get-windows)))
      (if (null? windows)
          #f
        (first windows))))
  
  
  (method public (get-windows (include-invisibles?: include-invisibles? #f))
    (collect-if (function (child)
                  (and (is? child Window)
                       (or include-invisibles? (get-visible?~ child))))
                (get-children)))


  (method public (front-window?)
    (eq? self (first-window~ parent)))


  ;;;
  ;;;; Layer
  ;;;
  
  
  (method override (bring-to-front)
    (nextmethod)
    (assert handle)
    (platform-bring-window-to-front handle))


  (method override (send-to-back)
    (nextmethod)
    (assert handle)
    (platform-send-window-to-back handle))
    

  ;;;
  ;;;; Layout
  ;;;


  (method override (get-location)
    location)


  (method override (set-location loc)
    (set! location loc))
  
  
  (method public (layout-window)
    (layout))
  
  
  (method public virtual (layout)
    )
  
  
  ;;;
  ;;;; View
  ;;;
  
  
  (method public (mouse-view)
    (find-view (mouse-position)))
  
  
  (method public (find-view pos)
    (let ((window (find-window pos)))
      (when (is? window View-Player)
        (let ((root (get-root~ window)))
          (find-drawing~ root (acquire~ window self pos))))))
  
  
  (method public (to-view shape view)
    (let* ((player (get-player~ view))
           (acquired (acquire~ player self shape)))
      (acquire~ view (get-root~ player) acquired)))


  ;;;
  ;;;; Debugging
  ;;;
  
  
  (method public virtual (debug-window pos)
    (unimplemented 'debug-window)
    @Development
    (set-%~ Development self))


  (method public virtual (describe-window pos)
    (unimplemented 'describe-window)
    @WINSTYLE(
    (format :console "{%}")
    (format :console "Class:            {s}{%}" (class-of self))
    (format :console "Name:             {s}{%}" (get-name))
    (format :console "Class Background: {s}{%}" (get-class-background))
    (format :console "Clip Children?:   {s}{%}" (mask-bit-set? (get-style) WS_CLIPCHILDREN))
    (format :console "Clip Siblings?:   {s}{%}" (mask-bit-set? (get-style) WS_CLIPSIBLINGS))
    (format :console "Position:         {s}{%}" (get-position))
    (format :console "Size:             {s}{%}" (get-size))
    (format :console "Window rect:      {s}{%}" (get-bounds))
    (format :console "Client rect:      {s}{%}" (get-client-rect))
    (format :console "Style:            {s}{%}" (bits->symbolic (get-style) WINSTYLE))
    (format :console "Extended Style:   {s}{%}" (bits->symbolic (get-ex-style) WINEXSTYLE))))))
