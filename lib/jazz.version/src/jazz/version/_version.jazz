;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Jazz Version
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module jazz.version jazz


(hub ->64-bit-integer)
(hub compatible?)
(hub get-build)
(hub get-major)
(hub get-minor)
(hub get-revision)
(hub get-stage)
(hub present)
(hub present-string)


(definition package (construct-version major minor (revision 0) (build 0) (stage #f))
  (new Version major minor revision build stage))


;;;
;;;; Version
;;;


(class Version extends Object
  
  
  (method meta override (marshall-object self object)
    (serialize-object (class-of object)
                      (vector (serialize (get-major object))
                              (serialize (get-minor object))
                              (serialize (get-revision object))
                              (serialize (get-build object))
                              (serialize (get-stage object)))))
  
  
  (method meta override (unmarshall-object self content)
    (bind-vector (major minor revision build stage) content
      (allocate self
                (deserialize major)
                (deserialize minor)
                (deserialize revision)
                (deserialize build)
                (deserialize stage))))
  
  
  (slot major    getter generate)
  (slot minor    getter generate)
  (slot revision getter generate)
  (slot build    getter generate)
  (slot stage    getter generate)
    
    
  (method override (initialize self major minor (revision 0) (build 0) (stage #f))
    (nextmethod self)
    (set! self.major major)
    (set! self.minor minor)
    (set! self.revision revision)
    (set! self.build build)
    (set! self.stage stage))
  
  
  (method override (compare-object self target)
    (if (is-not? target Version)
        :incompatible
      (let ((try
             (lambda (x y)
               (cond ((< x y) :less)
                     ((> x y) :greater)
                     (else #f)))))
        (or (try major (get-major target))
            (try minor (get-minor target))
            (try revision (get-revision target))
            (try build (get-build target))
            :equal))))
  
  
  (method override (print self output readably)
    (format output "~{{a} {a}}"
            (category-name (class-of self))
            (present-string self)))
  
  
  (method package (present-string self)
    (format "{a} {a}{?: {a}~}{?: {a}~}{?: {a}~}"
            major
            minor
            (or (/= build 0) (/= revision 0)) revision
            (/= build 0) build
            stage stage))
  
  
  (method public (present self)
    (format "{a}.{a}{a}{a}{a}"
            major
            minor
            (if (and (= build 0) (= revision 0))
                ""
              (format ".{a}" revision))
            (if (= build 0)
                ""
              (format ".{a}" build))
            (if (not stage)
                ""
              (format " {a}" stage))))
  
  
  (method public (compatible? self version)
    (and (= major (get-major version))
         (= minor (get-minor version))))
  
  
  (method public (->64-bit-integer self)
    (+ (* major (arithmetic-shift 1 48))
       (* minor (arithmetic-shift 1 32))
       (* revision (arithmetic-shift 1 16))
       build)))


;;;
;;;; Utility
;;;


(definition public (jazz-version)
  (receive (major minor revision) (split-version kernel-version)
    (new Version major minor revision)))


(definition public (64-bit-integer->version version)
  (let ((major (arithmetic-shift version -48))
        (minor (modulo (arithmetic-shift version -32) 65536))
        (revision (modulo (arithmetic-shift version -16) 65536))
        (build (modulo version 65536)))
    (new Version major minor revision build))))
