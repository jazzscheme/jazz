;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Language
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.language.runtime.exception jazz.dialect


(proclaim (warn optimizations))


(import (jazz.language.runtime.kernel)
        (jazz.language.runtime.format)
        (jazz.language.runtime.functional)
        (jazz.language.runtime.geometry)
        (jazz.language.runtime.object))


(declare (proper-tail-calls))


;;;
;;;; Exception
;;;


(definition *exception-debugger*
  #f)


(definition public (get-exception-debugger)
  *exception-debugger*)

(definition public (set-exception-debugger exception-debugger)
  (set! *exception-debugger* exception-debugger))


(definition public current-exception-debugger
  (make-parameter #f))


(definition package (exception-debugger-hook exc other)
  (let ((exception-debugger (active-exception-debugger)))
    (if exception-debugger
        (exception-debugger exc)
      (system-exception-hook exc other))))


(definition public (with-exception-debugger exception-debugger thunk)
  (parameterize ((current-exception-debugger exception-debugger))
    (thunk)))


(definition public (active-exception-debugger)
  (or (current-exception-debugger) *exception-debugger*))


(definition public (system-exception-debugger exc)
  (invoke-exception-hook system-exception-hook exc))


(definition public (with-system-exception-debugger thunk)
  (with-exception-debugger system-exception-debugger
    thunk))


(class Exception extends Object
  
  
  (method core virtual (present-exception self))
  (method core virtual (exception-message self))
  (method core virtual (get-detail self))
  
  
  (method public virtual (get-message self)
    #f)
  
  
  (method public virtual (present-message self . rest)
    (list (get-message self)))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{?{s}~}" (get-message self))))))


(definition public (exception? obj)
  (and (object? obj)
       (is? obj Exception)))


(class package Exception-Detail-Class extends Class
  
  
  (method override (marshall-object self object)
    (serialize-object (class-of object)
                      (vector (get-icon object)
                              (get-title object)
                              (get-location object)
                              (get-children object))))
  
  
  (method override (unmarshall-object self content)
    (allocate Exception-Detail
              (vector-ref content 0)
              (vector-ref content 1)
              (vector-ref content 2)
              (vector-ref content 3))))


(class package Exception-Detail metaclass Exception-Detail-Class extends Object
  
  
  (slot icon     getter generate)
  (slot title    getter generate)
  (slot location getter generate)
  (slot children getter generate)
  
  
  (method override (initialize self icon title location children)
    (nextmethod self)
    (set! self.icon icon)
    (set! self.title title)
    (set! self.location location)
    (set! self.children children))
  
  
  (method override (print self output readably)
    (format output "~{{a} {s} {s} {s} {s}}"
            (category-name (class-of self))
            icon
            title
            location
            children)))


(class System-Exception extends Exception
  
  
  (slot exception getter generate)
  
  
  (method override (initialize self exception)
    (nextmethod self)
    (set! self.exception exception)))


(definition public (wrap-exception obj)
  (if (exception? obj)
      obj
    (new System-Exception obj)))


(class Break extends Exception
  
  
  (slot continue)
  
  
  (method override (initialize self continue)
    (nextmethod self)
    (set! self.continue continue)))


(class Continue extends Exception)


(class Error extends Exception
  
  
  (slot message setter generate getter (override generate))
  
  
  (method override (initialize self (message: message #f))
    (nextmethod self)
    (set-message self message)))


(definition public (error? obj)
  (and (object? obj)
       (is? obj Error)))


(class package Read-Error extends Error
  
  
  (slot source getter generate)
  (slot start  getter generate)
  (slot end    getter generate)
  
  
  (method override (initialize self . rest)
    (bind-keywords ((source #f) (start #f) (end #f) . others) rest
      (apply nextmethod self others)
      (set! self.source source)
      (set! self.start start)
      (set! self.end end)))
  
  
  (method public (get-range self)
    (new Range start end)))


(class package Parse-Error extends Error
  
  
  (slot source getter generate)
  (slot start  getter generate)
  (slot end    getter generate)
  
  
  (method override (initialize self . rest)
    (bind-keywords ((source #f) (start #f) (end #f) . others) rest
      (apply nextmethod self others)
      (set! self.source source)
      (set! self.start start)
      (set! self.end end))))


(class package Conversion-Error extends Parse-Error
  
  
  (slot object getter generate)
  (slot type   getter generate)
  
  
  (method override (initialize self . rest)
    (bind-keywords ((object #f) (type #f) . others) rest
      (apply nextmethod self others)
      (set! self.object object)
      (set! self.type type)))
  
  
  (method override (get-message self)
    (let ((type (if (string? type) type (category-identifier type))))
      (format "Unable to convert \"{a}\" into {a} {a}"
              object
              (format-gender type)
              type))))


(class Source-Error extends Error
  
  
  (slot source getter generate)
  (slot start  getter generate)
  (slot end    getter generate)
  
  
  (method override (initialize self source . rest)
    (bind-keywords ((start #f) (end #f) . others) rest
      (apply nextmethod self others)
      (set! self.source source)
      (set! self.start start)
      (set! self.end end)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-range self)
    (new Range$Cell$ start end)))


(class Signal extends Exception)


(class package Cancel-Signal extends Signal)


(definition public (call-with-catch predicate/type catcher thunk)
  (catch-exception-filter
    (lambda (obj)
      (if (procedure? predicate/type)
          (predicate/type obj)
        ;; we have to be very carefull as we can
        ;; potentially receive system exceptions
        (and (object? obj)
             (is? obj predicate/type))))
    catcher
    thunk))


;;;
;;;; Context
;;;


(definition exception-contexts
  (make-table test: eq? weak-keys: #t))


(definition public (get-exception-context exception)
  (table-ref exception-contexts exception #f))


(definition public (set-exception-context exception continuation)
  (table-set! exception-contexts exception continuation))


(definition public (capture-exception-context exception)
  (continuation-capture
    (lambda (cont)
      (set-exception-context exception cont))))


;;;
;;;; Control
;;;


(definition public (throw exception)
  (raise exception))


(definition public (error . rest)
  (throw (new Error message: (apply format rest))))


(definition public (disabled-functionality)
  (error "This functionality is temporarily disabled"))


(definition public (must-implement definition-name (what #f))
  (error "The {a} definition must be implemented for {a} to work correctly"
         definition-name
         (or what "this application")))


(definition public (warn string . rest)
  (format :console "{%}Warning: ")
  (apply format :console string rest))


(definition public (warn-unimplemented name)
  (format :console "{%}Warning: {a} is unimplemented" name))


(definition public (signal signal)
  (raise signal))


(definition public inline (signal-cancel)
  (signal (new Cancel-Signal)))


;;;
;;;; Restart
;;;


(class Restart extends Object
  
  
  (slot name    getter generate)
  (slot message getter generate)
  (slot handler getter generate)
  
  
  (method override (initialize self name message handler)
    (nextmethod self)
    (set! self.name name)
    (set! self.message message)
    (set! self.handler handler))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}"
                name)))))


(definition public current-restarts
  (make-parameter '()))


(definition public (with-restart-handler name message handler thunk)
  (parameterize ((current-restarts (cons (new Restart name message handler) (current-restarts))))
    (thunk)))


(definition public (with-restart-catcher name message thunk)
  (continuation-capture
    (lambda (catcher-cont)
      (with-restart-handler name message
        (lambda rest
          (continuation-return catcher-cont
            (if (not-null? rest)
                (car rest)
              #f)))
        thunk))))


(definition public (find-restart name)
  (find-if (lambda (restart)
             (eq? (get-name restart) name))
           (current-restarts)))


(definition public (find-restarts name)
  (collect-if (lambda (restart)
                (eq? (get-name restart) name))
              (current-restarts)))


(definition public (invoke-restart restart . rest)
  (let ((handler (get-handler restart)))
    (apply handler rest)))


;;;
;;;; Thread
;;;


(definition public (new-system-thread thunk name . rest)
  (apply new-thread
         (lambda ()
           (with-system-exception-debugger
             thunk))
         name
         rest)))
