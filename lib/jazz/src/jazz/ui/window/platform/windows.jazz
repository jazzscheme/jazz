;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Platform-dependant Window Definitions (Windows)
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Assem Hakmeh
;;;    Jeremie Lasalle Ratelle
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.window.platform.windows jazz


(export (jazz.ui.window.platform.independant))


(import (jazz.library)
        (jazz.io)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.platform.types)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.window.platform.independant)
        (jazz.utilities))


;;;
;;;; Processed
;;;


(definition public unprocessed '(unprocessed))
(definition public processed '(processed))


(definition (processed-result return)
  (if (and (pair? return) (eq? (car return) 'processed))
      (if (null? (cdr return))
          0
        (cadr return))
    0))


;;;
;;;; Window
;;;


(definition public Windows-Table
  (make-table test: eqv?))


(definition public inline (get-window hwnd)
  (let ((hwnd (if (foreign? hwnd) (foreign-address hwnd) hwnd)))
    (table-ref Windows-Table hwnd #f)))


(definition public inline (set-window hwnd window)
  (table-set! Windows-Table (foreign-address hwnd) window))


;; So that the window<->hwnd association can be made on the very first windows message
(definition in-creation {})


(definition (set-in-creation in)
  (set! in-creation in))


(definition (get-in-creation)
  in-creation)


(definition (window-class)
  "JWindow")


;;tofix : Transient CS_SAVEBITS
(definition (class-style)
  (bitwise-ior CS_DBLCLKS))


(definition (class-background)
  (GetSysColorBrush COLOR_BTNFACE))


(definition (get-class-background handle)
  (GetClassLong handle GCL_HBRBACKGROUND))


(definition (platform-erase-background handle)
  unprocessed)


(definition CLASS_ATOM
  #f)


(definition (register-window-class)
  (let ((instance (current-instance))
        (background (class-background))
        (wc (WNDCLASS-make)))
    (WNDCLASS-style-set! wc          (class-style))
    (WNDCLASS-lpfnWndProc-set! wc    call-process-hwnd-message)
    (WNDCLASS-cbClsExtra-set! wc     0)
    (WNDCLASS-cbWndExtra-set! wc     0)
    (WNDCLASS-hInstance-set! wc      instance)
    (WNDCLASS-hIcon-set! wc          (load-product-icon))
    (WNDCLASS-hCursor-set! wc        NULL)
    (WNDCLASS-hbrBackground-set! wc  background)
    (WNDCLASS-lpszMenuName-set! wc   NULL)
    (WNDCLASS-lpszClassName-set! wc  "JWindow")
    (set! CLASS_ATOM (RegisterClass wc))))


(definition (load-product-icon)
  (let ((product (either (jazz-product) 'jazz)))
    (let ((file (format "build/products/{a}/{a}.ico" product product)))
      (if (file-exists? file)
          (LoadImage NULL file IMAGE_ICON 16 16 LR_LOADFROMFILE)
        NULL))))


(register-window-class)


(definition (window-style window rest)
  (typecase window
    ((Frame)            (bitwise-ior WS_CHILD
                                     WS_SYSMENU
                                     WS_CLIPCHILDREN
                                     WS_CLIPSIBLINGS))
    ((Popup)            (bitwise-ior WS_POPUP
                                     WS_CLIPCHILDREN))
    ((Palette)          (bitwise-ior WS_POPUP
                                     WS_OVERLAPPEDWINDOW
                                     WS_CLIPCHILDREN))
    ((Toplevel)         (bitwise-ior WS_CLIPSIBLINGS
                                     WS_CLIPCHILDREN
                                     WS_POPUP
                                     WS_OVERLAPPEDWINDOW
                                     WS_MINIMIZEBOX
                                     WS_SYSMENU))
    ((Dialog)           (bitwise-ior WS_CLIPCHILDREN
                                     WS_CLIPSIBLINGS
                                     WS_POPUP
                                     WS_BORDER
                                     WS_OVERLAPPED
                                     WS_CAPTION
                                     WS_DLGFRAME
                                     (if (getf rest sizable?: not-found: #t) WS_THICKFRAME 0)
                                     (if (getf rest closable?: not-found: #t) WS_SYSMENU 0)))
    (else               (bitwise-ior WS_CHILD
                                     WS_CLIPCHILDREN
                                     WS_CLIPSIBLINGS))))


(definition (window-ex-style window rest)
  (let ((transparent (if (window-transparent?~ window) WS_EX_TRANSPARENT 0)))
    (bitwise-ior transparent
                 (typecase window
                   ((Popup)     WS_EX_TOOLWINDOW)
                   ((Palette)   WS_EX_TOOLWINDOW)
                   ((Toplevel)  WS_EX_APPWINDOW)
                   ((Dialog)    WS_EX_DLGMODALFRAME)
                   ((Transient) (bitwise-ior WS_EX_TOOLWINDOW WS_EX_TOPMOST))
                   (else        0)))))


(definition (window-parent window)
  (let ((parent (get-parent~ window)))
    (if parent
        (get-handle~ parent)
      NULL)))


(definition (window-default-parent window)
  (typecase window
    ((Frame) (get-stage))
    ((Dialog) (get-toplevel))
    (else #f)))


(definition (create-handle window rest)
  (typecase window
    ((Desktop) (GetDesktopWindow))
    (else
     (CreateWindowEx
       (window-ex-style window rest)
       CLASS_ATOM @w (either hwnd-class (window-class))
       NULL
       (window-style window rest)
       0 0 500 500
       (window-parent window)
       NULL
       (current-instance)
       NULL))))


(definition (prepare-handle window rest)
  (bind-keywords ((hwnd {}) . others) rest
    (set-in-creation window)
    (let ((hwnd (create-handle window rest)))
      (if (not hwnd)
          (error "Unable to create window {t} error {t}"
                 (list class: (window-class) style: (window-style window rest) ex-style: (window-ex-style window rest) parent: (window-parent window))
                 (GetLastError))
        (DragAcceptFiles hwnd #t)
        hwnd))))


(definition (bring-window-to-top hwnd)
  (BringWindowToTop hwnd))


(definition (show-window handle)
  (ShowWindow handle SW_SHOWNORMAL))


(definition (show-maximized handle)
  (ShowWindow handle SW_MAXIMIZE))


(definition (show-restored handle)
  (ShowWindow handle SW_RESTORE))


(definition (hide-window handle)
  (ShowWindow handle SW_HIDE))
 
 
(definition (minimize-window handle)
  (ShowWindow handle SW_MINIMIZE))

 
(definition (maximize-window handle)
  (ShowWindow handle SW_MAXIMIZE))

 
(definition (restore-window handle)
  (ShowWindow handle SW_RESTORE))


(definition (destroy-window window)
  (DestroyWindow (get-handle~ window)))


(definition (dispatch-message window msg <int> wparam <int> lparam <int>)
  (let ((return (process-window-message window msg wparam lparam)))
    (if (eq? return unprocessed)
        (DefWindowProc (get-handle~ window) msg wparam lparam)
      (processed-result return))))


;;;
;;;; Events
;;;


(definition (setup-events)
  ;; one millisecond
  (define events-heartbeat
    1)
  
  (if (get-events-single-threaded?)
      (SetTimer (get-toplevel-hwnd) 0 events-heartbeat NULL)
    (thread-priority-boost-set! event-thread 1)))


;;;
;;;; Message Pump
;;;


(definition (prime-message-pump)
  (new-thread (function ()
                (process-messages))
              "Message Pump"))


(definition (process-messages)
  (define (process-message msg)
    (execute-event
      (function ()
        (unless (translate-shortcut msg)
          (TranslateMessage msg)
          (DispatchMessage msg)))))
  
  (declare (proper-tail-calls))
  
  ;; put back here to see if moving it before the read was the cause of many
  ;; intermittent bugs
  
  ;; because MsgWaitForMultipleObjects will *not* remove messages already present
  ;; in the message queue, we need to process those before the blocking call to read
  (let ((msg (MSG-make)))
    (while (PeekMessage msg NULL 0 0 PM_REMOVE)
      (process-message msg))
    (MSG-free msg))
  
  (let ((port (open-event-queue QS_ALLINPUT))
        (thread (current-thread)))
    (let (loop)
      (let ((msg (read port)))
        (process-message msg)
        (MSG-free msg))
      (when (thread-loop? thread)
        (loop)))))


(definition (process-messages-single-threaded thunk)
  (define (process-message msg)
    (unless (translate-shortcut msg)
      (TranslateMessage msg)
      (DispatchMessage msg)))
  
  (declare (proper-tail-calls))
  
  (let ((msg (MSG-make)))
    (let (loop)
      (when (GetMessage msg NULL 0 0)
        (process-message msg)
        (thunk)
        (loop)))
    (MSG-free msg)))


;;;
;;;; Dispatch
;;;


(definition callback-level
  0)

(definition callback-thread-mutex
  (make-mutex 'callback-thread-mutex))

(definition callback-thread-condition
  (make-condition 'callback-thread-condition))


(definition (message-callback hwnd <foreign> msg <int> wparam <int> lparam <int>)
  (define (process)
    (let ((window (get-window hwnd)))
      (when (and (not window) (get-in-creation))
        (set! window (get-in-creation))
        (set-handle~ window hwnd)
        (set-window hwnd window)
        (set-in-creation {}))
      (if (not window)
          (DefWindowProc hwnd msg wparam lparam)
        (dispatch-message window msg wparam lparam))))
  
  (if (get-events-single-threaded?)
      (if (primordial-thread?)
          (process)
        (snapshot-process reason: "Message callback from non-primordial thread")
        (exit 1))
    (let ((level (begin
                   (mutex-lock! callback-thread-mutex)
                   (increase! callback-level)
                   (prog1 callback-level
                     (mutex-unlock! callback-thread-mutex)))))
      (unwind-protect
          (call-event process)
        (let (iterate)
          (mutex-lock! callback-thread-mutex)
          (if (= level callback-level)
              (begin
                (decrease! callback-level)
                (mutex-unlock! callback-thread-mutex))
            @debug
            (terminal 'interleaved-problem-detected (current-thread) level callback-level hwnd msg wparam lparam)
            (mutex-unlock! callback-thread-mutex callback-thread-condition)
            (iterate)))))))


(set-process-hwnd-message message-callback)


;;;
;;;; Shortcuts
;;;


;; - standard usage -
;; ()                     : letters
;; (:shift)               : capital letters
;; (:control)             : mostly for focus
;; (:alt)                 : mostly for focus
;; (:control :alt)        : mostly for platform standards and window selection
;; (:control :shift)      : mostly for window selection
;; (:alt :shift)          : workspace management
;; (:control :alt :shift) : favorite files


(definition public Symbolic-Keys
  (list->table
    (list
      (cons VK_OEM_MINUS  #\-)
      (cons VK_OEM_PLUS   #\+)
      (cons VK_OEM_COMMA  #\,)
      (cons VK_OEM_PERIOD #\.)
      (cons VK_OEM_1      #\;)
      (cons VK_OEM_2      #\/)
      (cons VK_OEM_3      #\~)
      (cons VK_OEM_4      #\[)
      (cons VK_OEM_5      #\\)
      (cons VK_OEM_6      #\])
      (cons VK_OEM_7      #\')
      (cons VK_F1         'F1)
      (cons VK_F2         'F2)
      (cons VK_F3         'F3)
      (cons VK_F4         'F4)
      (cons VK_F5         'F5)
      (cons VK_F6         'F6)
      (cons VK_F7         'F7)
      (cons VK_F8         'F8)
      (cons VK_F9         'F9)
      (cons VK_F10        'F10)
      (cons VK_F11        'F11)
      (cons VK_F12        'F12)
      (cons VK_ESCAPE     'Escape)
      (cons VK_BACK       'Backspace)
      (cons VK_RETURN     'Return)
      (cons VK_SPACE      'Space)
      (cons VK_TAB        'Tab)
      (cons VK_HOME       'Home)
      (cons VK_END        'End)
      (cons VK_LEFT       'Left)
      (cons VK_UP         'Up)
      (cons VK_RIGHT      'Right)
      (cons VK_DOWN       'Down)
      (cons VK_PRIOR      'Prior)
      (cons VK_NEXT       'Next)
      (cons VK_INSERT     'Insert)
      (cons VK_PAUSE      'Pause)
      (cons VK_DELETE     'Delete)
      (cons VK_ADD        'Add)
      (cons VK_SUBTRACT   'Subtract)
      (cons VK_MULTIPLY   'Multiply)
      (cons VK_DIVIDE     'Divide))
    test: eqv?))


(definition (translate-shortcut msg)
  (define (modifiers)
    (let ((modifiers '()))
      (let ((try
              (function (down? modifier)
                (when down?
                  (set! modifiers (cons modifier modifiers))))))
        (try (alt-down?) :alt)
        (try (shift-down?) :shift)
        (try (control-down?) :control)
        modifiers)))
  
  (define (key code)
    (and (/= code VK_SHIFT)
         (/= code VK_CONTROL)
         (/= code VK_MENU)
         (or (table-ref Symbolic-Keys code #f)
             (integer->char code))))
  
  (let ((message (MSG-message-ref msg)))
    (if (or (= message WM_KEYDOWN)
            (= message WM_SYSKEYDOWN))
        (let ((key (key (MSG-wParam-ref msg))))
          (if key
              (dispatch-shortcut~ (get-application) (new Shortcut (modifiers) key))
            #f))
      #f)))


;;;
;;;; Messages
;;;


(definition get-x-lparam signed-loword)
(definition get-y-lparam signed-hiword)
(definition get-wheel-delta-wparam signed-hiword)


(definition (process-window-message window msg <int> wparam <int> lparam <int>)
  (ecase msg
    ((WM_ERASEBKGND)      (erase-background~ window wparam))
    ((WM_PAINT)           (with-closed ((region (platform-get-update-region (get-handle~ window))))
                            (paint~ window region)))
    ((WM_SETCURSOR)       (cursor-update~ window (= (loword lparam) HTCLIENT)))
    ((WM_MOUSEMOVE)       (mouse-move~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_MOUSEWHEEL)      (mouse-wheel~ window (get-x-lparam lparam) (get-y-lparam lparam) (get-wheel-delta-wparam wparam)))
    ((WM_LBUTTONDOWN)     (mouse-down~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_LBUTTONUP)       (mouse-up~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_LBUTTONDBLCLK)   (double-click~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_MBUTTONDOWN)     (middle-mouse-down~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_MBUTTONUP)       (middle-mouse-up~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_RBUTTONDOWN)     (right-mouse-down~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_RBUTTONUP)       (right-mouse-up~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_CONTEXTMENU)     (call-context-menu~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_SYSKEYDOWN)      (if (= wparam VK_MENU) (system-key-down~ window) unprocessed))
    ((WM_SYSKEYUP)        (if (= wparam VK_MENU) (system-key-up~ window) unprocessed))
    ((WM_KEYDOWN)         (key-down~ window wparam))
    ((WM_KEYUP)           (key-up~ window wparam))
    ((WM_CHAR)            (call-key-press~ window (integer->char wparam)))
    ((WM_MOVE)            (call-position-change~ window (get-x-lparam lparam) (get-y-lparam lparam)))
    ((WM_SIZE)            ;; Windows sends a size message with both the width
                          ;; and the height equal to zero when minimizing a window...
                          (let ((width (get-x-lparam lparam))
                                (height (get-y-lparam lparam)))
                            (if (or (/= width 0) (/= height 0))
                                (call-size-change~ window width height)
                              unprocessed)))
    ((WM_MOUSEACTIVATE)   (if (mouse-activate?~ window)
                              (on-mouse-activate~ window)
                            (list 'processed MA_NOACTIVATE)))
    ((WM_ACTIVATE)        (let ((kind (if (= (loword wparam) WA_INACTIVE) 'inactive 'active)))
                            (on-activate~ window kind)))
    ((WM_ACTIVATEAPP)     (activate-app~ window wparam))
    ((WM_CLOSE)           (on-close~ window {}))
    ((WM_SETFOCUS)        (focus-gain~ window))
    ((WM_KILLFOCUS)       (focus-lose~ window (get-window wparam)))
    ((WM_DROPFILES)       (drop-files~ window wparam))
    ((WM_SETTINGCHANGE)   (settings-change window wparam))
    ((WM_TIMER)           (timer-pulse))
    (else                 unprocessed)))


(definition (set-capture handle)
  (SetCapture handle))


(definition (unset-capture)
  (ReleaseCapture))


(definition (with-platform-painting window platform-data proc)
  (let ((handle (get-handle~ window)))
    (with ((ps (PAINTSTRUCT-make)))
      (let ((hdc (BeginPaint handle ps)))
        (with ((surface (new Surface (cairo_win32_surface_create hdc))))
          (unwind-protect
              (proc surface)
            (close~ surface)
            (EndPaint handle ps))))
      (PAINTSTRUCT-free ps)))
  processed)


(definition public (get-window-surface window)
  (cairo_win32_surface_create (GetDC (get-handle~ window))))


(definition (settings-change window wparam)
  (ecase wparam
    ((SPI_SETWORKAREA)
     @not-correct-to-call-set-fullscreen?
     (when (is? window Toplevel)
       (set-fullscreen?~ window #t)))))


(definition (timer-pulse)
  (thread-yield!))


;;;
;;;; Style
;;;


(definition (get-style handle)
  (GetWindowLong handle GWL_STYLE))


(definition (set-style handle style)
  (SetWindowLong handle GWL_STYLE style))


(definition (get-ex-style handle)
  (GetWindowLong handle GWL_EXSTYLE))


(definition (set-ex-style handle style)
  (SetWindowLong handle GWL_EXSTYLE style))


(definition (child-window? handle)
  (mask-bit-set? (get-style handle) WS_CHILD))


;;;
;;;; Toplevel
;;;


(definition (get-toplevel-hwnd)
  (let ((toplevel (get-toplevel)))
    (when toplevel
      (get-handle~ toplevel))))


;;;
;;;; Stage
;;;


(definition (get-stage)
  (let ((appl (get-application)))
    (when appl
      (get-stage~ appl))))


;;;
;;;; Layers
;;;


(definition (get-layered? handle)
  (mask-bit-set? (get-ex-style handle) WS_EX_LAYERED))


(definition (set-layered? handle flag)
  (SetWindowLong handle GWL_EXSTYLE (mask-bit-set (GetWindowLong handle GWL_EXSTYLE) WS_EX_LAYERED flag)))


(definition (set-alpha-blending handle n)
  (set-layered? handle #t)
  (SetLayeredWindowAttributes handle 0 n LWA_ALPHA)
  (RedrawWindow handle NULL NULL (bitwise-ior RDW_ERASE RDW_INVALIDATE)))


;;;
;;;; Tracking
;;;


(definition (track-mouse-event handle flags (time: time HOVER_DEFAULT))
  (with ((rec (TRACKMOUSEEVENT-make)))
    (TRACKMOUSEEVENT-cbSize-set! rec (size-of-TRACKMOUSEEVENT))
    (TRACKMOUSEEVENT-dwFlags-set! rec flags)
    (TRACKMOUSEEVENT-hwndTrack-set! rec handle)
    (TRACKMOUSEEVENT-dwHoverTime-set! rec time)
    (TrackMouseEvent rec)
    (TRACKMOUSEEVENT-free rec)))


;;;
;;;; Management
;;;


(definition (platform-set-parent handle parent)
  (SetParent handle (if parent (get-handle~ parent) NULL)))


(definition (set-window-title handle title)
  (SetWindowText handle title))

  
(definition (get-window-owner-handle handle)
  (GetWindow handle GW_OWNER))


(definition (platform-get-first-child handle)
  (GetWindow handle GW_CHILD))


(definition (platform-get-first-visible-child handle)
  (let loop ((child-handle (platform-get-first-child handle)))
    (if child-handle
        (if (platform-is-window-visible? child-handle)
            child-handle
            (loop (platform-get-next-window-handle handle)))
        #f)))


(definition (platform-get-next-window-handle handle)
  (GetWindow handle GW_HWNDNEXT))


(definition (platform-is-window-visible? handle)
  (IsWindowVisible handle))


(definition (get-top-window-handle handle)
  (GetTopWindow handle))


(definition (acquire-point to-handle from-handle shape)
  (when (not to-handle)
    (error "No window handle"))
  (let ((acquired (point->POINT shape)))
    (MapWindowPoints from-handle to-handle acquired 1)
    (prog1 (POINT->point acquired)
      (POINT-free acquired))))


(definition (acquire-rect to-handle from-handle shape)
  (let ((acquired (rect->RECT shape)))
    (MapWindowPoints from-handle to-handle (RECT->POINTS acquired) 2)
    (prog1 (RECT->rect acquired)
      (RECT-free acquired))))


(definition (platform-translate-to-root handle pt)
  (with ((pos (point->POINT pt)))
    (MapWindowPoints handle NULL pos 1)
    (prog1 (new Point (POINT-x-ref pos) (POINT-y-ref pos))
      (POINT-free pos))))


(definition (platform-get-position handle parent-handle) <Point>
  (with ((rect (RECT-make)))
    (GetWindowRect handle rect)
    (when (and parent-handle (child-window? handle))
      (MapWindowPoints NULL parent-handle (RECT->POINTS rect) 2))
    (prog1 (new Point (RECT-left-ref rect) (RECT-top-ref rect))
      (RECT-free rect))))


(definition (platform-set-position handle pos)
  (SetWindowPos handle NULL (get-h~ pos) (get-v~ pos) 0 0 (bitwise-ior SWP_NOSIZE SWP_NOZORDER SWP_NOACTIVATE)))


(definition (platform-get-size handle) <Dimension>
  (with ((rect (RECT-make)))
    (GetClientRect handle rect)
    (prog1 (new Dimension (RECT-right-ref rect) (RECT-bottom-ref rect))
      (RECT-free rect))))


(definition (platform-get-window-size handle) <Dimension>
  (let ((rect (RECT-make)))
    (GetWindowRect handle rect)
    (prog1 (new Dimension (- (RECT-right-ref rect)  (RECT-left-ref rect))
                          (- (RECT-bottom-ref rect) (RECT-top-ref rect)))
      (RECT-free rect))))


(definition (platform-set-window-size handle size)
  (SetWindowPos handle NULL 0 0 (get-width~ size) (get-height~ size) (bitwise-ior SWP_NOCOPYBITS SWP_NOMOVE SWP_NOZORDER SWP_NOACTIVATE)))


(definition (platform-set-bounds handle rect)
  (SetWindowPos handle NULL (get-left~ rect) (get-top~ rect) (rect-width rect) (rect-height rect) (bitwise-ior SWP_NOZORDER SWP_NOACTIVATE)))


(definition (platform-get-rect handle parent-handle) <Rect>
  (with ((rect (RECT-make)))
    (GetWindowRect handle rect)
    (when parent-handle
      (MapWindowPoints NULL parent-handle (RECT->POINTS rect) 2))
    (prog1 (new Rect (RECT-left-ref rect) (RECT-top-ref rect) (RECT-right-ref rect) (RECT-bottom-ref rect))
      (RECT-free rect))))


(definition (platform-get-bounds handle) <Rect>
  (with ((rect (RECT-make)))
    (GetWindowRect handle rect)
    (MapWindowPoints NULL handle rect 2)
    (prog1 (new Rect (RECT-left-ref rect) (RECT-top-ref rect) (RECT-right-ref rect) (RECT-bottom-ref rect))
      (RECT-free rect))))


(definition (platform-mouse-position) <Point>
  (with ((point (POINT-make)))
    (GetCursorPos point)
    (prog1 (new Point (POINT-x-ref point) (POINT-y-ref point))
      (POINT-free point))))


(definition (platform-window-from-point point)
  (with ((pt (POINT-make)))
    (POINT-x-set! pt (get-h~ point))
    (POINT-y-set! pt (get-v~ point))
    (prog1 (WindowFromPoint pt)
      (POINT-free pt))))


(definition (platform-child-window-from-point handle point)
  (with ((pt (POINT-make)))
    (POINT-x-set! pt (get-h~ point))
    (POINT-y-set! pt (get-v~ point))
    (prog1 (ChildWindowFromPointEx handle pt (bitwise-ior CWP_SKIPINVISIBLE CWP_SKIPDISABLED CWP_SKIPTRANSPARENT))
      (POINT-free pt))))


(definition (platform-move-window handle x y cx cy)
  (MoveWindow handle x y cx cy #t))


(definition (platform-window-enabled? handle)
  (IsWindowEnabled handle))


(definition (platform-enable-window handle)
  (EnableWindow handle #t))


(definition (platform-disable-window handle)
  (EnableWindow handle #f))


(definition (platform-get-client-rect handle)
  (with ((rect (RECT-make)))
    (GetClientRect handle rect)
    (prog1 (RECT->rect rect)
      (RECT-free rect))))


(definition (platform-scroll-window handle dh dv rect clip-rect)
  (let ((flags SW_INVALIDATE))
    (with ((rect (rect->RECT rect))
           (clip-rect (rect->RECT clip-rect)))
      (prog1 (ScrollWindowEx handle dh dv rect clip-rect NULL NULL flags)
        (RECT-free rect)
        (RECT-free clip-rect)))))


(definition (platform-invalidate-rect handle rect)
  ;; is this test correct or was it a patch made during the port
  (when handle
    (with ((rect (rect->RECT rect)))
      (prog1 (InvalidateRect handle rect #f)
        (RECT-free rect)))))


(definition (platform-invalidate-region handle region)
  ;; is this test correct or was it a patch made during the port
  (when handle
    (InvalidateRgn handle (get-handle~ region) #f)))


(definition (platform-invalidate-window handle)
  ;; is this test correct or was it a patch made during the port
  (when handle
    (InvalidateRect handle NULL #f)))


(definition (platform-redraw-window handle rect region)
  ;; is this test correct or was it a patch made during the port
  (when handle
    ;; Changed RDW_NOCHILDREN to RDW_ALLCHILDREN so that a redraw-window on the
    ;; toplevel would redraw it's children. Hope it doesn't break any code...
    (let ((flags (bitwise-ior RDW_ERASENOW RDW_UPDATENOW RDW_INVALIDATE RDW_ALLCHILDREN)))
      (with ((rect (if (not rect) NULL (rect->RECT rect))))
        (prog1 (RedrawWindow handle rect (if region (get-handle~ region) NULL) flags)
          (when rect (RECT-free rect)))))))


(definition (platform-get-update-rect handle)
  (with ((rect (RECT-make)))
    (GetUpdateRect handle rect #f)
    (prog1 (RECT->rect rect)
      (RECT-free rect))))


(definition (platform-get-update-region handle)
  (let ((region (new Region)))
    (GetUpdateRgn handle (get-handle~ region) #f)
    region))


(definition (platform-update-window handle)
  (UpdateWindow handle))
  
  
(definition (platform-show-noactivate handle)
  (SetWindowPos handle HWND_TOP 0 0 0 0 (bitwise-ior SWP_SHOWWINDOW SWP_NOACTIVATE SWP_NOMOVE SWP_NOSIZE)))
  
  
(definition (platform-hide-noactivate handle)
  (SetWindowPos handle 0 0 0 0 0 (bitwise-ior SWP_HIDEWINDOW SWP_NOACTIVATE SWP_NOMOVE SWP_NOSIZE)))


(definition (platform-bring-window-to-front handle)
  (BringWindowToTop handle))


(definition (platform-send-window-to-back handle)
  (SetWindowPos handle HWND_BOTTOM 0 0 0 0 (bitwise-ior SWP_NOMOVE SWP_NOSIZE)))


(definition (platform-select-foreground-window handle)
  (SetForegroundWindow handle))


(definition (platform-get-window-handle-title handle)
  (let* ((size (GetWindowTextLength handle))
         (string (make-string size #\space)))
    (receive (ret str) (GetWindowText handle string (+ size 1))
      str)))


(definition (platform-lock-window-update handle)
  (LockWindowUpdate handle))


;;;
;;;; Focus
;;;


(definition (platform-get-focus)
  (GetFocus))


(definition (platform-set-focus handle)
  (SetFocus handle))


;;;
;;;; Cursor
;;;


;;; We need a platform independant interface for this.

(definition public Current-Cursor
  {})


(definition public Cursors
  (list
    (list :arrow               IDC_ARROW)
    (list :wait                IDC_WAIT)
    (list :ibeam               IDC_IBEAM)
    (list :cross               IDC_CROSS)
    (list :finger              IDC_HAND)
    (list :all                 IDC_SIZEALL)
    (list :west-east           IDC_SIZEWE)
    (list :no                  IDC_NO)
    (list :move                {Cursor-Resource "Move"})
    (list :opened-hand         {Cursor-Resource "OpenedHand"})
    (list :closed-hand         {Cursor-Resource "ClosedHand"})
    (list :zoom                {Cursor-Resource "Zoom"})
    (list :zoom-in             {Cursor-Resource "ZoomIn"})
    (list :zoom-out            {Cursor-Resource "ZoomOut"})
    (list :spy                 {Cursor-Resource "Spy"})
    (list :left-arrow          {Cursor-Resource "LeftArrow"})
    (list :vertical-splitter   {Cursor-Resource "VerticalSplitter"})
    (list :horizontal-splitter {Cursor-Resource "HorizontalSplitter"})))


(definition (load-cursor cursor)
  (if (number? cursor)
      (LoadCursorInt cursor)
    (let* ((cursor (if (file? cursor) cursor (get-resource~ (get-application) 'cursor (get-name~ cursor))))
           (ident (parse~ cursor)))
      (LoadImage NULL ident IMAGE_CURSOR 0 0 LR_LOADFROMFILE))))


(definition (get-named-cursor name)
  (let ((info (assq name Cursors)))
    (if (not info)
        (error "Unknown cursor: {t}" name)
      (cadr info))))


(definition public (get-cursor)
  Current-Cursor)


(definition public (set-cursor cursor)
  (unless #f @to-fix-this-doesnt-work-when-switching-overlapped-windows! (= cursor Current-Cursor)
          (set! Current-Cursor cursor)
          (let ((cursor (if (symbol? cursor) (get-named-cursor cursor) cursor)))
            (SetCursor (load-cursor cursor)))))


(definition public (hide-cursor)
  (while (>= (ShowCursor #f) 0)
    ))


(definition public (show-cursor)
  (while (< (ShowCursor #t) 0)
    ))


;;;
;;;; System Message
;;;


(definition public (system-message text . rest)
  (bind-keywords ((type 'message) (title #f)) rest
    (let ((handle (get-toplevel-hwnd))
          (title
            (or title (case type
                        ((error) "Error")
                        ((problem) "Problem")
                        ((message) "Message")
                        ((question) "Question")
                        ((confirmation) "Confirmation"))))
          (flags
            (case type
              ((error) (bitwise-ior MB_OKCANCEL MB_ICONERROR))
              ((problem) (bitwise-ior MB_OK MB_ICONERROR))
              ((message) (bitwise-ior MB_OK MB_ICONINFORMATION))
              ((question) (bitwise-ior MB_YESNO MB_ICONWARNING))
              ((confirmation) (bitwise-ior MB_YESNOCANCEL MB_ICONWARNING)))))
      (MessageBox handle text title (bitwise-ior MB_TASKMODAL flags)))))


;;;
;;;; Redraw View Only
;;;


(definition (platform-redraw-view-only view hwnd)
  (with ((hrgn (CreateRectRgn 0 0 0 0)))
    ;; preserve the current update region
    (GetUpdateRgn hwnd hrgn #f)
    ;; reset it
    (ValidateRect hwnd NULL)
    ;; redraw the view only
    (redraw-view~ view)
    ;; reinvalidate the preserved update region
    (InvalidateRgn hwnd hrgn #f)
    (DeleteObject hrgn)))


(definition (platform-get-desktop)
  (GetDesktopWindow))


;;;
;;;; Work Area
;;;


(definition public (get-work-area) <Rect>
  (with ((area (RECT-make)))
    (SystemParametersInfo SPI_GETWORKAREA 0 area 0)
    (prog1 (adjust-work-area-with-taskbar (RECT->rect area))
      (RECT-free area))))


;; Because fucking Window's SPI_GETWORKAREA doesn't take autohide into account!
(definition (adjust-work-area-with-taskbar rect)
  rect
  @convert
  (let ((state (get-taskbar-state))
        (edge (get-taskbar-edge)))
    (if (and (mask-bit-set? state ABS_AUTOHIDE) (mask-bit-set? state ABS_ALWAYSONTOP))
        (ecase edge
          ((ABE_LEFT)   (expand-rect rect  1  0  0  0))
          ((ABE_TOP)    (expand-rect rect  0  1  0  0))
          ((ABE_RIGHT)  (expand-rect rect  0  0 -1  0))
          ((ABE_BOTTOM) (expand-rect rect  0  0  0 -1)))
      rect)))


(definition public (get-taskbar-state)
  (unimplemented 'get-taskbar-state)
  @convert
  (with ((data (new APPBARDATA)))
    (set data 'cbSize (size-of APPBARDATA))
    (SHAppBarMessage ABM_GETSTATE data)))


(definition public (get-taskbar-edge)
  (unimplemented 'get-taskbar-edge)
  @convert
  (with ((data (new APPBARDATA)))
    (set data 'cbSize (size-of APPBARDATA))
    (SHAppBarMessage ABM_GETTASKBARPOS data)
    (get data 'uEdge)))


;;;
;;;; Drop Files
;;;


(definition (platform-drop-files window wparam)
  (let ((hdrop (WPARAM->HDROP wparam)))
    (let ((files (received-files hdrop))
          (pt (POINT-make)))
      (DragQueryPoint hdrop pt)
      (DragFinish hdrop)
      (let ((pos (POINT->point pt)))
        (receive-files~ window pos files))
      (prog1 (platform-select-foreground-window (get-toplevel-hwnd))
        (POINT-free pt)))))


(definition (received-files hdrop)
  (let ((count (DragQueryFile hdrop #xFFFFFFFF NULL 0)))
    (map (function (n)
           (let* ((size MAX_PATH)
                  (buffer (WCHAR-array-make size)))
             (WCHAR-array-set! buffer 0 #\nul)
             (DragQueryFile hdrop n buffer size)
             (let* ((filename (WCHAR-string buffer))
                    (pathname (new (case (pathname-type filename) ((directory) Directory) (else File)) (tokenise-windows filename))))
               (prog1 (anchorize~ pathname)
                 (WCHAR-array-free buffer)))))
         (naturals 0 count)))))
