;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Language Tests
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.validate.suite.Language-Tester jazz


(import (jazz.jml)
        (jazz.validation))


(class Language-Tester extends Tester
  
  
  (form
    (<install> title: "Language"))
  
  
  (method override (test self)
    (invoke self #f test-language)
    (invoke self #f test-binding)
    (invoke self #f test-conditionals))
  
  
  ;;;
  ;;;; Language
  ;;;
  
  
  (method (test-language self)
    (define (square x) (* x x))
    (define (double-proc proc x) (* (proc x) 2))
    
    ;; comments
    (check (eq? 5 (+ 1 4 @comment-next-sexp (+ 1 2))))
    (check @comment-next-step #f #t)
    
    ;; lambda
    (check (walk-failed? (lambda)))
    (check (eq? 16 ((lambda (x) (* x x)) 4)))
    (check (error-occurred? ((lambda x (* x x)) 2)))
    (check (procedure? (lambda ())))
    (check (walk-failed? (lambda (()))))
    (check (walk-failed? (lambda (1))))
    (check (walk-failed? (lambda (display 1))))
    
    ;; functional syntax
    (check (procedure? square))
    (check (eq? 4 (square 2)))
    (check (eq? 8 (double-proc square 2)))
    
    ;; error throwing
    (check (walk-failed? (throw)))
    (check (walk-failed? (throw new Error)))
    (check (walk-failed? (throw (new Error) (new Error))))
    (check (error-occurred? (throw (new Error))))
    (check (error-occurred? (throw 1)))
    
    ;; error catching
    (check (walk-failed? (catch)))
    (check (walk-failed? (catch ()
                              (throw (new Error)))))
    (check (walk-failed? (catch (throw (new Error)) 
                              (Error error))))
    (check (walk-failed? (catch 1)))
    (check (error-occurred? (catch (Error error #f)
                                 (throw 'foo)))))
  
  
  ;;;
  ;;;; Binding
  ;;;
  
  
  (method (test-binding self)
    ;; define
    (define foo 1)
    (define (bar) 1)
    (check (walk-failed? (let () (define))))
    (check (walk-failed? (let () (define a))))
    (check (eq? foo 1))
    (check (not (equal? 'foo 1)))
    (check (procedure? bar))
    (check (eq? (bar) 1))
    (check (walk-failed? (let () (define (a 1)))))
    (check (walk-failed? unresolved-symbol))
    (check (walk-failed? (unresolved-symbol)))
    
    ;; define-macro
    (check (walk-failed? (let () (define-macro))))
    (check (walk-failed? (let () (define-macro a))))
    
    ;; let
    (check (eq? 2 (let () 2)))
    (check (procedure? (let (func (arg 'val)) func)))
    (check (procedure? (let (func) func)))
    (check (walk-failed? (let)))
    (check (walk-failed? (let (()))))
    (check (walk-failed? (let ((())))))
    (check (walk-failed? (let ((a)))))
    (check (walk-failed? (let ((a 1) (b a)) a)))
    (check (walk-failed? (let 1)))
    (check (walk-failed? (let ((fact (lambda (n)
                                          (if (= 0 n)
                                              1
                                            (* n (fact (- n 1)))))))
                              (fact 3))))
    (check (let (opposite (bool #f))
                (if bool 
                    bool 
                  (opposite (not bool)))))
    
    ;; let*
    (check (walk-failed? (let*)))
    (check (eq? 25 (let* ((func (lambda (x) (* x x)))
                             (val (func 5)))
                        val)))
    (check (eq? 4 (let* ((a 2)
                            (b (* a 2)))
                       b)))
    @not-failing
    (check (walk-failed? (let* ())))
    (check (walk-failed? (let* ((a b) (b 2)) b)))
    (check (walk-failed? (let* ((fact (lambda (n)
                                           (if (= 0 n)
                                               1
                                             (* n (fact (- n 1)))))))
                              (fact 3))))
    
    ;; letrec
    (check (walk-failed? (letrec)))
    @this-error-is-not-trapped-by-gambit
    (check (neq? 2 (letrec ((a 2) (b a)) b)))
    @this-error-is-not-trapped-by-gambit
    (check (not (walk-failed? (letrec ((a b) (b 2)) b))))
    (check (eq? 6 (letrec ((fact (lambda (n)
                                      (if (= 0 n)
                                          1
                                        (* n (fact (- n 1)))))))
                       (fact 3))))
    
    ;; bind
    @incorrectly-returns-no-error-when-compiled
    (check (error-occurred? (bind (a) 1)))
    @crashed-when-compiled
    (check (error-occurred? (bind (a) (1))))
    @incorrectly-returns-no-error-when-compiled
    (check (error-occurred? (bind (a b) '(1))))
    (check (walk-failed? (bind (a b) (list 1 a))))
    (check (equal? '(2 3) (bind (first . rest) '(1 2 3) rest)))
    
    ;; receive
    (check (walk-failed? (receive)))
    (check (eq? 1 (receive (x) 1 x)))
    @crashed-when-compiled
    (check (error-occurred? (receive (x) (1) x)))
    (check (list? (receive (x) '(1) x)))
    (check (eq? -3 (receive (x y) (values 2 5) (- x y))))
    (check (error-occurred? (receive (x y) '(2 5) (- x y))))
    @wait-for-crash-proof-test-method
    (begin 
      (receive x 1 x))
    
    ;; set!
    (check (walk-failed? (set!)))
    (check (walk-failed? (set! test)))
    (check (walk-failed? (set! 'test 3)))
    (check (walk-failed? (set! foo 2 3)))
    (check (not (walk-failed? (set! foo 10)))))
  
  
  ;;;
  ;;;; Conditionals
  ;;;
  
  
  (method (test-conditionals self)
    ;; if
    (check (walk-failed? (if)))
    (check (walk-failed? (if #t)))
    (check (eq? 1 (if #t 1 2)))
    (check (eq? 3 (if #f 1 2 3)))
    
    ;; when
    (check (walk-failed? (when)))
    (check (eq? 2 (when #t 2)))
    
    ;; unless
    (check (walk-failed? (unless)))
    (check (eq? 2 (unless #f 2)))
    
    ;; cond
    (check (walk-failed? (cond ())))
    (check (walk-failed? (cond (else 1) (else 2))))
    (check (walk-failed? (cond 1 2)))
    
    ;; equal?
    (check (error-occurred? (equal?)))
    (check (error-occurred? (equal? 'foo)))
    (check (error-occurred? (equal? 'foo 'bar 'test)))
    
    ;; ecase
    (check (walk-failed? (ecase)))
    (check (walk-failed? (ecase 1
                           (0 (display "zero"))
                           (1 (display "one"))
                           (2 (display "two")))))
    
    ;; typecase
    (check (walk-failed? (typecase)))
    (check (eq? 2 (typecase "Test string"
                       ((List) 1)
                       ((String) 2))))
    (check (eq? 2 (typecase "Test string"
                       ((List) 1)
                       ((String) 2)
                       ((String) 3)))))))
