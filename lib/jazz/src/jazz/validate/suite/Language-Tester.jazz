;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Language Tests
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.validate.suite.Language-Tester jazz


(import (jazz.jml)
        (jazz.validation))


(class Language-Tester extends Tester
  
  
  (form
    (<install> title: "Language"))
  
  
  (method override (test)
    (invoke #f test-language)
    (invoke #f test-binding)
    (invoke #f test-conditionals))
  
  
  ;;;
  ;;;; Language
  ;;;
  
  
  (method (test-language)
    ;; required defines
    (define (square x) (* x x))
    (define (double-proc proc x) (* (proc x) 2))
    
    ;; comments
    (validate (eq? 5 (+ 1 4 @comment-next-sexp (+ 1 2))))
    (validate @comment-next-step #f #t)
    
    ;; lambda
    (validate (walk-failed? (lambda)))
    (validate (eq? 16 ((lambda (x) (* x x)) 4)))
    (validate (error-occurred? ((lambda x (* x x)) 2)))
    (validate (procedure? (lambda ())))
    (validate (walk-failed? (lambda (()))))
    (validate (walk-failed? (lambda (1))))
    (validate (walk-failed? (lambda (display 1))))
    
    ;; functional syntax
    (validate (procedure? square))
    (validate (eq? 4 (square 2)))
    (validate (eq? 8 (double-proc square 2)))
    
    ;; error throwing
    (validate (walk-failed? (throw)))
    (validate (walk-failed? (throw new Error)))
    (validate (walk-failed? (throw (new Error) (new Error))))
    (validate (error-occurred? (throw (new Error))))
    (validate (error-occurred? (throw 1)))
    
    ;; error catching
    (validate (walk-failed? (catch)))
    (validate (walk-failed? (catch ()
                              (throw (new Error)))))
    (validate (walk-failed? (catch (throw (new Error)) 
                              (Error error))))
    (validate (walk-failed? (catch 1)))
    (validate (error-occurred? (catch (Error error #f)
                                 (throw 'foo)))))
  
  
  ;;;
  ;;;; Binding
  ;;;
  
  
  (method (test-binding)
    ;; define
    (define foo 1)
    (define (bar) 1)
    @crash
    (validate (walk-failed? (let () (define))))
    @crash
    (validate (walk-failed? (let () (define a))))
    (validate (eq? foo 1))
    (validate (not (equal? 'foo 1)))
    (validate (procedure? bar))
    (validate (eq? (bar) 1))
    (validate (walk-failed? (let () (define (a 1)))))
    (validate (walk-failed? unresolved-symbol))
    (validate (walk-failed? (unresolved-symbol)))
    
    ;; define-macro
    (validate (walk-failed? (let () (define-macro))))
    (validate (walk-failed? (let () (define-macro a))))
    
    ;; let
    (validate (eq? 2 (let () 2)))
    (validate (procedure? (let (func (arg 'val)) func)))
    (validate (procedure? (let (func) func)))
    (validate (walk-failed? (let)))
    (validate (walk-failed? (let (()))))
    (validate (walk-failed? (let ((())))))
    (validate (walk-failed? (let ((a)))))
    (validate (walk-failed? (let ((a 1) (b a)) a)))
    (validate (walk-failed? (let 1)))
    (validate (walk-failed? (let ((fact (lambda (n)
                                          (if (= 0 n)
                                              1
                                            (* n (fact (- n 1)))))))
                              (fact 3))))
    (validate (let (opposite (bool #f))
                (if bool 
                    bool 
                  (opposite (not bool)))))
    
    ;; let*
    (validate (walk-failed? (let*)))
    (validate (eq? 25 (let* ((func (lambda (x) (* x x)))
                             (val (func 5)))
                        val)))
    (validate (eq? 4 (let* ((a 2)
                            (b (* a 2)))
                       b)))
    @not-failing
    (validate (walk-failed? (let* ())))
    (validate (walk-failed? (let* ((a b) (b 2)) b)))
    (validate (walk-failed? (let* ((fact (lambda (n)
                                           (if (= 0 n)
                                               1
                                             (* n (fact (- n 1)))))))
                              (fact 3))))
    
    ;; letrec
    (validate (walk-failed? (letrec)))
    @this-error-is-not-trapped-by-gambit
    (validate (neq? 2 (letrec ((a 2) (b a)) b)))
    @this-error-is-not-trapped-by-gambit
    (validate (not (walk-failed? (letrec ((a b) (b 2)) b))))
    (validate (eq? 6 (letrec ((fact (lambda (n)
                                      (if (= 0 n)
                                          1
                                        (* n (fact (- n 1)))))))
                       (fact 3))))
    
    ;; bind
    @incorrectly-returns-no-error-when-compiled
    (validate (error-occurred? (bind (a) 1)))
    @crashed-when-compiled
    (validate (error-occurred? (bind (a) (1))))
    @incorrectly-returns-no-error-when-compiled
    (validate (error-occurred? (bind (a b) '(1))))
    (validate (walk-failed? (bind (a b) (list 1 a))))
    (validate (equal? '(2 3) (bind (first . rest) '(1 2 3) rest)))
    
    ;; receive
    (validate (walk-failed? (receive)))
    (validate (eq? 1 (receive (x) 1 x)))
    @crashed-when-compiled
    (validate (error-occurred? (receive (x) (1) x)))
    (validate (list? (receive (x) '(1) x)))
    (validate (eq? -3 (receive (x y) (values 2 5) (- x y))))
    (validate (error-occurred? (receive (x y) '(2 5) (- x y))))
    @wait-for-crash-proof-test-method
    (begin 
      (receive x 1 x))
    
    ;; set!
    (validate (walk-failed? (set!)))
    (validate (walk-failed? (set! test)))
    (validate (walk-failed? (set! 'test 3)))
    (validate (walk-failed? (set! foo 2 3)))
    (validate (not (walk-failed? (set! foo 10)))))
  
  
  ;;;
  ;;;; Conditionals
  ;;;
  
  
  (method (test-conditionals)
    ;; if
    (validate (walk-failed? (if)))
    (validate (walk-failed? (if #t)))
    (validate (eq? 1 (if #t 1 2)))
    (validate (eq? 3 (if #f 1 2 3)))
    
    ;; when
    (validate (walk-failed? (when)))
    (validate (eq? 2 (when (lambda ()) 2)))
    
    ;; unless
    (validate (walk-failed? (unless)))
    (validate (not (unless (lambda ()) 3)))
    
    ;; cond
    (validate (walk-failed? (cond ())))
    @not-failing
    (validate (walk-failed? (cond (else 1) (else 2))))
    (validate (walk-failed? (cond 1 2)))
    
    ;; equal?
    (validate (error-occurred? (equal?)))
    (validate (error-occurred? (equal? 'foo)))
    (validate (error-occurred? (equal? 'foo 'bar 'test)))
    
    ;; ecase
    (validate (walk-failed? (ecase)))
    ;;; Important : This is on the jazzscheme website in the tutorial
    ;;;             and actually does not work! This should be fixed.
    (validate (walk-failed? (ecase 1
                              (0 (display "zero"))
                              (1 (display "one"))
                              (2 (display "two")))))
    
    ;; typecase
    (validate (walk-failed? (typecase)))
    (validate (eq? 2 (typecase "Test string"
                       ((List) 1)
                       ((String) 2))))
    (validate (eq? 2 (typecase "Test string"
                       ((List) 1)
                       ((String) 2)
                       ((String) 3)))))))
