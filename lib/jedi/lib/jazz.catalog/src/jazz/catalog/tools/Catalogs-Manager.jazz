;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Catalogs Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.catalog.tools.Catalogs-Manager jazz


(import (jazz.catalog)
        (jazz.jml)
        (jazz.library)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window))


(class undocumented Catalogs-Manager extends View


  (form
    (<install>                                               size: {Dimension 553 608}
      (<Push-Button>                   name: update          title: "Update" position: {Point 449 7} size: {Dimension 100 22} action-handler: {Event-Handler :form on-update} default?: #t)
      (<Border-View>                   name: catalogs-border position: {Point 2 35} size: {Dimension 549 571} border-type: edge style: entry
        (<Scroller-View>                                     vscroll?: #t
          (<!>                         name: content         layout-type: fill
            (<Tree-Header>                                   style: document
              (<!>                     name: content
                (<Tree-View>           name: catalogs        filled-column: name selection-handler: {Event-Handler :form on-selection-change} double-click-handler: {Event-Handler :form on-double-click} multiple-selection?: #t
                  (<Tree-Node-Column>  name: name            title: "Name" width: 150 toplevel-controls?: #f display-images?: #f)
                  (<Tree-Label-Column> name: references?     title: "References?" width: 100)
                  (<Tree-Label-Column> name: statistics      title: "Statistics" width: 250)))))))))
  
  
  (method meta override (external-name . rest)
    'catalogs-manager)
  
  
  ;;;
  ;;;; Component
  ;;;

  
  (method override (finish initargs)
    (nextmethod initargs)
    (view-catalogs)
    (update-buttons))
    
  
  ;;;
  ;;;; Host
  ;;;
    
  
  (method meta override (host-icon)
    {Bitmap-Resource "Catalog"})
  
  
  (method meta override (host-title)
    "Catalogs Manager")
  
  
  (method meta override (host-position)
    {Point 90 70})
  
  
  (method meta override (host-size)
    {Dimension 573 508})
  
  
  (method override (host-workspaces)
    '(home))
  
  
  ;;;
  ;;;; Events
  ;;;


  (method override (resize dh dv)
    (nextmethod dh dv)
    (move-figure~ (locate 'update) dh 0)
    (size-figure~ (locate 'catalogs-border) dh dv))
  
  
  (method (on-selection-change evt)
    (update-buttons))
  
  
  ;;;
  ;;;; Update
  ;;;


  (method public (view-catalogs)
    (let ((tree (locate 'catalogs)))
      (with-update-locked~ tree
        (lambda ()
          (remove-every-row~ tree)
          (for-each (lambda (catalog)
                      (let ((c0 (new Tree-Node title: (->string (get-name~ catalog))))
                            (c1 (new Tree-Label title: (->string (get-references?~ catalog))))
                            (c2 (new Tree-Label title: (get-statistics~ catalog))))
                        (add-row~ tree children: (list c0 c1 c2) user-data: catalog)))
                    (get-filing-catalogs))))))
  
  
  (method (get-filing-catalogs)
    (collect-if (lambda (catalog)
                  (is? catalog Filing-Catalog))
                (get-catalogs~ (get-cataloguer~ (get-application)))))


  (method package (on-update evt)
    (with-cursor :wait
      (lambda ()
        (update-selection))))
  
  
  (method (on-double-click evt)
    (let ((sender (get-sender~ evt))
          (alt? (get-alt?~ evt)))
      (case (get-name~ sender)
        ((catalogs)
         (with-cursor :wait
           (lambda ()
             (update-selection)))))))
  
  
  (method (update-selection)
    (let* ((tree (locate 'catalogs))
           (selection (get-selection~ tree)))
      (define (effective-selection selection)
        (if (null? selection)
            (naturals 0 (get-row-count~ tree))
          selection))
      
      (post-catalog
        (lambda ()
          (for-each (lambda (row)
                      (let ((catalog (get-user-data~ (get-row~ tree row))))
                        (update~ catalog)
                        (execute-event
                          (lambda ()
                            (let ((cell (new Cell row 2)))
                              (set-title~ (get-cell-data~ tree cell) (get-statistics~ catalog))
                              (invalidate-cell~ tree cell))))))
                    (effective-selection selection))))))
  
  
  (method (update-buttons)
    (let ((tree (locate 'catalogs)))
      (let ((count (get-row-count~ tree)))
        (set-enabled?~ (locate 'update) (> count 0)))))
  

  ;;;
  ;;;; Session
  ;;;
  
  
  (method override (confirm-close . rest)
    (save-session))
  
  
  (method override (save-session)
    @to-convert
    (let ((info
           (list (external-name)
                 position: (get-position)
                 size:     (get-size))))
      (push-property~ properties 'Opened-Windows info)))))
