;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Console Manager
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.console.Console-Manager jazz


(import (jazz.console)
        (jazz.graphic)
        (jazz.jml)
        (jazz.library)
        (jazz.literals)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.ui.window)
        (jazz.utilities))


(class Console-Manager extends Layout-View


  (form
    (<install>                                  size: {Dimension 650 400} layout-type: border
      (<Layout-View>             name: tools
        @convert
        (<Push-Combo>            name: detail
          (<!>                   name: body     portfolio: :icons image-name: {Bitmap-Resource "PrintDetail"} tooltip?: #t tooltip-text: "Print Detail" action-handler: {Event-Handler :form on-print-detail-menu})
          (<!>                   name: pick     action-handler: {Event-Handler :form on-print-detail-menu}))
        (<Separator>                            location: tail)
        (<Push-Tool>             name: clear    size: {Dimension 22 22} location: tail tooltip?: #t tooltip-text: "Clear Console" image-name: {Bitmap-Resource "Clear"} portfolio: :icons action-handler: {Event-Handler :form on-clear-console}))
      (<Pad-View>                name: pad      location: center)))
    
  
  (method meta (external-name . rest)
    'console-manager)

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta (host-icon)
    {Bitmap-Resource "Console"})
  
  
  (method meta (host-title)
    "Console")

  
  ;;;
  ;;;; Docked
  ;;;
  
  
  (method (get-tools-view)
    (locate 'tools))
  
  
  (method (focus-default)
    (let ((text (get-text)))
      (when text
        (acquire-focus~ text))))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-pad)
    (locate 'pad))

  
  (method public (get-text)
    (let ((console (get-selection~ (get-pad))))
      (essay console (get-text~ console))))
  
  
  ;;;
  ;;;; Register
  ;;;
  
  
  (method (register-console title)
    (let ((tool (new Console-Button title: title auto-size?: #t location: 'head image-name: {Bitmap-Resource "Process"} portfolio: :icons)))
      (add-tool~ (get-host) tool)
      (let ((pad (get-pad))
            (view (new Console-View button: tool)))
        (let ((text (get-text~ view))
              (select? (not (get-selection~ pad))))
          (add-view~ pad view)
          (when select?
            (set-selection~ pad view))
          (layout-view~ pad)
          (set-action-handler~ tool
            (new Event-Handler
              execute: (function (event)
                         (set-selection~ pad view)
                         (acquire-focus~ text))))
          (when select?
            (set-selected?~ tool #t))
          text))))
  
  
  (method (unregister-console console)
    (let ((host (get-host))
          (pad (get-pad))
          (view (get-parent~ (get-parent~ console))))
      (let ((focused? (or (eq? console (get-focus)) (eq? console (get-focused~ host))))
            (next (next-child~ pad view)))
        (remove-tool~ host (get-button~ view))
        (remove-view~ pad view)
        (when (and focused? next)
          (set-selection~ pad next)
          (set-selected?~ (get-button~ next) #t)
          (if (eq? host (get-focused-host))
              (acquire-focus~ next)
            (set-focused~ host (get-text~ next))))))
    (post-event
      (function ()
        (invalidate-window~ (get-player)))))
  
  
  ;;;
  ;;;; Events
  ;;;


  (method (on-print-detail-menu evt)
    (let* ((sender (get-sender~ evt))
           (combo (typecase sender ((Push-Icon) (get-combo~ sender)) ((Combo-Button) (get-combo~ sender))))
           (pos (new Point 0 (get-height~ (get-size~ sender))))
           (menu (new Print-Detail-Menu client: self))
           (pref (get-preferences '(tools console)))
           (detail (get-print-detail~ pref)))
      (set-checked?~ (child~ menu detail) #t)
      (layout-menu~ menu)
      (track-popup-menu~ sender menu pos initiator: combo)))


  (method (on-print-detail evt)
    (let ((pref (get-preferences '(tools console)))
          (detail (get-property~ evt detail:)))
      (set-preference pref 'print-detail detail)
      (user-message "Print detail set to {a}" detail)))
  
  
  (method (on-clear-console evt)
    (let ((text (get-text)))
      (clear-console~ text)
      (acquire-focus~ text))))


;;;
;;;; Console-View
;;;


(class Console-View extends Scroller-View
  
  
  (property button initialize {} getter generate)
  
  
  (form
    (<install>                             vscroll?: #t hscroll?: #t flat?: #t
      (<!>                   name: content
        (<Console-Text-View> name: text))))
  
  
  (method (get-text)
    (locate 'text)))


(register-autoclass Console-View)


;;;
;;;; Console-Button
;;;


(class Console-Button extends Radio-Tool
  
  
  (method (draw-background surface context)
    (if selected?
        (nextmethod surface context)
      (fill-rect~ surface (get-bounds) {Color name: Medium})))


  (method (draw-frame surface)
    (nextmethod surface)
    (let* ((rect (get-bounds))
           (left (get-left~ rect))
           (top (get-top~ rect))
           (right (- (get-right~ rect) 1))
           (bottom (- (get-bottom~ rect) 1)))
      (set-color~ surface {Color name: Medium})
      (pixel-to~ surface left top)
      (pixel-to~ surface right top)
      (pixel-to~ surface left bottom)
      (pixel-to~ surface right bottom))))


;;;
;;;; Print-Detail-Menu
;;;


(class Print-Detail-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Check-Item> name: :human    title: "Human" action-handler: {Event-Handler (:form :client) on-print-detail detail: :human})
      (<Check-Item> name: :text     title: "Textual" action-handler: {Event-Handler (:form :client) on-print-detail detail: :text})
      (<Check-Item> name: :reader   title: "Reader" action-handler: {Event-Handler (:form :client) on-print-detail detail: :reader})
      (<Check-Item> name: :describe title: "Describe" action-handler: {Event-Handler (:form :client) on-print-detail detail: :describe})))))
