;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Command Text-View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.ide.Command-Text-View jazz


(import (jazz.ide)
        (jazz.jml)
        (jazz.platform)
        (jazz.ui)
        (jazz.utilities))


(class Command-Text-View extends IDE-Text-View
  
  
  (slot mode             initialize {} getter generate)
  (slot client           initialize {} getter generate)
  (slot search-context   initialize {})
  (slot search-ranges    initialize '())
  (slot search-pristine? initialize #t)
  
  
  (form
    (<install>
      focus-selects-all?: #t
      show-unfocused-selection?: #f
      accepts-returns?: #f
      left-padding: 2
      top-padding: 2
      tooltip?: #t
      tooltip-text: "Command Shell"))
  
  
  ;;;
  ;;;; Text
  ;;;
  
  
  (method (auto-complete?)
    (eq? mode 'execute))
  
  
  ;;;
  ;;;; Focus
  ;;;
  
  
  (method (focus-gain old)
    (nextmethod old)
    (set! client old))
  
  
  (method (focus-actions)
    (cons (case mode
            ((search-forward search-backward) (find-actions 'command-search))
            (else (find-actions 'command-execute)))
          (nextmethod)))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method (key-press key)
    (nextmethod key)
    (case mode
      ((search-forward search-backward)
       (search mode #f))))
  
  
  (method (backspace-press key (word?: word? #f))
    (nextmethod key word?: word?)
    (case mode
      ((search-forward search-backward)
       (search mode #f))))
  
  
  ;;;
  ;;;; Mode
  ;;;
  
  
  (method public (select-mode mode value)
    (case mode
      ((search-forward search-backward)
       (select-mode-search mode)
       (acquire-focus)
       (when value
         (set-text value)
         (search mode #t)))
      (else
       (select-mode-execute)
       (acquire-focus))))
  
  
  ;;;
  ;;;; Execute
  ;;;
  
  
  (method (select-mode-execute)
    (set! mode 'execute))
  
  
  (method (on-done evt)
    (when client
      (acquire-focus~ client)))
  
  
  ;;;
  ;;;; Search
  ;;;
  
  
  (method (next-page evt)
    (search 'search-forward #t))
  
  
  (method (prior-page evt)
    (search 'search-backward #t))
    
  
  (method (select-mode-search mode)
    (set! mode~self mode)
    (set! search-pristine? #t))
  
  
  (method (on-search-forward evt)
    (search 'search-forward #t))
  
  
  (method (on-search-backward evt)
    (search 'search-backward #t))
  
  
  (method (on-search-finish evt)
    (search-finish))

  
  (method (search direction again?)
    (when (is? client Text-View)
      (when search-pristine?
        (set! search-context (make-search-context "" ignore-case?: #t))
        (set! search-ranges (list (get-selection~ client)))
        (set! search-pristine? #f))
      (let ((content (get-string-content))
            (target (get-target~ search-context)))
        (let ((prefix (prefix (list content target))))
          (reset-prefix (cardinality prefix))
          (let ((suffix (subseq content (cardinality prefix) (cardinality content))))
            (if (and (empty-string? suffix) (not again?))
                (set-selection~ client (last search-ranges))
              (for-each (function (key)
                          (search-key key direction))
                        suffix)
              (when again?
                (search-key {} direction))))))
      (when again?
        (set-caret (text-ending)))))
  
  
  (method (search-pos range direction)
    (case direction
      ((search-forward) (get-start~ range))
      ((search-backward) (get-end~ range))))
  
  
  (method (reset-prefix len)
    (set-target~ search-context (subseq (get-target~ search-context) 0 len))
    (set! search-ranges (subseq search-ranges 0 (+ len 1))))
  
  
  (method (search-key key direction)
    (let ((target (if (not key)
                      (get-target~ search-context)
                    (concatenate (get-target~ search-context) (format "{c}" key))))
          (last-range (last search-ranges)))
      (let ((pos (case direction
                   ((search-forward) (if (not key) (get-end~ last-range) (get-start~ last-range)))
                   ((search-backward) (if (not key) (get-start~ last-range) (nu+ (get-start~ last-range) (cardinality target))))))
            (reversed? (eq? direction 'search-backward)))
        (set-target~ search-context target)
        (let ((found (text-multisearch~ client (list search-context) start: pos reversed?: reversed?)))
          (define (update-range range)
            (if (not key)
                (let ((pair (last-pair search-ranges)))
                  (set-car! pair range))
              (set! search-ranges (append search-ranges (list range)))))
          
          (if (not found)
              (begin
                (update-range last-range)
                (bell))
            (bind (context . pos) found
              (let ((range (new Range$Cell$ pos (nu+ pos (cardinality target)))))
                (update-range range)
                (set-selection~ client range))))))))
  
  
  (method (search-finish)
    (when client
      (acquire-focus~ client)))))
