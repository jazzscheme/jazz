;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Command Text-View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library protected jazz.ide.Command-Text-View jazz


(import (jazz.graphic)
        (jazz.ide)
        (jazz.jml)
        (jazz.library)
        (jazz.platform)
        (jazz.ui)
        (jazz.utilities))


(class Command-Text-View extends IDE-Text-View
  
  
  (slot mode             initialize {} getter generate)
  (slot client           initialize {} getter generate)
  (slot client-selection initialize {} getter generate)
  
  
  (form
    (<install>
      focus-selects-all?: #t
      show-unfocused-selection?: #f
      accepts-returns?: #f
      left-padding: 2
      top-padding: 2
      tooltip?: #t
      tooltip-text: "Command Shell"
      default-style: {Text-Style name: Default font: {Font name: Label}}))
  
  
  (method (component-alias name)
    (case name
      ((:mode) mode)
      (else (nextmethod name))))
  
  
  ;;;
  ;;;; Focus
  ;;;
  
  
  (method (focus-gain old)
    (nextmethod old)
    (set! client old)
    (set! client-selection (essay (and client (is? client Text-View)) (get-selection~ client))))
  
  
  (method (focus-lose view)
    (nextmethod view)
    (finish~ mode))
  
  
  (method (focus-actions)
    (when (not mode)
      (select-mode-execute))
    (cons (focus-actions~ mode)
          (nextmethod)))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method (key-press key)
    (nextmethod key)
    (key-press~ mode key))
  
  
  (method (backspace-press key (word?: word? #f))
    (nextmethod key word?: word?)
    (backspace-press~ mode key word?: word?))
  
  
  (method (escape-press key)
    (escape-press~ mode key))
  
  
  (method (next-page evt)
    (next-page~ mode))
  
  
  (method (prior-page evt)
    (prior-page~ mode))
  
  
  (method (auto-complete?)
    (auto-complete?~ mode))
  
  
  ;;;
  ;;;; Mode
  ;;;
  
  
  (method public (select-search direction range value again? whole-words?)
    (select-mode-search direction range)
    (acquire-focus)
    (when value
      (set-text value)
      (select-all)
      (search~ mode direction again? whole-words?: whole-words?)))
    
  
  (method (select-mode-search direction range)
    (set! mode~self (new Search-Mode self direction range)))
  
  
  (method public (select-extended)
    (select-mode-extended)
    (acquire-focus))
  
  
  (method (select-mode-extended)
    (set! mode (new Extended-Mode self)))
  
  
  (method public (select-execute)
    (select-mode-execute)
    (acquire-focus))
  
  
  (method (select-mode-execute)
    (set! mode (new Execute-Mode self))))


;;;
;;;; Command-Mode
;;;


(class Command-Mode extends Object
  
  
  (slot text)
  
  
  (method (initialize text)
    (nextmethod)
    (set! text~self text))
  
  
  (method (get-client)
    (get-client~ text))
  
  
  (method (get-client-selection)
    (get-client-selection~ text))
    
  
  (method virtual (key-press key)
    )
  
  
  (method virtual (backspace-press key (word?: word? #f))
    )
  
  
  (method virtual (escape-press key)
    (cancel))
  
  
  (method virtual (next-page)
    )
  
  
  (method virtual (prior-page)
    )

  
  (method virtual (auto-complete?)
    #f)
  
  
  (method virtual (finish)
    (let ((client (get-client)))
      (when client
        (acquire-focus~ client))))
  
  
  (method virtual (cancel)
    (let ((client (get-client)))
      (when client
        (acquire-focus~ client)))))


;;;
;;;; Search-Mode
;;;


(class Search-Mode extends Command-Mode
  
  
  (slot direction       initialize {})
  (slot context         initialize (make-search-context ""))
  (slot ranges          initialize '())
  (slot found-style     initialize {})
  (slot highlight-style initialize {})
  
  
  (method (initialize text direction range)
    (nextmethod text)
    (set! direction~self direction)
    (prepare-search range))
  
  
  (method (prepare-search range)
    (set-target~ context "")
    (let ((client (get-client))
          (client-selection (get-client-selection)))
      (let ((text? (and client (is? client Text-View))))
        (set! ranges (if text? (list (or range client-selection)) '()))
        (set! found-style (if text? (new Text-Style name: 'Found base: 'Default domain: (get-styles-domain~ client) highlight: {Color red: 255 green: 162 blue: 47}) {}))
        (set! highlight-style (if text? (new Text-Style name: 'Highlight base: 'Default domain: (get-styles-domain~ client) highlight: {Color red: 255 green: 255 blue: 120}) {})))))
  
  
  (method (focus-actions)
    search-actions)
  
  
  (method (key-press key)
    (search direction #f whole-words?: #f))
  
  
  (method (backspace-press key (word?: word? #f))
    (search direction #f whole-words?: #f))
  
  
  (method (escape-press key)
    (cancel))
  
  
  (method (next-page)
    (search 'forward #t))
  
  
  (method (prior-page)
    (search 'backward #t))
  
  
  (method (select-whole-words? whole-words?)
    (set-whole-words?~ context whole-words?)
    (user-message "Search whole-words {a}" (if whole-words? "on" "off")))
  
  
  (method (select-ignore-case? ignore-case?)
    (set-ignore-case?~ context ignore-case?)
    (user-message "Search ignore-case {a}" (if ignore-case? "on" "off")))
  
  
  (method (on-whole-words evt)
    (select-whole-words? (not (get-whole-words?~ context)))
    (rehighlight-target))
  
  
  (method (on-ignore-case evt)
    (select-ignore-case? (not (get-ignore-case?~ context)))
    (rehighlight-target))
  
  
  (method (on-forward evt)
    (search 'forward #t))
  
  
  (method (on-backward evt)
    (search 'backward #t))
  
  
  (method (on-finish evt)
    (finish))
  
  
  (method (on-cancel evt)
    (cancel))

  
  (method (search direction again? (whole-words?: whole-words? 'keep))
    (define (reset-prefix len)
      (set-target~ context (subseq (get-target~ context) 0 len))
      (set-regexp~ context #f)
      (set! ranges (subseq ranges 0 (+ len 1))))
    
    (define (search-key client key whole-words?)
      (let ((target (if (not key)
                        (get-target~ context)
                      (concatenate (get-target~ context) (format "{c}" key))))
            (last-range (last ranges)))
        (let ((pos (case direction
                     ((forward) (if (not key) (get-end~ last-range) (get-start~ last-range)))
                     ((backward) (if (not key) (get-start~ last-range) (nu+ (get-start~ last-range) (cardinality target))))))
              (reversed? (eq? direction 'backward)))
          (set-target~ context target)
          (set-regexp~ context #f)
          (set-whole-words?~ context whole-words?)
          (let ((found (text-multisearch~ client (list context) start: pos reversed?: reversed?)))
            (define (update-range range)
              (if (not key)
                  (let ((pair (last-pair ranges)))
                    (set-car! pair range))
                (set! ranges (append ranges (list range)))))
            
            (if (not found)
                (begin
                  (update-range last-range)
                  (bell))
              (bind (from . to) found
                (let ((range (new Range$Cell$ from to)))
                  (update-range range))))))))
    
    (define (highlight-target client found-range)
      (remove-highlights)
      (let ((target (get-target~ context)))
        (unless (empty-string? target)
          (for-each (function (found)
                      (bind (start . end) found
                        (let ((range (new Range$Cell$ start end)))
                          (unless (nu=? range found-range)
                            (add-highlight~ client (new Text-Highlight range highlight-style))))))
                    (text-multisearch-all~ client (list context)))))
      (add-highlight~ client (new Text-Highlight found-range found-style))
      (set-selection~ client found-range))
    
    (let ((client (get-client)))
      (when (is? client Text-View)
        (unless (eq? whole-words? 'keep)
          (select-whole-words? whole-words?))
        (let ((content (get-string-content~ text))
              (target (get-target~ context)))
          (let ((prefix (prefix (list content target))))
            (reset-prefix (cardinality prefix))
            (let ((suffix (subseq content (cardinality prefix) (cardinality content))))
              (when (or again? (not (empty-string? suffix)))
                (let ((whole-words? (get-whole-words?~ context)))
                  (iterate suffix
                           (function (key)
                             (search-key client key #f)))
                  (when again?
                    (search-key client {} whole-words?))))
              (highlight-target client (last ranges))))))))
  
  
  (method (rehighlight-target)
    (search direction #f))
  
  
  (method (remove-highlights)
    (let ((client (get-client)))
      (when (is? client Text-View)
        (remove-all-highlights~ client))))
  
  
  (method (finish)
    (let ((client (get-client)))
      (when client
        (remove-highlights)
        (acquire-focus~ client))))
  
  
  (method (cancel)
    (let ((client (get-client))
          (client-selection (get-client-selection)))
      (when client
        (remove-highlights)
        (when client-selection
          (set-selection~ client client-selection))
        (acquire-focus~ client)))))


;;;
;;;; Search-Actions
;;;


(class Search-Actions extends Actions
  
  
  (form
    (<install>
      (<Action-Item> name: whole-words action-handler: {Event-Handler (:focus :mode) on-whole-words} shortcut: {Shortcut :alt #\W})
      (<Action-Item> name: ignore-case action-handler: {Event-Handler (:focus :mode) on-ignore-case} shortcut: {Shortcut :alt #\I})
      (<Action-Item> name: forward     action-handler: {Event-Handler (:focus :mode) on-forward} shortcut: {Shortcut :control #\S})
      (<Action-Item> name: backward    action-handler: {Event-Handler (:focus :mode) on-backward} shortcut: {Shortcut :control #\R})
      (<Action-Item> name: current     action-handler: {Event-Handler (:focus :mode) on-cancel} shortcut: {Shortcut :alt #\S})
      (<Action-Item> name: finish      action-handler: {Event-Handler (:focus :mode) on-finish} shortcut: {Shortcut Return})
      (<Action-Item> name: cancel      action-handler: {Event-Handler (:focus :mode) on-cancel} shortcut: {Shortcut :control #\G}))))


(definition search-actions
  (new Search-Actions))


;;;
;;;; Extended-Mode
;;;


(class Extended-Mode extends Command-Mode
  
  
  (method (focus-actions)
    extended-actions)
  
  
  (method (on-find-file evt)
    )
  
  
  (method (on-finish evt)
    (finish))
  
  
  (method (on-cancel evt)
    (cancel)))


;;;
;;;; Extended-Actions
;;;


(class Extended-Actions extends Actions
  
  
  (form
    (<install>
      (<Action-Item> name: find-file action-handler: {Event-Handler (:focus :mode) on-find-file} shortcut: {Shortcut :control #\F})
      (<Action-Item> name: finish    action-handler: {Event-Handler (:focus :mode) on-finish} shortcut: {Shortcut Return})
      (<Action-Item> name: cancel    action-handler: {Event-Handler (:focus :mode) on-cancel} shortcut: {Shortcut :control #\G}))))


(definition extended-actions
  (new Extended-Actions))


;;;
;;;; Execute-Mode
;;;


(class Execute-Mode extends Command-Mode
  
  
  (method (focus-actions)
    execute-actions)
  
  
  (method (auto-complete?)
    #t)
  
  
  (method (on-finish evt)
    (finish))
  
  
  (method (on-cancel evt)
    (cancel)))


;;;
;;;; Execute-Actions
;;;


(class Execute-Actions extends Actions
  
  
  (form
    (<install>
      (<Action-Item> name: finish action-handler: {Event-Handler (:focus :mode) on-finish} shortcut: {Shortcut Return})
      (<Action-Item> name: cancel action-handler: {Event-Handler (:focus :mode) on-cancel} shortcut: {Shortcut :control #\G}))))


(definition execute-actions
  (new Execute-Actions)))
