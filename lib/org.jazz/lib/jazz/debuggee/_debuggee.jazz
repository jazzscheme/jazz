;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Debuggee
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.debuggee jazz


(export (jazz.debuggee.autoload))
(import (jazz.debuggee.autoload)
        (jazz.debuggee.stub)
        (jazz.jrm)
        (jazz.jrm.register.stub))


;;;
;;;; Local
;;;


(definition local-process
  {})


(definition public (get-local-process)
  local-process)


(definition public (setup-local-process)
  (when (not local-process)
    (set! local-process (new Debuggee-Process-Local-Proxy (new Debuggee-Process)))))


;;;
;;;; Controller
;;;


(definition controller-debugger
  {})


(definition public (get-controller-debugger)
  controller-debugger)


(definition public (attach-to-controller controller-register)
  (detach-from-controller)
  (set! controller-debugger (require-object~ controller-register 'debugger))
  (setup-local-process)
  (setup-debuggee-console-ports)
  (attach-process~ controller-debugger local-process)
  (set-exception-debugger jazz-exception-debugger)
  (set-exception-hook exception-debugger-hook))


(definition public (detach-from-controller)
  (when controller-debugger
    (detach-from-debugger)
    (set! local-process {})))


(definition public (detach-from-debugger)
  (when controller-debugger
    (when (live?~ controller-debugger)
      (detach-process~ controller-debugger local-process))
    (set! controller-debugger {})))


(definition (setup-debuggee)
  (when (command-argument "debug")
    (start-remoting-server))
  (when (command-argument "controllerhost")
    (let ((host (string->host (command-argument "controllerhost")))
          (port (string->port (command-argument "controllerport"))))
      (attach-to-controller (new-remote-register host port)))))


(definition (update-debuggee)
  (when controller-debugger
    (update-process~ controller-debugger local-process)))


;;;
;;;; Stops
;;;


(definition *stops*
  '())


(definition with-stops-mutex
  (let ((mutex (make-mutex 'stops)))
    (lambda (thunk)
      (mutex-lock! mutex)
      (thunk)
      (mutex-unlock! mutex))))


(definition (register-stop stop)
  (with-stops-mutex
    (function ()
      (set! *stops* (append *stops* (list stop))))))


(definition (unregister-stop stop)
  (with-stops-mutex
    (function ()
      (set! *stops* (remove! stop *stops*)))))


(definition (get-thread-stops thread)
  (let ((queue (new-queue)))
    (for-each (function (stop)
                (when (eq? (get-thread~ stop) thread)
                  (enqueue queue (new Debuggee-Stop-Local-Proxy stop))))
              *stops*)
    (queue-list queue)))


;;;
;;;; Console
;;;


(definition *debuggee-console-port*
  {})


(definition (debuggee-console-port)
  *debuggee-console-port*)

(definition (set-debuggee-console-port port)
  (set! *debuggee-console-port* port))


(definition (setup-debuggee-console-ports)
  (receive (head tail) (open-string-pipe)
    (set-debuggee-console-port head)
    (set-console-input-port tail)
    (set-console-output-port tail)
    (start-debuggee-console-pump)
    (start-repl-thread)))


(definition (start-debuggee-console-pump)
  (let ((port (debuggee-console-port))
        (console (register-console~ controller-debugger local-process)))
    (thread-start!
      (new-thread
        (function ()
          (let (iterate)
            (let ((line (read-line port)))
              (console-output~ controller-debugger console line)
              (iterate))))
        "Debuggee Console Pump"))))


(definition (start-repl-thread)
  (let ((port (console-input-port)))
    (thread-start!
      (new-thread
        (function ()
          (let (iterate)
            (with-restart-catcher 'resume-repl "Resume repl" {}
              (function ()
                (read-eval-print port)))
            (iterate)))
        "Repl"))))


(definition (read-eval-print port)
  (format port #"\($\)\(color Dark-Red {a}\)\(prompt\){%}{!}"#
    (evaluate (read port))))


(definition (evaluate expr)
  (let ((process (get-process)))
    (if (not process)
        "Error: Process not yet started"
      (car (eval-expressions~ process (list expr))))))


;;;
;;;; Exception
;;;


(definition (jazz-exception-debugger exc)
  (let ((debugger (get-controller-debugger)))
    (if (or (not debugger) (not (jazz-debugger?)))
        (invoke-exception-hook system-exception-hook exc)
      (with-system-exception-debugger
        (function ()
          (continuation-capture
            (function (continuation)
              (let ((reason (exception-reason exc)))
                (invoke-debugger 'exception reason continuation)))))))))


(definition (with-jazz-exception-debugger thunk)
  (with-exception-debugger jazz-exception-debugger
    thunk))


(definition (break (reason {}))
  (continuation-capture
    (function (continuation)
      (invoke-debugger 'break reason continuation))))


;;;
;;;; Debugger
;;;


(definition (invoke-debugger kind reason continuation)
  (let* ((thread (current-thread))
         (restarts (compute-restarts thread))
         (stop (new Debuggee-Stop thread kind reason continuation restarts)))
    (dynamic-wind
      (function ()
        (register-stop stop))
      (function ()
        (let ((thread-proxy (new Debuggee-Thread-Local-Proxy (new Debuggee-Thread thread)))
              (stop-proxy (new Debuggee-Stop-Local-Proxy stop)))
          (debuggee-stop~ controller-debugger local-process thread-proxy stop-proxy kind reason))
        (let ((process (get-process)))
          (if (not process)
              (debuggee-loop stop)
            (debuggee-loop~ process stop))))
      (function ()
        (unregister-stop stop)))))


(definition (compute-restarts thread)
  (let ((previous-stops (get-thread-stops thread))
        (all-restarts (current-restarts)))
    (map (function (restart)
           (new Debuggee-Restart-Local-Proxy (new Debuggee-Restart thread restart)))
         (if (null? previous-stops)
             all-restarts
           (let ((skip (apply max (map (function (stop) (length (get-restarts~ stop))) previous-stops))))
             (subseq all-restarts 0 (- (length all-restarts) skip)))))))


(definition (debuggee-loop stop)
  (loop~ stop))


;;;
;;;; View
;;;


(definition *current-view-debugger*
  #f)


(definition public (current-view-debugger)
  (when (not *current-view-debugger*)
    (set! *current-view-debugger* (new View-Debugger)))
  *current-view-debugger*))
