;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Debugger Server
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.debugger.server.Debugger-Server jazz


(import (jazz.debugger)
        (jazz.jrm)
        (jazz.library)
        (jazz.platform)
        (jazz.ui.window)
        (jazz.utilities))


(class Debugger-Server extends Object
  
  
  (slot debuggees         initialize '())
  (slot selected-debuggee initialize {})
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-debuggees (validate?: validate? #f))
    (when validate?
      (validate-debuggees))
    debuggees)
  
  
  (method public (get-selected-debuggee)
    selected-debuggee)
  
  
  (method public (set-selected-debuggee debuggee)
    (set! selected-debuggee debuggee)
    {})
  
  
  ;;;
  ;;;; Window
  ;;;
  
  
  (method public (get-toplevel-hwnd)
    (get-handle~ (get-toplevel)))
  
  
  ;;;
  ;;;; Process
  ;;;


  (method public (attach-debuggee debuggee)
    (unless (member? debuggee debuggees test: proxy=?)
      (set! debuggees (append! debuggees (list debuggee)))
      (when (not selected-debuggee)
        (select-debuggee debuggee))
      {}))


  (method public (detach-debuggee debuggee)
    (when (member? debuggee debuggees test: proxy=?)
      (set! debuggees (remove! debuggee debuggees test: proxy=?))
      (when (proxy=? debuggee selected-debuggee)
        (select-debuggee (and (not-null? debuggees) (car debuggees))))
      {}))
  
  
  ;;;
  ;;;; Selected
  ;;;
  
  
  (method public (select-debuggee debuggee)
    (when (not (proxy=? debuggee selected-debuggee))
      (set! selected-debuggee debuggee)
      (let* ((appl (get-application))
             (icon (essay debuggee (get-icon~ debuggee)))
             (status (get-status-bar~ appl)))
        (when status
          (let ((item (locate-component~ status 'debugger)))
            (when item
              (set-icon~ item icon))))
        (let ((presented (essay debuggee (present-debuggee~ appl debuggee))))
          (user-message (format "{a} selected" (if (not debuggee) "None" presented)))))
      {}))
  
  
  (method public (select-next)
    (let* ((rank (find debuggees selected-debuggee test: proxy=?))
           (debuggee (element debuggees (modulo (+ rank 1) (length debuggees)))))
      (select-debuggee debuggee)
      {}))
  
  
  ;;;
  ;;;; Validate
  ;;;
  
  
  (method (validate-debuggees)
    (set! debuggees (collect-if valid-debuggee? debuggees))
    (when (not (member? selected-debuggee debuggees test: proxy=?))
      (select-debuggee (and (not-null? debuggees) (car debuggees)))))
  
  
  (method (valid-debuggee? debuggee)
    (or (is? debuggee Local-Proxy)
        (live?~ debuggee)))
  
  
  ;;;
  ;;;; Close
  ;;;
  
  
  (method public (detach-debuggees)
    (validate-debuggees)
    (let ((local-debuggee (get-local-debuggee~ (get-application))))
      (for-each (function (debuggee)
                  (when (not (proxy=? debuggee local-debuggee))
                    (detach-from-controller~ debuggee)))
                debuggees))
    {})
  
  
  ;;;
  ;;;; Debugging
  ;;;
  
  
  (method public (debug-message message)
    (debug message)
    {})
  
  
  (method public (status-message message)
    (user-message message)
    {})
  
  
  ;;;
  ;;;; Debugger
  ;;;


  (method public (debug-exception type message stack)
    (when (eq? (present-exception type message) 'yes)
      (message-box stack)
      {}))


  (method (present-exception type message)
    (message-box (format "{a}: {a}" type message)
                 type: 'error
                 yes: "Debug"
                 cancel: "Ignore"))))
