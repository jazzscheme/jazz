;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Inspector
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.language.jazz.inspector jazz


(import (jazz.language.jazz))


;;;
;;;; Package
;;;


(definition Unbound
  (cons {} {}))


(definition public (package-unbound)
  Unbound)


(definition public (package-info name value (kind :value))
  (let* ((unbound? (eq? value Unbound))
         (serial (object->serial value))
         (presented (cond (unbound? "<unbound>") (else (->string value :reader))))
         (more? (if unbound? #f (or (memq? kind '(frame: context:)) (more-value? value)))))
    (when (> (cardinality presented) 512)
      (set! presented (concatenate (subseq presented 0 128) "...")))
    (list name serial presented more? kind)))


(definition (more-value? value)
  (and (not (null? value))
       (not (empty-string? value))
       (is-not? value Symbol)
       (is-not? value Boolean)
       (is-not? value Char)
       (is-not? value Number)
       (is-not? value Field)
       (not (category? value))))


;;;
;;;; Inspect
;;;


(definition public (inspect-value value)
  (typecase value
    ((List) (inspect-list value))
    ((Table) (inspect-table value))
    ((Sequence) (inspect-sequence value))
    (else (inspect-slots value))))


(definition (inspect-list lst)
  (let ((fact (new List-Factory))
        (rank 0))
    (while (not-null? lst)
      (if (pair? lst)
          (begin
            (put~ fact (package-info rank (car lst)))
            (set! lst (cdr lst)))
        (put~ fact (package-info 'rest lst))
        (set! lst '()))
      (increase! rank))
    (get-output~ fact)))


(definition (inspect-table table)
  (let ((fact (new List-Factory)))
    (iterate-table table
      (function (key value)
        (put~ fact (package-info key value))))
    (get-output~ fact)))


(definition (inspect-sequence seq)
  (map package-info (naturals 0 (cardinality seq)) (coerce seq List)))


(definition (inspect-slots object)
  (let* ((class (class-of object))
         (slots (all-slots class))
         (infos
           (map (function (slot)
                  (package-info (field-name slot)
                                (cond ((property? slot)
                                       (read-property object slot))
                                      (else
                                       (slot-value object (field-name slot))))))
                slots)))
    (if #t
        infos
      (sort < infos key: car)))))
