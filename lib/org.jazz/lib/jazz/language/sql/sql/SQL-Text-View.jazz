;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; SQL Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.language.sql.sql.SQL-Text-View jazz


(import (jazz.language.sql)
        (jazz.language.sql.view)
        (jazz.library)
        (jazz.utilities)
        (jazz.ui)
        (jazz.ui.view))


(class SQL-Text-View extends Code-Text-View)


@convert-search-string


(class SQL-Text-View extends Code-Text-View
  
  
  (method (install rest)
    (nextmethod rest)
    (let ((pref (preferences)))
      (add-style (new Text-Style name: 'Title        base: 'Default color: (get-title-color~ pref)))
      (add-style (new Text-Style name: 'Line-Comment base: 'Default color: (get-line-comment-color~ pref)))
      (add-style (new Text-Style name: 'Definition   base: 'Default color: (get-definition-color~ pref)))
      (add-style (new Text-Style name: 'Keyword      base: 'Default color: (get-keyword-color~ pref)))))
  
  
  (method meta (external-name . rest)
    'sql-text)

  
  (method (preferences)
    (get-preferences '(documents sql-text)))

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method (host-workspaces)
    '(sql))
  

  ;;;
  ;;;; Actions
  ;;;
  

  (method (focus-actions)
    (cons (find-actions 'sql-text)
          (nextmethod)))

  
  ;;;
  ;;;; Completion
  ;;;


  (method (favorite-completions)
    (get-completions~ (get-preferences '(documents sql-text))))
  
  
  (method (commented? pos)
    (let ((style (get-style-at pos)))
      (and style
           (or (inherits?~ style 'Title)
               (inherits?~ style 'Line-Comment)))))

  
  ;;;
  ;;;; Tabulation
  ;;;


  (method (return-press key)
    (nextmethod key)
    (tabulate))


  ;;;
  ;;;; Explorer
  ;;;


  (method (explorer-class)
    SQL-Explorer)
 
  
  ;;;
  ;;;; Expr
  ;;;
  
  
  (method (get-context)
    '(sql))


  (method (current-name)
    (name-at (current-expr-range)))
  
  
  (method (name-at range)
    (when range
      (let ((name (get-string range)))
        (when (> (length name) 0)
          (if (= (element name 0) #\open-brace)
              (read-string-element name)
            name)))))


  (method (current-expr)
    (current-name))
  
  
  (method (current-expr-range)
    (if (and (not completion-active?) (has-selection?))
        (get-selection)
      (expr-range (get-end))))
  
  
  (method (expr-range pos)
    (let ((expl (new SQL-Explorer self start: pos end: pos)))
      (outer-expr~ expl)
      (get-range~ expl)))
  
  
  (method (expr-at pos)
    (name-at (expr-range pos)))

  
  ;;;
  ;;;; Chapters
  ;;;
  
  
  (constant headers
    (list
      "--- "))
  
  
  (method (get-headers)
    headers)
  
  
  (method (get-first-special?)
    #f)

  
  ;;;
  ;;;; Definitions
  ;;;
  
  
  (constant Definitions
    '("*"))
  
  
  (method (get-definitions)
    Definitions)

  
  ;;;
  ;;;; Keywords
  ;;;
  
  
  (constant Keywords
    '(;"="
      ;"<>"
      ;"<"
      ;"<="
      ;">"
      ;">="
      ;"("
      ;")"
      ;";"
      "ACTION"
      "ADD"
      "ALL"
      "ALTER"
      "AND"
      "ANY"
      "AS"
      "ASC"
      "BEGIN"
      "BETWEEN"
      "BY"
      "CASCADE"
      "CASE"
      "CHECK"
      "CLUSTERED"
      "COLLATE"
      "COMMIT"
      "CONSTRAINT"
      "CONTAINS"
      "COUNT"
      "CREATE"
      "CURSOR"
      "DECLARE"
      "DEFAULT"
      "DEFINE"
      "DELETE"
      "DESC"
      "DISTINCT"
      "DROP"
      "END"
      "EXEC"
      "EXECUTE"
      "EXISTS"
      "FILLFACTOR"
      "FOR"
      "FOREIGN"
      "FROM"
      "FULL"
      "GROUP"
      "HAVING"
      "IN"
      "INNER"
      "INSENSITIVE"
      "INSERT"
      "INTO"
      "IS"
      "JOIN"
      "KEY"
      "LEFT"
      "LIKE"
      "MAX"
      "MIN"
      "MODIFY"
      "NO"
      "NOCHECK"
      "NONCLUSTERED"
      "NOT"
      "NULL"
      "OF"
      "ON"
      "OR"
      "ORDER"
      "OUTER"
      "PARAMETERS"
      "PIVOT"
      "PRIMARY"
      "PRINT"
      "PROC"
      "PROCEDURE"
      "REFERENCES"
      "REPLICATION"
      "RETURN"
      "RIGHT"
      "ROWGUIDCOL"
      "SELECT"
      "SET"
      "SOME"
      "SUM"
      "TABLE"
      "TEXTIMAGE_ON"
      "THEN"
      "TOP"
      "TRANSACTION"
      "TRANSFORM"
      "TRIGGER"
      "TRUNCATE"
      "UNION"
      "UNIQUE"
      "UPDATE"
      "VALUES"
      "VIEW"
      "WHEN"
      "WHERE"
      "WITH"))
  
  
  (method protected virtual (get-keywords)
    Keywords)

  
  ;;;
  ;;;; Coloring
  ;;;
  
  
  (definition Syntax-Targets
    {})
  
  (definition Syntax-Automata
    {})
  
  
  (method (syntax-targets)
    (unless Syntax-Targets
      (set! Syntax-Targets (compute-syntax-targets)))
    Syntax-Targets)
  
  
  (method (syntax-automata)
    (unless Syntax-Automata
      (set! Syntax-Automata (compute-syntax-automata)))
    Syntax-Automata)
  
  
  (method (compute-syntax-targets)
    (append (list (list "--" 'line-comment #f))
            (map (function (str) (list str 'definition)) (get-definitions))
            (map (function (str) (list str 'keyword)) (get-keywords))))
        
  
  (method (compute-syntax-automata)
    (build-search-automata (syntax-targets) #t #t self sql-constituent?))
  
  
  (method (colorize-type found type start end colorizer recolorize?)
    (case type
      ((line-comment) (colorize-line-comment found start end colorizer 'Line-Comment 'Title #\minus))
      ((definition)   (colorize-keyword      found start end colorizer 'Definition))
      ((keyword)      (colorize-keyword      found start end colorizer 'Keyword))))
  
  
  ;;;
  ;;;; Eval
  ;;;
  
  
  (method (on-evaluate evt)
    (evaluate-selection))
  
  
  (method protected virtual (evaluate-selection)
    (let* ((range (if (has-selection?) (get-selection) (statement-range)))
           (string (uncommented-string range))
           (statements (collect-if (function (statement) (not (whitespace? statement))) (split string ";")))
           (console (get-sql-console-text)))
      (evaluate-statements~ console statements #f)))
  
  
  (method (statement-range)
    (let* ((pos (statement-pos))
           (start (search-string ";" position: pos reverse?: #t))
           (end (search-string ";" position: pos)))
      ;; to try and debug a bug
      @not-now
      (debug 'start: start 'end: end)
      (new Range$Cell$
        (if start (next start 1) (text-beginning))
        (if end end (text-ending)))))
  
  
  (method (statement-pos)
    (let* ((start (get-start))
           (previous (previous start 1)))
      (if (and previous (= (get-char previous) #\semi-colon))
          previous
        start)))
  
  
  (method (uncommented-string range)
    (let ((lines (range-strings range)))
      (join-lines (map uncommented-line lines))))
  
  
  (method (uncommented-line line)
    (let ((pos (search-string~ line "--")))
      (if (null? pos)
          line
        (subseq line 0 pos))))))
