;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; COM
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.platform.windows.com jazz


(import (jazz.library)
        (jazz.platform.types)
        (jazz.platform.windows.WinBase)
        (jazz.platform.windows.WinTypes)
        (jazz.platform.windows.WinKernel)
        (time))


;;;
;;;; Initialize
;;;


(definition initialize-com
  (let ((initialized? #f))
    (function ()
      (unless initialized?
        (LoadLibrary "ole32")
        (CoInitializeEx #f COINIT_APARTMENTTHREADED)
        (set! initialized? #t)))))


(definition COINIT_APARTMENTTHREADED 2)
(definition CLSCTX_SERVER (+ 1 4 16))


(definition (CoInitializeEx n/a flags)
  (let ((proc (c-function (VOID* DWORD) HRESULT "CoInitializeEx")))
    (validate-hresult (proc #f flags))))


;;;
;;;; HRESULT
;;;


(definition S_OK
  0)

(definition S_FALSE
  1)


(class COM-Exception extends Exception
  
  
  (slot code)
  (slot message)
  
  
  (method (initialize code message)
    (set! code~self code)
    (set! message~self message))
  
  
  (method (print printer readably)
    (print-unreadable self printer
      (function (printer)
        (format printer "#x{x}{? {a}~}" code message))))
  
  
  (method (get-code)
    code)
  
  
  (method (get-message)
    (either message (format "COM exception (#x{x})" code))))


(c-type HRESULT (native unsigned-long))


(definition (validate-hresult hresult)
  (case hresult
    ((0 1) ; S_OK or S_FALSE
     hresult)
    ((#x80004002)
     (error "ERROR - E_NOINTERFACE" hresult))
    ((#x80040154)
     (error "ERROR - class not registered?" hresult))
    ((#x800401f0)
     (error "ERROR - CO_E_NOTINITIALIZED" hresult))
    ((#x80070057)
     (error "ERROR - E_INVALIDARG" hresult))
    ((#x800a0d5d)
     (error "ERROR - wrong value type" hresult))
    (else
     (throw (new COM-Exception hresult {})))))


(definition (validate-hresult2 hresult iunknown refiid)
  (case hresult
    ((0 1) ; S_OK or S_FALSE
     hresult)
    (else
     (let ((message (extract-errorinfo-message hresult iunknown refiid)))
       (if message
           (throw (new COM-Exception hresult message))
         (validate-hresult hresult))))))


(definition (extract-errorinfo-message hresult iunknown refiid)
  (receive (result isupport) (CoQueryNoValidate iunknown (cointerface-guid ISupportErrorInfo))
    (when (= result S_OK)
      (prog1
          (when (= (ISupportErrorInfo$InterfaceSupportsErrorInfo isupport refiid) S_OK)
            (let ((ierror (GetErrorInfo)))
              (when ierror
                (receive (result message) (IErrorInfo$GetDescription ierror)
                  (CoRelease ierror)
                  (when (= result S_OK)
                    (BSTR-decode message))))))
        (CoRelease isupport)))))


;;;
;;;; BSTR
;;;


(c-type BSTR  (pointer (native wchar_t)))
(c-type BSTR* (pointer BSTR))


(definition BSTR*-make
  (c-function () BSTR*
    "___result_voidstar = malloc(sizeof(BSTR));"))


(definition BSTR*-ref
  (c-function (BSTR*) BSTR
    "___result_voidstar = *___arg1;"))


(definition BSTR*-free
  (c-function (BSTR*) void
    "free(___arg1);"))


(definition BSTR*-set!
  (c-function (BSTR* BSTR) void
    "*___arg1 = ___arg2;"))


(definition (BSTR*-deref bstr*)
  (let ((bstr (BSTR*-ref bstr*)))
    (BSTR*-free bstr*)
    (BSTR-decode bstr)))


(definition (BSTR*-enref bstr)
  (let ((ptr (BSTR*-make)))
    (BSTR*-set! ptr (BSTR-encode bstr))
    ptr))

  
;; BSTR-set! undefined

(definition BSTR-free
  (let ((SysFreeString (c-function (BSTR) void "SysFreeString")))
    (lambda (bstr)
      (SysFreeString bstr))))


(definition BSTR-ref
  (let ((cast (c-function (BSTR) wchar_t-string "___result = (wchar_t*)___arg1;")))
    (lambda (bstr)
      (cast bstr))))


(definition BSTR-encode
  (let ((SysAllocString (c-function (wchar_t-string) BSTR "SysAllocString")))
    (lambda (str)
      (SysAllocString str))))


(definition (BSTR-decode bstr)
  (let ((str (BSTR-ref bstr)))
    (BSTR-free bstr)
    str))


;;;
;;;; GUID
;;;


(definition (zero-fill str n)
  (let ((len (string-length str)))
    (if (> n len)
        (string-append (make-string (- n len) #\0) str)
      str)))


(c-structure GUID
  (DWORD Data1)
  (WORD Data2)
  (WORD Data3)
  (byte Data4 8))


(definition (GUID-set! guid str)
  (GUID-Data1-set! guid (string->number (substring str 0 8) 16))
  (GUID-Data2-set! guid (string->number (substring str 9 13) 16))
  (GUID-Data3-set! guid (string->number (substring str 14 18) 16))
  (let ((data4+ (GUID-Data4-ref guid))
        (string->byte (lambda (offset)
                        (string->number (substring str offset (+ 2 offset)) 16))))
    (for-each (lambda (n byte) (byte*-set! data4+ n byte))
              (naturals 0 8)
              (map string->byte '(19 21 24 26 28 30 32 34)))))


(definition (GUID-ref guid)
  (let* ((proc (lambda (i n)
                 (zero-fill (number->string i 16) n)))
         (data4+ (GUID-Data4-ref guid))
         (proc4 (lambda (n) (proc (byte*-ref data4+ n) 2))))
    (apply string-append
           (proc (GUID-Data1-ref guid) 8) "-"
           (proc (GUID-Data2-ref guid) 4) "-"
           (proc (GUID-Data3-ref guid) 4) "-"
           (proc4 0) (proc4 1) "-"
           (map proc4 (naturals 2 8)))))


(definition (GUID-encode str)
  (let ((guid (GUID-make)))
    (GUID-set! guid str)
    guid))


(definition (GUID-decode guid)
  (let ((str (GUID-ref guid)))
    (GUID-free guid)
    str))


;;;
;;;; INT
;;;


(c-type INT (native int))
(c-type INT* (pointer INT))


(definition INT*-make
  (c-function () INT*
    "___result = (INT*) malloc(sizeof(INT));"))


(definition INT*-free
  (c-function (INT*) void
    "free(___arg1);"))


(definition INT*-ref
  (c-function (INT*) INT
    "___result = *___arg1;"))


(definition INT*-set!
  (c-function (INT* INT) void
    "*___arg1 = ___arg2;"))


(definition (INT-enref int)
  (let ((ptr (INT*-make)))
    (INT*-set! ptr int)
    ptr))


(definition (INT-deref int*)
  (let ((int (INT*-ref int*)))
    (INT*-free int*)
    int))


;;;
;;;; LONG
;;;


(c-type LONG (native long))
(c-type LONG* (pointer LONG))


(definition LONG*-make
  (c-function () LONG*
    "___result = (LONG*) malloc(sizeof(LONG));"))


(definition LONG*-free
  (c-function (LONG*) void
    "free(___arg1);"))


(definition LONG*-ref
  (c-function (LONG*) LONG
    "___result = *___arg1;"))


(definition LONG*-set!
  (c-function (LONG* LONG) void
    "*___arg1 = ___arg2;"))


(definition (LONG-enref long)
  (let ((ptr (LONG*-make)))
    (LONG*-set! ptr long)
    ptr))


(definition (LONG-deref long*)
  (let ((long (LONG*-ref long*)))
    (LONG*-free long*)
    long))



;;;
;;;; IUnknown*
;;;


(c-type IUnknown* (pointer (native void)))
(c-type IUnknown** (pointer IUnknown*))
(c-type LPVOID* IUnknown**)


(definition IUnknown**-make
  (c-function () LPVOID*
    "___result = (LPVOID*) malloc(sizeof(IUnknown*));"))


(definition IUnknown**-free
  (c-function (LPVOID*) void
    "free(___arg1);"))


(definition IUnknown**-ref
  (c-function (LPVOID*) IUnknown*
    "___result = *___arg1;"))


(definition IUnknown**-set!
  (c-function (LPVOID* IUnknown*) void
    "*___arg1 = ___arg2;"))


(definition (IUnknown*-enref coptr)
  (let ((ptr (IUnknown**-make)))
    (IUnknown**-set! ptr coptr)
    ptr))


(definition (IUnknown*-deref coptr*)
  (let ((coptr (IUnknown**-ref coptr*)))
    (IUnknown**-free coptr*)
    coptr))


(coexternal 0 HRESULT (CoQuery (in GUID) (out IUnknown**)))
(coexternal 0 ULONG (CoQueryNoValidate (in GUID*) (out IUnknown**)))
(coexternal 1 ULONG (CoAddRef))
(coexternal 2 ULONG (CoRelease))


(definition (CoCreateInstance class interface)
  (let ((class-guid (GUID-encode class))
        (interface-guid (GUID-encode interface))
        (coptr* (IUnknown*-enref #f))
        (proc (c-function (GUID* IUnknown* DWORD GUID* LPVOID*) HRESULT "CoCreateInstance")))
    (validate-hresult (proc class-guid #f CLSCTX_SERVER interface-guid coptr*))
    (GUID-free class-guid)
    (GUID-free interface-guid)
    (IUnknown*-deref coptr*)))


(definition (cointerface guid)
  guid)

(definition (cointerface-guid coint)
  coint)


(definition (coclass guid coint)
  (cons guid coint))

(definition (coclass-guid coclass)
  (car coclass))

(definition (coclass-interface coclass)
  (cdr coclass))


(definition public (cocreate coclass)
  (let ((class-guid (coclass-guid coclass))
        (interface-guid (cointerface-guid (coclass-interface coclass))))
    (CoCreateInstance class-guid interface-guid)))


(definition public (corelease coptr)
  (CoRelease$ coptr))


;;;
;;;; CY
;;;


(c-structure CY
  (ulong Lo)
  (long Hi))


(definition CY-ref
  (c-function (CY*) (native int64)
    "___result = *(___S64*)___arg1;"))


;;;
;;;; DECIMAL
;;;

@a
(c-structure DECIMAL
  (USHORT wReserved)
  (BYTE scale)
  (BYTE sign)
  (ULONG Hi32)
  (ULONG Lo32)
  (ULONG Mid32))


(c-type DECIMAL "DECIMAL")
(c-type DECIMAL* (pointer DECIMAL))



(definition VARIANT*->ULONG
  (c-function (VARIANT*) ulong
    "ULONG temp;
     VarUI4FromDec((DECIMAL*)___arg1, &temp);
     ___result = temp;"))


;;;
;;;; VT_DATE
;;;


;; todo


;;;
;;;; VARIANT_BOOL
;;;


(c-type VARIANT_BOOL (native short))
(c-type VARIANT_BOOL* (pointer VARIANT_BOOL))


(definition VARIANT_BOOL*-make
  (c-function () VARIANT_BOOL*
    "___result = (VARIANT_BOOL*) malloc(sizeof(VARIANT_BOOL));"))


(definition VARIANT_BOOL*-free
  (c-function (VARIANT_BOOL*) void
    "free(___arg1);"))


(definition VARIANT_BOOL*-ref
  (function (vtbool*)
    (VARIANT_BOOL-decode
      ((c-function (VARIANT_BOOL*) VARIANT_BOOL
         "___result = *___arg1;")
       vtbool*))))


(definition VARIANT_BOOL*-set!
  (c-function (VARIANT_BOOL* VARIANT_BOOL) void
    "*___arg1 = ___arg2;"))


(definition (VARIANT_BOOL-encode boolean)
  (if boolean -1 0))


(definition (VARIANT_BOOL-decode vtbool)
  (/= 0 vtbool))


(definition (VARIANT_BOOL-enref boolean)
  (let ((ptr (VARIANT_BOOL*-make)))
    (VARIANT_BOOL*-set! ptr (VARIANT_BOOL-encode boolean))
    ptr))


(definition (VARIANT_BOOL-deref vtbool*)
  (let ((bool (VARIANT_BOOL*-ref vtbool*)))
    (VARIANT_BOOL*-free vtbool*)
    bool))


;;;
;;;; SAFEARRAYBOUND
;;;


(c-structure SAFEARRAYBOUND
  (ULONG cElements)
  (LONG lLbound))


;;;
;;;; SAFEARRAY
;;;


(c-structure SAFEARRAY
  (USHORT cDims)
  (USHORT fFeatures)
  (ULONG cbElements)
  (ULONG cLocks)
  (PVOID pvData)
  (SAFEARRAYBOUND rgsabound 1))


;;;
;;;; VARIANT
;;;


(c-type VARTYPE (native unsigned-short))
(c-type SCODE (native long))


(c-structure VARIANT
  (VARTYPE vt)
  (long lVal)
  (short iVal)
  (float fltVal)
  (double dblVal)
  (VARIANT_BOOL boolVal)
  (SAFEARRAY* parray)
  (SCODE scode)
  (CY cyVal)
  (BSTR bstrVal)
  (DECIMAL* pdecVal)
  (IUnknown* punkVal)
  (VARIANT_BOOL* pboolVal)
  (INT intVal))


(definition VariantInit
  (c-function (VARIANT*) void "VariantInit"))


(definition VariantClear
  (c-function (VARIANT*) HRESULT "VariantClear"))


(definition (VARIANT-set! variant value)
  (cond
    ((null? value)
     (VARIANT-vt-set! variant 10)
     (VARIANT-scode-set! variant -1))
    ((boolean? value)
     (VARIANT-vt-set! variant 11)
     (VARIANT-boolVal-set! variant (if value -1 0)))
    ((integer? value)
     (VARIANT-vt-set! variant 3)
     (VARIANT-lVal-set! variant value))
    ((real? value)
     (VARIANT-vt-set! variant 5)
     (VARIANT-dblVal-set! variant value))
    ((string? value)
     (VARIANT-vt-set! variant 8)
     (VARIANT-bstrVal-set! variant (BSTR-encode value)))
    (else
     (CoAddRef value)
     (VARIANT-vt-set! variant 13)
     (VARIANT-punkVal-set! variant value))))


(definition (VARIANT-ref variant)
  (case (VARIANT-vt-ref variant)
    ((0) ; VT_EMPTY
     #f)
    ((1) ; VT_NULL
     '())
    ((2) ; VT_I2
     (VARIANT-iVal-ref variant))
    ((3) ; VT_I4
     (VARIANT-lVal-ref variant))
    ((4) ; VT_R4
     (VARIANT-fltVal-ref variant))
    ((5) ; VT_R8
     (VARIANT-dblVal-ref variant))
    ((6)
     (CY-ref (VARIANT-cyVal-ref variant)))
    ((7)
     (VariantTimeToSystemTime (VARIANT-dblVal-ref variant)))
    ((8) ; VT_BSTR
     (BSTR-ref (VARIANT-bstrVal-ref variant)))
    ((11) ; VT_BOOL
     (/= 0 (VARIANT-boolVal-ref variant)))
    ((13)
     (list 'unimplemented 13))
    ;; Decimal not correctly implemented
    ((14)
     (VARIANT*->ULONG variant))
    ((22) ; VT_INT
     (VARIANT-intVal-ref variant))
    ((#x400B) ; VT_BOOL + VT_BYREF
     (/= 0 (short*-ref (VARIANT-pboolVal-ref variant) 0)))
    ((#x2011)
     (let* ((safearray (VARIANT-parray-ref variant))
            (bound (SAFEARRAY-rgsabound-ref safearray))
            (array (void*->byte* (SAFEARRAY-pvData-ref safearray)))
            (size (SAFEARRAYBOUND-cElements-ref bound))
            (vector (make-u8vector size)))
       (let (loop (i 0))
         (when (< i size)
           (u8vector-set! vector i (byte*-ref array i))
           (loop (+ i 1))))
       vector))
    (else
     ;; VARIANT-punkVal-ref dangerous
     (error "Unknown variant type: {s}" (VARIANT-vt-ref variant)))))


(definition (VARIANT-encode data)
  (let ((variant (VARIANT-make)))
    (VariantInit variant)
    (VARIANT-set! variant data)
    variant))


(definition (VARIANT-decode variant)
  (let ((data (VARIANT-ref variant)))
    (VariantClear variant)
    (VARIANT-free variant)
    data))


(definition (SystemTimeToVariantTime date)
  (let ((system-time (SYSTEMTIME-make))
        (buffer (make-f64vector 1))
        (fun (c-function (SYSTEMTIME* F64PTR) void "SystemTimeToVariantTime")))
    (SYSTEMTIME-wYear-set! system-time (date-year date))
    (SYSTEMTIME-wMonth-set! system-time (date-month date))
    (SYSTEMTIME-wDayOfWeek-set! system-time (date-week-day date))
    (SYSTEMTIME-wDay-set! system-time (date-day date))
    (SYSTEMTIME-wHour-set! system-time (date-hour date))
    (SYSTEMTIME-wMinute-set! system-time (date-minute date))
    (SYSTEMTIME-wSecond-set! system-time (date-second date))
    (SYSTEMTIME-wMilliseconds-set! system-time (quotient (date-nanosecond date) 1000))
    (fun system-time buffer)
    (SYSTEMTIME-free system-time)
    (f64vector-ref buffer 0)))


(definition (VariantTimeToSystemTime variant-time)
  (let ((system-time (SYSTEMTIME-make))
        (fun (c-function (double SYSTEMTIME*) void "VariantTimeToSystemTime")))
    (fun variant-time system-time)
    (let ((year (SYSTEMTIME-wYear-ref system-time))
          (month (SYSTEMTIME-wMonth-ref system-time))
          (day-of-week (SYSTEMTIME-wDayOfWeek-ref system-time))
          (day (SYSTEMTIME-wDay-ref system-time))
          (hour (SYSTEMTIME-wHour-ref system-time))
          (minute (SYSTEMTIME-wMinute-ref system-time))
          (second (SYSTEMTIME-wSecond-ref system-time))
          (milliseconds (SYSTEMTIME-wMilliseconds-ref system-time)))
      (SYSTEMTIME-free system-time)
      (make-date (* milliseconds 1000) second minute hour day month year 0))))


;;;
;;;; IErrorInfo
;;;


(definition IErrorInfo
  (cointerface "1CF2B120-547D-101B-8E65-08002B2BD119"))


(coexternal 3 ULONG (IErrorInfo$GetGUID (out GUID*)))
(coexternal 4 ULONG (IErrorInfo$GetSource (out BSTR*)))
(coexternal 5 ULONG (IErrorInfo$GetDescription (out BSTR*)))
(coexternal 6 ULONG (IErrorInfo$GetHelpFile (out BSTR*)))
(coexternal 7 ULONG (IErrorInfo$GetHelpContext (out DWORD*)))


(definition (GetErrorInfo)
  (let* ((ptr (IUnknown**-make))
         (result ((c-function (IUnknown**) HRESULT
                    "___result = GetErrorInfo(0,(IErrorInfo**)___arg1);")
                  ptr)))
    (if (= result S_OK)
        (IUnknown*-deref ptr)
      #f)))


;;;
;;;; ISupportErrorInfo
;;;


(definition ISupportErrorInfo
  (cointerface "DF0B3D60-548F-101B-8E65-08002B2BD119"))
  
  
(coexternal 3 ULONG (ISupportErrorInfo$InterfaceSupportsErrorInfo (in GUID*))))
