;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; NewFile Request
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.dialog.NewFile-Request jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.literals)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.utilities))


(class NewFile-Request extends Pathname-Request
  
  
  (form
    (<install>
      (<!>                         name: buttonbar
        (<Separator>                                 size: {Dimension 4 0})
        (<Layout-View>                               layout-type: fill layout-insets: {Rect 0 3 0 0} size: {Dimension 288 22}
          (<Border-View>                             border-type: solid border-color: {Color name: Entry-Border}
            (<Scroller-View>                         hscroll?: #f vscroll?: #f
              (<!>                 name: content     layout-type: fill
                (<Plain-Text-View> name: filename    accepts-returns?: #f left-padding: 2 top-padding: 2 show-unfocused-selection?: #f default-style: {Text-Style name: Default font: {Font name: Ansi-Var}} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press} content-change-handler: {Event-Handler :form on-content-change})))))
        (<!>                       name: ok          title: "Save"))))
  
  
  (method meta (host-title)
    "Choose New File")
  
  
  (property initial-filename  initialize #f)
  (property default-extension initialize #f)
  
  
  (method (finish rest)
    (nextmethod rest)
    (when initial-filename
      (let ((text (locate 'filename)))
        (let ((content
                (if (and default-extension (ends-with? initial-filename (concatenate "." default-extension)))
                    (subseq initial-filename 0 (- (cardinality initial-filename) (cardinality default-extension) 1))
                  initial-filename)))
          (set-string-content~ text content)
          (select-all~ text)))))
  
  
  (method (focus-default)
    (acquire-focus~ (locate 'filename)))
  

  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update)
    (update-hierarchy)
    (update-history)
    (update-content)
    (update-filename)
    (update-buttons))
  
  
  (method (update-content)
    (let ((directories (get-content~ directory nodes?: #t leaves?: #f))
          (files (get-content~ directory nodes?: #f leaves?: #t)))
      (let ((tree (get-tree)))
        (define (add pathname)
          (add-pathname tree pathname))
        
        (with-update-locked~ tree
          (function ()
            (remove-every-row~ tree)
            (for-each add directories)
            (for-each add files))))))
  
  
  (method (update-filename)
    (set-string-content~ (locate 'filename) ""))
  
  
  (method (update-buttons)
    (let ((text (locate 'filename)))
      (set-enabled?~ (locate 'ok) (not (empty-string? (get-string-content~ text))))))
  

  ;;;
  ;;;; Modal
  ;;;
  
  
  (method (get-modal-result)
    (current-directory (parse~ directory))
    (let ((content (get-string-content~ (locate 'filename))))
      (let ((filename
              (if (and default-extension (not (search content ".")))
                  (concatenate content "." default-extension)
                content)))
        (anchorize (new-file~ directory filename)))))
  

  ;;;
  ;;;; Events
  ;;;
  
  
  (method (selection-change)
    (let ((pathname (selected-pathname)))
      (when (file? pathname)
        (set-string-content~ (locate 'filename) (get-name~ pathname))
        (update-buttons))))
  
  
  (method (on-content-change evt)
    (update-buttons))
  
  
  (method (default-action)
    (let ((pathname (selected-pathname)))
      (cond ((directory? pathname)
             (down pathname))
            ((ok-enabled?)
             (end-modal~ (get-host) (get-modal-result))))))))
