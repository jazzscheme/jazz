;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Login Dialog
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.login.Login-Dialog jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.utilities))


(class Login-Dialog extends View


  (form
    (<install>                                              size: {Dimension 400 206} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
      (<Label-View>                 name: title             title: "Welcome to Jedi" position: {Point 10 9} size: {Dimension 350 16} font: {Font name: Label-Bold})
      (<Label-View>                 name: description       title: "Please enter the profile that will be used for this session" position: {Point 30 27} size: {Dimension 350 16} font: {Font name: Default-GUI})
      (<Radio-Button>               name: new-button        title: "New" position: {Point 25 83} tooltip?: #t tooltip-text: "Create a new profile" action-handler: {Event-Handler :form on-new})
      (<Radio-Button>               name: existing-button   title: "Existing" position: {Point 25 109} tooltip?: #t tooltip-text: "Open an existing profile from the standard directories" action-handler: {Event-Handler :form on-existing})
      (<Horizontal-Line>                                    position: {Point 0 48} size: {Dimension 400 8})
      (<Vertical-Line>                                      position: {Point 110 59} size: {Dimension 8 91})
      (<Label-View>                 name: new-label         title: "Name:" position: {Point 136 100} size: {Dimension 33 16} visible?: #f font: {Font name: Default-GUI})
      (<Border-View>                name: new-border        position: {Point 173 97} size: {Dimension 194 19} visible?: #f border-type: edge style: entry
        (<Scroller-View>                                    hscroll?: #f vscroll?: #f
          (<!>                      name: content           layout-type: fill
            (<Profile-Text-View>    name: new-profile       left-padding: 2 top-padding: 2 accepts-returns?: #f show-unfocused-selection?: #f default-style: {Text-Style name: Default font: {Font name: Ansi-Var}} tooltip?: #t tooltip-text: "Profile name. Can contain letters, digits, dashes, underscores and periods" return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press} content-change-handler: {Event-Handler :form on-content-change}))))
      (<Border-View>                name: existing-border   position: {Point 140 64} size: {Dimension 251 82} visible?: #f border-type: edge style: entry
        (<Scroller-View>                                    vscroll?: #t style: document
          (<!>                      name: content           layout-type: fill
            (<Tree-View>            name: profiles          show-header?: #f font: {Font name: Ansi-Var} default-row-height: 15 selection-mode: row can-select-empty?: #f tooltip?: #t tooltip-text: "List of profiles found in the standard directories" double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
              (<Tree-Node-Column>                           title: "Profile" width: 176 toplevel-controls?: #f display-images?: #f)))))
      (<Horizontal-Line>                                    position: {Point 0 160} size: {Dimension 400 8})
      (<Push-Button>                name: login             title: "Login" position: {Point 117 172} size: {Dimension 80 24} default?: #t action-handler: {Event-Handler :host on-ok})
      (<Push-Button>                name: cancel            title: "Cancel" position: {Point 203 172} size: {Dimension 80 24} action-handler: {Event-Handler :host on-cancel})
      (<Branch>                     name: new
        (<install>
          (<!>                      name: new-label         visible?: #t)
          (<!>                      name: new-border        visible?: #t)))
      (<Branch>                     name: existing
        (<install>
          (<!>                      name: existing-border   visible?: #t)))))
  
  
  (method meta (host-title)
    "Login")
  
  
  (method (finish rest)
    (nextmethod rest)
    (setup-new-profile)
    (setup-existing-profiles))
  
  
  (method (setup-new-profile)
    (set-string-content~ (locate 'new-profile) (user-name)))
  
  
  (method (setup-existing-profiles)
    (let ((profiles (collect-existing-profiles)))
      (cond ((null? profiles)
             (install-new)
             (set-selected?~ (locate 'new-button) #t))
            (else
             (install-existing profiles)
             (set-selected?~ (locate 'existing-button) #t)))))
  

  ;;;
  ;;;; Modal
  ;;;
  
  
  (method (validate-dialog)
    (let ((message (validate-profile)))
      (when message
        (message-box message)
        (throw-cancel))))
  
  
  (method (get-modal-result)
    (case (get-name~ branch)
      ((new)
       (let ((name (get-string-content~ (locate 'new-profile))))
         (compose-name 'profile (string->symbol name))))
      ((existing)
       (let* ((tree (locate 'profiles))
              (rank (get-single-selection~ tree)))
         (when rank
           (let ((row (get-row~ tree rank)))
             (compose-name 'profile (get-user-data~ row))))))))
  

  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method (get-tab-stops)
    (case (get-name~ branch)
      ((new)
       '())
      ((existing)
       '())))
  

  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-new evt)
    (install-new))
  
  
  (method (on-existing evt)
    (install-existing (collect-existing-profiles)))
  
  
  (method (on-content-change evt)
    (update-login))
  
  
  (method (on-moniker-update evt)
    (update-login))
  
  
  (method (on-double-click evt)
    (when (get-enabled?~ (locate 'login))
      (validate-dialog)
      (let ((dialog (get-host)))
        (end-modal~ dialog (get-modal-result~ dialog)))))
  
  
  ;; Should really be generic in the Dialog class
  (method (on-return-press evt)
    (when (get-enabled?~ (locate 'login))
      (validate-dialog)
      (let ((dialog (get-host)))
        (end-modal~ dialog (get-modal-result~ dialog)))))
  
  
  ;; Should really be generic in the Dialog class
  (method (on-escape-press evt)
    (let ((dialog (get-host)))
      (cancel-modal~ dialog)))
  

  ;;;
  ;;;; Branches
  ;;;
  
  
  (method (install-new)
    (set-active-branch (locate 'new))
    (let ((text (locate 'new-profile)))
      (acquire-focus~ text)
      (select-all~ text))
    (update-login))
  
  
  (method (install-existing profiles)
    (set-active-branch (locate 'existing))
    (let ((tree (locate 'profiles))
          (rank (either (find profiles (either (command-argument "defaultprofile") (get-system-property 'Default-Profile)) test: equal?) (essay profiles 0))))
      (remove-every-row~ tree)
      (for-each (function (profile)
                  (let ((name (string->symbol profile)))
                    (add-row~ tree children: (list (new Tree-Node title: profile)) user-data: name)))
                profiles)
      (when (and rank (< rank (get-visible-count~ tree)))
        (set-single-selection~ tree rank)
        (ensure-displayed~ tree))
      (acquire-focus~ tree))
    (update-login))
  
  
  (method (collect-existing-profiles)
    (let ((lst '()))
      (define (collect-directory dir)
        (when (exists?~ dir)
          (iterate-directory~ dir
            (function (dir)
              (let ((file (new-file~ dir (format "_{a}.jazz" (get-name~ dir)))))
                (when (exists?~ file)
                  (let ((base (get-base~ file)))
                    (let ((profile (substring base 1 (string-length base))))
                      (set! lst (cons profile lst)))))))
            files?: #f
            directories?: #t
            recursive?: #f)))
      
      (collect-directory {Directory Profiles})
      
      (sort ci<? lst)))
  

  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update-login)
    (when branch
      (case (get-name~ branch)
        ((new)
         (set-enabled?~ (locate 'login) (not (empty-string? (get-string-content~ (locate 'new-profile))))))
        ((existing)
         (set-enabled?~ (locate 'login) (not-null? (get-selection~ (locate 'profiles))))))))
  

  ;;;
  ;;;; Validate
  ;;;
  
  
  (method (validate-profile)
    (case (get-name~ branch)
      ((new)
       (let ((profile-name (get-string-content~ (locate 'new-profile))))
         (validate-new-profile profile-name)))
      (else
       {})))
  
  
  (method (validate-new-profile profile-name)
    (cond ((let ((first (first profile-name)))
             (not (or (eqv? first #\_)
                      (alphabetic? first))))
           "Profile name must start with a letter or an underscore")
          ((not (every? (function (char)
                          (or (alphabetic? char)
                              (numeric? char)
                              (memv? char '(#\minus #\_ #\period))))
                        profile-name))
           "Profile name can only contain letters, digits, dashes, underscores and periods")
          ((let ((dir (new Directory (list 'Profiles profile-name))))
             (exists?~ dir))
           (format "The profile name \"{a}\" already exists in your home library" profile-name))
          (else
           {})))
  

  ;;;
  ;;;; Drawing
  ;;;
  
  
  (definition Header-Color
    (new Color red: 120 green: 120 blue: 255))

  
  (method (draw surface context update lh lv)
    (let ((size (get-size)))
      (gradient-fill~ surface (new Rect 0 0 (get-width~ size) 50) (list (list 0 {Color name: White}) (list 1 Header-Color))))))


;;;
;;;; Profile-Text-View
;;;


(class Profile-Text-View extends Plain-Text-View
  
  
  (method (get-context-menu)
    (new-text-context-menu)))


(register-autoclass Profile-Text-View))
