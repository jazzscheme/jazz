;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Text Search
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.search.Text-Search jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.literals)
        (jazz.library)
        (jazz.platform)
        (jazz.ui)
        (jazz.ui.activity)
        (jazz.ui.window)
        (jazz.utilities))


(class Text-Search extends Search-View

  
  (form
    (<install>
      (<!>              name: domain         title: "Recherche de textes"
        (<Radio-Button> name: active-window  title: "Active Window" position: {Point 15 26} size: {Dimension 120 18})
        (<Radio-Button> name: jazz-files     title: "Jazz Files" position: {Point 15 46} size: {Dimension 120 18})
        (<Radio-Button> name: c-files        title: "C Files" position: {Point 15 66} size: {Dimension 120 18}))))
  

  ;;;
  ;;;; Domains
  ;;;

  
  (method (get-search-domains)
    '(active-window
      jazz-files
      c-files))
  
  
  (method (get-active-window-domain)
    'active-window)


  ;;;
  ;;;; Events
  ;;;
  
  
  (method (search-targets targets)
    (case (get-search-domain)
      ((active-window)
       (search-active-window targets))
      (else
       (search-threaded targets)
       @convert
       (new Thread
         name: "Search"
         context: self
         execute: (function (thread)
                    (search-threaded targets))))))
  
  
  (method synchronized (search-threaded targets)
    (let* ((reporter (new User-Reporter) @convert (get-search-reporter (count-files)))
           (found (search targets reporter)))
      (let ((results (get-results)))
        (set-mode~ results 'text)
        (add-results~ results found (get-search-targets))
        (select-results~ results))
      (when reporter
        (let ((manager (get-activity-manager)))
          (unregister-activity~ manager reporter)
          (close-manager~ manager)))))


  (method (search targets reporter)
    (case (get-search-domain)
      ((jazz-files) (search-jazz-files targets reporter))
      ((c-files) (search-c-files targets reporter))))
  
  
  (method (get-constituent-test)
    (case (get-search-domain)
      ((jazz-files) jazz-constituent?)
      ((c-files) c-constituent?)
      (else (nextmethod))))


  ;;;
  ;;;; Session
  ;;;
  
  
  (method (set-session-properties config)
    (nextmethod config)
    (set-search-domain (get-active-domain~ config)))


  ;;;
  ;;;; Reporting
  ;;;
  
  
  (method (count-files)
    (case (get-search-domain)
      ((jazz-files) (count-jazz-files))
      ((c-files) (count-c-files))))
  
  
  (definition Jazz-Count
    {})
  
  
  (method (count-jazz-files)
    (unless Jazz-Count
      (set! Jazz-Count (count-jazz-directories (jazz-search-directories))))
    Jazz-Count)
  
  
  (method (count-jazz-directories directories)
    (let ((count 0))
      (for-each (function (directory)
                  (iterate-directory~ directory
                    (function (file)
                      (when (member? (get-extension~ file) '("jazz") test: ci=?)
                        (increase! count)))
                    ignored-directories: '("Debug" "Release" "Work")))
                directories)
      count))
  
  
  (method (jazz-search-directories)
    (list (cond @wait
                ((is-alias? 'Jazz-Branch)
                 {Directory Jazz-Branch})
                ((is-alias? 'Jazz)
                 {Directory Jazz})
                (else
                 {Directory Launch}))
          {Directory Home "jazz.user"}))
  
  
  (definition C-Count
    {})
  
  
  (method (count-c-files)
    (let ((count
           (function ()
             (let ((count 0))
               (for-each (function (directory)
                           (iterate-directory~ directory
                             (function (file)
                               (when (member? (get-extension~ file) '("c" "cpp" "h" "java"))
                                 (increase! count)))))
                         (get-c-directories~ (get-preferences '(tools search-manager))))
               count))))
      (unless C-Count
        (set! C-Count (count)))
      C-Count))
  
  
  (method (setup-search-reporter reporter count)
    (set-range~ reporter (new Range 1 count))
    (set-pos~ reporter 1)
    (set-step~ reporter 1))


  ;;;
  ;;;; Search
  ;;;
  
  
  (method (search-active-window targets)
    (let ((frames (get-windows~ (get-stage))))
      (when (>= (length frames) 1)
        (let ((frame (car frames)))
          (when (is? frame Host-Frame)
            (let ((guest (get-guest~ frame)))
              (when (is? guest Text-View)
                (set-caret~ guest (get-start~ guest))
                (find-forward guest targets #f)
                (bring-to-front~ frame)
                (acquire-focus~ guest))))))))
  
  
  (method (search-jazz-files targets reporter)
    (let ((factory (new List-Factory)))
      (search-in-directories targets (jazz-search-directories) '("scm" "jazz" "jml") (get-expression?) factory reporter)
      (get-output~ factory)))
  
  
  (method (search-c-files targets reporter)
    (let ((factory (new List-Factory))
          (directories (get-c-directories~ (get-preferences '(tools search-manager)))))
      (search-in-directories targets directories '("c" "cpp" "h" "java") (get-expression?) factory reporter)
      (get-output~ factory)))))
