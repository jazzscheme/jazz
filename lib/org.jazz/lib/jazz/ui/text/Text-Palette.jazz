;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Text Palette
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.text.Text-Palette jazz


(import (jazz.designer)
        (jazz.jml)
        (jazz.literals)
        (jazz.library)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.view)
        (jazz.utilities))


(class Text-Palette extends View
    
  
  (slot flow-layout)
  (slot client      initialize {})


  (form
    (<install>
      (<Border-View>          name: style-border     size: {Dimension 120 21} border-insets: {Rect 2 2 2 2} border-color: {Color name: Medium}
        (<Border-View>                               border-color: {Color red: 200 green: 188 blue: 166}
          (<Text-Style-Combo> name: style            enabled?: #f)))
      (<Border-View>          name: font-border      size: {Dimension 120 21} border-insets: {Rect 2 2 2 2} border-color: {Color name: Medium}
        (<Border-View>                               border-color: {Color red: 200 green: 188 blue: 166}
          (<Text-Font-Combo>  name: font             enabled?: #f)))
      (<Border-View>          name: size-border      size: {Dimension 40 21} border-insets: {Rect 2 2 2 2} border-color: {Color name: Medium}
        (<Border-View>                               border-color: {Color red: 200 green: 188 blue: 166}
          (<Text-Size-Combo>  name: size             enabled?: #f)))
      (<Separator>)
      (<Toggle-Tool>          name: bold             size: {Dimension 22 22} tooltip?: #t tooltip-text: "Bold" image-name: {Bitmap-Resource "Bold"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-bold})
      (<Toggle-Tool>          name: italic           size: {Dimension 22 22} tooltip?: #t tooltip-text: "Italic" image-name: {Bitmap-Resource "Italic"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-italic})
      (<Toggle-Tool>          name: underline        size: {Dimension 22 22} tooltip?: #t tooltip-text: "Underline" image-name: {Bitmap-Resource "Underline"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-underline})
      (<Separator>)
      (<Radio-Tool>           name: alignment-bottom size: {Dimension 22 22} group: alignment tooltip?: #t tooltip-text: "Align Bottom" image-name: {Bitmap-Resource "AlignmentBottom"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-alignment alignment: bottom})
      (<Radio-Tool>           name: alignment-center size: {Dimension 22 22} group: alignment tooltip?: #t tooltip-text: "Align Center" image-name: {Bitmap-Resource "AlignmentCenter"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-alignment alignment: center})
      (<Radio-Tool>           name: alignment-top    size: {Dimension 22 22} group: alignment selected?: #t tooltip?: #t tooltip-text: "Align Top" image-name: {Bitmap-Resource "AlignmentTop"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-alignment alignment: top})
      (<Separator>)
      (<Radio-Tool>           name: justify-left     size: {Dimension 22 22} group: justification selected?: #t tooltip?: #t tooltip-text: "Justify Left" image-name: {Bitmap-Resource "JustifyLeft"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-justify :justification left})
      (<Radio-Tool>           name: justify-centered size: {Dimension 22 22} group: justification tooltip?: #t tooltip-text: "Justify Centered" image-name: {Bitmap-Resource "JustifyCentered"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-justify :justification centered})
      (<Radio-Tool>           name: justify-right    size: {Dimension 22 22} group: justification tooltip?: #t tooltip-text: "Justify Right" image-name: {Bitmap-Resource "JustifyRight"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-justify :justification right})
      (<Separator>)
      (<Toggle-Tool>          name: bulleted         size: {Dimension 22 22} tooltip?: #t tooltip-text: "Bulleted" image-name: {Bitmap-Resource "Bulleted"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-bulleted})
      (<Separator>)
      (<Push-Tool>            name: decrease-indent  size: {Dimension 22 22} tooltip?: #t tooltip-text: "Decrease Indent" image-name: {Bitmap-Resource "DecreaseIndent"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-decrease-indent})
      (<Push-Tool>            name: increase-indent  size: {Dimension 22 22} tooltip?: #t tooltip-text: "Increase Indent" image-name: {Bitmap-Resource "IncreaseIndent"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-increase-indent})
      (<Separator>)
      (<Text-Color-Combo>     name: text-color       size: {Dimension 31 22} enabled?: #f action-handler: {Event-Handler :focus on-text-color}
        (<!>                  name: body             color: {Color name: Dark-Blue} image-name: {Bitmap-Resource "TextColor"} tooltip-text: "Select Text Color" action-handler: {Event-Handler :focus on-text-color})
        (<!>                  name: pick             tooltip-text: "Text Color Menu" action-handler: {Event-Handler :focus on-pick-text-color}))
      (<Separator>                                   size: {Dimension 4 0})
      (<Text-Color-Combo>     name: highlight-color  size: {Dimension 31 22} enabled?: #f action-handler: {Event-Handler :focus on-highlight-color}
        (<!>                  name: body             color: {Color name: Dark-Red} image-name: {Bitmap-Resource "HighlightColor"} tooltip-text: "Select Highlight Color" action-handler: {Event-Handler :focus on-highlight-color})
        (<!>                  name: pick             tooltip-text: "Highlight Color Menu" action-handler: {Event-Handler :focus on-pick-highlight-color}))
      (<Separator>            name: ruler-separator)
      (<Toggle-Tool>          name: toggle-ruler     size: {Dimension 22 22} tooltip?: #t tooltip-text: "Toggle Ruler" image-name: {Bitmap-Resource "Ruler"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :focus on-toggle-ruler})
      (<Separator>            name: image-separator)
      (<Push-Tool>            name: insert-image     size: {Dimension 22 22} tooltip?: #t tooltip-text: "Insert Image" image-name: {Bitmap-Resource "MsgUser"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :form on-insert-image})
      (<Separator>            name: table-separator)
      (<Push-Tool>            name: insert-table     size: {Dimension 22 22} tooltip?: #t tooltip-text: "Insert Table" image-name: {Bitmap-Resource "DesignTable"} portfolio: :icons enabled?: #f action-handler: {Event-Handler :form on-insert-table})))
    
  
  @convert-external
  (method meta (external-name . rest)
    'text-palette)
  
  
  (method (mouse-activate?)
    #f)

  
  ;;;
  ;;;; Host
  ;;;
  
  
  (method meta (host-icon)
    {Bitmap-Resource "Document"})
  

  (method meta (host-title)
    "Text")
  
  
  (method (host-activate)
    (let ((appl (get-application)))
      (add-focus-listener~ appl (new Event-Handler target: self method-name: 'on-focus-change))))
  
  
  (method (host-deactivate)
    (let ((appl (get-application)))
      (remove-focus-listener~ appl self)))

  
  ;;;
  ;;;; Component
  ;;;


  (method (finish initargs)
    (nextmethod initargs)
    (set! flow-layout (new Flow-Layout insets: {Rect 4 5 0 0}))
    (layout-view))
  
  
  (method (layout)
    (layout~ flow-layout children (get-width) (get-height)))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-focus-change evt)
    (let ((appl (get-application))
           (old-focus (get-old~ evt))
           (new-focus (get-new~ evt)))
      (when (and (is? old-focus Formatted-Text-View)
                 (text-listener?~ old-focus self))
        (remove-text-listener~ old-focus self))
      (when (and (is? new-focus Formatted-Text-View)
                 (not (text-listener?~ new-focus self)))
        (add-text-listener~ new-focus (new Event-Handler target: self method-name: 'on-text))
        (activate-text new-focus))
      (enable-buttons
        (and new-focus
             (or (and (is? new-focus Formatted-Text-View)
                      (text-listener?~ new-focus self))
                 (let ((player (get-player~ new-focus)))
                   (and (is? player View-Host)
                        (eq? (get-guest~ player) self))))))))
  
  
  (method (on-text evt)
    (case (get-kind~ evt)
      ((:selection-change) (selection-change))))
  
  
  (method (on-insert-image evt)
    (insert-view~ client (form>> (<Push-Button> title: "Push Me")))
    @w
    (let* ((moniker (choose-file))
           (style (new Image-Style moniker: moniker)))
      (insert-styled~ client " " style)))
  
  
  (method (on-insert-table evt)
    (insert-table~ client size: {Dimension 304 101} header?: #f node-column?: #f columns: '((width: 100) (width: 100) (width: 100)) row-count: 2))


  (method protected virtual (enable-buttons enabled?)
    (set-enabled?~ (locate 'style) enabled?)
    (set-enabled?~ (locate 'font) enabled?)
    (set-enabled?~ (locate 'size) enabled?)
    (set-enabled?~ (locate 'bold) enabled?)
    (set-enabled?~ (locate 'italic) enabled?)
    (set-enabled?~ (locate 'underline) enabled?)
    (set-enabled?~ (locate 'alignment-bottom) enabled?)
    (set-enabled?~ (locate 'alignment-center) enabled?)
    (set-enabled?~ (locate 'alignment-top) enabled?)
    (set-enabled?~ (locate 'justify-left) enabled?)
    (set-enabled?~ (locate 'justify-centered) enabled?)
    (set-enabled?~ (locate 'justify-right) enabled?)
    @waiting
    (set-enabled?~ (locate 'numbered) enabled?)
    (set-enabled?~ (locate 'bulleted) enabled?)
    (set-enabled?~ (locate 'decrease-indent) enabled?)
    (set-enabled?~ (locate 'increase-indent) enabled?)
    (set-enabled?~ (locate 'text-color) enabled?)
    (set-enabled?~ (locate 'highlight-color) enabled?)
    (set-enabled?~ (locate 'toggle-ruler) enabled?)
    (set-enabled?~ (locate 'insert-image) enabled?)
    (set-enabled?~ (locate 'insert-table) enabled?))

  
  ;;;
  ;;;; Text
  ;;;
  
  
  (method (activate-text text)
    (when (neq? text client)
      (set! client text)
      (set-client~ (locate 'style) client)
      (set-client~ (locate 'font) client)
      (set-client~ (locate 'size) client)
      (selection-change)))

  
  ;;;
  ;;;; Selection
  ;;;


  (method (selection-change)
    (selection-change~ (locate 'style))
    (selection-change~ (locate 'font))
    (selection-change~ (locate 'size))
    (let* ((end (get-end~ client))
           (format (get-format~ (get-paragraph~ client (get-row~ end))))
           (style (get-style-at~ client end))
           (alignment (either (get-alignment~ style) 'bottom))
           (font (essay (is? style Text-Style) (get-font~ style))))
      (set-selected?~ (locate 'bold) (essay font (get-bold?~ font)))
      (set-selected?~ (locate 'italic) (essay font (get-italic?~ font)))
      (set-selected?~ (locate 'underline) (essay font (get-underline?~ font)))
      (set-selected?~ (locate 'alignment-bottom) (eq? alignment 'bottom))
      (set-selected?~ (locate 'alignment-center) (eq? alignment 'center))
      (set-selected?~ (locate 'alignment-top) (eq? alignment 'top))
      (set-selected?~ (locate 'justify-left) (eq? (get-justification~ format) 'left))
      (set-selected?~ (locate 'justify-centered) (eq? (get-justification~ format) 'centered))
      (set-selected?~ (locate 'justify-right) (eq? (get-justification~ format) 'right))
      (set-selected?~ (locate 'bulleted) (get-bulleted?~ format))
      (typecase style
        ((View-Style)
         (let* ((view (get-view~ style))
                (designer (new Designer form: (new Form action: 'new model-name: (type-name (class-of client))) reference: view)))
           (set-selected~ (get-application) designer (list view))))
        (else
         (let ((designer (new Designer form: (new Form action: 'new model-name: (type-name (class-of client))) reference: client)))
           (set-selected~ (get-application) designer (list client)))))))))
