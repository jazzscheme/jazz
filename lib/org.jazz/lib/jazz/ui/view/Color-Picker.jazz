;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Color Picker
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.view.Color-Picker jazz


(import (jazz.jml)
        (jazz.ui)
        (jazz.ui.view)
        (jazz.literals)
        (jazz.library)
        (jazz.utilities))


(class Color-Picker extends Layout-View


  (property colors   initialize {} getter get-colors   setter set-colors)
  (property geometry initialize {} getter get-geometry setter set-geometry)
  
  
  (form
    (<install>                    layout-type: border layout-insets: {Rect 4 4 4 4}
      (<View>        name: colors location: center)
      (<Layout-View>              size: {Dimension 100 20} location: south layout-type: fill layout-insets: {Rect 4 0 4 4}
        (<More-Tool> name: more   title: "More Colors..."))))

  
  ;;;
  ;;;; Access
  ;;;


  (method public (get-colors)
    colors)


  (method public (set-colors value)
    (set! colors value)
    (invalidate-tools)
    (layout-view))


  (method public (get-geometry)
    geometry)


  (method public (set-geometry value)
    (set! geometry value)
    (layout-view))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (set-action-handler handler)
    (set! action-handler handler)
    (for-each (function (tool)
                (set-action-handler~ tool handler))
              (get-children~ (locate 'colors)))
    (set-action-handler~ (locate 'more) handler))

  
  ;;;
  ;;;; Colors
  ;;;
  
  
  (definition Default-Colors
    '(()
      
      {Color name: White}
      {Color name: Black}
      
      {Color name: Light-Red}
      {Color name: Red}
      {Color name: Dark-Red}
      
      {Color name: Light-Orange}
      {Color name: Orange}
      {Color name: Dark-Orange}
      
      {Color name: Light-Yellow}
      {Color name: Yellow}
      {Color name: Dark-Yellow}
      
      {Color name: Light-Green}
      {Color name: Green}
      {Color name: Dark-Green}
      
      {Color name: Light-Blue}
      {Color name: Blue}
      {Color name: Dark-Blue}
      
      {Color name: Light-Purple}
      {Color name: Purple}
      {Color name: Dark-Purple}
      
      {Color name: Light-Gray}
      {Color name: Gray}
      {Color name: Dark-Gray}))
  
  
  (method (effective-colors)
    (either colors Default-Colors))

  
  ;;;
  ;;;; Tools
  ;;;
  
  
  (method (invalidate-tools)
    (let ((view (locate 'colors)))
      (for-each (function (tool)
                  (close~ tool))
                (get-children~ view))
      (for-each (function (color)
                  (new Color-Tool parent: view size: {Dimension 18 18} color: color action-handler: action-handler))
                (effective-colors))))
  
  
  ;;;
  ;;;; Layout
  ;;;
  
  
  (method (layout)
    (let* ((view (locate 'colors))
           (children (get-children~ view)))
      (when children
        (let* ((geometry (either geometry {Cell 3 8}))
               (rows (get-row~ geometry))
               (columns (get-col~ geometry)))
          (for-each (function (h)
                      (for-each (function (v)
                                  (let* ((n (+ (* h rows) v))
                                         (tool (element children n)))
                                    (set-position~ tool (new Point (+ 2 (* h 18)) (+ 2 (* v 18))))))
                                (naturals 0 rows)))
                    (naturals 0 columns))
          (set-size (new Dimension (+ 2 (* columns 18) 2 8) (+ 2 (* rows 18) 2 30))))))
    (nextmethod)))


;;;
;;;; Color-Tool
;;;


(class Color-Tool extends Push-Tool
  
  
  (property client)
  (property color  initialize {} getter get-color setter set-color)
  
  
  (method public (get-color)
    color)
  
  
  (method public (set-color value)
    (set! color value))
  
  
  (method (get-tooltip?)
    #t)
  
  
  (method (get-tooltip-text)
    (if (null? color)
        "No Color"
      (->string (get-name~ color))))
  
  
  (method (action-properties)
    (list color: color))
  
  
  (method (mouse-enter)
    (set-state 'highlighted)
    (invalidate-view))
  
  
  (method (mouse-leave)
    (set-state 'inactive)
    (invalidate-view))
  
  
  (method (mouse-up pos)
    (process-action self))
  
  
  (method (up)
    )
  
  
  (method (draw-highlighted surface)
    (if mouse-down?
        (draw-pushed surface)
      (nextmethod surface)))
  
  
  (method (draw-tool surface context)
    (nextmethod surface context)
    (let* ((rect (get-bounds))
           (outside (inflate-rect rect -3 -3))
           (inside (inflate-rect outside -1 -1)))
      (frame-rect~ surface outside {Color name: Dark})
      (if color
          (fill-rect~ surface inside color)
        (set-pen~ surface {Pen name: Dark})
        (line~ surface (get-right~ inside) (- (get-top~ inside) 1) (- (get-left~ inside) 2) (+ (get-bottom~ inside) 1))))))


;;;
;;;; More-Tool
;;;


(class More-Tool extends Label-Tool
  
  
  (method (mouse-enter)
    (set-state 'highlighted)
    (invalidate-view))
  
  
  (method (mouse-leave)
    (set-state 'inactive)
    (invalidate-view))
  
  
  (method (mouse-up pos)
    (close-popups)
    (process-action self (list color: (get-modal ChooseColor-Dialog))))
  
  
  (method (up)
    )
  
  
  (method (draw-highlighted surface)
    (if mouse-down?
        (draw-pushed surface)
      (nextmethod surface)))))
