;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Radio Tools
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.view.Radio-Tool jazz


(import (jazz.library)
        (jazz.ui)
        (jazz.literals)
        (jazz.utilities))


(class Radio-Tool extends Image-Tool implements (Groupable Selectable)


  (property group     initialize {} getter get-group     setter set-group)
  (property selected? initialize #f getter get-selected? setter set-selected?)

  
  (method (default-size)
    {Dimension 22 22})
  
  
  ;;;
  ;;;; Drawing
  ;;;


  (method (draw-frame surface)
    (if (and selected? (memq? (get-state) '(inactive highlighted out-pushed)))
        (draw-pushed surface)
      (nextmethod surface)))


  (method (draw-tool surface context)
    (let ((offset (if (or selected? (eq? (get-state) 'pushed)) 2 1)))
      (draw~ image surface context offset offset enabled?: (get-enabled?))))
  
  
  (method (draw-background surface context)
    (let ((state (get-state)))
      (if selected?
          (let ((bounds (get-bounds)))
            (fill-rect~ surface bounds {Color name: Light-Background}))
        (nextmethod surface context))))
  
  
  ;;;
  ;;;; Mouse
  ;;;


  (method (up)
    (when (and (eq? (get-state) 'pushed) (not selected?))
      (let ((pushed (find-pushed)))
        (when pushed
          (set-selected?~ pushed #f)))
      (set-selected? #t)
      (process-action self)))
  
  
  ;;;
  ;;;; Group
  ;;;


  (method (radio-group)
    (collect-if (function (component)
                  (and (is? component Radio-Tool)
                       (eq? (get-group~ component) group)))
                (get-children~ parent)))


  (method (find-pushed)
    (find-if (function (view)
               (get-selected?~ view))
             (radio-group)))

  
  ;;;
  ;;;; Groupable
  ;;;


  (method (get-group)
    group)


  (method (set-group value)
    (set! group value))
  
  
  ;;;
  ;;;; Selectable
  ;;;


  (method (get-selected?)
    selected?)
  

  (method (set-selected? flag)
    (when (neq? flag selected?)
      (let ((current (find-pushed)))
        (set-selected-state flag)
        (when current
          (set-selected-state~ current (not flag))))))
  
  
  (method (unselect)
    (when selected?
      (set-selected-state #f)))
  
  
  (method (set-selected-state flag)
    (set! selected? flag)
    (invalidate-view))))
