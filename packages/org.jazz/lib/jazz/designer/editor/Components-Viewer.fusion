;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Components Viewer
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.designer.editor.Components-Viewer jazz


(import (jazz.designer)
        (jazz.ui)
        (jazz.library)
        (jazz.utilities)
        (jazz.literals))


(class Components-Viewer extends Tree-View
  
  
  (slot editor   initialize {})
  (slot client   initialize {})
  (slot designer initialize {})

  
  ;;;
  ;;;; Actions
  ;;;
  

  (method (focus-actions)
    (cons (find-actions 'components-viewer)
          (nextmethod)))


  ;;;
  ;;;; Events
  ;;;
  
  
  (method (destroy)
    (set-designer {})
    (set-editor {})
    (nextmethod))
  
  
  (method (context-menu pos)
    (with-context-click pos
      (function dynamic ()
        (let ((menu (get-context-menu)))
          (when menu
            (track-popup-menu menu pos))))))
  
  
  (method (get-context-menu)
    (new Component-Menu client: self))
  
  
  (method public virtual (update-menu menu)
    (let* ((appl (get-application))
           (components (selected-components))
           (component (car components))
           (count (length components))
           (can-cut? (get-action-enabled? {Action view cut}))
           (can-copy? (get-action-enabled? {Action view copy}))
           (can-paste? (get-action-enabled? {Action view paste}))
           (can-paste-properties? (get-action-enabled? {Action view paste-properties}))
           (can-delete? (get-action-enabled? {Action view delete}))
           (can-rename? (get-action-enabled? {Action components-tree rename}))
           (can-properties? (and component (is? component Component) (can-view-properties?~ component)))
           (empty-add? #f))
      (let ((add-menu (locate~ menu 'add-menu))
            (add-item (locate~ menu 'add-item)))
        (if (and (use-add-menu?) (= count 1) (add-components~ add-menu self))
            (let ((children (collect-type Label-Item (get-children~ add-menu))))
              (when (= (length children) 1)
                (set-visible?~ add-item #f)
                (let ((unique (car children)))
                  (set-icon~ unique (get-icon~ add-item))
                  (set-title~ unique (format "{a} {a}" (localize "Add") (get-title~ unique)))
                  (set-accelerator~ unique '{Shortcut :control #\D})
                  (set-parent~ unique menu)
                  (set-before~ unique 'end-add-group))))
          (set-visible?~ add-item #f)
          (set! empty-add? #t)))
      (when (not can-cut?)
        (set-visible?~ (locate~ menu 'cut) #f))
      (when (not can-copy?)
        (set-visible?~ (locate~ menu 'copy) #f))
      (when (not can-paste?)
        (set-visible?~ (locate~ menu 'paste) #f))
      (when (not can-paste-properties?)
        (set-visible?~ (locate~ menu 'paste-properties) #f))
      (when (not can-delete?)
        (set-visible?~ (locate~ menu 'delete-selection) #f))
      (when (not can-rename?)
        (set-visible?~ (locate~ menu 'rename-component) #f))
      (when (not can-properties?)
        (set-visible?~ (locate~ menu 'properties) #f))
      (cleanup-separators~ menu)))
  
  
  (method public virtual (use-add-menu?)
    #t)
  
  
  (method protected virtual (on-add-component evt)
    )

  
  ;;;
  ;;;; Add
  ;;;
  
  
  (method public virtual (addable-classes)
    '())
  
  
  (method public virtual (addable-root-classes)
    '())
  
  
  (method public virtual (add-component-class class . rest)
    )


  ;;;
  ;;;; Editor
  ;;;
  
  
  (method public (get-editor)
    editor)
    
  
  (method public (set-editor edt)
    (when (/= edt editor)
      (when editor
        (remove-editor-listener~ editor self))
      (set! editor edt)
      (when editor
        (add-editor-listener~ editor (new Event-Handler target: self method-name: 'on-editor)))
      (editor-update)))
  
  
  (method protected virtual (editor-update)
    )
    
  
  (method protected virtual (on-editor evt)
    (case (get-kind~ evt)
      ((:client-change) (set-client (get-property~ evt client:)))
      ((:designer-change) (set-designer (get-property~ evt designer:)))))


  ;;;
  ;;;; Client
  ;;;


  (method public (get-client)
    client)


  (method public (set-client clt)
    (when (/= clt client)
      (set! client clt)
      (client-update)))
  
  
  (method protected virtual (client-update)
    )
  
  
  ;;;
  ;;;; Designer
  ;;;
  
  
  (method public (get-designer)
    designer)
  
  
  (method public virtual (set-designer dsg)
    (when (/= dsg designer)
      (when designer
        (uninstall-listener designer))
      (set! designer dsg)
      (when designer
        (install-listener designer))))
  
  
  ;;;
  ;;;; Listener
  ;;;
  
  
  (method (install-listener designer)
    (let ((listener (new Event-Handler target: self method-name: 'on-designer)))
      (add-designer-listener~ designer listener)))
  
  
  (method (uninstall-listener designer)
    (remove-designer-listener~ designer self))

  
  ;;;
  ;;;; Designer
  ;;;
  
  
  (method protected virtual (on-designer evt)
    )
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method public (selected-component)
    (let ((selection (get-single-selection)))
      (when selection
        (get-user-data~ (get-row selection)))))
  
  
  (method public (selected-components)
    (map (function dynamic (row)
           (get-user-data~ (get-row row)))
         (get-ordered-selection)))
  
  
  (method public (component-row component)
    (call/ec
      (lambda (return)
        (for-each-row
          (function dynamic (row rank)
            (when (eq? (get-user-data~ row) component)
              (return row))))
        {})))
  
  
  (method (component-visible-row component)
    (user-data-index component))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method public (preserving-update)
    (update expand-state: (get-expand-state) selected-components: (selected-components)))


  (method public virtual (update . rest)
    )
  
  
  (method public virtual (update-selection expand-state selected-components)
    (set-expand-state expand-state)
    (when selected-components
      (set-selection (collect component-visible-row selected-components)
                     force?: #t generate-event?: #t)))))
