;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Comparing Trees
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Alain Marcotte
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.groupware.compare.Compare-Trees jazz


(import (jazz.groupware)
        (jazz.jml)
        (jazz.platform)
        (jazz.literals)
        (jazz.library)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.view)
        (jazz.utilities))


(class Compare-Trees extends Tool-View
  
  
  (property mode                                         getter get-mode               setter set-mode)
  (property base-date                                    getter get-base-date          setter set-base-date)
  (property left-moniker       initialize :uninitialized getter get-left-moniker       setter set-left-moniker)
  (property right-moniker      initialize :uninitialized getter get-right-moniker      setter set-right-moniker)
  (property left-content       initialize #t             getter get-left-content       setter set-left-content)
  (property right-content      initialize #t             getter get-right-content      setter set-right-content)
  (property ignored-nodes      initialize '()            getter get-ignored-nodes      setter set-ignored-nodes)
  (property ignored-leaves     initialize '()            getter get-ignored-leaves     setter set-ignored-leaves)
  (property ignored-extensions initialize '()            getter get-ignored-extensions setter set-ignored-extensions)
  (property check-algorithm    initialize {}             getter get-check-algorithm    setter set-check-algorithm)
  
  
  (slot left-root)
  (slot left-base)
  (slot right-root)
  (slot right-base)
  (slot root-entry)
  (slot installed-configuration initialize {})


  (form
    (<install>                                               size: {Dimension 895 455}
      (<Group-Box>                     name: mode            title: "Mode" position: {Point 5 5} size: {Dimension 210 76} style: styled
        (<Radio-Button>                name: backup          title: "Backup" position: {Point 15 27} size: {Dimension 60 16})
        (<Radio-Button>                name: merge           title: "Merge" position: {Point 15 47} size: {Dimension 60 16} selected?: #t)
        (<Border-View>                                       position: {Point 83 44} size: {Dimension 111 19} border-type: edge style: entry
          (<Scroller-View>                                   hscroll?: #f vscroll?: #f
            (<!>                       name: content         layout-type: fill
              (<Text-View>             name: base-date       font: {Font name: Default-GUI} accepts-returns?: #f left-padding: 2 top-padding: 2 return-press-handler: {Event-Handler :form on-return-press})))))
      (<Group-Box>                     name: trees           title: "Trees" position: {Point 222 5} size: {Dimension 375 76} style: styled
        (<Label-View>                  name: green-label     title: "Green:" position: {Point 8 21} size: {Dimension 35 16} font: {Font name: Default-GUI})
        (<Border-View>                 name: green-border    position: {Point 48 19} size: {Dimension 320 19} border-type: edge style: entry
          (<Moniker-Browser>           name: green           return-press-handler: {Event-Handler :form on-return-press}))
        (<Label-View>                  name: blue-label      title: "Blue:" position: {Point 8 46} size: {Dimension 30 16} font: {Font name: Default-GUI})
        (<Border-View>                 name: blue-border     position: {Point 48 44} size: {Dimension 320 19} border-type: edge style: entry
          (<Moniker-Browser>           name: blue            return-press-handler: {Event-Handler :form on-return-press})))
      (<Group-Box>                     name: check-algorithm title: "Check" position: {Point 603 4} size: {Dimension 111 78} style: styled
        (<Radio-Button>                name: timestamp       title: "Timestamp" position: {Point 15 17} size: {Dimension 79 16} selected?: #t)
        (<Radio-Button>                name: content         title: "Content" position: {Point 15 37} size: {Dimension 79 16})
        (<Radio-Button>                name: whitespace      title: "Whitespace" position: {Point 15 57} size: {Dimension 79 16}))
      (<Push-Button>                   name: compare         title: "Compare" position: {Point 810 7} size: {Dimension 80 24} action-handler: {Event-Handler :form on-compare} default?: #t)
      (<Push-Tool>                     name: copy-right      position: {Point 723 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Mark Left -> Right" image-name: {Bitmap-Resource "CopyRight"} portfolio: :icons action-handler: {Event-Handler :form on-copy-right})
      (<Push-Tool>                     name: merge-leaves    position: {Point 745 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Merge Files" image-name: {Bitmap-Resource "Merge"} portfolio: :icons action-handler: {Event-Handler :form on-merge-leaves})
      (<Push-Tool>                     name: copy-left       position: {Point 767 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Mark Left <- Right" image-name: {Bitmap-Resource "CopyLeft"} portfolio: :icons action-handler: {Event-Handler :form on-copy-left})
      (<Push-Tool>                     name: no-action       position: {Point 789 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "No Action" image-name: {Bitmap-Resource "Empty"} portfolio: :icons action-handler: {Event-Handler :form on-no-action})
      @wait
      (<Push-Tool>                     name: build-updater   position: {Point 830 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Build Updater" image-name: {Bitmap-Resource "Profile"} portfolio: :icons action-handler: {Event-Handler :form on-build-updater})
      (<Push-Tool>                     name: transfer        position: {Point 869 57} size: {Dimension 22 22} tooltip?: #t tooltip-text: "Transfer" image-name: {Bitmap-Resource "Execute"} portfolio: :icons action-handler: {Event-Handler :form on-transfer})
      (<Border-View>                   name: results-border  position: {Point 2 90} size: {Dimension 891 363} border-type: edge style: outline
        (<Scroller-View>                                     style: document vscroll?: #t
          (<!>                         name: content         layout-type: fill
            (<Tree-Header>                                   style: document
              (<!>                     name: content
                (<Tree-View>           name: results         filled-column: name multiple-selection?: #t double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} context-menu-handler: {Event-Handler :form on-context-menu} selection-mode: row portfolio: :icons
                  (<Tree-Node-Column>  name: name            title: "Name" width: 235)
                  (<Tree-Label-Column> name: left-value      title: "Left Value" width: 200)
                  (<Tree-Label-Column> name: right-value     title: "Right Value" width: 200)
                  (<Left-Column>                             title: "Left" width: 80 justification: center)
                  (<Action-Column>                           title: "Action" width: 75 justification: center)
                  (<Right-Column>                            title: "Right" width: 80 justification: center)))))))))

  
  ;;;
  ;;;; Constants
  ;;;
  
  
  (constant $Left-Value-Column  1)
  (constant $Right-Value-Column 2)
  (constant $Left-Column        3)
  (constant $Action-Column      4)
  (constant $Right-Column       5)
  
  
  ;;;
  ;;;; Initialization
  ;;;
  
  
  (method (install rest)
    (nextmethod rest)
    (load-session))
  
  
  ;;;
  ;;;; Frame
  ;;;
  
  
  (method (frame-workspaces)
    '(groupware))
  
  
  (method (focus-default)
    (acquire-focus~ (locate 'results)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-left-content)
    left-content)
  
  
  (method public (set-left-content content)
    (set! left-content content))
  
  
  (method public (get-right-content)
    right-content)
  
  
  (method public (set-right-content content)
    (set! right-content content))
  
  
  (method public (get-ignored-nodes)
    ignored-nodes)
  
  
  (method public (set-ignored-nodes ignored)
    (set! ignored-nodes ignored))
  
  
  (method public (get-ignored-leaves)
    ignored-leaves)
  
  
  (method public (set-ignored-leaves ignored)
    (set! ignored-leaves ignored))
  

  (method public (get-ignored-extensions)
    ignored-extensions)


  (method public (set-ignored-extensions ignored)
    (set! ignored-extensions ignored))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method (guest-actions)
    (cons (find-actions 'compare-trees)
          (nextmethod)))

  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (frame-activate)
    (nextmethod)
    (when (get-finished?)
      (acquire-focus~ (locate 'results))))


  (method (resize dh dv)
    (nextmethod dh dv)
    (size-figure~ (locate 'trees) dh 0)
    (move-figure~ (locate 'check-algorithm) dh 0)
    (size-figure~ (locate 'green-border) dh 0)
    (size-figure~ (locate 'blue-border) dh 0)
    (move-figure~ (locate 'compare) dh 0)
    (move-figure~ (locate 'copy-right) dh 0)
    (move-figure~ (locate 'merge-leaves) dh 0)
    (move-figure~ (locate 'copy-left) dh 0)
    (move-figure~ (locate 'no-action) dh 0)
    @wait
    (move-figure~ (locate 'build-updater) dh 0)
    (move-figure~ (locate 'transfer) dh 0)
    (size-figure~ (locate 'results-border) dh dv)
    (invalidate-header~ (locate 'results)))
  
  
  (method (on-copy-right evt)
    (copy-right)
    (when (get-property~ evt next?:)
      (unless (goto-next-comparable)
        (bell))))
  
  
  (method protected (copy-right)
    (let ((tree (locate 'results)))
      (with-update-locked~ tree
        (function ()
          (for-each (function (n)
                      (let ((entry (get-user-data~ (get-row~ tree n))))
                        (unless (and (eq? (get-kind~ entry) 'node) (get-left~ entry) (get-right~ entry))
                          (set-action~ entry 'copy-right)
                          (let ((cell (new Cell n $Action-Column)))
                            (invalidate-cell~ tree cell)))))
                    (get-selection~ tree))))))
  
  
  (method (on-copy-left evt)
    (copy-left)
    (when (get-property~ evt next?:)
      (unless (goto-next-comparable)
        (bell))))
  
  
  (method protected (copy-left)
    (let ((tree (locate 'results)))
      (with-update-locked~ tree
        (function ()
          (for-each (function (n)
                      (let ((entry (get-user-data~ (get-row~ tree n))))
                        (unless (and (eq? (get-kind~ entry) 'node) (get-left~ entry) (get-right~ entry))
                          (let ((cell (new Cell n $Action-Column)))
                            (set-action~ entry 'copy-left)
                            (invalidate-cell~ tree cell)))))
                    (get-selection~ tree))))))
  
  
  (method (on-merge-leaves evt)
    (merge-leaves))
  
  
  (method protected (merge-leaves)
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree)))
      (when (not-null? selection)
        (if (> (length selection) 1)
            (bell)
          (let ((entry (get-user-data~ (get-row~ tree (car selection)))))
            (when (and (eq? (get-kind~ entry) 'leaf) (get-left~ entry) (get-right~ entry))
              (compare-leaves (get-left~ entry) (get-right~ entry) entry)))))))
  
  
  (method (on-no-action evt)
    (let ((tree (locate 'results)))
      (with-update-locked~ tree
        (function ()
          (for-each (function (n)
                      (let ((entry (get-user-data~ (get-row~ tree n))))
                        (unless (and (eq? (get-kind~ entry) 'node) (get-left~ entry) (get-right~ entry))
                          (set-action~ entry {})
                          (let ((cell (new Cell n $Action-Column)))
                            (invalidate-cell~ tree cell)))))
                    (get-selection~ tree))))))
  
  
  (method (on-compare-next evt)
    (compare-next))
  
  
  (method (on-compare-previous evt)
    (compare-previous))
  
  
  (method protected (compare-next)
    (if (not (goto-next-comparable))
        (bell)
      (merge-leaves)))
  
  
  (method protected (compare-previous)
    (if (not (goto-previous-comparable))
        (bell)
      (merge-leaves)))
  
  
  @old-code
  (method (on-build-updater evt)
    (let ((file (choose-updater)))
      (build-updater file)
      (updater-built file)))
  
  
  (method protected virtual (choose-updater)
    (choose-new-file))
  
  
  (method (on-transfer evt)
    (transfer evt))
  
  
  (method protected virtual (transfer evt)
    (let* ((tree (locate 'results))
           (count (get-visible-count~ tree))
           (n 0))
      (with-update-locked~ tree
        (function ()
          (while (< n count)
            (let* ((row (get-row~ tree n))
                   (entry (get-user-data~ row))
                   (entry-father (get-user-data~ (get-father~ row)))
                   (left (get-left~ entry))
                   (right (get-right~ entry))
                   (action (get-action~ entry)))
              (when (memq? action '(copy-right copy-left))
                (case action
                  ((copy-right)
                   (if right
                       (if left
                           (copy-item left right)
                         (delete-item right))
                     (let ((new-right (make-item left right-root (get-right~ entry-father))))
                       (set-right~ entry new-right)
                       (add-item left new-right))))
                  ((copy-left)
                   (if left
                       (if right
                           (copy-item right left)
                         (delete-item left))
                     (let ((new-left (make-item right left-root (get-left~ entry-father))))
                       (set-left~ entry new-left)
                       (add-item right new-left)))))
                ;; When copying nodes, treatment is a bit different because they stay showed...
                (if (and (eq? (get-kind~ entry) 'node)
                         (or (and (eq? action 'copy-right) left (not right))
                             (and (eq? action 'copy-left) right (not left))))
                    (begin
                      (case action
                        ((copy-right)
                         (let ((cell (new Cell n $Left-Column)))
                           (invalidate-cell~ tree cell)))
                        ((copy-left)
                         (let ((cell (new Cell n $Right-Column)))
                           (invalidate-cell~ tree cell))))
                      (set-action~ entry {})
                      (let ((cell (new Cell n $Action-Column)))
                        (invalidate-cell~ tree cell)))
                  (remove-row~ tree row)
                  (decrease! count)
                  (decrease! n))))
            (increase! n))))))
  
  
  (method (on-double-click evt)
    (let ((sender (get-sender~ evt)))
      (case (get-name~ sender)
        ((results)
          (on-merge-leaves evt)))))
  
  
  (method (on-return-press evt)
    (let ((sender (get-sender~ evt)))
      (case (get-name~ sender)
        ((results)
          (on-merge-leaves evt))
        (else
          (on-compare evt)))))
  
  
  (method protected virtual (on-compare evt)
    (with-cursor :wait
      (function ()
        (compare-trees))))
    
  
  (method (on-context-menu evt)
    (let* ((sender (get-sender~ evt))
           (pos (get-position~ evt))
           (acquired (acquire sender pos)))
      (with-context-click~ sender pos
        (function ()
          (track-popup-menu (get-compare-menu) acquired)))))
  
  
  (method (on-next-comparable evt)
    (unless (goto-next-comparable)
      (bell)))
  
  
  (method (on-previous-comparable evt)
    (unless (goto-previous-comparable)
      (bell)))
  
  
  (method (on-next-uncomparable evt)
    (unless (goto-next-uncomparable)
      (bell)))
  
  
  (method (on-previous-uncomparable evt)
    (unless (goto-previous-uncomparable)
      (bell)))
  

  ;;;
  ;;;; Tab
  ;;;
  
  
  (method (get-tab-stops)
    (list
      (locate 'base-date)
      (locate~ (locate 'green) 'text)
      (locate~ (locate 'blue) 'text)))
  

  ;;;
  ;;;; Session
  ;;;
  
  
  (method (load-session)
    (let ((pref (get-preferences '(tools compare-directories))))
      (set! Base-Dates (get-base-dates~ pref))))
  
  
  (method (save-guest designer session)
    (add-guest-preferences designer session
      mode:          (get-mode)
      base-date:     (get-base-date)
      left-moniker:  (get-left-moniker)
      right-moniker: (get-right-moniker)
      base-dates:    Base-Dates))
  

  ;;;
  ;;;; Configure
  ;;;
  
  
  (definition Base-Dates
    '())
  
  
  (method public (on-configure evt)
    (install-configuration (get-property~ evt configuration:)))
  
  
  (method public (on-register-base-date evt)
    (unimplemented)
    @convert-date
    (if (not installed-configuration)
        (error "You need an installed configuration to register a base date")
      (bind (name mode) installed-configuration
        (if (/= mode 'merge)
            (error "Base dates can only be registered in merge mode")
          (let ((date (present~ (universal-date)))
                (pair (assq name Base-Dates)))
            (if (not pair)
                (set! Base-Dates (cons (cons name date) Base-Dates))
              (set-cdr! pair date))
            (set-base-date date))))))
  
  
  (method public (on-reset-actions evt)
    (reset-actions))
  
  
  (method public (configure name)
    (install-configuration (child~ (get-preferences '(tools compare-directories)) name)))
  
  
  (method public (install-configuration config)
    (set! installed-configuration config)
    (set-mode (get-mode~ config))
    (set-base-date (essay (eq? (get-mode~ config) 'merge) (find-base-date (get-name~ config))))
    (set-left-moniker (get-left~ config))
    (set-right-moniker (get-right~ config))
    (set-ignored-nodes (get-ignored-nodes~ config))
    (set-ignored-leaves (get-ignored-leaves~ config))
    (set-ignored-extensions (get-ignored-extensions~ config))
    (set-check-algorithm (get-check-algorithm~ config))
    (remove-every-row~ (locate 'results)))
  
  
  (method (find-base-date name)
    (assq-value name Base-Dates #f))
  
  
  (method (on-open-green evt)
    (let* ((tree (locate 'results))
           (selection (get-single-selection~ tree)))
      (when selection
        (let ((entry (get-user-data~ (get-row~ tree selection))))
          (if (or (eq? (get-kind~ entry) 'node) (not (get-left~ entry)))
              (bell)
            (let ((appl (get-application)))
              (edit-document~ appl (get-left~ entry) workspace: 'groupware)))))))
  
  
  (method (on-open-blue evt)
    (let* ((tree (locate 'results))
           (selection (get-single-selection~ tree)))
      (when selection
        (let ((entry (get-user-data~ (get-row~ tree selection))))
          (if (or (eq? (get-kind~ entry) 'node) (not (get-right~ entry)))
              (bell)
            (let ((appl (get-application)))
              (edit-document~ appl (get-right~ entry) workspace: 'groupware)))))))
  
  
  ;;;
  ;;;; Interface
  ;;;
  
  
  (method (get-mode)
    (get-selected-element~ (locate 'mode)))
  
  
  (method (set-mode mode)
    (set-selected-element~ (locate 'mode) mode))
  
  
  (method (get-check-algorithm)
    (get-selected-element~ (locate 'check-algorithm)))
  
  
  (method (set-check-algorithm algorithm)
    (set-selected-element~ (locate 'check-algorithm) (either algorithm 'timestamp)))
  
  
  (method (get-base-date)
    #f
    @convert
    (let ((text (get-string-content~ (locate 'base-date))))
      (if (empty-string? text)
          {}
        (parse-string~ Date text))))
  
  
  (method (set-base-date date)
    @convert
    (let ((text (cond ((not date) "") ((is? date Date) (present~ date)) (else date))))
      (set-string-content~ (locate 'base-date) text)))
  
  
  (method protected virtual (get-left-moniker)
    (let ((browser (locate 'green)))
      (if (get-read-only?~ browser)
          left-moniker
        (set! left-moniker (get-moniker~ browser))
        left-moniker)))
  
  
  (method protected virtual (set-left-moniker moniker)
    (set! left-moniker moniker)
    (set-moniker~ (locate 'green) moniker))
  
  
  (method protected virtual (get-right-moniker)
    (let ((browser (locate 'blue)))
      (if (get-read-only?~ browser)
          right-moniker
        (set! right-moniker (get-moniker~ browser))
        right-moniker)))
  
  
  (method protected virtual (set-right-moniker moniker)
    (set! right-moniker moniker)
    (set-moniker~ (locate 'blue) moniker))
  
  
  (method (select-all . rest)
    (select-all~ (locate 'results) user-origin?: #t))
  
  
  ;;;
  ;;;; Compare
  ;;;
  
  
  (method (require-left-moniker)
    (either (get-left-moniker)
            (error "Unable to get green moniker")))
  
  
  (method (require-right-moniker)
    (either (get-right-moniker)
            (error "Unable to get blue moniker")))
  
  
  (method protected virtual (new-comparer)
    (new Tree-Comparer))
  
  
  (method protected virtual (get-trees)
    (values (moniker->tree (require-left-moniker))
            (moniker->tree (require-right-moniker))))
  
  
  (method protected virtual (moniker->tree moniker)
    moniker)
  
  
  (method public (compare-trees)
    (receive (left-tree right-tree) (get-trees)
      (set! left-root  left-tree)
      (set! right-root right-tree)
      (set! left-base  (tree-base left-tree))
      (set! right-base (tree-base right-tree))
      (let ((comparer (new-comparer)))
        (receive (entry has-differences? scanned-values)
            (compare-nodes~ comparer {} left-tree right-tree
             left-content: left-content
             right-content: right-content
             ignored-nodes: ignored-nodes
             ignored-leaves: ignored-leaves
             ignored-extensions: ignored-extensions
             check-algorithm: (get-check-algorithm)
             feedback: (function (left right) (scan-feedback left right)))
          (display-results entry)
          (user-message "Done")
          (essay has-differences? scanned-values)))))
  
  
  (method protected virtual (scan-feedback left right)
    (user-message "Scanning {a}..." left))
  
  
  ;;;
  ;;;; Results
  ;;;
  
  
  (method (display-results entry)
    (let* ((mode (get-mode))
           (date (get-merge-date mode))
           (tree (locate 'results)))
      (set! root-entry entry)
      (with-update-locked~ tree
        (function ()
          (remove-every-row~ tree)
          (display-entry mode date tree {} entry 0)
          (set-selection~ tree '())))))
  
  
  (method (display-entry mode date tree father entry level)
    (let* ((kind (get-kind~ entry))
           (left (get-left~ entry))
           (right (get-right~ entry))
           (action (get-action~ entry))
           (ref (either left right))
           (lvl? (and (eq? kind 'node) left right))
           (name (if (< level 2) (present-root left right) (present-item ref)))
           (image (entry-image entry ref))
           (c0 (new Tree-Node title: name image: image))
           (c1 (new Tree-Label title: (present-value kind (get-left-value~ entry))))
           (c2 (new Tree-Label title: (present-value kind (get-right-value~ entry))))
           (c3 entry)
           (c4 entry)
           (c5 entry)
           (row (add-row~ tree father: father state: (if lvl? 'collapsed 'expanded) children: (list c0 c1 c2 c3 c4 c5) user-data: entry)))
      (when (not lvl?)
        (setup-action mode date entry)
        (ensure-expanded~ tree row))
      (for-each (function (child)
                  (display-entry mode date tree row child (+ level 1)))
                (get-children~ entry))))
  
  
  (method (setup-action mode base-date entry)
    (case mode
      ((backup) (setup-backup base-date entry))
      ((merge) (setup-merge base-date entry))))
  
  
  (method (setup-backup base-date entry)
    (let ((left        (get-left~ entry))
          (left-value  (get-left-value~ entry))
          (right       (get-right~ entry))
          (right-value (get-right-value~ entry)))
      (set-action~ entry (backup-action base-date left left-value right right-value))))
  
  
  (method (setup-merge base-date entry)
    (when (eq? (get-kind~ entry) 'leaf)
      (let ((left        (get-left~ entry))
            (left-value  (get-left-value~ entry))
            (right       (get-right~ entry))
            (right-value (get-right-value~ entry)))
        (set-action~ entry (merge-action base-date left left-value right right-value)))))
  
  
  (method (reset-actions)
    (let* ((mode (get-mode))
           (base-date (get-merge-date mode))
           (tree (locate 'results))
           (comparer self)
           (proc (function (row)
                   (let ((entry (get-user-data~ row)))
                     (when (is? entry Compare-Entry)
                       (setup-action~ comparer mode base-date entry))))))
      (for-each (function (n)
                  (let ((row (get-row~ tree n)))
                    (proc row)))
                (get-selection~ tree))
      (invalidate-view~ tree)))
  
  
  (method protected virtual (backup-action base-date left left-value right right-value)
    'copy-right)
  
  
  (method protected virtual (merge-action base-date left left-value right right-value)
    {})
  
  
  (method protected virtual (get-merge-date mode)
    (when (eq? mode 'merge)
      (let ((date (get-base-date)))
        (when date
          (get-time~ date)))))
  
  
  ;;;
  ;;;; Updater
  ;;;

  
  @old-code
  (method protected virtual (build-updater file)
    (with-closed ((builder (new Updater-Builder file)))
      (let* ((tree (locate 'results))
             (count (get-visible-count~ tree))
             (n 0))
        (while (< n count)
          (let* ((row (get-row~ tree n))
                 (entry (get-user-data~ row))
                 (entry-father (get-user-data~ (get-father~ row)))
                 (left (get-left~ entry))
                 (right (get-right~ entry))
                 (action (get-action~ entry)))
            (case action
              ((copy-right) (add-action left (either right (make-item left right-root (get-right~ entry-father))) builder))
              ((copy-left)  (add-action right (either left (make-item right left-root (get-left~ entry-father))) builder))))
          (increase! n)))))
  
  
  (method (add-action src dst builder)
    (if (not src)
        (add-delete~ builder dst)
      (add-copy~ builder src dst)))
  
  
  (method protected virtual (updater-built file)
    (edit-document~ (get-application) file workspace: 'groupware))

  
  ;;;
  ;;;; Actions
  ;;;
  
  
  (method protected virtual (add-item src dst)
    (copy-item src dst))
  
  
  (method protected virtual (copy-item src dst)
    (error "Unable to copy {t} to {t}" src dst))

  
  (method protected virtual (delete-item item)
    (error "Unable to delete {t}" item))
  
  
  (method protected virtual (compare-leaves left right user-data)
    )
  
  
  (method public (merge-done user-data)
    (let* ((tree (locate 'results))
           (n (user-data-index~ tree user-data)))
      (when n
        (let ((row (get-row~ tree n)))
          (remove-row~ tree row))
        (goto-next-comparable n))))
  
  
  (method protected (goto-next-comparable (rank {}))
    (goto-next comparable? rank))
  
  
  (method protected (goto-previous-comparable (rank {}))
    (goto-previous comparable? rank))
  
  
  (method protected (goto-next-uncomparable (rank {}))
    (goto-next uncomparable? rank))
  
  
  (method protected (goto-previous-uncomparable (rank {}))
    (goto-previous uncomparable? rank))
  
  
  (method protected (goto-next predicate (rank {}))
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree))
           (rank (either rank (if (null? selection) 0 (+ (apply max selection) 1))))
           (next (find-next tree rank predicate)))
      (when next
        (set-selection~ tree (list next))
        next)))
  
  
  (method protected (goto-previous predicate (rank {}))
    (let* ((tree (locate 'results))
           (selection (get-selection~ tree))
           (rank (either rank (if (null? selection) (- (get-visible-count~ tree) 1) (- (apply min selection) 1))))
           (previous (find-previous tree rank predicate)))
      (when previous
        (set-selection~ tree (list previous))
        previous)))
  
  
  @new
  (method (find-next tree rank predicate)
    (loop (for n from rank below (get-visible-count~ tree))
          (with row (get-row~ tree n))
          (with entry (get-user-data~ row))
          (when (predicate entry)
            (return n))
          (finally {})))
  
  
  (method (find-next tree rank predicate)
    (call/ec
      (function (return)
        (loop (for n from rank below (get-visible-count~ tree))
              (do (let* ((row (get-row~ tree n))
                         (entry (get-user-data~ row)))
                    (when (predicate entry)
                      (return n)))))
        {})))
  
  
  (method (find-previous tree rank predicate)
    (call/ec
      (function (return)
        (loop (for n from rank downto 0)
              (do (let* ((row (get-row~ tree n))
                         (entry (get-user-data~ row)))
                    (when (predicate entry)
                      (return n)))))
        {})))
  
  
  (method (comparable? entry)
    (and (eq? (get-kind~ entry) 'leaf) (get-left~ entry) (get-right~ entry)))
  
  
  (method (uncomparable? entry)
    (xor (get-left~ entry) (get-right~ entry)))
  
  
  ;;;
  ;;;; Utilities
  ;;;
  
  
  (method protected virtual (make-item item root father)
    item)
  
  
  (method protected virtual (tree-base tree)
    {})
  
  
  (method protected virtual (trim-left item)
    item)
  
  
  (method protected virtual (trim-right item)
    item)
  
  
  (method protected virtual (present-root left right)
    (present-item (either left right)))
  
  
  (method protected virtual (present-item item)
    )
  
  
  (method protected virtual (present-value kind value)
    )
  
  
  (method protected virtual (entry-image entry ref)
    (case (get-kind~ entry)
      ((node) {Bitmap-Resource "TypeFolder"})
      ((leaf) {Bitmap-Resource "TypeFile"})))
  
  
  (method protected virtual (action-image action left right)
    (case action
      ((copy-right)
       (cond ((not left) {Bitmap-Resource "DeleteRight"})
             ((not right) {Bitmap-Resource "AddRight"})
             (else {Bitmap-Resource "CopyRight"})))
      ((merge)
       {Bitmap-Resource "Merge"})
      ((copy-left)
       (cond ((not left) {Bitmap-Resource "AddLeft"})
             ((not right) {Bitmap-Resource "DeleteLeft"})
             (else {Bitmap-Resource "CopyLeft"})))))
  
  
  (method protected virtual (get-compare-menu)
    (new Compare-Menu)))


;;;
;;;; Item-Column
;;;


(class Item-Column extends Tree-Column
  
  
  (method (draw-cell surface context row cell width height)
    (draw-background surface context row cell width height)
    (let ((lvl? (and (eq? (get-kind~ cell) 'node) (get-left~ cell) (get-right~ cell))))
      (when (and (not lvl?) (present? cell))
        (let ((image (present-image cell)))
          (draw-image~ (get-icons-cache~ (get-application)) surface context image (quotient (- width 16) 2) 0)))))
  
  
  (method abstract protected virtual (present? cell)
    )
  
  
  (method abstract protected virtual (present-image cell)
    ))


;;;
;;;; Left-Column
;;;


(class Left-Column extends Item-Column
  
  
  (method (present? cell)
    (get-left~ cell))
  
  
  (method (present-image cell)
    {Bitmap-Resource "Green"}))


(register-autoclass Left-Column)


;;;
;;;; Action-Column
;;;


(class Action-Column extends Tree-Column
  
  
  (method (draw-cell surface context row cell width height)
    (draw-background surface context row cell width height)
    (let ((action (get-action~ cell)))
      (when action
        (let ((appl (get-application))
              (image (action-image~ creator action (get-left~ cell) (get-right~ cell))))
          (draw-image~ (get-icons-cache~ appl) surface context image (+ (quotient (- width 16) 2) 1) 0))))))


(register-autoclass Action-Column)


;;;
;;;; Right-Column
;;;


(class Right-Column extends Item-Column
  
  
  (method (present? cell)
    (get-right~ cell))
  
  
  (method (present-image cell)
    {Bitmap-Resource "Blue"}))


(register-autoclass Right-Column)


;;;
;;;; Compare-Menu
;;;


(class Compare-Menu extends Context-Menu
  
  
  (form
    (<install>
      (<Label-Item>                                title: "&Configure" icon: {Bitmap-Resource "All"}
        (<Configure-Menu>))
      (<Separator-Item>   name: action-group)
      (<Label-Item>       name: register-base-date title: "&Register Base Date" action-handler: {Event-Handler :document on-register-base-date})
      (<Label-Item>       name: reset-actions      title: "Reset &Actions" action-handler: {Event-Handler :document on-reset-actions})
      (<Separator-Item>   name: open-group)
      (<Label-Item>       name: open-green         title: "Open &Green" icon: {Bitmap-Resource "Green"} action-handler: {Event-Handler :document on-open-green})
      (<Label-Item>       name: open-blue          title: "Open &Blue" icon: {Bitmap-Resource "Blue"} action-handler: {Event-Handler :document on-open-blue}))))


;;;
;;;; Configure-Menu
;;;


(class Configure-Menu extends Context-Menu
  
  
  (method (finish rest)
    (nextmethod rest)
    (update-menu))
  
  
  (method public (update-menu)
    (for-each (function (config)
                (let ((title (get-presentation~ config)))
                  (new Label-Item parent: self title: title action-handler: (new Event-Handler target-name: :document method-name: 'on-configure properties: (list configuration: config)))))
              (get-children~ (get-preferences '(tools compare-directories))))))


(register-autoclass Configure-Menu))
