;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Web Server
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.library.http.Web-Server jazz


(import (jazz.library)
        (jazz.utilities))


(class Web-Server extends HTTP-Server
  
  
  (slot root-directory)
  (slot default-resource)
  (slot unauthentified-resource)
  (slot html-generator)
  
  
  (method (initialize . rest)
    (bind-keywords ((root-directory {}) (default-resource {}) (unauthentified-resource {}) . others) rest
      (apply nextmethod others)
      (set! [root-directory self] root-directory)
      (set! [default-resource self] default-resource)
      (set! [unauthentified-resource self] unauthentified-resource)
      (set! [html-generator self] {})))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method public (get-root-directory)
    root-directory)
  
  
  (method public (get-default-resource)
    default-resource)
  
  
  (method public (get-unauthentified-resource)
    unauthentified-resource)
  
  
  (method public (get-html-generator)
    html-generator)
  
  
  (method public (set-html-generator value)
    (set! html-generator value))
  
  
  ;;;
  ;;;; Server
  ;;;
  
  
  (method (server-name)
    "Web Server")

  
  ;;;
  ;;;; Resource
  ;;;
  
  
  (method (find-file-resource resource)
    (when (and root-directory (not (empty-string? resource)))
      (let ((file (new-file~ root-directory (tokenise resource))))
        (when (exists?~ file)
          file))))

  
  ;;;
  ;;;; Generate
  ;;;
  
  
  (method (generate-response request)
    (let ((resource (get-resource~ request)))
      (if (and (empty-string? resource) default-resource)
          (redirect-response request default-resource)
        (let ((generator (find-generator request resource)))
          (if generator
              (generator request)
            (let ((servlet-info (find-servlet-info resource)))
              (if servlet-info
                  (bind (resource servlet-class . rest) servlet-info
                    (bind-optionals ((needs-authentification? false)) rest
                      (process-servlet request resource servlet-class needs-authentification?)))
                (let ((file (find-file-resource resource)))
                  (if (null? file)
                      (nextmethod request)
                    (let ((ext (get-extension~ file)))
                      (cond ((ci= ext "jml") (process-jml-file request file))
                            ((ci= ext "html") (process-html-file request file))
                            ((member? ext '("gif" "png" "jpg" "bmp" "css" "js") test: ci=) (process-resource-file request file))
                            (else (process-unknown-file request file)))))))))))))
  
  
  (method (find-generator request resource)
    (let ((associations (get-associations)))
      (cdr (assoc resource associations))))
  
  
  (method protected virtual (get-associations)
    '())

  
  ;;;
  ;;;; Servlet
  ;;;
  
  
  (method (find-servlet-info resource)
    (let ((associations (get-servlet-associations)))
      (assoc resource associations)))
  
  
  (method protected virtual (get-servlet-associations)
    '())
  
  
  (method (process-servlet request resource servlet-class needs-authentification?)
    (catch (HTTP-Redirect redirect
             (redirect-response request [resource redirect]))
      (validate-authentification request needs-authentification?)
      (generate-servlet request resource servlet-class)))
  
  
  (method (validate-authentification request needs-authentification?)
    (when (and needs-authentification? (null? (get-session~ request)))
      (redirect-servlet unauthentified-resource)))
  
  
  (method (generate-servlet request resource servlet-class)
    (when html-generator
      (generate-static-page request resource servlet-class html-generator))
    (call-listeners-receive-servlet-request request resource servlet-class)
    (let* ((pref (jml-preferences))
           (servlet (new (autoload servlet-class) self request {} pref))
           (renderer (jml-renderer pref))
           (jml (process-request~ servlet request resource))
           (printer (new String-Printer)))
      (render~ renderer jml printer)
      (get-output~ printer)))
  
  
  (method (generate-static-page request resource servlet-class html-generator)
    (let* ((pref (jml-preferences))
           (servlet (new (autoload servlet-class) self request html-generator pref)))
      (when (generate-page?~ html-generator resource servlet)
        (setup-directory~ html-generator request resource servlet)
        (let ((renderer (jml-renderer pref))
              (jml (process-request~ servlet request resource))
              (static-file (generated-page~ html-generator request root-directory resource servlet)))
          (create-directories~ static-file)
          (with-closed ((printer (new File-Printer static-file)))
            (render~ renderer jml printer))))))

  
  ;;;
  ;;;; Redirect
  ;;;
  
  
  (method public (redirect-servlet resource)
    (throw (new HTTP-Redirect resource)))
  
  
  (method (redirect-response request resource)
    (let ((host (get-header~ request "Host"))
          (session (get-session~ request))
          (response (new HTTP-Response :code 'see-other)))
      (add-connection-close response)
      (add-content-type response "text/html")
      (add-location response (format "http://{a}{a}" host resource))
      (add-session-cookie response session)
      response))

  
  ;;;
  ;;;; JML
  ;;;
  
  
  (method (process-jml-file request file)
    (with-closed ((reader (new File-Reader file)))
      (let ((jml (read reader)))
        (process-jml request jml))))

  
  ;;;
  ;;;; HTML
  ;;;
  
  
  (method (process-html-file request file)
    (let* ((response (new HTTP-File-Response file: file))
           (content (get-content~ response)))
      (add-connection-close response)
      (add-content-type response "text/html")
      (add-content-length response content)
      response))

  
  ;;;
  ;;;; File
  ;;;
  
  
  (method (process-resource-file request file)
    (let* ((response (new HTTP-File-Response file: file))
           (content (get-content~ response)))
      (add-connection-close response)
      (add-content-type response "text/plain")
      (add-content-length response content)
      response))

  
  ;;;
  ;;;; Unknown
  ;;;
  
  
  (method (process-unknown-file request file)
    `(<html>
       (<head>
         (<title> "Error"))
       (<body>
         ,(format "Unknown resource type: {a}" (parse~ file))
         (<br>)
         (<br>)
         ,(request-content request)))))


;;;
;;;; Redirect
;;;


(class HTTP-Redirect extends Exception
  
  
  (slot resource)
  
  
  (method (initialize res)
    (nextmethod)
    (set! resource res))))
