;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Calendar Days
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.calendar.Calendar-Day jazz


(import (jazz.ui)
        (jazz.literals)
        (jazz.library)
        (jazz.utilities))


(class Calendar-Day extends View


  (property week initialize {} accessors generate)
  (property day  initialize {} accessors generate)
  (property date initialize {} accessors generate)
  
  
  (slot selected? initialize false)

  
  (method (print printer readably)
    (print-unreadable self printer
      (function dynamic (printer)
        (format printer "{a}" date))))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-manager)
    (get-manager~ (get-calendar)))
  
  
  ;;;
  ;;;; Calendar
  ;;;
  
  
  (method public (get-calendar)
    parent)
  
  
  (method (same-day?)
    (= date (get-date~ (get-calendar))))
  
  
  (method (same-month?)
    (= (get-month~ date) (get-month~ (get-date~ (get-calendar)))))
  
  
  ;;;
  ;;;; Selection
  ;;;


  (method public (get-selected?)
    selected?)


  (method public (set-selected? flag)
    (set! selected? flag)
    (invalidate-view))


  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method (mouse-down pos)
    (let ((calendar (get-calendar)))
      (acquire-focus~ calendar)
      (set-selection~ calendar self)))


  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method (double-click pos)
    (double-click-day~ (get-manager) self))


  ;;;
  ;;;; Drawing
  ;;;
  
  
  (definition Current-Day-Font
    {Font font-name: "MS Sans Serif" point-size: 9 bold?: #t})

  
  (method (draw dc context update lh lv)
    (with ((bounds (get-bounds)))
      (let ((calendar (get-calendar)))
        (fill-rect~ dc bounds (either (day-color~ calendar self) (day-color)))))
    (draw-day dc)
    (draw-header dc context))
  
  
  (method protected virtual (draw-day dc)
    )
  
  
  (method protected virtual (draw-header dc context)
    (with ((bounds (get-bounds)))
      (when selected?
        (let ((header (new Rect 0 0 [right bounds] 14)))
          (fill-rect~ dc header (selection-color))))
      (set-font~ dc (if (current-day?) Current-Day-Font {Font name: Ansi-Var}))
      (let* ((same-month? (same-month?))
             (day-label (day-label same-month?)))
        (set-text-color~ dc (cond ((not same-month?) {Color name: White}) (selected? {Color name: White}) (else {Color name: Black})))
        (draw-text~ dc 0 0 day-label))))
  
  
  (method (selection-color)
    (if (focused?)
        {Color name: Focused-Selection}
      {Color name: Unfocused-Selection}))
  
  
  (method (day-color)
    (if (same-month?)
        {Color name: White}
      {Color name: Light-Purple}))
  
  
  (method (current-day?)
    (= (day-date~ date) (day-date~ (local-date))))
  
  
  (method (day-label same-month?)
    (let ((day (get-day~ date)))
      (if (and (= day 1) same-month?)
          (format "{s} {a}" day (localize (month-abbrev~ date)))
        (->string day))))
  
  
  @wait
  (method (draw-day dc)
    (nextmethod dc)
    (let* ((calendar (get-calendar))
           (editor (get-editor~ calendar))
           (recurring (collect-recurring editor))
           (holidays (collect-holidays editor))
           (events (collect-events editor))
           (displayed (sort < (append events recurring) key: (function dynamic (time) (get-date~ time))))
           (h 1)
           (v 15)
           (row 0)
           (col 0))
      (when holidays
        (with ((bounds (get-bounds)))
          (fill-rect~ dc bounds (get-holiday-color~ (get-client~ editor)))))
      (for-each (function dynamic (event)
                  (let ((color (get-color~ event)))
                    (fill-rect~ dc (new Rect h v (+ h 10) (+ v 10)) color)
                    (increase! col)
                    (if (<= col 3)
                        (increase! h 11)
                      (increase! v 11)
                      (set! h 1)
                      (increase! row)
                      (set! col 0))))
                displayed)))


  ;;;
  ;;;; Focus
  ;;;
  
  
  (method protected virtual (focused?)
    (eq? (get-focus) parent))))
