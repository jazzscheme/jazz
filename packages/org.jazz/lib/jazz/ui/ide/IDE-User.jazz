;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; IDE User
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.ide.IDE-User jazz


(import (jazz.platform)
        (jazz.library)
        (jazz.sql)
        (jazz.sql.view)
        (jazz.ui)
        (jazz.ui.clipboard)
        (jazz.ui.view)
        (jazz.utilities))


(class IDE-User extends User uses (Utilities System Development)


  (method (init)
    (nextmethod)
    @wait
    (start-messaging))
    
  
  ;;;
  ;;;; Read/Write
  ;;;
  
  
  ;; Usefull when hand installing from a cdrom drive
  (method (make-home-read/write)
    (iterate-directory~ {Directory Launch}
      (function (file)
        (set-read/write~ file))))

  
  ;;;
  ;;;; SQL
  ;;;
  
  
  (method (format-sql value)
    (cond ((string? value) (format "'{a}'" value))
          (else (error "Unable to format SQL value: {s}" value))))
  
  
  (method (output-tables tables (name: name {}) (select?: select? #t))
    (let ((text (get-sql-console-text)))
      (output-tables~ text tables name))
    (when select?
      (let ((appl (get-application)))
        (select-workspace~ appl 'sql))))
  
  
  (method (output-table table (name: name {}) (select?: select? #t))
    (output-tables (list table) name: name select?: select?))
  
  
  (method (log-tables tables)
    (for-each log-table tables))
  
  
  (method (log-table table)
    (log-entry name: (get-name~ table) :data table))
  
  
  ;;;
  ;;;; Glue
  ;;;
  
  
  (method (glue n (kind 1))
    (let* ((appl (get-application))
           (stack (get-clipboard-stack~ appl))
           (lst (cons (get-clipboard) (subseq stack 0 (- n 1))))
           (glued (case kind
                    ((1) (format "{l}" lst))
                    ((2) (join lst ", ")))))
      (set-clipboard glued)
      (user-message "Clipboard set to {a}" glued)))


  ;;;
  ;;;; Source
  ;;;
  
  
  (method (get-source class-name)
    (either (find-document class-name)
            (find-file class-name)
            (find-zipped class-name)))


  (method (find-document class-name)
    (unless (get-manifest-source class-name)
      (let ((target (->string class-name)))
        (call/ec
          (lambda (return)
            (for-each (function dynamic (frame)
                        (when (is? frame Host-Frame)
                          (let ((guest (get-guest~ frame)))
                            (when (is? guest Document)
                              (let ((moniker (get-moniker~ (get-controller~ guest))))
                                (when moniker
                                  (let ((base (get-base~ moniker))
                                        (ext (get-extension~ moniker)))
                                    (when (and (= base target)
                                               (ci=? ext "jazz"))
                                      (return moniker)))))))))
                      (get-children~ (get-stage)))
            {})))))
  
  
  (method (find-file class-name)
    (let ((name (tie "{class-name}.jazz")))
      (call/ec
        (lambda (return)
          (for-each (function dynamic (dir)
                      (let ((file (new-file~ dir name)))
                        (when (exists?~ file)
                          (return file))))
                    (source-directories))
          {}))))
  
  
  (method protected virtual (source-directories)
    '())
  
  
  (method (find-zipped class-name)
    (let ((target (format "{a}.jazz" class-name)))
      (call/ec
        (lambda (return)
          (for-each (function dynamic (zip)
                      (when (exists?~ zip)
                        (with-closed ((unzip (new Unzip-File zip)))
                          (iterate-files~ unzip
                            (function dynamic (entry size)
                              (when (= (extract-name entry) target)
                                (return (new Zipped zip entry)))))
                          {})))
                    (source-zips))
          {}))))
  
  
  (method protected virtual (source-zips)
    '())))
