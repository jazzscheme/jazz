;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Split Layout
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.layout.Split-Layout jazz


(import (jazz.ui)
        (jazz.library)
        (jazz.utilities))


(class Split-Layout extends Layout

  
  (method (layout figures width height . rest)
    (bind-keywords ((separator nil) (state nil) (orientation nil) (mode nil) (division nil) (insets nil)) rest
      (if (not state)
          (layout-empty figures width height orientation mode division insets)
        (case state
          ((zoom) (layout-zoom figures width height separator orientation mode division insets))
          ((split) (layout-split figures width height separator orientation mode division insets))))))
  
  
  (method (layout-empty figures width height orientation mode division insets)
    (for-each (function dynamic (figure)
                (set-visible?~ figure false))
              figures)
    nil)
  
  
  (method (layout-zoom figures width height separator orientation mode division insets)
    (for-each (function dynamic (figure)
                (when (get-visible?~ figure)
                  (set-position~ figure (new Point [left insets] [top insets]))
                  (set-size~ figure (new Dimension (- width [left insets] [right insets]) (- height [top insets] [bottom insets])))))
              figures)
    nil)
  
  
  (method (layout-split figures width height separator orientation mode division insets)
    (when figures
      (let* ((find (function dynamic (location) (find-if (function (figure) (= (get-location~ figure) location)) figures)))
             (first (find 'first))
             (second (find 'second))
             (bar))
        ;; because workspace edition is still an error prone manual process
        (when (not first)
          (error "Unable to locate first figure in {t}" figures))
        ;; because workspace edition is still an error prone manual process
        (when (not second)
          (error "Unable to locate second figure in {t}" figures))
        (let ((size (case orientation ((horz) width) ((vert) height))))
          (case mode
            ((absolute)
             (when (negative? division)
               (set! division (+ size division))))
            ((relative)
             (let ((pct (round (percent (abs division) size))))
               (if (positive? division)
                   (set! division pct)
                 (set! division (- size pct)))))))
        (let ((splitter-size (case separator ((resize) 4) ((edge) 2) ((line) 1))))
          (case orientation
            ((horz)
             (set-position~ first (new Point [left insets] [top insets]))
             (set-size~ first (new Dimension (- division [left insets]) (- height [top insets] [bottom insets])))
             (set-position~ second (new Point (+ division splitter-size) [top insets]))
             (set-size~ second (new Dimension (- width division splitter-size [right insets]) (- height [top insets] [bottom insets])))
             (set! bar (new Rect division [top insets] (+ division splitter-size) (- height [bottom insets]))))
            ((vert)
             (set-position~ first (new Point [left insets] [top insets]))
             (set-size~ first (new Dimension (- width [left insets] [right insets]) (- division [top insets])))
             (set-position~ second (new Point [left insets] (+ division splitter-size)))
             (set-size~ second (new Dimension (- width [left insets] [right insets]) (- height division splitter-size [top insets])))
             (set! bar (new Rect [left insets] division (- width [right insets]) (+ division splitter-size))))))
        (set-visible?~ first true)
        (set-visible?~ second true)
        bar)))))
