;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Login Dialog
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.login.Login-Dialog jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.platform)
        (jazz.platform.windows)
        (jazz.ui)
        (jazz.literals)
        (jazz.library)
        (jazz.system)
        (jazz.utilities))


(class Login-Dialog extends View


  (form
    (<install>                                                size: {Dimension 400 206}
      (<Label-View>                 name: title               title: "Welcome to Jedi" position: {Point 10 9} size: {Dimension 350 16} font: {Font name: Label-Bold})
      (<Label-View>                 name: description         title: "Please enter the user that will be used for this session" position: {Point 30 27} size: {Dimension 350 16} font: {Font name: Default-GUI})
      (<Group-Box>                  name: content             position: {Point -2 44} size: {Dimension 404 116}
        (<Radio-Button>             name: default-button      title: "Default" position: {Point 25 32} tooltip?: #t tooltip-text: "Use default user" action-handler: {Event-Handler form: on-default})
        (<Radio-Button>             name: new-button          title: "New" position: {Point 25 55} tooltip?: #t tooltip-text: "Create a new user" action-handler: {Event-Handler form: on-new})
        (<Radio-Button>             name: existing-button     title: "Existing" position: {Point 25 78} tooltip?: #t tooltip-text: "Open an existing user from the standard directories" action-handler: {Event-Handler form: on-existing})
        (<Vertical-Line>                                      position: {Point 110 15} size: {Dimension 8 91})
        (<Label-View>               name: new-label           title: "Name:" position: {Point 136 56} size: {Dimension 33 16} visible?: #f font: {Font name: Default-GUI})
        (<Border-View>              name: new-border          position: {Point 173 53} size: {Dimension 194 19} visible?: #f border-type: edge style: entry
          (<Scroller-View>                                    hscroll?: #f vscroll?: #f
            (<!>                    name: content             layout-type: fill
              (<User-Text-View>     name: new-user            left-padding: 2 top-padding: 2 accepts-returns?: #f show-unfocused-selection?: #f default-style: {Text-Style name: Default font: {Font name: Ansi-Var}} tooltip?: #t tooltip-text: "Username. Can contain letters, digits, dashes, underscores and periods" return-press-handler: {Event-Handler form: on-return-press} escape-press-handler: {Event-Handler form: on-escape-press} content-change-handler: {Event-Handler form: on-content-change}))))
        (<Border-View>              name: existing-border     position: {Point 140 20} size: {Dimension 251 82} visible?: #f border-type: edge style: entry
          (<Scroller-View>                                    vscroll?: #t style: document
            (<!>                    name: content             layout-type: fill
              (<Tree-View>          name: users               show-header?: #f font: {Font name: Ansi-Var} default-row-height: 15 selection-mode: row can-select-empty?: #f tooltip?: #t tooltip-text: "List of users found in the standard directories" double-click-handler: {Event-Handler form: on-double-click} return-press-handler: {Event-Handler form: on-return-press} escape-press-handler: {Event-Handler form: on-escape-press}
                (<Tree-Node-Column>                           title: "User" width: 176 toplevel-controls?: #f display-images?: #f))))))
      (<Push-Button>                name: login               title: "Login" position: {Point 117 172} size: {Dimension 80 24} default?: #t action-handler: {Event-Handler :host on-ok})
      (<Push-Button>                name: cancel              title: "Cancel" position: {Point 203 172} size: {Dimension 80 24} action-handler: {Event-Handler :host on-cancel})
      (<Branch>                     name: default)
      (<Branch>                     name: new
        (<install>
          (<!>                      name: content
            (<!>                    name: new-label           visible?: #t)
            (<!>                    name: new-border          visible?: #t))))
      (<Branch>                     name: existing
        (<install>
          (<!>                      name: content
            (<!>                    name: existing-border     visible?: #t))))))
  
  
  (method (finish rest)
    (nextmethod rest)
    (setup-default-user)
    (setup-new-user)
    (setup-existing-users))
  
  
  (method (setup-default-user)
    )
  
  
  (method (setup-new-user)
    (set-string-content~ (locate 'new-user) "" @convert (get-system-user-name)))
  
  
  (method (setup-existing-users)
    (let ((users (collect-existing-users)))
      (cond ((null? users)
             (install-default)
             (set-selected?~ (locate 'default-button) #t))
            (else
             (install-existing users)
             (set-selected?~ (locate 'existing-button) #t)))))
  

  ;;;
  ;;;; Modal
  ;;;
  
  
  (method (validate-dialog)
    (let ((message (validate-user)))
      (when message
        (message-box message)
        (throw-cancel))))
  
  
  (method (get-modal-result)
    (case (get-name~ branch)
      ((default)
       'jazz.user.Me)
      ((new)
       (let ((name (get-string-content~ (locate 'new-user))))
         (compose-name 'user (string->symbol name))))
      ((existing)
       (let* ((tree (locate 'users))
              (rank (get-single-selection~ tree)))
         (when rank
           (let ((row (get-row~ tree rank)))
             (compose-name 'user (get-user-data~ row))))))))
  

  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method (get-tab-stops)
    (case (get-name~ branch)
      ((default)
       '())
      ((new)
       '())
      ((existing)
       '())))
  

  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-default evt)
    (install-default))
  
  
  (method (on-new evt)
    (install-new))
  
  
  (method (on-existing evt)
    (install-existing (collect-existing-users)))
  
  
  (method (on-content-change evt)
    (update-login))
  
  
  (method (on-moniker-update evt)
    (update-login))
  
  
  (method (on-double-click evt)
    (when (get-enabled?~ (locate 'login))
      (validate-dialog)
      (let ((dialog (get-host)))
        (end-modal~ dialog (get-modal-result~ dialog)))))
  
  
  ;; Should really be generic in the Dialog class
  (method (on-return-press evt)
    (when (get-enabled?~ (locate 'login))
      (validate-dialog)
      (let ((dialog (get-host)))
        (end-modal~ dialog (get-modal-result~ dialog)))))
  
  
  ;; Should really be generic in the Dialog class
  (method (escape-press key)
    (let ((dialog (get-host)))
      (cancel-modal~ dialog)))
  

  ;;;
  ;;;; Branches
  ;;;
  
  
  (method (install-default)
    (set-active-branch (locate 'default))
    (update-login))
  
  
  (method (install-new)
    (set-active-branch (locate 'new))
    (let ((text (locate 'new-user)))
      (acquire-focus~ text)
      (select-all~ text))
    (update-login))
  
  
  (method (install-existing users)
    (set-active-branch (locate 'existing))
    (let ((tree (locate 'users))
          (rank (either (find users (either (command-argument "defaultuser") (get-system-property 'Default-User)) test: equal?) (essay users 0))))
      (remove-every-row~ tree)
      (for-each (function dynamic (user)
                  (let ((name (string->symbol user)))
                    (add-row~ tree children: (list (new Tree-Node title: user)) user-data: name)))
                users)
      (when (and rank (< rank (get-visible-count~ tree)))
        (set-single-selection~ tree rank)
        (ensure-displayed~ tree))
      (acquire-focus~ tree))
    (update-login))
  
  
  (method (collect-existing-users)
    (let ((lst '()))
      (define (collect-directory dir)
        (when (exists?~ dir)
          (iterate-directory~ dir
            (function dynamic (dir)
              (let ((file (new-file~ dir (format "_{a}.jazz" (get-name~ dir)))))
                (when (exists?~ file)
                  (let ((base (get-base~ file)))
                    (let ((user (substring base 1 (string-length base))))
                      (set! lst (cons user lst)))))))
            files?: #f
            directories?: #t
            recursive?: #f)))
      
      (collect-directory {Directory Users})
      
      (sort ci<? lst)))
  

  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update-login)
    (when branch
      (case (get-name~ branch)
        ((default)
         (set-enabled?~ (locate 'login) #t))
        ((new)
         (set-enabled?~ (locate 'login) (not (empty-string? (get-string-content~ (locate 'new-user))))))
        ((existing)
         (set-enabled?~ (locate 'login) (get-selection~ (locate 'users)))))))
  

  ;;;
  ;;;; Validate
  ;;;
  
  
  (method (validate-user)
    (case (get-name~ branch)
      ((new)
       (let ((username (get-string-content~ (locate 'new-user))))
         (validate-new-user username)))
      (else
       {})))
  
  
  (method (validate-new-user username)
    (cond ((let ((first (first username)))
             (not (or (eqv? first #\_)
                      (alphabetic? first))))
           "Username must start with a letter or an underscore")
          ((not (every? (function dynamic (char)
                          (or (alphabetic? char)
                              (numeric? char)
                              (memv? char '(#\minus #\_ #\period))))
                        username))
           "Username can only contain letters, digits, dashes, underscores and periods")
          ((let ((dir (new Directory (list 'Users username))))
             (exists?~ dir))
           (format "The username \"{a}\" already exists in your home library" username))
          (else
           {})))
  

  ;;;
  ;;;; Drawing
  ;;;
  
  
  (definition Header-Color
    (new Color red: 120 green: 120 blue: 255))

  
  (method (draw surface context update lh lv)
    (let ((size (get-size)))
      (gradient-fill~ surface (new Rect 0 0 (get-width~ size) 50) {Color name: White} Header-Color))))


;;;
;;;; User-Text-View
;;;


(class User-Text-View extends Plain-Text-View
  
  
  (method (get-context-menu)
    (new-text-context-menu)))


(register-autoclass User-Text-View))
