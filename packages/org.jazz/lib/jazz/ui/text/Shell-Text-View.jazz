;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Shell Text View
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2007
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(library jazz.ui.text.Shell-Text-View jazz


(import (jazz.ui)
        (jazz.library)
        (jazz.utilities))


(class Shell-Text-View extends Code-Text-View
  
  
  (method (install rest)
    (nextmethod rest)
    (let ((pref (preferences)))
      (add-style (new Text-Style name: 'Title   base: 'Default color: (get-title-color~ pref)))
      (add-style (new Text-Style name: 'Comment base: 'Default color: (get-comment-color~ pref)))
      (add-style (new Text-Style name: 'Keyword base: 'Default color: (get-keyword-color~ pref)))))

  
  ;;;
  ;;;; Frame
  ;;;
  
  
  @convert-guest
  (method meta (frame-icon)
    {Bitmap-Resource "Document"})
  
  
  (method (frame-workspaces)
    '(text))


  ;;;
  ;;;; Explorer
  ;;;


  (method (explorer-class)
    CppJava-Explorer)
 
  
  ;;;
  ;;;; Expr
  ;;;
  
  
  (method (get-context)
    '(cpp/java))


  (method (current-name)
    (name-at (current-expr-range)))
  
  
  (method (name-at range)
    (when range
      (let ((name (get-string range)))
        (if (= (element name 0) #\open-brace)
            (read-string-element name)
          name))))


  (method (current-expr)
    (current-name))
  
  
  (method (current-expr-range)
    (if (and (not completion-active?) (has-selection?))
        (get-selection)
      (expr-range (get-end))))
  
  
  (method (expr-range pos)
    (let ((expl (new CppJava-Explorer self start: pos end: pos)))
      (outer-expr~ expl)
      (get-range~ expl)))
  
  
  (method (expr-at pos)
    (name-at (expr-range pos)))

  
  ;;;
  ;;;; Chapters
  ;;;
  
  
  (constant headers
    (list
      (append "rem ---" (string #\tab))
      (append "rem ---" (string #\space))))
  
  
  (method (get-headers)
    headers)
  
  
  (method (get-first-special?)
    #t)
  
  
  (method (get-first-chapter lst)
    (if (get-first-special?)
        (car lst)
      (let ((file (get-moniker)))
        (list (text-beginning)
              ""
              (if (null? file)
                  "<no file>"
                (get-name~ file))))))
  
  
  (method (get-remaining-chapters lst)
    (if (get-first-special?)
        (cdr lst)
      lst))

  
  ;;;
  ;;;; Keywords
  ;;;
  
  
  (constant Keywords
    '("set"
      "if"
      "not"
      "else"
      "goto"
      "for"
      "in"
      "do"
      "shift"
      "call"
      "cd"
      "echo"
      "defined"
      "exist"
      "exit"))

  
  (method (get-keywords)
    Keywords)

  
  ;;;
  ;;;; Targets
  ;;;
  
  
  (definition Syntax-Targets
    {})
  
  (definition Syntax-Automata
    {})
  
  
  (method (syntax-targets)
    (unless Syntax-Targets
      (set! Syntax-Targets (compute-syntax-targets)))
    Syntax-Targets)
  
  
  (method (syntax-automata)
    (unless Syntax-Automata
      (set! Syntax-Automata (compute-syntax-automata)))
    Syntax-Automata)
  
  
  (method (compute-syntax-targets)
    (append (list (list "rem" 'comment #f))
            (map (function dynamic (str) (list str 'keyword)) (get-keywords))))
        
  
  (method (compute-syntax-automata)
    (build-search-automata (syntax-targets) #t #f self cpp/java-constituent?))

  
  ;;;
  ;;;; Coloring
  ;;;
  
  
  (method (preferences)
    (get-preferences '(documents shell-text)))
  
  
  (method (colorize-type found type start end colorizer recolorize?)
    (case type
      ((comment) (colorize-line-comment found start end colorizer 'Comment 'Title #\minus))
      ((keyword) (colorize-keyword      found start end colorizer 'Keyword))))))
